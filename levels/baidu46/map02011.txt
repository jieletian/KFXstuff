REM *********************************************************
REM             Script for Level 2011.死亡之翼
REM *********************************************************             
REM             本地图制作者： 始作俑者
REM                  QQ：331672578
REM  欢迎加1128460424（地下城守护者 - Dungeon Keeper）群交流
REM *********************************************************
REM 地图的大概情况与参考建议：

REM 1、最后需要末日审判,消灭镰刀怪过关
     
REM 2、镰刀初始属性爆表一击必杀，全部救出兽人后，根据提示通过合成会削弱其属性。

REM 3、占领并清理左下方四个房间可以得到破墙术

REM ***********************************************************************
REM 修改内容及BUG修正：

REM 1、去掉怪物繁殖宝盒
     
REM 2、左下方四个房间的动作标记范围扩大，确保能够触发

REM 3、招募中心调整到找到所有兽人后可以建造

REM 4、加入防传怪机制

REM ***********************************************************************

LEVEL_VERSION(1)
SET_GENERATE_SPEED(400)

SET_CREATURE_HEALTH(HORNY,7895)
SET_CREATURE_STRENGTH(HORNY,0）
SET_CREATURE_ARMOUR(HORNY,255） 

SET_CREATURE_HEALTH(HELL_HOUND,1500)
SET_CREATURE_STRENGTH(HELL_HOUND,255）
SET_CREATURE_ARMOUR(HELL_HOUND,150）

MAX_CREATURES(PLAYER0,20)
MAX_CREATURES(PLAYER1,30)
MAX_CREATURES(PLAYER2,30)
MAX_CREATURES(PLAYER2,30)

COMPUTER_PLAYER(PLAYER1,1)
COMPUTER_PLAYER(PLAYER2,0)
COMPUTER_PLAYER(PLAYER3,0)

START_MONEY(PLAYER0,5000)
START_MONEY(PLAYER1,20000)
START_MONEY(PLAYER2,20000)
START_MONEY(PLAYER3,20000)

REM *******************************添加怪物到入口 *********************************
REM   (添加到入口的生物名字,数量)

ADD_CREATURE_TO_POOL(FAIRY,15)
ADD_CREATURE_TO_POOL(KNIGHT,30)
ADD_CREATURE_TO_POOL(WIZARD,15)
ADD_CREATURE_TO_POOL(BILE_DEMON,40)

REM *******************************指定阵营（玩家）允许/禁止从入口（怪物池）中招指定生物*********************************
REM  (玩家,生物名称,1（只能是1）,[1允许招募/0禁止招募])

CREATURE_AVAILABLE(PLAYER0,WIZARD,1,1)
CREATURE_AVAILABLE(PLAYER0,KNIGHT,1,1)
CREATURE_AVAILABLE(PLAYER0,FAIRY,1,1)
CREATURE_AVAILABLE(PLAYER2,BILE_DEMON,1,1)
CREATURE_AVAILABLE(PLAYER3,BILE_DEMON,1,1)

REM ********************指定阵营（玩家）允许/禁止研究和建造指定房子（建筑）**********************************
REM  (玩家,房子名称,是否可研究或建造[1是允许；0是禁止],是否已经可建造[1已经可建/0待研究])

ROOM_AVAILABLE(ALL_PLAYERS,TREASURE,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,LAIR,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,GARDEN,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,TRAINING,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,RESEARCH,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,WORKSHOP,1,0)

REM ********************指定阵营（玩家）允许/禁止研究和使用指定魔法**********************************
REM  (玩家,魔法名称,是否可研究或使用[1是允许；0是禁止],是否已经可使用[1已经可建/0待研究])

MAGIC_AVAILABLE(ALL_PLAYERS,POWER_OBEY,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_IMP,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SLAP,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SPEED,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CALL_TO_ARMS,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HAND,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CONCEAL,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HEAL_CREATURE,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_PROTECT,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CHICKEN,1,0)

REM *********************指定阵营（玩家）允许/禁止制造指定的门*********************************
REM  (玩家,门的名称,[1是允许；0是禁止],1[0是允许；数字是已经制造出来/赠送的数量]

DOOR_AVAILABLE(ALL_PLAYERS,WOOD,1,0)
DOOR_AVAILABLE(ALL_PLAYERS,STEEL,1,0)

REM *********************指定阵营（玩家）允许/禁止制造指定陷阱*********************************
REM  (玩家,陷阱名称,[1是允许；0是禁止],1[0是允许；数字是已经制造出来/赠送的数量]

TRAP_AVAILABLE(ALL_PLAYERS,ALARM,1,0)		
TRAP_AVAILABLE(ALL_PLAYERS,LIGHTNING,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,WORD_OF_POWER,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,POISON_GAS,1,0)
TRAP_AVAILABLE(PLAYER3,BOULDER,1,0)

REM CREATE_PARTY =>组建队伍                (队伍名称)   
REM ADD_TO_PARTY=>添加生物到队伍中   (队伍名称,生物名称,生物等级,携带金钱数量,任务,延时)

CREATE_PARTY(ONE)
	ADD_TO_PARTY(ONE,WIZARD,10,1500,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(ONE,ARCHER,10,1500,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(ONE,DWARFA,10,1500,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(ONE,GIANT,10,1500,ATTACK_ENEMIES,0)
        ADD_TO_PARTY(ONE,SAMURAI,10,1600,ATTACK_ENEMIES,0)
        ADD_TO_PARTY(ONE,BARBARIAN,10,1600,ATTACK_ENEMIES,0)

CREATE_PARTY(TWO)
	ADD_TO_PARTY(TWO,FAIRY,10,1600,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(TWO,FAIRY,10,1600,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(TWO,ARCHER,10,1500,ATTACK_ENEMIES,0)
        ADD_TO_PARTY(TWO,WIZARD,10,1500,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(TWO,WIZARD,10,1500,ATTACK_ENEMIES,0)
        ADD_TO_PARTY(TWO,DWARFA,10,1500,ATTACK_ENEMIES,0)
	
CREATE_PARTY(THREE)
        ADD_TO_PARTY(THREE,GIANT,10,1500,ATTACK_ENEMIES,0)
        ADD_TO_PARTY(THREE,GIANT,10,1500,ATTACK_ENEMIES,0)
        ADD_TO_PARTY(THREE,SAMURAI,10,1600,ATTACK_ENEMIES,0)
        ADD_TO_PARTY(THREE,SAMURAI,10,1600,ATTACK_ENEMIES,0)
        ADD_TO_PARTY(THREE,BARBARIAN,10,1600,ATTACK_ENEMIES,0)
        ADD_TO_PARTY(THREE,KNIGHT,10,1800,ATTACK_ENEMIES,0)
        ADD_TO_PARTY(THREE,KNIGHT,10,1800,ATTACK_ENEMIES,0)

CREATE_PARTY(FOUR)
	ADD_TO_PARTY(FOUR,FAIRY,10,1600,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(FOUR,FAIRY,10,1600,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(FOUR,ARCHER,10,1500,ATTACK_ENEMIES,0)
        ADD_TO_PARTY(FOUR,WIZARD,10,1500,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(FOUR,WIZARD,10,1500,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(FOUR,AVATAR,10,1500,ATTACK_ENEMIES,0)
        ADD_TO_PARTY(FOUR,DWARFA,10,1500,ATTACK_ENEMIES,0)

CREATE_PARTY(FIVE)
    	ADD_TO_PARTY(FIVE,HELL_HOUND,10,1000,ATTACK_DUNGEON_HEART,0)
    	ADD_TO_PARTY(FIVE,HELL_HOUND,10,1000,ATTACK_DUNGEON_HEART,0)
    	ADD_TO_PARTY(FIVE,HELL_HOUND,10,1000,ATTACK_DUNGEON_HEART,0)
    	ADD_TO_PARTY(FIVE,HELL_HOUND,10,1000,ATTACK_DUNGEON_HEART,0)
    	ADD_TO_PARTY(FIVE,HELL_HOUND,10,1000,ATTACK_DUNGEON_HEART,0)
        ADD_TO_PARTY(FIVE,HELL_HOUND,10,1000,ATTACK_DUNGEON_HEART,0)

CREATE_PARTY(SIX)
        ADD_TO_PARTY(SIX,BILE_DEMON,10,0,ATTACK_DUNGEON_HEART,0)
        ADD_TO_PARTY(SIX,BILE_DEMON,10,0,ATTACK_DUNGEON_HEART,0)
        ADD_TO_PARTY(SIX,BILE_DEMON,10,0,ATTACK_DUNGEON_HEART,0)
        ADD_TO_PARTY(SIX,BILE_DEMON,10,0,ATTACK_DUNGEON_HEART,0)
        ADD_TO_PARTY(SIX,BILE_DEMON,10,0,ATTACK_DUNGEON_HEART,0)
        

CREATE_PARTY(SEVEN)
        ADD_TO_PARTY(SEVEN,DARK_MISTRESS,10,0,ATTACK_DUNGEON_HEART,0)
        ADD_TO_PARTY(SEVEN,DARK_MISTRESS,10,0,ATTACK_DUNGEON_HEART,0)
        ADD_TO_PARTY(SEVEN,DARK_MISTRESS,10,0,ATTACK_DUNGEON_HEART,0)
        ADD_TO_PARTY(SEVEN,DARK_MISTRESS,10,0,ATTACK_DUNGEON_HEART,0)
        ADD_TO_PARTY(SEVEN,DARK_MISTRESS,10,0,ATTACK_DUNGEON_HEART,0)
        
    

REM ****************************脚本主体(主要内容、核心内容）**********************************

QUICK_OBJECTIVE(0,"任务:消灭死亡之翼。一座巨大的山脉横亘在中央，四个强大的恶魔名为死亡之翼住在山顶上.",PLAYER0)

QUICK_INFORMATION(1,"据说有一些兽人被分散囚禁在各处，如果能救出它们，也许可以得知恶魔的弱点",PLAYER0)

IF(PLAYER2,DUNGEON_DESTROYED==1)
    ROOM_AVAILABLE(PLAYER0,GUARD_POST,1,1)
ENDIF

IF(PLAYER3,DUNGEON_DESTROYED==1)
    ROOM_AVAILABLE(PLAYER0,PRISON,1,0)
ENDIF


IF(PLAYER_GOOD,DUNGEON_DESTROYED == 1)
    ROOM_AVAILABLE(PLAYER0,BRIDGE,1,0)
    QUICK_INFORMATION(4,"可以建造桥梁了。",PLAYER0) 
ENDIF

IF_ACTION_POINT(1,PLAYER0)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,ONE,1,2)
        IF_ACTION_POINT(2,PLAYER0)
            ADD_PARTY_TO_LEVEL(PLAYER_GOOD,TWO,2,2)
                IF_ACTION_POINT(3,PLAYER0)
                    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,THREE,3,2)
                        IF_ACTION_POINT(4,PLAYER0)
                            ADD_PARTY_TO_LEVEL(PLAYER_GOOD,FOUR,4,2)
                                IF(PLAYER_GOOD,AVATAR<1)
                                    MAGIC_AVAILABLE(PLAYER0,POWER_DESTROY_WALLS,1,1)
				ENDIF
			ENDIF
		ENDIF
	ENDIF
ENDIF

IF_ACTION_POINT(5,PLAYER0)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,FIVE,5,1)
ENDIF

IF_ACTION_POINT(6,PLAYER0)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SIX,6,2)
ENDIF

IF_ACTION_POINT(7,PLAYER0)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SEVEN,7,2)
ENDIF

IF_ACTION_POINT(8,PLAYER0)
    IF_ACTION_POINT(9,PLAYER0)
        IF_ACTION_POINT(10,PLAYER0)
            IF_ACTION_POINT(11,PLAYER0)
                QUICK_INFORMATION(2,"你救出了所有兽人，从奄奄一息的兽人口中得知需要找到龙和地狱犬，连同最强的兽人一起……祭……突然无法说话了！",PLAYER0)
                ROOM_AVAILABLE(PLAYER0,SCAVENGER,1,0)
	    ENDIF
	ENDIF
    ENDIF
ENDIF

IF(PLAYER0,HORNY > 0)
    QUICK_INFORMATION(3,"只见冥界使者从泉水中出现，挥舞着镰刀划破长空，然后消失于天际，死亡之翼的力量已被削弱，该是决战的时刻了",PLAYER0)
    SET_TIMER(PLAYER0,TIMER0)
    SET_CREATURE_HEALTH(HORNY,7895)
    SET_CREATURE_STRENGTH(HORNY,150）
    SET_CREATURE_ARMOUR(HORNY,0）
    MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CHICKEN,0,0)
ENDIF

IF(PLAYER0,TIMER0 >= 200)
    KILL_CREATURE(PLAYER0,HORNY,MOST_EXPERIENCED,1)
ENDIF

REM ****************************胜利/失败条件**********************************

IF(PLAYER_GOOD,HORNY == 0)
    WIN_GAME
ENDIF

IF(PLAYER0,DUNGEON_DESTROYED == 1)
    LOSE_GAME
ENDIF

IF(PLAYER0,GAME_TURN <= 30)
    IF(PLAYER0,TOTAL_CREATURES>=6)
        QUICK_OBJECTIVE(6,"防传怪脚本启动！",PLAYER0)
        LOSE_GAME
    ENDIF
ENDIF