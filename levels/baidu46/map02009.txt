REM *********************************************************
REM             Script for Level 2009.Siege
REM *********************************************************             
REM             本地图制作者： 始作俑者
REM                  QQ：331672578
REM  欢迎加1128460424（地下城守护者 - Dungeon Keeper）群交流
REM *********************************************************
REM 地图的大概情况与参考建议：

REM 1、找到巨魔后会加入怪物池
REM     
REM 2、鬼魂大于等于2时会判为失败
REM
REM 3、本关有大量敌人需做好打硬仗的准备，15分钟以后开始
REM ***********************************************************************

SET_GENERATE_SPEED(500)
MAX_CREATURES(PLAYER0,25)
START_MONEY(PLAYER0,20000)

REM *******************************添加怪物到入口 *********************************
REM   (添加到入口的生物名字,数量)

ADD_CREATURE_TO_POOL(ARCHER,10)
ADD_CREATURE_TO_POOL(DWARFA,10)
ADD_CREATURE_TO_POOL(SAMURAI,30)
ADD_CREATURE_TO_POOL(BARBARIAN,10)
ADD_CREATURE_TO_POOL(FAIRY,15)
ADD_CREATURE_TO_POOL(KNIGHT,20)
ADD_CREATURE_TO_POOL(WIZARD,15)
ADD_CREATURE_TO_POOL(BILE_DEMON,20)
ADD_CREATURE_TO_POOL(DARK_MISTRESS,20)

REM *******************************指定阵营（玩家）允许/禁止从入口（怪物池）中招指定生物*********************************
REM  (玩家,生物名称,1（只能是1）,[1允许招募/0禁止招募])

CREATURE_AVAILABLE(ALL_PLAYERS,ARCHER,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,DWARFA,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,SAMURAI,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,BARBARIAN,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,FAIRY,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,KNIGHT,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,WIZARD,1,1)

REM ********************指定阵营（玩家）允许/禁止研究和建造指定房子（建筑）**********************************
REM  (玩家,房子名称,是否可研究或建造[1是允许；0是禁止],是否已经可建造[1已经可建/0待研究])

ROOM_AVAILABLE(ALL_PLAYERS,TREASURE,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,LAIR,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,GARDEN,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,TRAINING,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,RESEARCH,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,WORKSHOP,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,BARRACKS,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,TORTURE,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,PRISON,1,0)

REM ********************指定阵营（玩家）允许/禁止研究和使用指定魔法**********************************
REM  (玩家,魔法名称,是否可研究或使用[1是允许；0是禁止],是否已经可使用[1已经可建/0待研究])

MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HAND,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SLAP,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_IMP,1,1)

REM *********************指定阵营（玩家）允许/禁止制造指定的门*********************************
REM  (玩家,门的名称,[1是允许；0是禁止],1[0是允许；数字是已经制造出来/赠送的数量]

DOOR_AVAILABLE(ALL_PLAYERS,WOOD,1,0)
DOOR_AVAILABLE(ALL_PLAYERS,BRACED,1,0)

REM *********************指定阵营（玩家）允许/禁止制造指定陷阱*********************************
REM  (玩家,陷阱名称,[1是允许；0是禁止],1[0是允许；数字是已经制造出来/赠送的数量]

TRAP_AVAILABLE(ALL_PLAYERS,POISON_GAS,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,ALARM,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,LIGHTNING,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,WORD_OF_POWER,1,0)

REM CREATE_PARTY =>组建队伍                (队伍名称)   
REM ADD_TO_PARTY=>添加生物到队伍中   (队伍名称,生物名称,生物等级,携带金钱数量,任务,延时)

CREATE_PARTY(PARTY1)
    ADD_TO_PARTY(PARTY1,FLY,10,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(PARTY1,FLY,10,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(PARTY1,FLY,10,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(PARTY1,FLY,10,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(PARTY1,FLY,10,0,ATTACK_DUNGEON_HEART,0)

CREATE_PARTY(PARTY2)
    ADD_TO_PARTY(PARTY2,BUG,10,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(PARTY2,BUG,10,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(PARTY2,BUG,10,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(PARTY2,BUG,10,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(PARTY2,BUG,10,0,ATTACK_DUNGEON_HEART,0)

CREATE_PARTY(PARTY3)
    ADD_TO_PARTY(PARTY3,SPIDER,10,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(PARTY3,SPIDER,10,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(PARTY3,SPIDER,10,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(PARTY3,SPIDER,10,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(PARTY3,SPIDER,10,0,ATTACK_DUNGEON_HEART,0)

CREATE_PARTY(PARTY4)
    ADD_TO_PARTY(PARTY4,TENTACLE,10,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(PARTY4,TENTACLE,10,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(PARTY4,TENTACLE,10,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(PARTY4,TENTACLE,10,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(PARTY4,TENTACLE,10,0,ATTACK_DUNGEON_HEART,0)

CREATE_PARTY(PARTY6)
    ADD_TO_PARTY(PARTY6,BILE_DEMON,10,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(PARTY6,BILE_DEMON,10,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(PARTY6,BILE_DEMON,10,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(PARTY6,BILE_DEMON,10,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(PARTY6,BILE_DEMON,10,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(PARTY6,BILE_DEMON,10,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(PARTY6,BILE_DEMON,10,0,ATTACK_DUNGEON_HEART,0)

CREATE_PARTY(PARTY7)
    ADD_TO_PARTY(PARTY7,VAMPIRE,10,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(PARTY7,VAMPIRE,10,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(PARTY7,VAMPIRE,10,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(PARTY7,VAMPIRE,10,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(PARTY7,VAMPIRE,10,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(PARTY7,VAMPIRE,10,0,ATTACK_DUNGEON_HEART,0)

CREATE_PARTY(PARTY8)
    ADD_TO_PARTY(PARTY8,HORNY,10,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(PARTY8,HORNY,10,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(PARTY8,HORNY,10,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(PARTY8,HORNY,10,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(PARTY8,HORNY,10,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(PARTY8,HORNY,10,0,ATTACK_DUNGEON_HEART,0)

CREATE_PARTY(PARTY9)
    ADD_TO_PARTY(PARTY9,HELL_HOUND,10,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(PARTY9,HELL_HOUND,10,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(PARTY9,HELL_HOUND,10,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(PARTY9,HELL_HOUND,10,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(PARTY9,HELL_HOUND,10,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(PARTY9,HELL_HOUND,10,0,ATTACK_DUNGEON_HEART,0)
    
REM ****************************脚本主体(主要内容、核心内容）**********************************

QUICK_OBJECTIVE(1,"任务：顶住所有进攻，消灭所有敌人",PLAYER0)

SET_FLAG(PLAYER0,FLAG0,0)

IF(PLAYER_GOOD,RESEARCH == 0)
    ADD_CREATURE_TO_LEVEL(PLAYER0,AVATAR,6,1,1,1600)
    QUICK_INFORMATION(11,"你解除了封印，释放了圣者")
ENDIF

IF(PLAYER0,BILE_DEMON > 0)
    CREATURE_AVAILABLE(ALL_PLAYERS,BILE_DEMON,1,1)
    QUICK_INFORMATION(1,"你胁迫了巨魔帮你作战！")
ENDIF

IF(PLAYER0,GAME_TURN >= 20000)
    QUICK_INFORMATION(5,"一大波害虫正在靠近！")
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY1,-1,2)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY1,-2,2)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY1,-7,2)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY1,-8,2)
ENDIF

IF(PLAYER0,GAME_TURN >= 20500)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY1,-1,2)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY1,-2,2)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY3,-7,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY2,-8,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY1,-9,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY1,-10,2)
ENDIF

IF(PLAYER0,GAME_TURN >= 20750)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY2,-6,2)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY4,-7,2)
ENDIF


IF(PLAYER0,GAME_TURN >= 23500)
    ROOM_AVAILABLE(ALL_PLAYERS,BRIDGE,1,1)
    QUICK_INFORMATION(6,"在敌人的猛攻之下你侥幸存活了下来，并且得到了跨越岩浆的方法！")
    QUICK_INFORMATION(12,"抓紧训练，10分钟以后敌人会再来！")
ENDIF

IF(PLAYER0,GAME_TURN >= 40000)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY9,-7,3)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY9,-8,3)
    QUICK_INFORMATION(7,"注意，敌人再次来袭！")
ENDIF


IF(PLAYER0,GAME_TURN >= 43000)
    ROOM_AVAILABLE(ALL_PLAYERS,SCAVENGER,1,0)
    QUICK_INFORMATION(2,"由于你的出色表现，你得到了奖励,现在可以建造招募中心了")
ENDIF

IF(PLAYER_GOOD,DUNGEON_DESTROYED == 1)
    SET_TIMER(PLAYER_GOOD,TIMER0)
    QUICK_INFORMATION(8,"你虽然击退了敌人，但是它们随时会卷土重来")
ENDIF

IF(PLAYER_GOOD,TIMER0 >= 500)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY1,-1,3)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY1,-2,3)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY3,-8,2)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY2,-4,2)
ENDIF

IF(PLAYER_GOOD,TIMER0 >= 1500)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY1,-1,2)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY1,-2,2)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY4,-10,2)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY3,-3,2)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY2,-4,2)
ENDIF

IF(PLAYER_GOOD,TIMER0 >= 2500)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY1,-2,2)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY4,-9,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY4,-10,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY3,-5,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY3,-6,2)
ENDIF

IF(PLAYER_GOOD,TIMER0 >= 3500)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY1,-1,2)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY1,-2,2)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY4,-9,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY4,-9,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY4,-10,2)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY3,-4,2)
ENDIF

IF(PLAYER_GOOD,TIMER0 >= 5000)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY1,-1,2)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY4,-3,2)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY4,-4,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY4,-5,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY4,-6,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY4,-7,1)
    SET_FLAG(PLAYER0,FLAG0,1)
    SET_TIMER(PLAYER0,TIMER1)
ENDIF

IF(PLAYER_GOOD, TOTAL_CREATURES == 0)
    IF(PLAYER0,FLAG0 == 1)
        IF(PLAYER0,TIMER1 >= 3500)
            ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY6,1,1)
            ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY6,2,1)
            ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY7,3,1)
            ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY6,4,1)
            ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY6,5,1)
            QUICK_INFORMATION(9,"注意伏兵！")
            SET_FLAG(PLAYER0,FLAG0,2)
            SET_TIMER(PLAYER0,TIMER2)
        ENDIF
    ENDIF
ENDIF

IF(PLAYER0,FLAG0 == 2)
    IF(PLAYER_GOOD,BILE_DEMON == 0)
        IF(PLAYER_GOOD,VAMPIRE == 0)
            IF(PLAYER0,TIMER2 >= 5000)
                ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY8,7,3)
                QUICK_INFORMATION(10,"觉悟吧圣光！")
                SET_FLAG(PLAYER0,FLAG0,3)
            ENDIF
        ENDIF
    ENDIF
ENDIF

IF(PLAYER0,GHOST >= 2)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY8,7,3)
    QUICK_INFORMATION(11,"死于你手中的冤魂吞噬了你的心灵！迎接它们愤怒的反击吧！")
ENDIF

REM ****************************胜利/失败条件**********************************

IF(PLAYER0,FLAG0 == 3) 
    IF(PLAYER_GOOD,HORNY == 0) 
        WIN_GAME 
    ENDIF  
ENDIF

IF(PLAYER0,DUNGEON_DESTROYED == 1)
    LOSE_GAME
ENDIF

REM ****************************提示信息**********************************

IF(PLAYER0,GAME_TURN >= 300)
    QUICK_INFORMATION(3,"对手十分强大，你有15分钟时间，要做好心理准备！")
ENDIF

IF(PLAYER0,GAME_TURN >= 600)
    QUICK_INFORMATION(13,"小心对待你的俘虏，否则将付出惨痛代价！")
ENDIF

IF(PLAYER0,GAME_TURN >= 1000)
    QUICK_INFORMATION(4,"仔细寻找大陆上散落的魔法书及巨魔，用正确的怪物会事半功倍.")
ENDIF

