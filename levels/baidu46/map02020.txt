REM *********************************************************
REM             Script for Level 2020.邪恶六芒星
REM *********************************************************             
REM             本地图制作者： 始作俑者
REM                  QQ：331672578
REM  欢迎加1128460424（地下城守护者 - Dungeon Keeper）群交流
REM *********************************************************
REM 地图的大概情况与参考建议：

REM 1、技巧图，玩家需了解一定知识，比如手榴弹炸碎滚石,魔法书会在两个相同颜色的图书馆传送
REM     
REM 2、加入惩罚机制，破墙术获得方法有两种，其中一种会惩罚玩家
REM
REM 3、倒计时结束会刷两拨怪物，合理利用祭坛

REM 4、尽量多的召集骷髅

REM 5、最后的boss非常厚

REM ****************************************************************************

LEVEL_VERSION(1)
START_MONEY(PLAYER0,0)
START_MONEY(PLAYER3,100000)
COMPUTER_PLAYER(PLAYER1,1)
COMPUTER_PLAYER(PLAYER2,1)
COMPUTER_PLAYER(PLAYER3,1)
SET_CREATURE_HEALTH(KNIGHT,6120)
SET_CREATURE_STRENGTH(KNIGHT,255)
SET_CREATURE_ARMOUR(KNIGHT,255)
SET_CREATURE_STRENGTH(ARCHER,255)
SET_CREATURE_HEALTH(ARCHER,50)

REM ********************指定阵营（玩家）允许/禁止研究和建造指定房子（建筑）**********************************
REM  (玩家,房子名称,是否可研究或建造[1是允许；0是禁止],是否已经可建造[1已经可建/0待研究])

ROOM_AVAILABLE(PLAYER0,LAIR,1,1)
ROOM_AVAILABLE(PLAYER0,BRIDGE,1,1)

REM ********************指定阵营（玩家）允许/禁止研究和使用指定魔法**********************************
REM  (玩家,魔法名称,是否可研究或使用[1是允许；0是禁止],是否已经可使用[1已经可建/0待研究])

MAGIC_AVAILABLE(PLAYER0,POWER_SLAP,1,1)
MAGIC_AVAILABLE(PLAYER0,POWER_HAND,1,1)
MAGIC_AVAILABLE(PLAYER0,POWER_POSSESS,1,1)
MAGIC_AVAILABLE(PLAYER3,POWER_SIGHT,1,1)
MAGIC_AVAILABLE(PLAYER3,POWER_SPEED,1,1)
MAGIC_AVAILABLE(PLAYER0,POWER_HEAL_CREATURE,1,0)

REM *********************指定阵营（玩家）允许/禁止制造指定的门*********************************
REM  (玩家,门的名称,[1是允许；0是禁止],1[0是允许；数字是已经制造出来/赠送的数量]

DOOR_AVAILABLE(ALL_PLAYERS,MAGIC,1,0)

REM *********************指定阵营（玩家）允许/禁止制造指定陷阱*********************************
REM  (玩家,陷阱名称,[1是允许；0是禁止],1[0是允许；数字是已经制造出来/赠送的数量]

TRAP_AVAILABLE(ALL_PLAYERS,LIGHTNING,1,0)

REM CREATE_PARTY =>组建队伍                (队伍名称)   
REM ADD_TO_PARTY=>添加生物到队伍中   (队伍名称,生物名称,生物等级,携带金钱数量,任务,延时)

CREATE_PARTY(GIANT)
	ADD_TO_PARTY(GIANT,GIANT,10,0,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(GIANT,GIANT,10,0,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(GIANT,GIANT,10,0,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(GIANT,GIANT,10,0,ATTACK_DUNGEON_HEART,0)
        ADD_TO_PARTY(GIANT,GIANT,10,0,ATTACK_DUNGEON_HEART,0)

CREATE_PARTY(FLY)
	ADD_TO_PARTY(FLY,FLY,10,100,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(FLY,FLY,10,100,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(FLY,FLY,10,100,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(FLY,FLY,10,100,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(FLY,FLY,10,100,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(FLY,FLY,10,100,ATTACK_DUNGEON_HEART,0)

CREATE_PARTY(HOUND)
    	ADD_TO_PARTY(HOUND,HELL_HOUND,10,1000,ATTACK_DUNGEON_HEART,0)
    	ADD_TO_PARTY(HOUND,HELL_HOUND,10,1000,ATTACK_DUNGEON_HEART,0)
    	ADD_TO_PARTY(HOUND,HELL_HOUND,10,1000,ATTACK_DUNGEON_HEART,0)
    	ADD_TO_PARTY(HOUND,HELL_HOUND,10,1000,ATTACK_DUNGEON_HEART,0)
    	ADD_TO_PARTY(HOUND,HELL_HOUND,10,1000,ATTACK_DUNGEON_HEART,0)

CREATE_PARTY(FAIRY)
	ADD_TO_PARTY(FAIRY,FAIRY,10,750,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(FAIRY,FAIRY,10,750,ATTACK_ENEMIES,0)
        ADD_TO_PARTY(FAIRY,FAIRY,10,750,ATTACK_ENEMIES,0)
        ADD_TO_PARTY(FAIRY,FAIRY,10,750,ATTACK_ENEMIES,0)
        ADD_TO_PARTY(FAIRY,FAIRY,10,750,ATTACK_ENEMIES,0)

CREATE_PARTY(HORNY)
	ADD_TO_PARTY(HORNY,HORNY,10,1500,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(HORNY,HORNY,10,1500,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(HORNY,HORNY,10,1500,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(HORNY,HORNY,10,1500,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(HORNY,HORNY,10,1500,ATTACK_DUNGEON_HEART,0)
	
CREATE_PARTY(VAMPIRE)
	ADD_TO_PARTY(VAMPIRE,VAMPIRE,10,1500,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(VAMPIRE,VAMPIRE,10,1500,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(VAMPIRE,VAMPIRE,10,1500,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(VAMPIRE,VAMPIRE,10,1500,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(VAMPIRE,VAMPIRE,10,1500,ATTACK_DUNGEON_HEART,0)
        ADD_TO_PARTY(VAMPIRE,KNIGHT,10,1500,ATTACK_DUNGEON_HEART,0)

CREATE_PARTY(TENTACLE)
	ADD_TO_PARTY(TENTACLE,TENTACLE,10,1500,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(TENTACLE,TENTACLE,10,1500,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(TENTACLE,TENTACLE,10,1500,ATTACK_DUNGEON_HEART,0)
        ADD_TO_PARTY(TENTACLE,TENTACLE,10,1500,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(TENTACLE,TENTACLE,10,1500,ATTACK_DUNGEON_HEART,0)
    
REM ****************************脚本主体(主要内容、核心内容）**********************************

QUICK_OBJECTIVE(0,"连接世界和异界的边际出现一条裂缝，正在不断扩大，届时魔物入侵，世界毁灭，只有神龙才知道关闭裂缝的方法，必须找到它！",PLAYER0)

QUICK_INFORMATION(1,"当务之急是找到金子，建造桥梁，但是金子被巨人掌控，要设法拿到才行，开动脑筋吧！",PLAYER0)

IF(PLAYER0,SPIDER > 0)
    QUICK_INFORMATION(2,"兽人在之前的战斗中负伤严重，力竭身亡",PLAYER0)
    KILL_CREATURE(PLAYER0,ORC,MOST_EXPERIENCED,1)
ENDIF

IF(PLAYER0,DRAGON > 0)
    SET_TIMER(PLAYER0,TIMER0)
    SET_FLAG(PLAYER0,FLAG1,0)
    BONUS_LEVEL_TIME(6000)
    QUICK_INFORMATION(3,"当你找到神龙时，得知一条恶龙试图将时空裂缝完全开启，你必须在其完成之前消灭它！在此之前，必须穿过毒气找到引路蜂，救出岩浆中被关押的小鬼，只有它知道恶龙的藏身之处",PLAYER0)
ENDIF

IF(PLAYER0,FLY > 0)
    MAGIC_AVAILABLE(PLAYER0,POWER_CALL_TO_ARMS,1,1)
    MAGIC_AVAILABLE(PLAYER0,POWER_OBEY,1,1)
    QUICK_INFORMATION(4,"引路蜂就在前面，但前方有一片浓雾，需要一股强风把雾吹散",PLAYER0)
    REVEAL_MAP_LOCATION(PLAYER0,14,5)
    TUTORIAL_FLASH_BUTTON(3,-1)
ENDIF

IF(PLAYER0,TIMER0 == 4000)
    IF(PLAYER0,FLAG1 == 0)
        QUICK_INFORMATION(5,"留给你的时间不多了！",PLAYER0)
    ENDIF
ENDIF


IF_CONTROLS(PLAYER0,IMP >= 1)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,DRAGON,-3,1,10,1000)
    QUICK_INFORMATION(6,"恶龙就在对面的房间里！",PLAYER0)
        IF_CONTROLS(PLAYER_GOOD,DRAGON == 0)
            IF(PLAYER0,FLAG1 == 0)
	        SET_FLAG(PLAYER0,FLAG1,1)
	        QUICK_INFORMATION(7,"你成功杀死了恶龙！",PLAYER0)
	        SET_TIMER(PLAYER0,TIMER1)
            ENDIF
       ENDIF
ENDIF

IF(PLAYER0,TIMER1 >= 400)
    QUICK_OBJECTIVE(9,"虽然裂缝的扩大被暂时压制了，想要彻底关闭裂缝必须摧毁敌人的魔法阵，然而东南方的邪恶之堡被铜墙铁壁包围，想要进入必须找到破墙术！",PLAYER0)
ENDIF

IF_ACTION_POINT(1,PLAYER0)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,GIANT,-1,2) 
ENDIF

SET_FLAG(PLAYER0,FLAG2,0)

IF(PLAYER0,FLAG2 == 0)
    IF(PLAYER0,BRIDGE == 0)
        NEXT_COMMAND_REUSABLE
        ROOM_AVAILABLE(PLAYER0,BRIDGE,1,1)
    ENDIF
ENDIF

IF(PLAYER0,FLAG2 == 0)
    IF(PLAYER0,BRIDGE == 1)
        REVEAL_MAP_LOCATION(PLAYER0,10,11)
        QUICK_INFORMATION(10,"很可惜，没有足够的材料来搭建桥梁，需要从敌人那夺取资源！西边有一股强大的势力，我们要碰碰运气！",PLAYER0)
        NEXT_COMMAND_REUSABLE
        ROOM_AVAILABLE(PLAYER0,BRIDGE,0,0)
    ENDIF
ENDIF

IF(PLAYER0,FLAG2 == 1)
    ROOM_AVAILABLE(PLAYER0,BRIDGE,1,1)
ENDIF

IF_ACTION_POINT(9,PLAYER0)
    REVEAL_MAP_LOCATION(PLAYER0,11,11)
    REVEAL_MAP_LOCATION(PLAYER0,15,16)
    QUICK_INFORMATION(11,"与黄色势力相差悬殊，必须智取！",PLAYER0)
ENDIF

IF_ACTION_POINT(13,PLAYER0)
    REVEAL_MAP_LOCATION(PLAYER0,12,11)
    QUICK_INFORMATION(12,"听说对岸的堡垒中有瘟疫术！",PLAYER0)
ENDIF


IF(PLAYER3,TOTAL_CREATURES == 0)
    SET_FLAG(PLAYER0,FLAG2,1)
    QUICK_INFORMATION(13,"计划虽然成功了，但是资源却被带走了！",PLAYER0)
    TUTORIAL_FLASH_BUTTON(18,-1)
    TUTORIAL_FLASH_BUTTON(2,-1)
ENDIF

IF(PLAYER3,DUNGEON_DESTROYED == 1)
    SET_TIMER(PLAYER0,TIMER2)
    ADD_CREATURE_TO_LEVEL(PLAYER0,SPIDER,2,15,4,100)
    QUICK_INFORMATION(14,"一支友军从天而降，这意味着什么？",PLAYER0)
    BONUS_LEVEL_TIME(1200)
ENDIF

IF(PLAYER0,TIMER2 >= 1200)
    QUICK_INFORMATION(15,"敌军正在偷袭！",PLAYER0)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,FLY,-2,10)
    BONUS_LEVEL_TIME(1200)
ENDIF

IF(PLAYER0,TIMER2 >= 2400)
    ADD_CREATURE_TO_LEVEL(PLAYER0,SPIDER,2,9,4,100)
    BONUS_LEVEL_TIME(1200)
ENDIF

IF(PLAYER0,TIMER2 >= 3600)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,HOUND,-2,2)
ENDIF

IF(PLAYER0,TIMER2 >= 4000)
    QUICK_INFORMATION(16,"恶魔的帮凶已损失殆尽，现在有足够的资源来搭建桥梁了，并且从敌人尸体中发现了破墙术的秘密！但是要如何拿到它呢？",PLAYER0)
    REVEAL_MAP_LOCATION(PLAYER0,7,11)
    REVEAL_MAP_LOCATION(PLAYER0,16,5)
ENDIF

IF_ACTION_POINT(16,PLAYER0)
    QUICK_INFORMATION(20,"魔法书的邪恶能量将你的一些部队吞噬了！",PLAYER0)
    KILL_CREATURE(PLAYER0,DARK_MISTRESS,MOST_EXPERIENCED,4)
    KILL_CREATURE(PLAYER0,DRAGON,MOST_EXPERIENCED,1)
    KILL_CREATURE(PLAYER0,BILE_DEMON,MOST_EXPERIENCED,5)
ENDIF

IF_ACTION_POINT(8,PLAYER0)
    MAGIC_AVAILABLE(PLAYER0,POWER_DESTROY_WALLS,0,0) 
    QUICK_OBJECTIVE(17,"你进入了邪恶之堡，破墙术的能量也已耗尽，目标找到魔法阵将其封印！",PLAYER0)
ENDIF

IF_ACTION_POINT(3,PLAYER0)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,FAIRY,3,2) 
ENDIF

IF_ACTION_POINT(4,PLAYER0)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,HORNY,4,2) 
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,HORNY,-4,2) 
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,HORNY,-5,2) 
ENDIF

IF_ACTION_POINT(5,PLAYER0)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,FAIRY,5,2)
    MAGIC_AVAILABLE(PLAYER0,POWER_IMP,1,1)
    ROOM_AVAILABLE(ALL_PLAYERS,TREASURE,1,1)
    TUTORIAL_FLASH_BUTTON(2,-1) 
    TUTORIAL_FLASH_BUTTON(3,-1)
    TUTORIAL_FLASH_BUTTON(21,-1) 
    TUTORIAL_FLASH_BUTTON(6,-1) 
ENDIF

IF_CONTROLS(PLAYER_GOOD,KNIGHT == 0)
    MAGIC_AVAILABLE(ALL_PLAYERS,POWER_DISEASE,1,0)
ENDIF

IF(PLAYER0,ALL_DUNGEONS_DESTROYED == 1)
    QUICK_INFORMATION(18,"敌军从魔法阵不断涌出！",PLAYER0)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,HORNY,6,2)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,VAMPIRE,6,2)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,TENTACLE,-2,2)
    SET_TIMER(PLAYER0,TIMER3)
ENDIF

REM ****************************胜利/失败条件**********************************

IF(PLAYER_GOOD,TOTAL_CREATURES == 0)
    IF(PLAYER0,TIMER3 >= 200)
    PLAY_MESSAGE(PLAYER0,SPEECH,109)
    QUICK_OBJECTIVE(19,"胜利了，浴血激战之后，你将邪恶送回了黑暗之中！",PLAYER0)
        WIN_GAME
    ENDIF 
ENDIF

IF(PLAYER0,DUNGEON_DESTROYED == 1)
    LOSE_GAME
ENDIF

IF(PLAYER0,TIMER0 >= 6000)
    IF(PLAYER0,FLAG1 == 0)
        QUICK_INFORMATION(8,"你没能阻止恶龙的计划，裂缝已经开启，世界即将毁灭！",PLAYER0)
	    LOSE_GAME 
    ENDIF
ENDIF








