REM *********************************************************
REM             Script for Level 2007.COMBIEND FORCES
REM *********************************************************             
REM             本地图制作者： 始作俑者
REM                  QQ：331672578
REM  欢迎加1128460424（地下城守护者 - Dungeon Keeper）群交流
REM *********************************************************
REM 地图的大概情况与参考建议：

REM 1、踩到A1和A2都会触发无限刷怪，其中一个在一大片金矿处
REM     
REM 2、消灭所有武士可关闭刷怪点
REM

REM ***********************************************************************    

SET_GENERATE_SPEED(400)

MAX_CREATURES(PLAYER0,20)
MAX_CREATURES(PLAYER1,30)

COMPUTER_PLAYER(PLAYER1,1)
ALLY_PLAYERS(PLAYER1,PLAYER_GOOD)

START_MONEY(PLAYER0,10000)
START_MONEY(PLAYER1,20000)

REM *******************************添加怪物到入口 *********************************
REM   (添加到入口的生物名字,数量)

ADD_CREATURE_TO_POOL(BUG,30)
ADD_CREATURE_TO_POOL(ORC,5)
ADD_CREATURE_TO_POOL(DARK_MISTRESS,6)
ADD_CREATURE_TO_POOL(TROLL,5)
ADD_CREATURE_TO_POOL(ARCHER,5)
ADD_CREATURE_TO_POOL(DEMONSPAWN,10)
ADD_CREATURE_TO_POOL(BILE_DEMON,20)
ADD_CREATURE_TO_POOL(DWARFA,5)
ADD_CREATURE_TO_POOL(SAMURAI,35)
ADD_CREATURE_TO_POOL(BARBARIAN,5)
ADD_CREATURE_TO_POOL(SPIDER,30)
ADD_CREATURE_TO_POOL(KNIGHT,30)
ADD_CREATURE_TO_POOL(DRAGON,20)
ADD_CREATURE_TO_POOL(GIANT,10)
ADD_CREATURE_TO_POOL(SORCEROR,15)
ADD_CREATURE_TO_POOL(WIZARD,15)

REM *******************************指定阵营（玩家）允许/禁止从入口（怪物池）中招指定生物*********************************
REM  (玩家,生物名称,1（只能是1）,[1允许招募/0禁止招募])

CREATURE_AVAILABLE(ALL_PLAYERS,BUG,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,SPIDER,1,1)
CREATURE_AVAILABLE(PLAYER1,WIZARD,1,1)
CREATURE_AVAILABLE(PLAYER1,GIANT,1,1)
CREATURE_AVAILABLE(PLAYER0,SORCEROR,1,1)
CREATURE_AVAILABLE(PLAYER0,DRAGON,1,1)
CREATURE_AVAILABLE(PLAYER0,ORC,1,1)
CREATURE_AVAILABLE(PLAYER0,DARK_MISTRESS,1,1)
CREATURE_AVAILABLE(PLAYER0,TROLL,1,1)
CREATURE_AVAILABLE(PLAYER0,DEMONSPAWN,1,1)
CREATURE_AVAILABLE(PLAYER0,BILE_DEMON,1,1)
CREATURE_AVAILABLE(PLAYER1,ARCHER,1,1)
CREATURE_AVAILABLE(PLAYER1,DWARFA,1,1)
CREATURE_AVAILABLE(PLAYER1,SAMURAI,1,1)
CREATURE_AVAILABLE(PLAYER1,BARBARIAN,1,1) 
CREATURE_AVAILABLE(PLAYER1,KNIGHT,1,1)

REM ********************指定阵营（玩家）允许/禁止研究和建造指定房子（建筑）**********************************
REM  (玩家,房子名称,是否可研究或建造[1是允许；0是禁止],是否已经可建造[1已经可建/0待研究])
            
ROOM_AVAILABLE(ALL_PLAYERS,TREASURE,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,LAIR,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,GARDEN,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,TRAINING,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,RESEARCH,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,WORKSHOP,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,BARRACKS,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,TEMPLE,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,TORTURE,1,0)

REM ********************指定阵营（玩家）允许/禁止研究和使用指定魔法**********************************
REM  (玩家,魔法名称,是否可研究或使用[1是允许；0是禁止],是否已经可使用[1已经可建/0待研究])

MAGIC_AVAILABLE(ALL_PLAYERS,POWER_OBEY,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_IMP,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SLAP,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SPEED,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CALL_TO_ARMS,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HAND,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CONCEAL,1,0)

REM *********************指定阵营（玩家）允许/禁止制造指定的门*********************************
REM  (玩家,门的名称,[1是允许；0是禁止],1[0是允许；数字是已经制造出来/赠送的数量]

DOOR_AVAILABLE(ALL_PLAYERS,WOOD,1,0)
DOOR_AVAILABLE(ALL_PLAYERS,BRACED,1,0)
DOOR_AVAILABLE(ALL_PLAYERS,STEEL,1,0)

REM *********************指定阵营（玩家）允许/禁止制造指定陷阱*********************************
REM  (玩家,陷阱名称,[1是允许；0是禁止],1[0是允许；数字是已经制造出来/赠送的数量]

TRAP_AVAILABLE(ALL_PLAYERS,ALARM,1,0)		
TRAP_AVAILABLE(ALL_PLAYERS,LIGHTNING,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,WORD_OF_POWER,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,LAVA,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,POISON_GAS,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,BOULDER,1,0)

REM CREATE_PARTY =>组建队伍                (队伍名称)   
REM ADD_TO_PARTY=>添加生物到队伍中   (队伍名称,生物名称,生物等级,携带金钱数量,任务,延时)

CREATE_PARTY(FIRST)
    ADD_TO_PARTY(FIRST,SAMURAI,6,750,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(FIRST,SAMURAI,6,1000,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(FIRST,SAMURAI,6,750,DEFEND_PARTY,0)
    ADD_TO_PARTY(FIRST,THIEF,6,600,STEAL_GOLD,0)
    ADD_TO_PARTY(FIRST,SAMURAI,6,750,DEFEND_PARTY,0)
    ADD_TO_PARTY(FIRST,SAMURAI,6,750,ATTACK_ENEMIES,0)

CREATE_PARTY(SECOND)
    ADD_TO_PARTY(SECOND,SAMURAI,8,750,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(SECOND,THIEF,8,1000,STEAL_GOLD,0)
    ADD_TO_PARTY(SECOND,SAMURAI,8,800,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(SECOND,SAMURAI,8,800,DEFEND_PARTY,0)
    ADD_TO_PARTY(SECOND,THIEF,8,750,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(SECOND,SAMURAI,8,750,ATTACK_ENEMIES,0)
   
REM ****************************脚本主体(主要内容、核心内容）**********************************

QUICK_OBJECTIVE(0,"任务:摧毁蓝色守护者心脏，消灭所有人类.",PLAYER0)

QUICK_INFORMATION(1,"清除附近及上方岛上的矮人就能造桥图纸!")

SET_FLAG(PLAYER0,FLAG0,0)

IF(PLAYER0,TEMPLE >= 9)
    ROOM_AVAILABLE(PLAYER0,TEMPLE,0,0)
ENDIF

IF(PLAYER_GOOD, DWARFA == 0)
    ROOM_AVAILABLE(PLAYER0,BRIDGE,1,1)
    QUICK_INFORMATION(2,"你从敌人手中获得了造桥图纸!")
    QUICK_INFORMATION(3,"但是要记住草率的挖金会招惹敌军进攻!")
ENDIF

IF_ACTION_POINT(1,PLAYER0)
    QUICK_INFORMATION(4,"敌人不断来袭请做好准备!")
    SET_TIMER(PLAYER0,TIMER1)
ENDIF

IF(PLAYER0,FLAG0 == 0)
    IF(PLAYER0,TIMER1 >= 2000)
        NEXT_COMMAND_REUSABLE
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,FIRST,-1,1)
        NEXT_COMMAND_REUSABLE
        SET_TIMER(PLAYER0,TIMER1)
    ENDIF
ENDIF  

IF_ACTION_POINT(2,PLAYER0)
    QUICK_INFORMATION(5,"敌人不断来袭请做好准备!")
    SET_TIMER(PLAYER0,TIMER2)
ENDIF

IF(PLAYER0,FLAG0 == 0)
    IF(PLAYER0,TIMER2 >= 2000)
        NEXT_COMMAND_REUSABLE
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SECOND,-2,1)
        NEXT_COMMAND_REUSABLE
        SET_TIMER(PLAYER0,TIMER2)
    ENDIF
ENDIF

IF(PLAYER_GOOD, SAMURAI == 0)                                                                                    
    SET_FLAG(PLAYER0,FLAG0,1)
    QUICK_INFORMATION(6,"敌人被你的勇猛击退了!")
ENDIF

REM ****************************胜利/失败条件**********************************

IF(PLAYER1,DUNGEON_DESTROYED == 1)
    IF(PLAYER_GOOD, TOTAL_CREATURES == 0)
	WIN_GAME
    ENDIF
ENDIF

IF(PLAYER0,TOTAL_CREATURES == 0)
    LOSE_GAME
ENDIF


