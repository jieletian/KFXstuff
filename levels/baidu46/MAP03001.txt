REM ***********************************************************************
REM   Script for Level 3001 光明的入侵2 正式版
REM ***********************************************************************
REM 地图剧情：
REM 在这个王国（地区）里，只剩下三个守护者，他们一直在相互在争斗着，
REM 近来，人类（光明/正义力量）想从平衡的世界入侵这个世界，在大家
REM 眼皮底下建了前哨（中央的熔岩岛），吸引守护者们的眼光，暗中在一角
REM 里用寺庙做掩护建造虫洞（时空隧道）入口，你是黑暗之子，直觉让
REM 你嗅到了光明的味道，从这个世界的另一个王国进来察看这个王国，发现
REM 了人类的计划，于是决定在这里建立基地并摧毁人类寺庙粉碎人类的计划，
REM 因为你刚刚从时空隧道到这个地方，力量很弱，没有钱没有资源，没有厉害的
REM 魔法，甚至招不到厉害的兵，但你是黑暗之子，有着王子的光环--有凶残、
REM 奸险、狡诈的本领，你要利用谋略在不杀同类的情况下粉碎人类的计划，
REM 中途要策反鬼魂和僧侣，策反了建造寺庙里虫洞的主持人，于是人类大怒
REM 失了理智，从寺庙或他们的心脏中挖出来报复你，中了你的计谋。
REM 最后那句台词是：,"哈哈，干得好，邪恶和狡诈的你策反了僧侣，成功地
REM 激怒了英雄，让那些蠢货失去理智，将从寺庙里出来复仇！"， 
REM 在DK中，凶残、邪恶、狡诈是对王者的赞美之词。
REM ***********************************************************************
REM 地图的大概情况与参考建议：
REM 1、把英雄的心脏换到了打不到的地方，胜利不需要摧毁敌人心脏 ，
REM     不要随意摧毁敌人心脏，否则容易失败。
REM 2、增加了英雄的寺庙，在特定条件下英雄会从心脏和寺庙杀出来 。
REM 3、限制了其它守护者建造工厂的数量。
REM 4、守护者的心脏也换到了不容易打到的位置。
REM 5、把黄色守护者与其它两守护者之间的墙用岩石封死了，把显示地图
REM     的宝物移动了黄色心脏附近防魔法作弊。
REM 第二版增加的难度：
REM 6、把东西两边守护者的地盘都填充了房子。
REM 7、禁止了建造寺庙和哨所（不能通过建哨所和寺庙来阻挡镰刀）。
正式版的修改内容 ：
REM 1、把兵营改小了；
REM 2、把塌方术、闪电术留在黄色家里，其它家没有；
REM 3、红色小鬼限制只能制造30只，黄色和绿色发巢穴；
REM 4、人类还有僧侣的时候不会刷镰刀，如果玩家先杀了圣者和骑士，僧侣一死就会刷两倍镰刀。
REM ***********************************************************************
SET_GENERATE_SPEED(500)
MAX_CREATURES(PLAYER0,20)
MAX_CREATURES(PLAYER1,25)
MAX_CREATURES(PLAYER2,25)
MAX_CREATURES(PLAYER3,25)
START_MONEY(PLAYER0,20000)
START_MONEY(PLAYER1,30000)
START_MONEY(PLAYER2,30000)
START_MONEY(PLAYER3,30000)

COMPUTER_PLAYER(PLAYER1,0)
COMPUTER_PLAYER(PLAYER2,0)
COMPUTER_PLAYER(PLAYER3,0)

REM *******************************添加怪物到入口 *********************************
REM   (添加到入口的生物名字,数量)
ADD_CREATURE_TO_POOL(SORCEROR,60)
ADD_CREATURE_TO_POOL(BILE_DEMON,50)
ADD_CREATURE_TO_POOL(DARK_MISTRESS,50)
ADD_CREATURE_TO_POOL(TROLL,55)
ADD_CREATURE_TO_POOL(ORC,55)
ADD_CREATURE_TO_POOL(DRAGON,50)
ADD_CREATURE_TO_POOL(DEMONSPAWN,30)
ADD_CREATURE_TO_POOL(HELL_HOUND,40)
ADD_CREATURE_TO_POOL(SPIDER,20)
ADD_CREATURE_TO_POOL(FLY,20)

REM *******************************指定阵营（玩家）允许/禁止从入口（怪物池）中招指定生物*********************************
REM  (玩家,生物名称,1（只能是1）,[1允许招募/0禁止招募])
CREATURE_AVAILABLE(ALL_PLAYERS,FLY,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,SORCEROR,1,1)
CREATURE_AVAILABLE(PLAYER0,SPIDER,1,1)
CREATURE_AVAILABLE(PLAYER0,TROLL,1,1)
CREATURE_AVAILABLE(PLAYER0,DEMONSPAWN,1,1)
CREATURE_AVAILABLE(PLAYER0,ORC,1,1)
CREATURE_AVAILABLE(PLAYER1,DARK_MISTRESS,1,1)
CREATURE_AVAILABLE(PLAYER1,ORC,1,1)
CREATURE_AVAILABLE(PLAYER2,DRAGON,1,1)
CREATURE_AVAILABLE(PLAYER2,TROLL,1,1)
CREATURE_AVAILABLE(PLAYER3,BILE_DEMON,1,1)
CREATURE_AVAILABLE(PLAYER3,HELL_HOUND,1,1)

REM ********************指定阵营（玩家）允许/禁止研究和建造指定房子（建筑）**********************************
REM  (玩家,房子名称,是否可研究或建造[1是允许；0是禁止],是否已经可建造[1已经可建/0待研究])
ROOM_AVAILABLE(ALL_PLAYERS,BARRACKS,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,WORKSHOP,1,0)
ROOM_AVAILABLE(PLAYER1,TREASURE,1,0)
ROOM_AVAILABLE(PLAYER2,TREASURE,1,0)
ROOM_AVAILABLE(PLAYER3,TREASURE,1,0)
ROOM_AVAILABLE(PLAYER1,LAIR,1,1)
ROOM_AVAILABLE(PLAYER2,LAIR,1,1)
ROOM_AVAILABLE(PLAYER3,LAIR,1,1)
ROOM_AVAILABLE(PLAYER1,GARDEN,1,1)
ROOM_AVAILABLE(PLAYER2,GARDEN,1,1)
ROOM_AVAILABLE(PLAYER3,GARDEN,1,1)
ROOM_AVAILABLE(PLAYER1,TRAINING,1,1)
ROOM_AVAILABLE(PLAYER2,TRAINING,1,1)
ROOM_AVAILABLE(PLAYER3,TRAINING,1,1)
ROOM_AVAILABLE(PLAYER1,RESEARCH,1,1)
ROOM_AVAILABLE(PLAYER2,RESEARCH,1,1)
ROOM_AVAILABLE(PLAYER3,RESEARCH,1,1)
ROOM_AVAILABLE(PLAYER1,TEMPLE,1,0)
ROOM_AVAILABLE(PLAYER2,TEMPLE,1,0)
ROOM_AVAILABLE(PLAYER3,TEMPLE,1,0)
ROOM_AVAILABLE(PLAYER1,GRAVEYARD,1,0)
ROOM_AVAILABLE(PLAYER2,GRAVEYARD,1,0)
ROOM_AVAILABLE(PLAYER3,GRAVEYARD,1,0)
ROOM_AVAILABLE(PLAYER1,PRISON,1,0)
ROOM_AVAILABLE(PLAYER2,PRISON,1,0)
ROOM_AVAILABLE(PLAYER3,PRISON,1,0)
ROOM_AVAILABLE(PLAYER1,TORTURE,1,0)
ROOM_AVAILABLE(PLAYER2,TORTURE,1,0)
ROOM_AVAILABLE(PLAYER3,TORTURE,1,0)
ROOM_AVAILABLE(PLAYER2,SCAVENGER,1,0)
ROOM_AVAILABLE(PLAYER3,SCAVENGER,1,0)

REM ********************指定阵营（玩家）允许/禁止研究和使用指定魔法**********************************
REM  (玩家,魔法名称,是否可研究或使用[1是允许；0是禁止],是否已经可使用[1已经可建/0待研究])
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HAND,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_IMP,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SLAP,1,1)
MAGIC_AVAILABLE(PLAYER1,POWER_SIGHT,1,0)
MAGIC_AVAILABLE(PLAYER2,POWER_SIGHT,1,0)
MAGIC_AVAILABLE(PLAYER3,POWER_SIGHT,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CALL_TO_ARMS,1,0)
MAGIC_AVAILABLE(PLAYER1,POWER_HEAL_CREATURE,1,0)
MAGIC_AVAILABLE(PLAYER2,POWER_HEAL_CREATURE,1,0)
MAGIC_AVAILABLE(PLAYER3,POWER_HEAL_CREATURE,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SPEED,1,0)
REM MAGIC_AVAILABLE(PLAYER1,POWER_LIGHTNING,1,0)
REM MAGIC_AVAILABLE(PLAYER2,POWER_LIGHTNING,1,0)
MAGIC_AVAILABLE(PLAYER3,POWER_LIGHTNING,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_PROTECT,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CONCEAL,1,0)
MAGIC_AVAILABLE(PLAYER1,POWER_DISEASE,1,0)
MAGIC_AVAILABLE(PLAYER2,POWER_DISEASE,1,0)
MAGIC_AVAILABLE(PLAYER3,POWER_DISEASE,1,0)
REM MAGIC_AVAILABLE(PLAYER1,POWER_CAVE_IN,1,0)
REM MAGIC_AVAILABLE(PLAYER2,POWER_CAVE_IN,1,0)
MAGIC_AVAILABLE(PLAYER3,POWER_CAVE_IN,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CHICKEN,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HOLD_AUDIENCE,1,0)

REM *********************指定阵营（玩家）允许/禁止制造指定的门*********************************
REM  (玩家,门的名称,[1是允许；0是禁止],1[0是允许；数字是已经制造出来/赠送的数量]
DOOR_AVAILABLE(ALL_PLAYERS,WOOD,1,0)
DOOR_AVAILABLE(ALL_PLAYERS,BRACED,1,0)
DOOR_AVAILABLE(ALL_PLAYERS,STEEL,1,0)
DOOR_AVAILABLE(PLAYER1,MAGIC,1,0)
DOOR_AVAILABLE(PLAYER2,MAGIC,1,0)
DOOR_AVAILABLE(PLAYER3,MAGIC,1,0)

REM *********************指定阵营（玩家）允许/禁止制造指定陷阱*********************************
REM  (玩家,陷阱名称,[1是允许；0是禁止],1[0是允许；数字是已经制造出来/赠送的数量]
TRAP_AVAILABLE(ALL_PLAYERS,POISON_GAS,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,ALARM,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,LIGHTNING,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,WORD_OF_POWER,1,0)

REM CREATE_PARTY =>组建队伍                (队伍名称)   
REM ADD_TO_PARTY=>添加生物到队伍中   (队伍名称,生物名称,生物等级,携带金钱数量,任务,延时)
CREATE_PARTY(PARTY1)
    ADD_TO_PARTY(PARTY1,HORNY,10,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(PARTY1,HORNY,10,0,ATTACK_DUNGEON_HEART,0)
CREATE_PARTY(PARTY2)
    ADD_TO_PARTY(PARTY2,HORNY,10,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(PARTY2,HORNY,10,0,ATTACK_DUNGEON_HEART,0)
CREATE_PARTY(PARTY3)
    ADD_TO_PARTY(PARTY3,HORNY,10,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(PARTY3,HORNY,10,0,ATTACK_DUNGEON_HEART,0)
CREATE_PARTY(PARTY5)
    ADD_TO_PARTY(PARTY5,HORNY,10,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(PARTY5,HORNY,10,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(PARTY5,HORNY,10,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(PARTY5,HORNY,10,0,ATTACK_DUNGEON_HEART,0)

REM ****************************脚本主体(主要内容、核心内容）**********************************
REM 小鬼数量达30个以后，制造小鬼的魔法失效；小鬼小于30个以后，制造小鬼的魔法有效
IF(PLAYER0,IMP >= 30)
    NEXT_COMMAND_REUSABLE
    MAGIC_AVAILABLE(PLAYER0,POWER_IMP,0,0)
ENDIF
IF(PLAYER0,IMP < 30)
    NEXT_COMMAND_REUSABLE
    MAGIC_AVAILABLE(PLAYER0,POWER_IMP,1,1)
ENDIF

SET_FLAG(PLAYER0,FLAG1,0)
IF(PLAYER1,DUNGEON_DESTROYED == 0)
    IF(PLAYER2,DUNGEON_DESTROYED == 0)
        IF(PLAYER0,PRISON > 36)
            ROOM_AVAILABLE(PLAYER0,PRISON,1,0)
            ROOM_AVAILABLE(PLAYER0,TREASURE,1,0)
            ROOM_AVAILABLE(PLAYER0,LAIR,1,1)
        ENDIF
        IF(PLAYER0,TORTURE > 36)
            ROOM_AVAILABLE(PLAYER0,TORTURE,1,0)
            ROOM_AVAILABLE(PLAYER0,GARDEN,1,1)
            ROOM_AVAILABLE(PLAYER0,TRAINING,1,1)
        ENDIF
        IF(PLAYER2,TOTAL_CREATURES <=5)
            IF(PLAYER0,SCAVENGER > 8)
                ROOM_AVAILABLE(PLAYER0,SCAVENGER,1,0)
                ROOM_AVAILABLE(PLAYER0,RESEARCH,1,1)
                MAGIC_AVAILABLE(PLAYER0,POWER_LIGHTNING,1,1)
            ENDIF
        ENDIF
    ENDIF
ENDIF
IF(PLAYER0,DARK_MISTRESS > 0)
    QUICK_INFORMATION(8,"哈哈，黑暗女王带来了她的两个重要追随者，太好了！")
    ADD_CREATURE_TO_LEVEL(PLAYER0,MONK,PLAYER0,1,10,0)
    ADD_CREATURE_TO_LEVEL(PLAYER0,GHOST,PLAYER0,1,10,0)
ENDIF

IF(PLAYER1,DUNGEON_DESTROYED == 1)
    IF(PLAYER2,DUNGEON_DESTROYED == 1)
        IF(PLAYER0,PRISON < 1)
            SET_FLAG(PLAYER0,FLAG1,1)
        ENDIF
        IF(PLAYER0,TORTURE < 9)
            SET_FLAG(PLAYER0,FLAG1,1)
        ENDIF
        IF(PLAYER0,SCAVENGER < 9)
            SET_FLAG(PLAYER0,FLAG1,1)
        ENDIF
    ENDIF
ENDIF
IF(PLAYER_GOOD,MONK == 2)
    IF(PLAYER0,MONK == 0)
        IF(PLAYER1,DUNGEON_DESTROYED == 1)
            SET_FLAG(PLAYER0,FLAG1,1)
        ENDIF
    ENDIF
ENDIF
IF(PLAYER0,DUNGEON_DESTROYED == 1)
    SET_FLAG(PLAYER0,FLAG1,1)
ENDIF

IF(PLAYER1,WORKSHOP >= 40)
    NEXT_COMMAND_REUSABLE
    ROOM_AVAILABLE(PLAYER1,WORKSHOP,0,0)
ENDIF
IF(PLAYER1,WORKSHOP < 40)
    NEXT_COMMAND_REUSABLE
    ROOM_AVAILABLE(PLAYER1,WORKSHOP,1,1)
ENDIF
IF(PLAYER2,WORKSHOP >= 40)
    NEXT_COMMAND_REUSABLE
    ROOM_AVAILABLE(PLAYER2,WORKSHOP,0,0)
ENDIF
IF(PLAYER2,WORKSHOP < 40)
    NEXT_COMMAND_REUSABLE
    ROOM_AVAILABLE(PLAYER2,WORKSHOP,1,1)
ENDIF
IF(PLAYER3,WORKSHOP >= 40)
    NEXT_COMMAND_REUSABLE
    ROOM_AVAILABLE(PLAYER3,WORKSHOP,0,0)
ENDIF
IF(PLAYER3,WORKSHOP < 40)
    NEXT_COMMAND_REUSABLE
    ROOM_AVAILABLE(PLAYER3,WORKSHOP,1,1)
ENDIF

IF(PLAYER_GOOD,GHOST == 0)
    QUICK_OBJECTIVE(6,"通过合并两部分描述桥梁建设的手稿。现在，是时候顺从血液里的指引，告诉你的敌人，你对他们控制你的同族，入侵这个世界你有多愤怒，尽情地发泄吧！",PLAYER0)
    ROOM_AVAILABLE(PLAYER0,BRIDGE,1,1)
ENDIF

IF(PLAYER_GOOD,MONK<1)
    QUICK_OBJECTIVE(7,"哈哈，干得好，邪恶、狡诈的你策反了僧侣，成功地激怒了英雄，让那些蠢货失去理智，将从寺庙里冲出来复仇！",PLAYER0)
    ADD_TUNNELLER_TO_LEVEL(PLAYER_GOOD,16,ACTION_POINT,11,10,400)
    ADD_TUNNELLER_TO_LEVEL(PLAYER_GOOD,17,ACTION_POINT,15,10,400)
    MAGIC_AVAILABLE(PLAYER0,POWER_DESTROY_WALLS,1,1)
    SET_TIMER(PLAYER_GOOD,TIMER1)
    IF(PLAYER_GOOD,TIMER1 >=500)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY1,16,1)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY2,16,1)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY3,16,1)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY5,16,1)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY1,17,1)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY2,17,1)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY3,17,1)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY5,17,1)
    ENDIF
    IF(PLAYER_GOOD,AVATAR == 0)
        IF(PLAYER_GOOD,KNIGHT == 0)
            QUICK_INFORMATION(5,"小心，守护者！被光明力量操控了的你的同族恶魔从实验室里释放出来了，他们不可能被挽救了，只能杀光那群脑袋混乱的收割者吧，太可恶！",PLAYER0)
            ADD_TUNNELLER_TO_LEVEL(PLAYER_GOOD,11,ACTION_POINT,15,10,400)
            ADD_TUNNELLER_TO_LEVEL(PLAYER_GOOD,15,ACTION_POINT,11,10,400)
            REM  挖向黄色守护者，1红色，2，蓝色，3绿色，4，黄色，0，最高分配合语句才能用。
            REM  ADD_TUNNELLER_TO_LEVEL(PLAYER_GOOD,11,DUNGEON_HEART,4,10,400)
            ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY3,1,1)
            ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY2,2,1)
            ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY1,3,1)
            ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY5,4,1)
            ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY5,5,1)
            ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY5,6,1)
            ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY5,8,1)
            ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY5,9,1)
            ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY5,10,1)
            ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY5,11,1)
            ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY5,12,1)
            ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY5,13,1)
            ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY5,14,1)
        ENDIF
    ENDIF
ENDIF

REM ****************************胜利/失败条件**********************************
IF(PLAYER_GOOD,HORNY == 0)
    IF(PLAYER_GOOD,MONK == 0)
        IF(PLAYER_GOOD,TEMPLE < 1)
            QUICK_OBJECTIVE(8,"当光明的力量希望利用你们黑暗同族相残入侵这个世界的时候，你成功地完成了不寻常的行动，挫败了平衡世界另一端的阴谋，站在尸体的山顶上，看着同族欢庆胜利，只有你知道这仅仅是个开始，我还是要说，恭喜你，你血液里的黑暗力量连接使命被唤醒了，黑暗之王。",PLAYER0)
            REM 以下修改游戏，增加胜利后可以建造其它房子，让你回味一下胜利的喜悦！
            LEVEL_VERSION(1)
            REVEAL_MAP_RECT(PLAYER0,127,127,254,254)
            ADD_GOLD_TO_PLAYER (PLAYER0,9999999)
            ROOM_AVAILABLE(PLAYER0,GRAVEYARD,1,1)
            ROOM_AVAILABLE(PLAYER0,GUARD_POST,1,1)
            REM ROOM_AVAILABLE(PLAYER0,TEMPLE,1,1)
            MAGIC_AVAILABLE(PLAYER0,POWER_DESTROY_WALLS,1,1)
            DOOR_AVAILABLE(PLAYER0,MAGIC,1,200)
            QUICK_INFORMATION(15,"送钱亮图，让你回味一下胜利的喜悦！",PLAYER0)
            WIN_GAME
        ENDIF
    ENDIF
ENDIF
IF(PLAYER0,FLAG1 == 1)
    LOSE_GAME
ENDIF

REM ****************************提示信息**********************************
QUICK_OBJECTIVE(1,"正在守护者们混战之际，作为黑暗之子的你凭着直觉就能感受到一股透着正气的力量在入侵这个世界。经过一段时间的暗中侦察，你确定了英雄正在寺庙里修建一个传送门，你决定破坏那个传送门，阻止英雄从其它平行的世界里传送援兵过来，从虫洞中穿越过来耗费了大部分力量，你现在还很虚弱，在这废墟中只有少量资源可利用，对狡诈的你这点小困难阻挡不了你的步伐，脑子里很快产生一个策略--策反守护寺庙的僧侣你轻松破坏传送门，另外，修建桥梁的图纸被封印在两个躲在洞穴里的阴魂的灵魂中，记得你的族人也流淌着黑暗之血，利用好他们对你计划的实施极有帮助。",PLAYER0)
IF(PLAYER0,GAME_TURN >= 1500)
    QUICK_INFORMATION(2,"自以为聪明的人类在这个王国了设立了明哨和暗哨，他们在这个王国中央建了个明哨作为幌子并派了圣者和骑士驻守麻痹大家，暗地里在寺庙建造传送门，这种小伎俩在凶残狡诈的你面前就是班门弄斧，黑暗之子流淌的血液告诉你必须摧毁它。但刚刚穿越虫洞你还很虚弱，只能利用这废墟的少量资源，从前两天的侦察中你发现了东南部有群矮子在山里好像挖掘什么，必须尽早过去看看。",PLAYER0)
ENDIF
IF(PLAYER0,GAME_TURN >= 7500)
    QUICK_INFORMATION(3,"你必须智取，利用所有可以利用的事物，经过你和刚刚建立起来的情报网暗中侦察发现，那三个脑残的土著守护者还没有发觉不对劲还在相互征伐中，他们占据了丰富的资源；而人类负责建造虫洞的是僧侣，另外，他们把建造桥梁的图纸封印在两个叛变的阴魂的灵魂中。",PLAYER0)
ENDIF
IF(PLAYER0,GAME_TURN >= 25000)
    QUICK_INFORMATION(4,"时刻记住，你是邪恶之子，身上流躺着最纯碎最强大黑暗力量的血液，请顺从你的直觉并让它指引你，记住这群土著守护者虽然是你成功路上的障碍，他们是也你的同类而不是你的仇敌。先放下对同族的仇恨，把渗透到这黑暗世界的光明力量吞噬干净，最终所有邪恶、混乱的黑暗同族最终会膜拜在你的脚下的，为你的凶残、邪恶、狡诈山呼，那就是你从黑暗之子成就黑暗之王的时刻！",PLAYER0)
ENDIF















