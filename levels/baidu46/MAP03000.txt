REM ***********************************************************************
REM   Script for Level 3000 光明的入侵（改自Lactobacillus）
REM ***********************************************************************
REM 地图的大概情况与参考建议：
REM 1、把英雄的心脏换到了打不到的地方，胜利不需要摧毁敌人心脏 ，
REM     不要随意摧毁敌人心脏，否则容易失败。
REM 2、增加了英雄的寺庙，在特定条件下英雄会从心脏和寺庙杀出来 。
REM 3、限制了其它守护者建行工厂的数量，地图上增加了几只红方小鬼。
REM 4、守护者的心脏也换到了不容易打到的位置。
REM 5、把黄色守护者与其它两守护者之间的墙用岩石封死了，把显示地图
REM     的宝物移动了黄色心脏附近防魔法作弊。
REM ***********************************************************************
SET_GENERATE_SPEED(500)
MAX_CREATURES(PLAYER0,20)
MAX_CREATURES(PLAYER1,25)
MAX_CREATURES(PLAYER2,25)
MAX_CREATURES(PLAYER3,25)
START_MONEY(PLAYER0,50000)
START_MONEY(PLAYER1,100000)
START_MONEY(PLAYER2,100000)
START_MONEY(PLAYER3,100000)

COMPUTER_PLAYER(PLAYER1,0)
COMPUTER_PLAYER(PLAYER2,0)
COMPUTER_PLAYER(PLAYER3,0)

REM *******************************添加怪物到入口 *********************************
REM   (添加到入口的生物名字,数量)
ADD_CREATURE_TO_POOL(SORCEROR,60)
ADD_CREATURE_TO_POOL(BILE_DEMON,50)
ADD_CREATURE_TO_POOL(DARK_MISTRESS,50)
ADD_CREATURE_TO_POOL(TROLL,55)
ADD_CREATURE_TO_POOL(ORC,55)
ADD_CREATURE_TO_POOL(DRAGON,50)
ADD_CREATURE_TO_POOL(DEMONSPAWN,30)
ADD_CREATURE_TO_POOL(HELL_HOUND,40)
ADD_CREATURE_TO_POOL(SPIDER,20)
ADD_CREATURE_TO_POOL(FLY,20)

REM *******************************指定阵营（玩家）允许/禁止从入口（怪物池）中招指定生物*********************************
REM  (玩家,生物名称,1（只能是1）,[1允许招募/0禁止招募])
CREATURE_AVAILABLE(PLAYER0,TROLL,1,1)
CREATURE_AVAILABLE(PLAYER2,TROLL,1,1)
CREATURE_AVAILABLE(PLAYER1,DARK_MISTRESS,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,SORCEROR,1,1)
CREATURE_AVAILABLE(PLAYER3,BILE_DEMON,1,1)
CREATURE_AVAILABLE(PLAYER3,HELL_HOUND,1,1)
CREATURE_AVAILABLE(PLAYER2,DRAGON,1,1)
CREATURE_AVAILABLE(PLAYER0,ORC,1,1)
CREATURE_AVAILABLE(PLAYER1,ORC,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,FLY,1,1)
CREATURE_AVAILABLE(PLAYER0,SPIDER,1,1)
CREATURE_AVAILABLE(PLAYER0,DEMONSPAWN,1,1)

REM ********************指定阵营（玩家）允许/禁止研究和建造指定房子（建筑）**********************************
REM  (玩家,房子名称,是否可研究或建造[1是允许；0是禁止],是否已经可建造[1已经可建/0待研究])
ROOM_AVAILABLE(ALL_PLAYERS,TREASURE,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,LAIR,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,GARDEN,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,TRAINING,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,RESEARCH,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,BARRACKS,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,GUARD_POST,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,TEMPLE,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,WORKSHOP,1,0)
ROOM_AVAILABLE(PLAYER1,GRAVEYARD,1,0)
ROOM_AVAILABLE(PLAYER2,GRAVEYARD,1,0)
ROOM_AVAILABLE(PLAYER3,GRAVEYARD,1,0)
ROOM_AVAILABLE(PLAYER1,PRISON,1,0)
ROOM_AVAILABLE(PLAYER2,PRISON,1,0)
ROOM_AVAILABLE(PLAYER3,PRISON,1,0)
ROOM_AVAILABLE(PLAYER1,TORTURE,1,0)
ROOM_AVAILABLE(PLAYER2,TORTURE,1,0)
ROOM_AVAILABLE(PLAYER3,TORTURE,1,0)
ROOM_AVAILABLE(PLAYER2,SCAVENGER,1,0)
ROOM_AVAILABLE(PLAYER3,SCAVENGER,1,0)

REM ********************指定阵营（玩家）允许/禁止研究和使用指定魔法**********************************
REM  (玩家,魔法名称,是否可研究或使用[1是允许；0是禁止],是否已经可使用[1已经可建/0待研究])
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HAND,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_IMP,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SLAP,1,1)
MAGIC_AVAILABLE(PLAYER1,POWER_SIGHT,1,0)
MAGIC_AVAILABLE(PLAYER2,POWER_SIGHT,1,0)
MAGIC_AVAILABLE(PLAYER3,POWER_SIGHT,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CALL_TO_ARMS,1,0)
MAGIC_AVAILABLE(PLAYER1,POWER_HEAL_CREATURE,1,0)
MAGIC_AVAILABLE(PLAYER2,POWER_HEAL_CREATURE,1,0)
MAGIC_AVAILABLE(PLAYER3,POWER_HEAL_CREATURE,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SPEED,1,0)
MAGIC_AVAILABLE(PLAYER1,POWER_LIGHTNING,1,0)
MAGIC_AVAILABLE(PLAYER2,POWER_LIGHTNING,1,0)
MAGIC_AVAILABLE(PLAYER3,POWER_LIGHTNING,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_PROTECT,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CONCEAL,1,0)
MAGIC_AVAILABLE(PLAYER1,POWER_DISEASE,1,0)
MAGIC_AVAILABLE(PLAYER2,POWER_DISEASE,1,0)
MAGIC_AVAILABLE(PLAYER3,POWER_DISEASE,1,0)
MAGIC_AVAILABLE(PLAYER1,POWER_CAVE_IN,1,0)
MAGIC_AVAILABLE(PLAYER2,POWER_CAVE_IN,1,0)
MAGIC_AVAILABLE(PLAYER3,POWER_CAVE_IN,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CHICKEN,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HOLD_AUDIENCE,1,0)

REM *********************指定阵营（玩家）允许/禁止制造指定的门*********************************
REM  (玩家,门的名称,[1是允许；0是禁止],1[0是允许；数字是已经制造出来/赠送的数量]
DOOR_AVAILABLE(ALL_PLAYERS,WOOD,1,0)
DOOR_AVAILABLE(ALL_PLAYERS,BRACED,1,0)
DOOR_AVAILABLE(ALL_PLAYERS,STEEL,1,0)
DOOR_AVAILABLE(PLAYER1,MAGIC,1,0)
DOOR_AVAILABLE(PLAYER2,MAGIC,1,0)
DOOR_AVAILABLE(PLAYER3,MAGIC,1,0)

REM *********************指定阵营（玩家）允许/禁止制造指定陷阱*********************************
REM  (玩家,陷阱名称,[1是允许；0是禁止],1[0是允许；数字是已经制造出来/赠送的数量]
TRAP_AVAILABLE(ALL_PLAYERS,POISON_GAS,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,ALARM,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,LIGHTNING,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,WORD_OF_POWER,1,0)

REM CREATE_PARTY =>组建队伍                (队伍名称)   
REM ADD_TO_PARTY=>添加生物到队伍中   (队伍名称,生物名称,生物等级,携带金钱数量,任务,延时)
CREATE_PARTY(PARTY1)
    ADD_TO_PARTY(PARTY1,HORNY,6,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(PARTY1,HORNY,7,0,ATTACK_DUNGEON_HEART,0)
CREATE_PARTY(PARTY2)
    ADD_TO_PARTY(PARTY2,HORNY,8,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(PARTY2,HORNY,9,0,ATTACK_DUNGEON_HEART,0)
CREATE_PARTY(PARTY3)
    ADD_TO_PARTY(PARTY3,HORNY,10,0,ATTACK_DUNGEON_HEART,0)
CREATE_PARTY(PARTY5)
    ADD_TO_PARTY(PARTY5,HORNY,10,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(PARTY5,HORNY,10,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(PARTY5,HORNY,10,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(PARTY5,HORNY,10,0,ATTACK_DUNGEON_HEART,0)


REM ****************************脚本主体(主要内容、核心内容）**********************************
SET_FLAG(PLAYER0,FLAG1,0)

IF(PLAYER1,DUNGEON_DESTROYED == 0)
    IF(PLAYER2,DUNGEON_DESTROYED == 0)
        IF(PLAYER0,PRISON > 36)
            ROOM_AVAILABLE(PLAYER0,PRISON,1,0)
        ENDIF
        IF(PLAYER0,TORTURE > 36)
            ROOM_AVAILABLE(PLAYER0,TORTURE,1,0)
        ENDIF
        IF(PLAYER2,TOTAL_CREATURES <=5)
            IF(PLAYER0,SCAVENGER > 8)
                ROOM_AVAILABLE(PLAYER0,SCAVENGER,1,0)
            ENDIF
        ENDIF
    ENDIF
ENDIF
IF(PLAYER0,DARK_MISTRESS > 0)
    QUICK_INFORMATION(8,"哈哈，黑暗女王带来了她的两个重要追随者，太好了！")
    ADD_CREATURE_TO_LEVEL(PLAYER0,MONK,PLAYER0,1,10,0)
    ADD_CREATURE_TO_LEVEL(PLAYER0,GHOST,PLAYER0,1,10,0)
ENDIF


IF(PLAYER1,DUNGEON_DESTROYED == 1)
    IF(PLAYER2,DUNGEON_DESTROYED == 1)
        IF(PLAYER0,PRISON < 1)
            SET_FLAG(PLAYER0,FLAG1,1)
        ENDIF
        IF(PLAYER0,TORTURE < 9)
            SET_FLAG(PLAYER0,FLAG1,1)
        ENDIF
        IF(PLAYER0,SCAVENGER < 9)
            SET_FLAG(PLAYER0,FLAG1,1)
        ENDIF
    ENDIF
ENDIF
IF(PLAYER_GOOD,MONK == 2)
    IF(PLAYER0,MONK == 0)
        IF(PLAYER1,DUNGEON_DESTROYED == 1)
            SET_FLAG(PLAYER0,FLAG1,1)
        ENDIF
    ENDIF
ENDIF
IF(PLAYER0,DUNGEON_DESTROYED == 1)
    SET_FLAG(PLAYER0,FLAG1,1)
ENDIF

IF(PLAYER1,WORKSHOP >= 40)
    NEXT_COMMAND_REUSABLE
    ROOM_AVAILABLE(PLAYER1,WORKSHOP,0,0)
ENDIF
IF(PLAYER1,WORKSHOP < 40)
    NEXT_COMMAND_REUSABLE
    ROOM_AVAILABLE(PLAYER1,WORKSHOP,1,1)
ENDIF
IF(PLAYER2,WORKSHOP >= 40)
    NEXT_COMMAND_REUSABLE
    ROOM_AVAILABLE(PLAYER2,WORKSHOP,0,0)
ENDIF
IF(PLAYER2,WORKSHOP < 40)
    NEXT_COMMAND_REUSABLE
    ROOM_AVAILABLE(PLAYER2,WORKSHOP,1,1)
ENDIF
IF(PLAYER3,WORKSHOP >= 40)
    NEXT_COMMAND_REUSABLE
    ROOM_AVAILABLE(PLAYER3,WORKSHOP,0,0)
ENDIF
IF(PLAYER3,WORKSHOP < 40)
    NEXT_COMMAND_REUSABLE
    ROOM_AVAILABLE(PLAYER3,WORKSHOP,1,1)
ENDIF

IF(PLAYER_GOOD,AVATAR == 0)
    IF(PLAYER_GOOD,KNIGHT == 0)
        QUICK_INFORMATION(5,"小心守护者。被光明力量操控了的恶魔从实验室里释放出来了，他们不可能被挽救了，杀光那些叛变了脑袋混乱的收割者吧。")
        ADD_TUNNELLER_TO_LEVEL(PLAYER_GOOD,11,ACTION_POINT,15,10,400)
        ADD_TUNNELLER_TO_LEVEL(PLAYER_GOOD,15,ACTION_POINT,11,10,400)
        REM  挖向黄色守护者，1红色，2，蓝色，3绿色，4，黄色，0，最高分配合语句才能用。
        REM  ADD_TUNNELLER_TO_LEVEL(PLAYER_GOOD,11,DUNGEON_HEART,4,10,400)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY3,1,1)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY2,2,1)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY1,3,1)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY5,4,1)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY5,5,1)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY5,6,1)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY5,8,1)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY5,9,1)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY5,10,1)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY5,11,1)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY5,12,1)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY5,13,1)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY5,14,1)
    ENDIF
ENDIF

IF(PLAYER_GOOD,GHOST == 0)
    QUICK_OBJECTIVE(6,"通过合并两部分描述桥梁建设的手稿。现在，是时候向你的竞争对手提出宣示你的力量，动手吧！",PLAYER0)
    ROOM_AVAILABLE(PLAYER0,BRIDGE,1,1)
ENDIF


IF(PLAYER_GOOD,MONK<1)
    QUICK_OBJECTIVE(7,"哈哈，干得好，邪恶和狡诈的你策反了僧侣，成功地激怒了英雄，让那些蠢货失去理智，将从寺庙里出来复仇！",PLAYER0)
    ADD_TUNNELLER_TO_LEVEL(PLAYER_GOOD,16,ACTION_POINT,11,10,400)
    ADD_TUNNELLER_TO_LEVEL(PLAYER_GOOD,17,ACTION_POINT,15,10,400)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY1,16,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY2,16,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY3,16,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY5,16,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY1,17,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY2,17,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY3,17,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY5,17,1)
ENDIF

REM ****************************胜利/失败条件**********************************
IF(PLAYER_GOOD,HORNY == 0)
    IF(PLAYER_GOOD,MONK == 0)
        IF(PLAYER_GOOD,TEMPLE < 1)
            QUICK_OBJECTIVE(7,"当光明的力量希望你们同族相残的时候，你成功地完成了不寻常的行动，挫败了平衡世界另一端的阴谋，站在尸体的山顶上，看着同族欢庆胜利，只有你知道这仅仅是个开始，我还是要说，恭喜你，你血液里的黑暗力量连接使命被唤醒了。",PLAYER0)
            REM 以下修改游戏，增加胜利后可以建造其它房子，让你回味一下胜利的喜悦！
            LEVEL_VERSION(1)
            REVEAL_MAP_RECT(PLAYER0,127,127,254,254)
            ADD_GOLD_TO_PLAYER (PLAYER0,9999999)
            ROOM_AVAILABLE(PLAYER0,GRAVEYARD,1,1)
            MAGIC_AVAILABLE(PLAYER0,POWER_DESTROY_WALLS,1,1)
            DOOR_AVAILABLE(PLAYER0,MAGIC,1,200)
            QUICK_INFORMATION(15,"送钱亮图，让你回味一下胜利的喜悦！",PLAYER0)
            WIN_GAME
        ENDIF
    ENDIF
ENDIF
IF(PLAYER0,FLAG1 == 1)
    LOSE_GAME
ENDIF

REM ****************************提示信息**********************************
QUICK_OBJECTIVE(1,"正在守护者们混战之际，邪恶的你凭着直觉就能感受到一股透着正气的力量在接近。英雄正在寺庙里修建一个传送门，必须破坏那个传送门才能阻止英雄从其它平行的世界里传送援兵过来，策反守护寺庙的僧侣你更容易破坏传送门，修建桥梁的图纸被封印在两个躲在洞穴里的阴魂的灵魂中。",PLAYER0)
IF(PLAYER0,GAME_TURN >= 1500)
    QUICK_INFORMATION(2,"光明的人类在这个王国了设置了明哨和暗哨，圣者和骑士所在的明哨个幌子，他们暗地里在建造传送门，摧毁它。但首先，你需要保护任何财政支持，掠夺东南部的矮人宝藏室是个好主意。")
ENDIF
IF(PLAYER0,GAME_TURN >= 7500)
    QUICK_INFORMATION(3,"竞争者都在富矿区，而且他们都很强大的军队，如果你必须智取，利用所有可以利用的事物，记住他们虽然是你成功路上的障碍是你的同类而不是你的仇敌。")
ENDIF
IF(PLAYER0,GAME_TURN >= 25000)
    QUICK_INFORMATION(4,"时刻记住，你是邪恶之子，身上流躺着强大黑暗力量的血液，请相信你的直觉并让它指引你，先放下同族的仇恨，把渗透黑暗世界的光明力量吞噬干净，最终所有邪恶黑暗的同族最后会跪在你的面前的。")
ENDIF

















