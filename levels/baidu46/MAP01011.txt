REM ****************************************************************************
REM                                 欢迎使用守护者软件
REM                本地图AI由<<地下城守护者I地图AI编辑器>>编制
REM                          软件作者：游者之风，QQ：3262900
REM ****************************************************************************
REM                                1011.小偷的二胎生育（修正版）
REM                     本地图制作者：~子    QQ：3383373
REM ****************************************************************************
REM                     PLAYER1=-1,PLAYER2=-1,PLAYER3=0
REM ****************************************************************************
REM 地图的大概情况与参考建议：
REM 1、地图上没有触发点，但会刷小偷，刷小偷的脚本有误，造成不断刷小偷
REM     直到地图上限为止；小偷很难打死，会隐形骚扰你，很烦人，打死后掉
REM     下比较多钱。
REM 2、摧毁敌人心脏后会在你心脏刷超级杀手 。
REM 3、北面的两守护者不在胜利条件中，早期老骚扰你，实际上它们但对你有帮助 。
REM 4、 。
REM 5、 。
REM ****************************************************************************
REM 地图修改情况：
REM 1、修正了刷小偷的脚本，不会一下刷满地图上限，刷一次死光后再刷。
REM 2、修改了桥的位置，地形上也有少量变化，防破墙，删除了一堆龙，
REM     增加了两格紫金（降低了难度） 。
REM 3、原来没有一个触发点，现在已经按脚本补齐 。
REM 4、杀敌人人数达到一定数量有奖励，如果先不挖太多的金子通过工厂赚钱
REM     达一定数量有奖励。
REM 5、当两守护者没钱了就给他们发少量钱，当他们没小鬼了就刷3只小鬼，
REM     直到玩家杀敌数量达到一定程度 。
REM 6、增加修改了一些提示信息。
REM     
REM ****************************************************************************
REM ************初始金钱及怪物上限**************
REM LEVEL_VERSION(1)
START_MONEY(PLAYER0,9)
MAX_CREATURES(PLAYER0,18)
START_MONEY(PLAYER1,99999)
MAX_CREATURES(PLAYER1,35)
COMPUTER_PLAYER(PLAYER1,1)
START_MONEY(PLAYER2,99999)
MAX_CREATURES(PLAYER2,35)
COMPUTER_PLAYER(PLAYER2,1)

REM ****************入口设置********************
SET_GENERATE_SPEED(300)
ADD_CREATURE_TO_POOL(WIZARD,30)
ADD_CREATURE_TO_POOL(FLY,20)
ADD_CREATURE_TO_POOL(SORCEROR,30)

REM **********各方怪物、魔法、陷阱设置**********
CREATURE_AVAILABLE(ALL_PLAYERS,WIZARD,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,FLY,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,SORCEROR,1,1)

ROOM_AVAILABLE(ALL_PLAYERS,TREASURE,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,LAIR,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,GARDEN,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,TRAINING,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,RESEARCH,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,WORKSHOP,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,PRISON,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,TEMPLE,1,0)

MAGIC_AVAILABLE(ALL_PLAYERS,POWER_IMP,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SIGHT,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SPEED,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_OBEY,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CALL_TO_ARMS,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CONCEAL,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HOLD_AUDIENCE,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CAVE_IN,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HEAL_CREATURE,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_LIGHTNING,1,0)

TRAP_AVAILABLE(ALL_PLAYERS,ALARM,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,BOULDER,1,0)
DOOR_AVAILABLE(ALL_PLAYERS,WOOD,1,0)
DOOR_AVAILABLE(ALL_PLAYERS,MAGIC,1,0)

REM ****************同盟设置********************
ALLY_PLAYERS(PLAYER1,PLAYER2)

REM ****************属性设置********************
SET_CREATURE_STRENGTH(THIEF,5)
SET_CREATURE_HEALTH(THIEF,7000)
SET_CREATURE_ARMOUR(THIEF,10)
SET_CREATURE_FEAR(THIEF,255)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,THIEF,10)
SET_CREATURE_HEALTH(KNIGHT,7000)
SET_CREATURE_ARMOUR(KNIGHT,255)

REM ****************其它设置********************

REM ****************队伍编制********************
CREATE_PARTY(Z)
ADD_TO_PARTY(Z,AVATAR,10,0,ATTACK_DUNGEON_HEART,0)
ADD_TO_PARTY(Z,AVATAR,10,0,ATTACK_DUNGEON_HEART,0)
ADD_TO_PARTY(Z,KNIGHT,10,0,ATTACK_DUNGEON_HEART,0)
ADD_TO_PARTY(Z,KNIGHT,10,0,ATTACK_DUNGEON_HEART,0)
ADD_TO_PARTY(Z,SAMURAI,10,0,ATTACK_DUNGEON_HEART,0)
ADD_TO_PARTY(Z,SAMURAI,10,0,ATTACK_DUNGEON_HEART,0)
CREATE_PARTY(H)
ADD_TO_PARTY(H,DARK_MISTRESS,10,0,ATTACK_DUNGEON_HEART,0)
ADD_TO_PARTY(H,DARK_MISTRESS,10,0,ATTACK_DUNGEON_HEART,0)
ADD_TO_PARTY(H,SPIDER,10,0,ATTACK_DUNGEON_HEART,0)
ADD_TO_PARTY(H,SPIDER,10,0,ATTACK_DUNGEON_HEART,0)
ADD_TO_PARTY(H,SPIDER,10,0,ATTACK_DUNGEON_HEART,0)
ADD_TO_PARTY(H,WIZARD,10,0,ATTACK_DUNGEON_HEART,0)

REM ***************无条件事件*******************

REM ***************条件关系式*******************
QUICK_OBJECTIVE(1,"在这穷乡僻壤里，人力物力财力都很缺乏，而英雄们却异常强大，前面的路很艰难，你得一步一步走，慢慢积累不要急于求成，把所有可以利用的资源都利用起来。",PLAYER0)  
QUICK_INFORMATION(2,"地下城心脏下方有小偷，因为二胎的开放，小偷很会生，他们可不是一般的小偷，他们个个都很有钱，所以……你懂的。",PLAYER0) 

IF_ACTION_POINT(2,PLAYER0)
    QUICK_INFORMATION(3,"据当地居民说，附近有座桥，你或者派人去看看！",PLAYER0) 
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,H,2,2)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,H,3,3)
ENDIF
IF_ACTION_POINT(4,PLAYER0)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,H,5,3)
ENDIF
IF_ACTION_POINT(6,PLAYER0)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,H,6,3)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,H,8,2)
ENDIF
IF_ACTION_POINT(7,PLAYER0)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,H,7,3)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,H,8,2)
ENDIF
IF_ACTION_POINT(11,PLAYER0)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,H,8,2)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,H,9,2)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,H,10,2)
ENDIF
IF_ACTION_POINT(16,PLAYER0)
    QUICK_INFORMATION(5,"探子报来的消息：敌人想要从后面堵截我们！",PLAYER0) 
    RESET_ACTION_POINT(11)
ENDIF
IF_ACTION_POINT(21,PLAYER0)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,H,20,4)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,H,19,3)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,H,18,3)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,H,17,3)
ENDIF
IF_ACTION_POINT(22,PLAYER0)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,H,23,4)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,H,24,3)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,H,25,3)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,H,2,3)
    QUICK_OBJECTIVE(6,"探子报来的消息：我们的情报网已经获得了此地区的信息，并得知敌人已经渗透了我们内部，如果我们摧毁他们的心脏，他们同样报复我们，总攻前得注意保护后方！",PLAYER0) 
    REVEAL_MAP_RECT(PLAYER0,127,127,254,254)
ENDIF

IF_ACTION_POINT(26,PLAYER0)
    TUTORIAL_FLASH_BUTTON(4,-1)
    TRAP_AVAILABLE(PLAYER0,LAVA,1,1)
    QUICK_INFORMATION(7,"你的脚踏实地得到了回报！",PLAYER0) 
ENDIF

IF(PLAYER0,BRIDGE>=1)
    TUTORIAL_FLASH_BUTTON(2,-1)
    ROOM_AVAILABLE(ALL_PLAYERS,BRIDGE,1,1)
    ROOM_AVAILABLE(ALL_PLAYERS,GUARD_POST,1,1)
    TRAP_AVAILABLE(ALL_PLAYERS,LAVA,0,1)
    QUICK_INFORMATION(4,"此地区剩下唯一一座废弃了的桥，从苍老的桥体中看得出它蕴含着卓越超凡的技术！",PLAYER0) 
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,H,12,2)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,H,13,2)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,H,14,2)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,H,15,3)
ENDIF

SET_FLAG(PLAYER0,FLAG0,0)
IF_ACTION_POINT(1,PLAYER0)
    IF(PLAYER0,FLAG0==0)
        QUICK_INFORMATION(10,"探险是要会出代价的！",PLAYER0) 
        SET_TIMER(PLAYER0,TIMER0)
        IF(PLAYER_GOOD,THIEF==0)
            IF(PLAYER0,TIMER0>=7500)
                NEXT_COMMAND_REUSABLE
                ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,THIEF,-1,2,10,1000)
                NEXT_COMMAND_REUSABLE
                ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,THIEF,-2,2,10,2000)
                NEXT_COMMAND_REUSABLE
                ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,THIEF,-3,2,10,5000)
                NEXT_COMMAND_REUSABLE
                ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,THIEF,-4,2,10,7000)
                NEXT_COMMAND_REUSABLE
                ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,THIEF,-5,2,10,8000)
                NEXT_COMMAND_REUSABLE
                ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,THIEF,-6,2,10,9000)
                NEXT_COMMAND_REUSABLE
                SET_TIMER(PLAYER0,TIMER0)
            ENDIF
        ENDIF
    ENDIF
ENDIF

REM 给两守护者少量钱和刷小鬼
SET_FLAG(PLAYER0,FLAG1,0)
IF(PLAYER0,FLAG1==0)
    IF(PLAYER1,MONEY < 300)
        NEXT_COMMAND_REUSABLE
        ADD_GOLD_TO_PLAYER (PLAYER1,2000)
    ENDIF
    IF(PLAYER2,MONEY < 300)
        NEXT_COMMAND_REUSABLE
        ADD_GOLD_TO_PLAYER (PLAYER2,2000)
    ENDIF
    IF(PLAYER1,TOTAL_IMPS < 1)
        NEXT_COMMAND_REUSABLE
        ADD_CREATURE_TO_LEVEL(PLAYER1,IMP,PLAYER1,3,1,0)
    ENDIF
    IF(PLAYER2,TOTAL_IMPS < 1)
        NEXT_COMMAND_REUSABLE
        ADD_CREATURE_TO_LEVEL(PLAYER2,IMP,PLAYER2,3,1,0)
    ENDIF
ENDIF

IF(PLAYER0,BATTLES_WON >= 200)
    TUTORIAL_FLASH_BUTTON(2,-1)
    ROOM_AVAILABLE(PLAYER0,TORTURE,1,0)
    IF(PLAYER0,BATTLES_WON >= 250)
        MAGIC_AVAILABLE(ALL_PLAYERS,POWER_DISEASE,1,1)
        REM 停止给两守护者供给
        SET_FLAG(PLAYER0,FLAG1,1)
    ENDIF
ENDIF

IF(PLAYER0,MONEY > 130000)
    IF(PLAYER0,TOTAL_GOLD_MINED < 130000)
        TUTORIAL_FLASH_BUTTON(3,-1)
        MAGIC_AVAILABLE(PLAYER0,POWER_DESTROY_WALLS,1,1)
    ENDIF
ENDIF

IF(PLAYER_GOOD,DUNGEON_DESTROYED==1)
    QUICK_INFORMATION(9,"受shi吧，愚蠢的守护者们--英雄的咆啸",PLAYER0) 
    SET_FLAG(PLAYER0,FLAG0,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,Z,-7,3)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,H,-7,3)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,Z,PLAYER0,2)
    ADD_CREATURE_TO_LEVEL(PLAYER0,AVATAR,PLAYER0,8,10,0)
    ADD_CREATURE_TO_LEVEL(PLAYER0,WITCH,PLAYER0,2,10,0)
ENDIF
IF(PLAYER0,FLAG0==1)
    IF(PLAYER_GOOD,TOTAL_CREATURES>=0)
        WIN_GAME
    ENDIF
ENDIF












