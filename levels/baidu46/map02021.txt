REM *********************************************************
REM             Script for Level 2021.巫魔天境
REM *********************************************************             
REM             本地图制作者： 始作俑者
REM                  QQ：331672578
REM  欢迎加1128460424（地下城守护者 - Dungeon Keeper）群交流
REM *********************************************************
REM 地图的大概情况与参考建议：

REM 1、本地图每个区域采取过关形式，必须使用给定生物，过关后上次使用的生物自动死亡
REM     
REM 2、玩家需根据提示一步步走完全图
REM
REM 3、最后决战阶段有三个选择，每种过关方式和难度不同

REM 4、工厂阶段机制做了调整，同时加入一个繁殖怪物宝盒，但不能直接获取

REM 5、国王满足一定条件会变成强化过的冥界使者

REM 6、加入防传怪机制，传送怪物到此图就会失败
REM ***********************************************************************

LEVEL_VERSION(1)
SET_GENERATE_SPEED(100)
MAX_CREATURES(PLAYER0,50)
MAX_CREATURES(PLAYER3,25)
START_MONEY(PLAYER0,5000)
START_MONEY(PLAYER3,900000)
COMPUTER_PLAYER(PLAYER3,0)
SET_CREATURE_HEALTH(ARCHER,30)

REM *******************************添加怪物到入口 *********************************
REM   (添加到入口的生物名字,数量)



REM *******************************指定阵营（玩家）允许/禁止从入口（怪物池）中招指定生物*********************************
REM  (玩家,生物名称,1（只能是1）,[1允许招募/0禁止招募])



REM ********************指定阵营（玩家）允许/禁止研究和建造指定房子（建筑）**********************************
REM  (玩家,房子名称,是否可研究或建造[1是允许；0是禁止],是否已经可建造[1已经可建/0待研究])

ROOM_AVAILABLE(ALL_PLAYERS,LAIR,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,GARDEN,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,TRAINING,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,GUARD_POST,1,1)

REM ********************指定阵营（玩家）允许/禁止研究和使用指定魔法**********************************
REM  (玩家,魔法名称,是否可研究或使用[1是允许；0是禁止],是否已经可使用[1已经可建/0待研究])

MAGIC_AVAILABLE(ALL_PLAYERS,POWER_IMP,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HAND,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SLAP,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SPEED,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_PROTECT,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_DISEASE,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_OBEY,1,1)

REM *********************指定阵营（玩家）允许/禁止制造指定的门*********************************
REM  (玩家,门的名称,[1是允许；0是禁止],1[0是允许；数字是已经制造出来/赠送的数量]

DOOR_AVAILABLE(PLAYER1,MAGIC,1,0)
DOOR_AVAILABLE(PLAYER2,MAGIC,1,0)
DOOR_AVAILABLE(PLAYER3,MAGIC,1,0)

REM *********************指定阵营（玩家）允许/禁止制造指定陷阱*********************************
REM  (玩家,陷阱名称,[1是允许；0是禁止],1[0是允许；数字是已经制造出来/赠送的数量]

TRAP_AVAILABLE(ALL_PLAYERS,LIGHTNING,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,LAVA,1,0)

REM CREATE_PARTY =>组建队伍                (队伍名称)   
REM ADD_TO_PARTY=>添加生物到队伍中   (队伍名称,生物名称,生物等级,携带金钱数量,任务,延时)

CREATE_PARTY(PARTY1)
    ADD_TO_PARTY(PARTY1,BARBARIAN,3,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(PARTY1,BARBARIAN,3,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(PARTY1,DWARFA,3,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(PARTY1,DWARFA,3,0,ATTACK_DUNGEON_HEART,0)
CREATE_PARTY(PARTY2)
    ADD_TO_PARTY(PARTY2,SAMURAI,3,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(PARTY2,SAMURAI,3,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(PARTY2,BARBARIAN,3,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(PARTY2,BARBARIAN,3,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(PARTY2,DWARFA,3,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(PARTY2,DWARFA,3,0,ATTACK_DUNGEON_HEART,0)
CREATE_PARTY(PARTY3)
    ADD_TO_PARTY(PARTY3,SAMURAI,10,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(PARTY3,SAMURAI,10,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(PARTY3,SAMURAI,10,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(PARTY3,SAMURAI,10,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(PARTY3,SAMURAI,10,0,ATTACK_DUNGEON_HEART,0)
CREATE_PARTY(PARTY4)
    ADD_TO_PARTY(PARTY4,HORNY,10,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(PARTY4,HORNY,10,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(PARTY4,HORNY,10,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(PARTY4,HORNY,10,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(PARTY4,HORNY,10,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(PARTY4,HORNY,10,0,ATTACK_DUNGEON_HEART,0)
CREATE_PARTY(PARTY5)
    ADD_TO_PARTY(PARTY5,BILE_DEMON,10,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(PARTY5,BILE_DEMON,10,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(PARTY5,BILE_DEMON,10,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(PARTY5,BILE_DEMON,10,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(PARTY5,BILE_DEMON,10,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(PARTY5,BILE_DEMON,10,0,ATTACK_DUNGEON_HEART,0)
CREATE_PARTY(PARTY6)
    ADD_TO_PARTY(PARTY6,WIZARD,10,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(PARTY6,SPIDER,10,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(PARTY6,BUG,10,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(PARTY6,FAIRY,10,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(PARTY6,SAMURAI,10,0,ATTACK_DUNGEON_HEART,0)
    
REM ****************************脚本主体(主要内容、核心内容）**********************************

SET_FLAG(PLAYER0,FLAG1,0)

IF(PLAYER_GOOD,HORNY < 1)
    IF(PLAYER_GOOD,KNIGHT < 5)
        IF(PLAYER_GOOD,GIANT < 3) 
            KILL_CREATURE(PLAYER0,DARK_MISTRESS,MOST_EXPERIENCED,10)
            QUICK_INFORMATION(17,"你的表现非常出色，但很遗憾黑女不能与你随行了",PLAYER0)
        ENDIF
    ENDIF
ENDIF

IF_ACTION_POINT(1,PLAYER0)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY1,1,1)
ENDIF

IF_ACTION_POINT(3,PLAYER0)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY2,3,1)
ENDIF

IF(PLAYER0,SORCEROR > 0)
    KILL_CREATURE(PLAYER0,FLY,MOST_EXPERIENCED,5)
    QUICK_INFORMATION(2,"法师不喜欢苍蝇，将其杀死了",PLAYER0)
ENDIF

IF(PLAYER0,WORKSHOP > 60)
    SET_TIMER(PLAYER0,TIMER0)
    QUICK_OBJECTIVE(3,"你占领了新的工厂，抓紧时间制造陷阱机关，200秒后恶魔的魔法就会减弱，地狱犬就会消失！届时英雄门会出现敌军，只有依靠陷阱了！",PLAYER0)
ENDIF

IF(PLAYER0,TIMER0 >= 4000)
    KILL_CREATURE(PLAYER_GOOD,HELL_HOUND,MOST_EXPERIENCED,50)
    QUICK_INFORMATION(19,"该死的狗终于消失了",PLAYER0)
    BONUS_LEVEL_TIME(2000)
    QUICK_INFORMATION(4,"计时开始，英雄门即将出现大量敌人，必须在这片区域快速布置陷阱，时间有限！",PLAYER0)
ENDIF

IF(PLAYER0,TIMER0 >= 6000)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY3,-1,2)
ENDIF

IF(PLAYER0,DRAGON > 0)
    KILL_CREATURE(PLAYER0,TROLL,MOST_EXPERIENCED,5)
    KILL_CREATURE(PLAYER0,SORCEROR,MOST_EXPERIENCED,5)
    QUICK_INFORMATION(5,"你的苦工被一股未知的力量抓走了！",PLAYER0)
ENDIF

IF(PLAYER0,PRISON > 0)
    QUICK_INFORMATION(6,"狱卒告诉你，俘虏下方那些肮脏的生物，扔进祭坛会有让你获得援军以及神奇的力量！但是要记住一段时间后它们都会消失！",PLAYER0)
    MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CALL_TO_ARMS,1,1)
    TUTORIAL_FLASH_BUTTON(36,-1)
    MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HEAL_CREATURE,1,1)
    TUTORIAL_FLASH_BUTTON(26,-1)
ENDIF     
       
IF(PLAYER0,TEMPLE > 0)
    QUICK_OBJECTIVE(7,"一个自恃高傲的君主盘踞在东方，只有消灭他南方的通道才能打开，但必须想办法飞过拦路阻隔的岩浆。",PLAYER0)          
ENDIF

IF(PLAYER_GOOD,AVATAR < 2)
    ADD_TUNNELLER_TO_LEVEL(PLAYER_GOOD,-2,DUNGEON_HEART,0,10,400)
    QUICK_INFORMATION(8,"你成功消灭了君主，南方的通路即将开启",PLAYER0)
ENDIF

IF(PLAYER0,KNIGHT > 0)
    KILL_CREATURE(PLAYER0,DARK_MISTRESS,MOST_EXPERIENCED,10)
    KILL_CREATURE(PLAYER0,SKELETON,MOST_EXPERIENCED,10)
    KILL_CREATURE(PLAYER0,DRAGON,MOST_EXPERIENCED,5)
    QUICK_INFORMATION(9,"经过不懈的努力，你终于找到了国王，祭坛的法术时间已到，之前的同伴也消失了",PLAYER0)
    QUICK_OBJECTIVE(10,"国王必须孤军奋战了，穿过弓箭手的堵截，找到训练室增强战力，消灭黄色势力",PLAYER0)
ENDIF

IF(PLAYER3,DUNGEON_DESTROYED == 1)
    KILL_CREATURE(PLAYER0,SKELETON,MOST_EXPERIENCED,50)
    QUICK_OBJECTIVE(11,"你成功的扫除了一大障碍，前方是法师的领地，需要开动脑筋找到获取破墙术的方法，才能到达最后的领地。",PLAYER0) 
ENDIF

IF(PLAYER0,RESEARCH > 80)
    QUICK_INFORMATION(12,"破墙术就在中间的房间里，如何才能得到呢？北边的图书馆为何空无一物？",PLAYER0)
    ADD_TUNNELLER_TO_LEVEL(PLAYER_GOOD,6,DUNGEON_HEART,0,10,400)
ENDIF

IF_ACTION_POINT(13,PLAYER0)
    QUICK_INFORMATION(13,"摆在你面前的有三个选择，你的选择将决定最终阶段的游戏方式，你只有一次机会，做出了选择就无法回头，祝你好运！",PLAYER0) 
ENDIF

IF_ACTION_POINT(4,PLAYER0)
    MAGIC_AVAILABLE(PLAYER0,POWER_DESTROY_WALLS,0,0)
ENDIF

IF(PLAYER0,FLAG1 == 0)
    IF(PLAYER0,KNIGHT > 0)
        IF(PLAYER0,HORNY > 0)
            KILL_CREATURE(PLAYER0,KNIGHT,MOST_EXPERIENCED,5)
            SET_FLAG(PLAYER0,FLAG1,1)
            QUICK_OBJECTIVE(14,"国王化身冥界使者，一股强大的力量涌边全身，挥动邪恶的镰刀，宰割眼前的敌人吧，只有将它们全部铲除才能获得永生！",PLAYER0)
        ENDIF 
    ENDIF
ENDIF

IF(PLAYER0,FLAG1 == 1)
    SET_CREATURE_HEALTH(HORNY,6120)
    SET_CREATURE_STRENGTH(HORNY,255）
    SET_CREATURE_ARMOUR(HORNY,255） 
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY6,7,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY6,8,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY6,9,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY6,10,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY6,11,2)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY6,12,2)
        IF(PLAYER0,SAMURAI > 0)
            KILL_CREATURE(PLAYER0,SAMURAI,MOST_EXPERIENCED,10)
            QUICK_INFORMATION(21,"弱小的生物，你的全部都属于我！",PLAYER0)
        ENDIF
        IF(PLAYER0,ORC > 0)
            KILL_CREATURE(PLAYER0,ORC,MOST_EXPERIENCED,5)
            QUICK_INFORMATION(22,"弱小的生物，你的全部都属于我！",PLAYER0)
        ENDIF
ENDIF

IF_ACTION_POINT(5,PLAYER0)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,SAMURAI,-3,3,10,100)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,SAMURAI,-4,3,10,100)
    SET_FLAG(PLAYER0,FLAG1,4)
ENDIF

IF(PLAYER0,FLAG1 == 0)
    IF(PLAYER0,SAMURAI > 0)
        SET_FLAG(PLAYER0,FLAG1,2)
        SET_CREATURE_HEALTH(AVATAR,6120)
        SET_CREATURE_STRENGTH(AVATAR,255)
        QUICK_OBJECTIVE(15,"你的选择也许是正确的，前往招募中心，将武士全部召唤过来吧，胜利与你越来越近！",PLAYER0)
    ENDIF
ENDIF

IF(PLAYER0,FLAG1 == 2)
    IF(PLAYER0,SCAVENGER > 0)
        ADD_CREATURE_TO_LEVEL(PLAYER0,SAMURAI,2,10,10,100)
        ADD_CREATURE_TO_LEVEL(PLAYER0,SAMURAI,2,10,10,100)
    ENDIF
    IF_ACTION_POINT(5,PLAYER0)
        SET_FLAG(PLAYER0,FLAG1,4)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY4,-3,1)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY4,-4,1)
        ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,SAMURAI,-3,2,10,100)
        ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,SAMURAI,-4,2,10,100)
    ENDIF
ENDIF

IF(PLAYER0,FLAG1 == 0)
    IF(PLAYER0,ORC > 0)
        SET_FLAG(PLAYER0,FLAG1,3)
        SET_CREATURE_HEALTH(AVATAR,4500)
        SET_CREATURE_STRENGTH(AVATAR,200)
        QUICK_OBJECTIVE(16,"挡在你面前的是水怪和巨魔，前往招募中心，将兽人全部集结过来，消灭后方横空出现的巨魔",PLAYER0) 
    ENDIF
ENDIF

IF(PLAYER0,FLAG1 == 3)
    IF(PLAYER0,SCAVENGER > 0)
        ADD_CREATURE_TO_LEVEL(PLAYER0,ORC,2,10,10,100)
        ADD_CREATURE_TO_LEVEL(PLAYER0,ORC,2,10,10,100)
    ENDIF
    IF_ACTION_POINT(5,PLAYER0)
        SET_FLAG(PLAYER0,FLAG1,4)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY5,5,5)
    ENDIF
ENDIF

REM ****************************胜利/失败条件**********************************

IF(PLAYER_GOOD,TOTAL_CREATURES == 0)
    IF(PLAYER0,ALL_DUNGEONS_DESTROYED == 1) 
        IF(PLAYER0,FLAG1 == 4)
             PLAY_MESSAGE(PLAYER0,SPEECH,109)
             QUICK_OBJECTIVE(18,"你成功地完成了不寻常的行动――结束了永恒的战争，熄灭了四面冲突的火焰，在尸体的山顶上，你戳破了恶魔和谐的旗帜！",PLAYER0)
             WIN_GAME
        ENDIF
    ENDIF
ENDIF

IF(PLAYER0,DUNGEONS_DESTROYED == 1)
    LOSE_GAME
ENDIF

IF(PLAYER0,GAME_TURN <= 30)
    IF(PLAYER0,TOTAL_CREATURES>=5)
        QUICK_OBJECTIVE(20,"防传怪脚本启动！",PLAYER0)
        LOSE_GAME
    ENDIF
ENDIF

REM ****************************提示信息**********************************

QUICK_OBJECTIVE(0,"恶魔入侵你的领地，仅有的工厂又被魔物占领，国王被囚禁在南方，你必须挣脱恶魔的束缚，消灭东方的君主，寻找落难的国王，这是唯一的希望",PLAYER0)

















