REM *********************************************************
REM             Script for Level 2010.胜利的苦涩
REM *********************************************************             
REM             本地图制作者： 始作俑者
REM                  QQ：331672578
REM  欢迎加1128460424（地下城守护者 - Dungeon Keeper）群交流
REM *********************************************************
REM 地图的大概情况与参考建议：

REM 1、两个发薪日过后开启循环刷怪
REM     
REM 2、坚持1小时左右会停止刷怪
REM
REM 3、占领中间的入口会触发决战

REM 4、消灭所有守护者才能占领中间入口

REM 5、不要让地上的金子过多，不然会卡死

REM ***********************************************************************

SET_GENERATE_SPEED(450)
MAX_CREATURES(PLAYER0,30)
START_MONEY(PLAYER0,20000)
START_MONEY(PLAYER1,50000)
START_MONEY(PLAYER2,50000)
START_MONEY(PLAYER3,50000)
COMPUTER_PLAYER(PLAYER1,0)
COMPUTER_PLAYER(PLAYER2,0)
COMPUTER_PLAYER(PLAYER3,0)
ALLY_PLAYERS(PLAYER1,PLAYER3)
ALLY_PLAYERS(PLAYER2,PLAYER3)
ALLY_PLAYERS(PLAYER1,PLAYER2)
ALLY_PLAYERS(PLAYER1,PLAYER_GOOD)
ALLY_PLAYERS(PLAYER3,PLAYER_GOOD)
ALLY_PLAYERS(PLAYER2,PLAYER_GOOD)

REM *******************************添加怪物到入口 *********************************
REM   (添加到入口的生物名字,数量)

ADD_CREATURE_TO_POOL(ARCHER,15)
ADD_CREATURE_TO_POOL(DWARFA,15)
ADD_CREATURE_TO_POOL(SAMURAI,15)
ADD_CREATURE_TO_POOL(BARBARIAN,20)
ADD_CREATURE_TO_POOL(FAIRY,15)
ADD_CREATURE_TO_POOL(KNIGHT,20)
ADD_CREATURE_TO_POOL(WIZARD,15)
ADD_CREATURE_TO_POOL(GIANT,20)
ADD_CREATURE_TO_POOL(WITCH,15)
ADD_CREATURE_TO_POOL(MONK,20)
ADD_CREATURE_TO_POOL(AVATAR,1)

REM *******************************指定阵营（玩家）允许/禁止从入口（怪物池）中招指定生物*********************************
REM  (玩家,生物名称,1（只能是1）,[1允许招募/0禁止招募])

CREATURE_AVAILABLE(PLAYER0,ARCHER,1,1)
CREATURE_AVAILABLE(PLAYER0,DWARFA,1,1)
CREATURE_AVAILABLE(PLAYER0,SAMURAI,1,1)
CREATURE_AVAILABLE(PLAYER0,BARBARIAN,1,1)
CREATURE_AVAILABLE(PLAYER0,FAIRY,1,1)
CREATURE_AVAILABLE(PLAYER0,KNIGHT,1,1)
CREATURE_AVAILABLE(PLAYER0,WIZARD,1,1)
CREATURE_AVAILABLE(PLAYER0,GIANT,1,1)
CREATURE_AVAILABLE(PLAYER0,WITCH,1,1)
CREATURE_AVAILABLE(PLAYER0,MONK,1,1)
CREATURE_AVAILABLE(PLAYER0,AVATAR,1,1)

REM ********************指定阵营（玩家）允许/禁止研究和建造指定房子（建筑）**********************************
REM  (玩家,房子名称,是否可研究或建造[1是允许；0是禁止],是否已经可建造[1已经可建/0待研究])

ROOM_AVAILABLE(ALL_PLAYERS,RESEARCH,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,WORKSHOP,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,GARDEN,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,LAIR,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,BARRACKS,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,TORTURE,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,GRAVEYARD,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,TRAINING,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,TREASURE,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,PRISON,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,SCAVENGER,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,TEMPLE,1,0)

REM ********************指定阵营（玩家）允许/禁止研究和使用指定魔法**********************************
REM  (玩家,魔法名称,是否可研究或使用[1是允许；0是禁止],是否已经可使用[1已经可建/0待研究])

MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HAND,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SLAP,1,1)
MAGIC_AVAILABLE(PLAYER1,POWER_IMP,1,1)
MAGIC_AVAILABLE(PLAYER2,POWER_IMP,1,1)
MAGIC_AVAILABLE(PLAYER3,POWER_IMP,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SIGHT,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CALL_TO_ARMS,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HOLD_AUDIENCE,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SPEED,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CONCEAL,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HEAL_CREATURE,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_DISEASE,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CHICKEN,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_LIGHTNING,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_OBEY,1,0)

REM *********************指定阵营（玩家）允许/禁止制造指定的门*********************************
REM  (玩家,门的名称,[1是允许；0是禁止],1[0是允许；数字是已经制造出来/赠送的数量]

DOOR_AVAILABLE(ALL_PLAYERS,WOOD,1,0)
DOOR_AVAILABLE(ALL_PLAYERS,BRACED,1,0)
DOOR_AVAILABLE(ALL_PLAYERS,STEEL,1,0)
DOOR_AVAILABLE(ALL_PLAYERS,MAGIC,1,0)

REM *********************指定阵营（玩家）允许/禁止制造指定陷阱*********************************
REM  (玩家,陷阱名称,[1是允许；0是禁止],1[0是允许；数字是已经制造出来/赠送的数量]

TRAP_AVAILABLE(ALL_PLAYERS,ALARM,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,POISON_GAS,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,LIGHTNING,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,WORD_OF_POWER,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,BOULDER,1,0)

REM CREATE_PARTY =>组建队伍                (队伍名称)   
REM ADD_TO_PARTY=>添加生物到队伍中   (队伍名称,生物名称,生物等级,携带金钱数量,任务,延时)

CREATE_PARTY(FIRST)
    ADD_TO_PARTY(FIRST,DRAGON,8,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(FIRST,DRAGON,8,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(FIRST,DRAGON,8,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(FIRST,VAMPIRE,8,5000,DEFEND_PARTY,0)
    ADD_TO_PARTY(FIRST,VAMPIRE,8,5000,DEFEND_PARTY,0)

CREATE_PARTY(SECOND)
    ADD_TO_PARTY(SECOND,DRAGON,8,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(SECOND,VAMPIRE,8,5000,DEFEND_PARTY,0)
    ADD_TO_PARTY(SECOND,VAMPIRE,8,5000,DEFEND_PARTY,0)
    ADD_TO_PARTY(SECOND,DRAGON,8,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(SECOND,DRAGON,8,0,ATTACK_ENEMIES,0
CREATE_PARTY(THIRD)
    ADD_TO_PARTY(THIRD,DRAGON,8,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(THIRD,DRAGON,8,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(THIRD,DRAGON,8,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(THIRD,VAMPIRE,8,1800,DEFEND_PARTY,0)
    ADD_TO_PARTY(THIRD,VAMPIRE,8,1700,DEFEND_PARTY,0)

CREATE_PARTY(FOUR)
    ADD_TO_PARTY(FOUR,DRAGON,8,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(FOUR,DRAGON,8,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(FOUR,DRAGON,8,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(FOUR,VAMPIRE,8,5000,DEFEND_PARTY,0)
    ADD_TO_PARTY(FOUR,VAMPIRE,8,5000,DEFEND_PARTY,0)

CREATE_PARTY(BRUTES_LIGHT)
    ADD_TO_PARTY(BRUTES_LIGHT,SKELETON,10,2200,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(BRUTES_LIGHT,SKELETON,10,2200,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(BRUTES_LIGHT,SKELETON,10,2200,DEFEND_PARTY,0)
    ADD_TO_PARTY(BRUTES_LIGHT,SKELETON,10,2200,ATTACK_ENEMIES,0)

CREATE_PARTY(DARK_MISTRESS_LIGHT)
    ADD_TO_PARTY(DARK_MISTRESS_LIGHT,DARK_MISTRESS,10,1850,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(DARK_MISTRESS_LIGHT,DARK_MISTRESS,10,2000,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(DARK_MISTRESS_LIGHT,DARK_MISTRESS,10,1850,DEFEND_PARTY,0)
    ADD_TO_PARTY(DARK_MISTRESS_LIGHT,DARK_MISTRESS,10,1850,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(DARK_MISTRESS_LIGHT,DARK_MISTRESS,10,1950,DEFEND_PARTY,0)

CREATE_PARTY(MYSTIC_LIGHT)
    ADD_TO_PARTY(MYSTIC_LIGHT,DARK_MISTRESS,10,2000,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(MYSTIC_LIGHT,SORCEROR,10,2200,STEAL_SPELLS,0)
    ADD_TO_PARTY(MYSTIC_LIGHT,SORCEROR,10,1900,DEFEND_PARTY,0)
    ADD_TO_PARTY(MYSTIC_LIGHT,SKELETON,10,1800,DEFEND_PARTY,0)
    ADD_TO_PARTY(MYSTIC_LIGHT,BILE_DEMON,10,1900,ATTACK_ROOMS,0)

CREATE_PARTY(BRUTES_HEAVY)
    ADD_TO_PARTY(BRUTES_HEAVY,BILE_DEMON,10,2500,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(BRUTES_HEAVY,BILE_DEMON,10,2500,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(BRUTES_HEAVY,SKELETON,10,2000,DEFEND_PARTY,0)
    ADD_TO_PARTY(BRUTES_HEAVY,SKELETON,10,2000,STEAL_SPELLS,0)

CREATE_PARTY(DARK_MISTRESS_HEAVY)
    ADD_TO_PARTY(DARK_MISTRESS_HEAVY,DARK_MISTRESS,10,2250,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(DARK_MISTRESS_HEAVY,DARK_MISTRESS,10,2500,STEAL_SPELLS,0)
    ADD_TO_PARTY(DARK_MISTRESS_HEAVY,DARK_MISTRESS,10,2000,DEFEND_PARTY,0)
    ADD_TO_PARTY(DARK_MISTRESS_HEAVY,DARK_MISTRESS,10,2000,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(DARK_MISTRESS_HEAVY,DARK_MISTRESS,10,2250,DEFEND_PARTY,0)

CREATE_PARTY(MYSTIC_HEAVY)
    ADD_TO_PARTY(MYSTIC_HEAVY,DARK_MISTRESS,10,2500,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(MYSTIC_HEAVY,SORCEROR,10,2500,STEAL_SPELLS,0)
    ADD_TO_PARTY(MYSTIC_HEAVY,SORCEROR,10,2000,DEFEND_PARTY,0)
    ADD_TO_PARTY(MYSTIC_HEAVY,DARK_MISTRESS,10,2000,DEFEND_PARTY,0)
    ADD_TO_PARTY(MYSTIC_HEAVY,BILE_DEMON,10,2000,ATTACK_ROOMS,0)

CREATE_PARTY(SUPPORT)
    ADD_TO_PARTY(SUPPORT,VAMPIRE,10,2000,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(SUPPORT,VAMPIRE,10,2000,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(SUPPORT,VAMPIRE,10,2000,DEFEND_PARTY,0)
    ADD_TO_PARTY(SUPPORT,VAMPIRE,10,2000,DEFEND_PARTY,0)
    ADD_TO_PARTY(SUPPORT,VAMPIRE,10,2000,ATTACK_ENEMIES,0)

CREATE_PARTY(BOSS)
    ADD_TO_PARTY(BOSS,VAMPIRE,10,2500,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(BOSS,KNIGHT,10,5000,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(BOSS,VAMPIRE,10,2500,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(BOSS,VAMPIRE,10,2500,DEFEND_PARTY,0)
    ADD_TO_PARTY(BOSS,HORNY,10,2000,DEFEND_PARTY,0)
    ADD_TO_PARTY(BOSS,HORNY,10,2000,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(BOSS,HORNY,10,2000,ATTACK_ENEMIES,0)
   
REM ****************************脚本主体(主要内容、核心内容）**********************************

QUICK_OBJECTIVE(0,"任务：占领并摧毁圣门",PLAYER0)

QUICK_OBJECTIVE(6,"第二个发薪日恶魔带着恶龙开始向你进攻，维持一个小时，做好准备！",PLAYER0)

SET_FLAG(PLAYER_GOOD,FLAG0,0)
SET_FLAG(PLAYER0,FLAG1,0)

IF(PLAYER0,IMP > 0)
    MAGIC_AVAILABLE(PLAYER0,POWER_IMP,1,1)
ENDIF

IF(PLAYER0,TEMPLE >= 9)
    ROOM_AVAILABLE(PLAYER0,TEMPLE,0,0)
ENDIF

IF(PLAYER0,FLAG1 == 0)
    IF(PLAYER0,BRIDGE == 0)
        NEXT_COMMAND_REUSABLE
        ROOM_AVAILABLE(PLAYER0,BRIDGE,1,1)
    ENDIF
ENDIF

IF(PLAYER0,FLAG1 == 0)
    IF(PLAYER0,BRIDGE == 1)
        NEXT_COMMAND_REUSABLE
        ROOM_AVAILABLE(PLAYER0,BRIDGE,0,0)
    ENDIF
ENDIF

IF(PLAYER0,FLAG1 == 1)
    ROOM_AVAILABLE(PLAYER0,BRIDGE,1,1)
ENDIF

IF(PLAYER1,TOTAL_CREATURES == 0)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,VAMPIRE,3,10,10,0)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,HORNY,3,10,10,0)
ENDIF

IF(PLAYER2,TOTAL_CREATURES == 0)  
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,VAMPIRE,5,10,10,0)
ENDIF

IF(PLAYER3,TOTAL_CREATURES == 0)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,VAMPIRE,4,10,10,0)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,HORNY,4,10,10,0)
ENDIF

SET_TIMER(PLAYER0,TIMER0)

IF(PLAYER0,TIMER0 >= 20000)
    SET_FLAG(PLAYER0,FLAG0,1)
ENDIF

IF(PLAYER0,FLAG0 == 1)
    SET_TIMER(PLAYER_GOOD,TIMER0)
    SET_TIMER(PLAYER_GOOD,TIMER1)
    SET_TIMER(PLAYER_GOOD,TIMER2)
    SET_TIMER(PLAYER_GOOD,TIMER3)
    SET_TIMER(PLAYER_GOOD,TIMER4)
ENDIF

IF(PLAYER_GOOD,FLAG0 == 0)
    IF(PLAYER_GOOD,TIMER0 >= 10)
        IF(PLAYER_GOOD,TIMER1 > 1500)
            NEXT_COMMAND_REUSABLE
            ADD_PARTY_TO_LEVEL(PLAYER_GOOD,FIRST,-1,1)
            NEXT_COMMAND_REUSABLE
            SET_TIMER(PLAYER_GOOD,TIMER1)
        ENDIF
    ENDIF
ENDIF


IF(PLAYER_GOOD,FLAG0 == 0)
    IF(PLAYER_GOOD,TIMER0 >= 3000)
        IF(PLAYER_GOOD,TIMER2 > 1500)
            NEXT_COMMAND_REUSABLE
            ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SECOND,-2,1)
            NEXT_COMMAND_REUSABLE
            SET_TIMER(PLAYER_GOOD,TIMER2)
        ENDIF
    ENDIF
ENDIF


IF(PLAYER_GOOD,FLAG0 == 0)
    IF(PLAYER_GOOD,TIMER0 >= 4500)
        IF(PLAYER_GOOD,TIMER3 > 1500)
            NEXT_COMMAND_REUSABLE
            ADD_PARTY_TO_LEVEL(PLAYER_GOOD,THIRD,-3,1)
            NEXT_COMMAND_REUSABLE
            SET_TIMER(PLAYER_GOOD,TIMER3)
        ENDIF
    ENDIF
ENDIF


IF(PLAYER_GOOD,FLAG0 == 0)
    IF(PLAYER_GOOD,TIMER0 >= 6000)
        IF(PLAYER_GOOD,TIMER4 > 1500)
            NEXT_COMMAND_REUSABLE
            ADD_PARTY_TO_LEVEL(PLAYER_GOOD,FOUR,-4,1)
            NEXT_COMMAND_REUSABLE
            SET_TIMER(PLAYER_GOOD,TIMER4)
        ENDIF
    ENDIF
ENDIF


IF(PLAYER_GOOD,TIMER0 >= 30000)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,BRUTES_LIGHT,-5,1)
ENDIF

IF(PLAYER_GOOD,TIMER0 >= 35500)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,MYSTIC_LIGHT,-5,1)
ENDIF

IF(PLAYER_GOOD,TIMER0 >= 42500)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,DARK_MISTRESS_LIGHT,-5,1)
    SET_FLAG(PLAYER_GOOD,FLAG0,1)
    QUICK_OBJECTIVE(5,"到达圣门之前必须消灭所有守护者！",PLAYER0)
ENDIF

IF(PLAYER_GOOD,TIMER0 >= 42500)
    IF(PLAYER0,ALL_DUNGEONS_DESTROYED == 1)
        IF(PLAYER_GOOD,FLAG0 == 1)
            SET_FLAG(PLAYER_GOOD,FLAG0,2)
            SET_FLAG(PLAYER0,FLAG1,1)
            QUICK_OBJECTIVE(1,"快去占领圣门并摧毁它！",PLAYER0)
        ENDIF
    ENDIF
ENDIF

IF_ACTION_POINT(1,PLAYER0)
    SET_TIMER(PLAYER_GOOD,TIMER5)
    QUICK_OBJECTIVE(2,"你来迟了，圣门仍受敌人控制！",PLAYER0)
ENDIF

IF(PLAYER_GOOD,FLAG0 == 2)
    IF(PLAYER_GOOD,TIMER5 >= 1000)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,BRUTES_HEAVY,-5,1)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,BRUTES_HEAVY,-5,1)
        QUICK_OBJECTIVE(3,"敌人又来了！",PLAYER0)
    ENDIF
ENDIF

IF(PLAYER_GOOD,FLAG0 == 2)
    IF(PLAYER_GOOD,TIMER5 >= 5000)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,DARK_MISTRESS_HEAVY,-5,1)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,MYSTIC_HEAVY,-5,1)
        SET_FLAG(PLAYER_GOOD,FLAG0,3)
    ENDIF
ENDIF

IF(PLAYER_GOOD,FLAG0 == 3)
    IF(PLAYER_GOOD,TIMER5 >= 10000)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,BOSS,1,3)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SUPPORT,1,3)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,DARK_MISTRESS_HEAVY,-5,3)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,DARK_MISTRESS_LIGHT,-5,3)
        SET_FLAG(PLAYER_GOOD,FLAG0,4)
    ENDIF
ENDIF

REM ****************************胜利/失败条件**********************************

IF(PLAYER_GOOD,TIMER5 >= 10000)
    IF(PLAYER_GOOD,FLAG0 == 4)
        IF(PLAYER_GOOD,TOTAL_CREATURES == 0)
            QUICK_OBJECTIVE(4,"你占领并摧毁了圣门！",PLAYER0)
            WIN_GAME
        ENDIF
    ENDIF
ENDIF

IF(PLAYER0,DUNGEONS_DESTROYED == 1)
    LOSE_GAME
ENDIF