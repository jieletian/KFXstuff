REM *********************************************************
REM             Script for Level 2008.Gold enough
REM *********************************************************             
REM             本地图制作者： 始作俑者
REM                  QQ：331672578
REM  欢迎加1128460424（地下城守护者 - Dungeon Keeper）群交流
REM *********************************************************
REM 地图的大概情况与参考建议：

REM 1、一个发薪日过后会有挖掘者从下方挖过来
REM     
REM 2、占领左下的要塞会触发循环刷怪
REM
REM 3、顶住进攻一段时间会停止刷怪

REM ***********************************************************************
      
SET_GENERATE_SPEED(400)

COMPUTER_PLAYER(PLAYER3,1)

MAX_CREATURES(PLAYER0,30)
MAX_CREATURES(PLAYER3,40)

ALLY_PLAYERS(PLAYER3,PLAYER_GOOD)

START_MONEY(PLAYER0,20000)
START_MONEY(PLAYER3,9000000)

REM *******************************添加怪物到入口 *********************************
REM   (添加到入口的生物名字,数量)

ADD_CREATURE_TO_POOL(ORC,5)
ADD_CREATURE_TO_POOL(DARK_MISTRESS,8)
ADD_CREATURE_TO_POOL(TROLL,5)
ADD_CREATURE_TO_POOL(ARCHER,5)
ADD_CREATURE_TO_POOL(DEMONSPAWN,5)
ADD_CREATURE_TO_POOL(BILE_DEMON,20)
ADD_CREATURE_TO_POOL(DWARFA,5)
ADD_CREATURE_TO_POOL(SAMURAI,35)
ADD_CREATURE_TO_POOL(BARBARIAN,5)
ADD_CREATURE_TO_POOL(TENTACLE,3)
ADD_CREATURE_TO_POOL(THIEF,3)
ADD_CREATURE_TO_POOL(KNIGHT,30)
ADD_CREATURE_TO_POOL(WIZARD,5)
ADD_CREATURE_TO_POOL(DRAGON,20)
ADD_CREATURE_TO_POOL(FAIRY,5)
ADD_CREATURE_TO_POOL(DRAGON,20)
ADD_CREATURE_TO_POOL(GIANT,10)
ADD_CREATURE_TO_POOL(SORCEROR,10)
ADD_CREATURE_TO_POOL(WIZARD,15)

REM *******************************指定阵营（玩家）允许/禁止从入口（怪物池）中招指定生物*********************************
REM  (玩家,生物名称,1（只能是1）,[1允许招募/0禁止招募])

CREATURE_AVAILABLE(PLAYER3,WIZARD,1,1)
CREATURE_AVAILABLE(PLAYER3,GIANT,1,1)
CREATURE_AVAILABLE(PLAYER0,SORCEROR,1,1)
CREATURE_AVAILABLE(PLAYER0,DRAGON,1,1)
CREATURE_AVAILABLE(PLAYER0,ORC,1,1)
CREATURE_AVAILABLE(PLAYER0,TENTACLE,1,1)
CREATURE_AVAILABLE(PLAYER0,DARK_MISTRESS,1,1)
CREATURE_AVAILABLE(PLAYER0,TROLL,1,1)
CREATURE_AVAILABLE(PLAYER0,DEMONSPAWN,1,1)
CREATURE_AVAILABLE(PLAYER0,BILE_DEMON,1,1)
CREATURE_AVAILABLE(PLAYER3,ARCHER,1,1)
CREATURE_AVAILABLE(PLAYER3,DWARFA,1,1)
CREATURE_AVAILABLE(PLAYER3,SAMURAI,1,1)
CREATURE_AVAILABLE(PLAYER3,BARBARIAN,1,1) 
CREATURE_AVAILABLE(PLAYER3,KNIGHT,1,1)
CREATURE_AVAILABLE(PLAYER3,THIEF,1,1)
CREATURE_AVAILABLE(PLAYER3,WIZARD,1,1)

REM ********************指定阵营（玩家）允许/禁止研究和建造指定房子（建筑）**********************************
REM  (玩家,房子名称,是否可研究或建造[1是允许；0是禁止],是否已经可建造[1已经可建/0待研究])
            
ROOM_AVAILABLE(ALL_PLAYERS,TREASURE,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,LAIR,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,GARDEN,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,TRAINING,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,RESEARCH,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,WORKSHOP,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,BARRACKS,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,GUARD_POST,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,TEMPLE,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,TORTURE,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,SCAVENGER,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,GRAVEYARD,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,PRISON,1,0)

REM ********************指定阵营（玩家）允许/禁止研究和使用指定魔法**********************************
REM  (玩家,魔法名称,是否可研究或使用[1是允许；0是禁止],是否已经可使用[1已经可建/0待研究])

MAGIC_AVAILABLE(ALL_PLAYERS,POWER_OBEY,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_IMP,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SLAP,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SPEED,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CALL_TO_ARMS,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HAND,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CONCEAL,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HEAL_CREATURE,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CHICKEN,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_PROTECT,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_DISEASE,1,0)

MAGIC_AVAILABLE(PLAYER3,POWER_LIGHTNING,1,1)
MAGIC_AVAILABLE(PLAYER3,POWER_CAVE_IN,1,0)
MAGIC_AVAILABLE(PLAYER3,POWER_SIGHT,1,1)

REM *********************指定阵营（玩家）允许/禁止制造指定的门*********************************
REM  (玩家,门的名称,[1是允许；0是禁止],1[0是允许；数字是已经制造出来/赠送的数量]

DOOR_AVAILABLE(ALL_PLAYERS,STEEL,1,0)
DOOR_AVAILABLE(ALL_PLAYERS,MAGIC,1,0)

REM *********************指定阵营（玩家）允许/禁止制造指定陷阱*********************************
REM  (玩家,陷阱名称,[1是允许；0是禁止],1[0是允许；数字是已经制造出来/赠送的数量]

TRAP_AVAILABLE(ALL_PLAYERS,ALARM,1,0)		
TRAP_AVAILABLE(ALL_PLAYERS,LIGHTNING,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,WORD_OF_POWER,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,LAVA,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,POISON_GAS,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,BOULDER,1,0)

REM CREATE_PARTY =>组建队伍                (队伍名称)   
REM ADD_TO_PARTY=>添加生物到队伍中   (队伍名称,生物名称,生物等级,携带金钱数量,任务,延时)

CREATE_PARTY(FIRST)
    ADD_TO_PARTY(FIRST,ARCHER,5,750,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(FIRST,BARBARIAN,5,1000,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(FIRST,ARCHER,5,750,DEFEND_PARTY,0)
    ADD_TO_PARTY(FIRST,THIEF,5,600,STEAL_GOLD,0)
    ADD_TO_PARTY(FIRST,ARCHER,5,750,DEFEND_PARTY,0)

CREATE_PARTY(SECOND)
    ADD_TO_PARTY(SECOND,THIEF,6,750,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(SECOND,THIEF,6,1000,STEAL_GOLD,0)
    ADD_TO_PARTY(SECOND,DWARFA,6,800,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(SECOND,DWARFA,6,800,DEFEND_PARTY,0)
    ADD_TO_PARTY(SECOND,THIEF,6,750,ATTACK_ENEMIES,0)

CREATE_PARTY(THIRD)
    ADD_TO_PARTY(THIRD,MONK,7,750,STEAL_SPELLS,0)
    ADD_TO_PARTY(THIRD,WIZARD,7,1000,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(THIRD,MONK,7,750,STEAL_SPELLS,0)
    ADD_TO_PARTY(THIRD,SAMURAI,7,800,DEFEND_PARTY,0)
    ADD_TO_PARTY(THIRD,ARCHER,7,700,DEFEND_PARTY,0)

CREATE_PARTY(FOUR)
    ADD_TO_PARTY(FOUR,GIANT,8,800,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(FOUR,GIANT,8,1000,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(FOUR,WITCH,8,800,STEAL_SPELLS,0)
    ADD_TO_PARTY(FOUR,WITCH,8,750,DEFEND_PARTY,0)

CREATE_PARTY(BRUTES_LIGHT)
    ADD_TO_PARTY(BRUTES_LIGHT,GIANT,6,1200,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(BRUTES_LIGHT,GIANT,6,1200,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(BRUTES_LIGHT,BARBARIAN,6,1200,DEFEND_PARTY,0)
    ADD_TO_PARTY(BRUTES_LIGHT,BARBARIAN,6,1200,STEAL_SPELLS,0)

CREATE_PARTY(SAMURAI_LIGHT)
    ADD_TO_PARTY(SAMURAI_LIGHT,SAMURAI,7,850,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(SAMURAI_LIGHT,SAMURAI,7,1000,STEAL_SPELLS,0)
    ADD_TO_PARTY(SAMURAI_LIGHT,SAMURAI,7,850,DEFEND_PARTY,0)
    ADD_TO_PARTY(SAMURAI_LIGHT,SAMURAI,7,850,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(SAMURAI_LIGHT,SAMURAI,7,950,DEFEND_PARTY,0)

CREATE_PARTY(MYSTIC_LIGHT)
    ADD_TO_PARTY(MYSTIC_LIGHT,SAMURAI,6,1000,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(MYSTIC_LIGHT,WIZARD,6,1200,STEAL_SPELLS,0)
    ADD_TO_PARTY(MYSTIC_LIGHT,MONK,6,900,DEFEND_PARTY,0)
    ADD_TO_PARTY(MYSTIC_LIGHT,ARCHER,6,800,DEFEND_PARTY,0)
    ADD_TO_PARTY(MYSTIC_LIGHT,GIANT,6,900,ATTACK_ROOMS,0)

CREATE_PARTY(BRUTES_HEAVY)
    ADD_TO_PARTY(BRUTES_HEAVY,GIANT,8,2500,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(BRUTES_HEAVY,GIANT,8,2500,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(BRUTES_HEAVY,BARBARIAN,8,2000,DEFEND_PARTY,0)
    ADD_TO_PARTY(BRUTES_HEAVY,BARBARIAN,8,2000,STEAL_SPELLS,0)

CREATE_PARTY(SAMURAI_HEAVY)
    ADD_TO_PARTY(SAMURAI_HEAVY,SAMURAI,8,1250,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(SAMURAI_HEAVY,SAMURAI,8,1500,STEAL_SPELLS,0)
    ADD_TO_PARTY(SAMURAI_HEAVY,SAMURAI,8,1000,DEFEND_PARTY,0)
    ADD_TO_PARTY(SAMURAI_HEAVY,SAMURAI,8,1000,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(SAMURAI_HEAVY,SAMURAI,8,1250,DEFEND_PARTY,0)

CREATE_PARTY(MYSTIC_HEAVY)
    ADD_TO_PARTY(MYSTIC_HEAVY,SAMURAI,8,1500,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(MYSTIC_HEAVY,WIZARD,9,1500,STEAL_SPELLS,0)
    ADD_TO_PARTY(MYSTIC_HEAVY,MONK,8,1000,DEFEND_PARTY,0)
    ADD_TO_PARTY(MYSTIC_HEAVY,ARCHER,8,1000,DEFEND_PARTY,0)
    ADD_TO_PARTY(MYSTIC_HEAVY,GIANT,9,1000,ATTACK_ROOMS,0)

CREATE_PARTY(SUPPORT)
    ADD_TO_PARTY(SUPPORT,ARCHER,9,1000,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(SUPPORT,BARBARIAN,9,1000,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(SUPPORT,ARCHER,9,1000,DEFEND_PARTY,0)
    ADD_TO_PARTY(SUPPORT,ARCHER,9,1000,DEFEND_PARTY,0)
    ADD_TO_PARTY(SUPPORT,BARBARIAN,9,1000,ATTACK_ENEMIES,0)

CREATE_PARTY(BOSS)
    ADD_TO_PARTY(BOSS,WIZARD,10,1500,STEAL_SPELLS,0)
    ADD_TO_PARTY(BOSS,KNIGHT,10,5000,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(BOSS,SAMURAI,10,1500,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(BOSS,GIANT,10,1500,DEFEND_PARTY,0)
    ADD_TO_PARTY(BOSS,ARCHER,10,1000,DEFEND_PARTY,0)
    ADD_TO_PARTY(BOSS,FAIRY,10,1000,STEAL_SPELLS,0)
    
REM ****************************脚本主体(主要内容、核心内容）**********************************

QUICK_OBJECTIVE(0,"任务：抵挡人类进攻，消灭所有敌人，占领中部岛上的石像",PLAYER0)

SET_TIMER(PLAYER0,TIMER0)

IF(PLAYER0,TIMER0 >= 11000)
    ADD_TUNNELLER_TO_LEVEL(PLAYER_GOOD,3,DUNGEON_HEART,0,5,1000)
    ADD_TUNNELLER_TO_LEVEL(PLAYER_GOOD,4,DUNGEON_HEART,0,4,1000)
ENDIF

IF(PLAYER0,TIMER0 >= 12000)
    ADD_TUNNELLER_TO_LEVEL(PLAYER_GOOD,5,DUNGEON_HEART,0,5,1000)
    ADD_TUNNELLER_TO_LEVEL(PLAYER_GOOD,3,DUNGEON_HEART,0,4,1000)
ENDIF

IF(PLAYER0,TIMER0 >= 13500)
    ADD_TUNNELLER_TO_LEVEL(PLAYER_GOOD,4,DUNGEON_HEART,0,5,1000)
    ADD_TUNNELLER_TO_LEVEL(PLAYER_GOOD,5,DUNGEON_HEART,0,4,1000)
ENDIF

IF(PLAYER0,TIMER0 >= 14500)
    ADD_TUNNELLER_TO_LEVEL(PLAYER_GOOD,3,DUNGEON_HEART,0,5,1000)
    ADD_TUNNELLER_TO_LEVEL(PLAYER_GOOD,4,DUNGEON_HEART,0,4,1000)
    ROOM_AVAILABLE(ALL_PLAYERS,BRIDGE,1,0)
ENDIF

IF_ACTION_POINT(2,PLAYER0)
    SET_FLAG(PLAYER0,FLAG0,1)
    QUICK_OBJECTIVE(4,"敌人的要塞被你攻占了，复仇大军即将到来！",PLAYER0)
ENDIF

IF(PLAYER3,DUNGEON_DESTROYED == 1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,BOSS,-1,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,BOSS,-2,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,BOSS,-3,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,BOSS,-4,1)
ENDIF

IF(PLAYER0,FLAG0 == 1)
    SET_TIMER(PLAYER_GOOD,TIMER0)
    SET_TIMER(PLAYER_GOOD,TIMER1)
    SET_TIMER(PLAYER_GOOD,TIMER2)
    SET_TIMER(PLAYER_GOOD,TIMER3)
    SET_TIMER(PLAYER_GOOD,TIMER4)
ENDIF

IF(PLAYER_GOOD,FLAG0 == 0)
    IF(PLAYER_GOOD,TIMER0 >= 1000)
        IF(PLAYER_GOOD,TIMER1 > 1750)
            NEXT_COMMAND_REUSABLE
            ADD_PARTY_TO_LEVEL(PLAYER_GOOD,FIRST,-1,1)
            NEXT_COMMAND_REUSABLE
            SET_TIMER(PLAYER_GOOD,TIMER1)
        ENDIF
    ENDIF
ENDIF


IF(PLAYER_GOOD,FLAG0 == 0)
    IF(PLAYER_GOOD,TIMER0 >= 1750)
        IF(PLAYER_GOOD,TIMER2 > 2350)
            NEXT_COMMAND_REUSABLE
            ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SECOND,-2,1)
            NEXT_COMMAND_REUSABLE
            SET_TIMER(PLAYER_GOOD,TIMER2)
        ENDIF
    ENDIF
ENDIF


IF(PLAYER_GOOD,FLAG0 == 0)
    IF(PLAYER_GOOD,TIMER0 >= 2500)
        IF(PLAYER_GOOD,TIMER3 > 3000)
            NEXT_COMMAND_REUSABLE
            ADD_PARTY_TO_LEVEL(PLAYER_GOOD,THIRD,-3,1)
            NEXT_COMMAND_REUSABLE
            SET_TIMER(PLAYER_GOOD,TIMER3)
        ENDIF
    ENDIF
ENDIF


IF(PLAYER_GOOD,FLAG0 == 0)
    IF(PLAYER_GOOD,TIMER0 >= 3250)
        IF(PLAYER_GOOD,TIMER4 > 3800)
            NEXT_COMMAND_REUSABLE
            ADD_PARTY_TO_LEVEL(PLAYER_GOOD,FOUR,-4,1)
            NEXT_COMMAND_REUSABLE
            SET_TIMER(PLAYER_GOOD,TIMER4)
        ENDIF
    ENDIF
ENDIF


IF(PLAYER_GOOD,TIMER0 >= 4200)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,BRUTES_LIGHT,-7,1)
ENDIF

IF(PLAYER_GOOD,TIMER0 >= 7250)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,MYSTIC_LIGHT,-8,1)
ENDIF

IF(PLAYER_GOOD,TIMER0 >= 12400)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SAMURAI_LIGHT,-9,1)
    SET_FLAG(PLAYER_GOOD,FLAG0,1)
ENDIF

IF(PLAYER_GOOD,TIMER0 >= 12500)
    IF(PLAYER_GOOD,TOTAL_CREATURES == 0)
        IF(PLAYER_GOOD,FLAG0 == 1)
            SET_FLAG(PLAYER_GOOD,FLAG0,2)
            SET_TIMER(PLAYER_GOOD,TIMER5)
            QUICK_OBJECTIVE(1,"你的抵抗使人类害怕，他们不得不给你奋力一击！",PLAYER0)
        ENDIF
    ENDIF
ENDIF

IF(PLAYER_GOOD,FLAG0 == 2)
    IF(PLAYER_GOOD,TIMER5 >= 8000)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,BRUTES_HEAVY,-6,1)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,BRUTES_HEAVY,-6,1)
        QUICK_OBJECTIVE(2,"敌人再次进攻.",PLAYER0)
    ENDIF
ENDIF

IF(PLAYER_GOOD,FLAG0 == 2)
    IF(PLAYER_GOOD,TIMER5 >= 10000)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SAMURAI_HEAVY,-6,1)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,MYSTIC_HEAVY,-6,1)
        SET_FLAG(PLAYER_GOOD,FLAG0,3)
    ENDIF
ENDIF

IF(PLAYER_GOOD,FLAG0 == 3)
    IF(PLAYER_GOOD,TIMER5 >= 13000)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,BOSS,-6,2)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SUPPORT,-6,1)
        SET_FLAG(PLAYER_GOOD,FLAG0,4)
    ENDIF
ENDIF

REM ****************************胜利/失败条件**********************************

IF(PLAYER_GOOD,TIMER5 >= 13000)
    IF(PLAYER_GOOD,FLAG0 == 4)
        IF_ACTION_POINT(1,PLAYER0)
            IF(PLAYER3,DUNGEON_DESTROYED == 1)
                QUICK_OBJECTIVE(3,"你获得了胜利！",PLAYER0)
                WIN_GAME
            ENDIF
        ENDIF
    ENDIF
ENDIF

IF(PLAYER0,DUNGEON_DESTROYED == 1)
    LOSE_GAME
ENDIF
