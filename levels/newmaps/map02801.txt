LEVEL_VERSION(1)
SET_GENERATE_SPEED(400)
START_MONEY(ALL_PLAYERS,1500)
MAX_CREATURES(ALL_PLAYERS,20)

REM TIMER0: Give some time to think how to acces the Destroy Walls spell before guiding to the Mage.
REM TIMER1: Triggers Hero Gates after finding the Mage
REM TIMER2: Triggers Hero Gates after claiming the Tempel
REM TIMER3: Triggers Hero Gates tu punish using Workshop as income.
REM TIMER4: Triggers a final wave for revenge after enemy heart is destroyed.

ADD_CREATURE_TO_POOL(BUG,2)
ADD_CREATURE_TO_POOL(DEMONSPAWN,5)
ADD_CREATURE_TO_POOL(TROLL,10)
ADD_CREATURE_TO_POOL(SPIDER,20)
ADD_CREATURE_TO_POOL(DRUID,10)
ADD_CREATURE_TO_POOL(ORC,10)
ADD_CREATURE_TO_POOL(BILE_DEMON,2)
ADD_CREATURE_TO_POOL(DRAGON,2)
ADD_CREATURE_TO_POOL(DARK_MISTRESS,2)

CREATURE_AVAILABLE(ALL_PLAYERS,BUG,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,DEMONSPAWN,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,TROLL,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,SPIDER,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,ORC,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,BILE_DEMON,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,DRAGON,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,DARK_MISTRESS,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,DRUID,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,SORCEROR,1,0)

ROOM_AVAILABLE(ALL_PLAYERS,TREASURE,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,LAIR,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,GARDEN,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,TRAINING,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,GUARD_POST,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,WORKSHOP,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,BARRACKS,1,0)

MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HAND,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SLAP,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_POSSESS,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SPEED,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_OBEY,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HOLD_AUDIENCE,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CALL_TO_ARMS,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CONCEAL,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_PROTECT,1,0)

TRAP_AVAILABLE(ALL_PLAYERS,ALARM,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,POISON_GAS,1,0)

DOOR_AVAILABLE(ALL_PLAYERS,STEEL,1,0)
DOOR_AVAILABLE(ALL_PLAYERS,MAGIC,1,0)

REM ----------------------------------------
CREATE_PARTY(CUSTOMERS)
	ADD_TO_PARTY(CUSTOMERS,GIANT,10,1000,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(CUSTOMERS,GIANT,10,1000,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(CUSTOMERS,GIANT,8,500,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(CUSTOMERS,BARBARIAN,8,300,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(CUSTOMERS,BARBARIAN,8,300,ATTACK_DUNGEON_HEART,0)

CREATE_PARTY(MAGE)
	ADD_TO_PARTY(MAGE,Wizard,8,150,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(MAGE,Wizard,8,150,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(MAGE,Fairy,8,0,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(MAGE,WITCH,8,0,ATTACK_ENEMIES,0)
	
CREATE_PARTY(TEMPLE)
	ADD_TO_PARTY(TEMPLE,MONK,10,0,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(TEMPLE,MONK,10,0,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(TEMPLE,MONK,10,0,ATTACK_ENEMIES,0)
	
CREATE_PARTY(HORNIES)
	ADD_TO_PARTY(HORNIES,HORNY,8,0,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(HORNIES,HORNY,8,0,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(HORNIES,HORNY,8,0,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(HORNIES,HORNY,8,0,ATTACK_DUNGEON_HEART,0)

CREATE_PARTY(REVEAL)
	ADD_TO_PARTY(REVEAL,HORNY,10,1500,ATTACK_DUNGEON_HEART,0)

CREATE_PARTY(LANDLORD)
	ADD_TO_PARTY(LANDLORD,KNIGHT,10,2500,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(LANDLORD,SAMURAI,10,0,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(LANDLORD,SAMURAI,10,0,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(LANDLORD,FAIRY,10,0,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(LANDLORD,GIANT,10,0,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(LANDLORD,GIANT,10,0,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(LANDLORD,BARBARIAN,10,0,ATTACK_ENEMIES,0)

CREATE_PARTY(SNEAKY)
	ADD_TO_PARTY(SNEAKY,THIEF,10,1000,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(SNEAKY,THIEF,10,1000,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(SNEAKY,THIEF,10,1000,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(SNEAKY,ARCHER,10,400,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(SNEAKY,ARCHER,10,400,ATTACK_DUNGEON_HEART,0)
	
CREATE_PARTY(REVENGE)
	ADD_TO_PARTY(REVENGE,KNIGHT,10,1000,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(REVENGE,GIANT,8,0,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(REVENGE,BARBARIAN,8,0,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(REVENGE,SAMURAI,8,0,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(REVENGE,MONK,8,0,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(REVENGE,FAIRY,8,0,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(REVENGE,WIZARD,8,0,ATTACK_DUNGEON_HEART,0)
	
CREATE_PARTY(ROOM1)
	ADD_TO_PARTY(ROOM1,THIEF,10,250,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(ROOM1,THIEF,10,250,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(ROOM1,THIEF,10,250,ATTACK_ENEMIES,0)
	
CREATE_PARTY(ROOM2)
	ADD_TO_PARTY(ROOM2,DWARFA,8,250,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(ROOM2,DWARFA,8,250,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(ROOM2,DWARFA,8,250,ATTACK_ENEMIES,0)
	
CREATE_PARTY(ROOM3)
	ADD_TO_PARTY(ROOM3,GIANT,8,250,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(ROOM3,GIANT,8,250,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(ROOM3,GIANT,8,250,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(ROOM3,SAMURAI,8,450,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(ROOM3,SAMURAI,8,450,ATTACK_ENEMIES,0)
	
CREATE_PARTY(ROOM4)
	ADD_TO_PARTY(ROOM4,BARBARIAN,8,150,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(ROOM4,BARBARIAN,8,150,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(ROOM4,BARBARIAN,10,150,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(ROOM4,BARBARIAN,10,150,ATTACK_ENEMIES,0)	
	ADD_TO_PARTY(ROOM4,FAIRY,8,0,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(ROOM4,FAIRY,8,0,ATTACK_ENEMIES,0)

CREATE_PARTY(ROOM5)
	ADD_TO_PARTY(ROOM5,BARBARIAN,6,100,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(ROOM5,BARBARIAN,6,100,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(ROOM5,SAMURAI,6,0,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(ROOM5,SAMURAI,6,0,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(ROOM5,SAMURAI,6,0,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(ROOM5,WIZARD,6,0,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(ROOM5,WIZARD,6,0,ATTACK_ENEMIES,0)

CREATE_PARTY(ROOM_HEAL)
	ADD_TO_PARTY(ROOM_HEAL,WIZARD,6,200,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(ROOM_HEAL,WIZARD,6,200,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(ROOM_HEAL,FAIRY,6,0,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(ROOM_HEAL,FAIRY,6,0,ATTACK_ENEMIES,0)

CREATE_PARTY(SNEAKY_SMALL)
	ADD_TO_PARTY(SNEAKY_SMALL,THIEF,5,500,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(SNEAKY_SMALL,THIEF,5,500,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(SNEAKY_SMALL,THIEF,5,500,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(SNEAKY_SMALL,ARCHER,3,250,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(SNEAKY_SMALL,ARCHER,3,250,ATTACK_ENEMIES,0)
REM----------------------------------------

QUICK_OBJECTIVE(1,"这几天你听到了一些奇怪的声音，但当一群盗贼偷走你的金子时，事情已经变得太严重了。你的一个手下看到他们向东逃窜，你意识到是时候看看外面发生了什么事了。我们必须确保这片土地属于我们！",PLAYER0)

IF(PLAYER0,RESEARCH>=20)
	QUICK_INFORMATION(2,"你在想“嗯……图书馆……我们已经很久没读过书了……太老派了……那些来自当地森林的德鲁伊，想为你做一些研究，无论如何……”。由于你的王国里没有人对书籍感兴趣，所以没有预算用于图书馆的扩建或维修。",Player0)
	ADD_CREATURE_TO_LEVEL(PLAYER0,DRUID,PLAYER0,3,4,0)
ENDIF

IF_ACTION_POINT(6,PLAYER0)
	QUICK_INFORMATION(17,"当你忙着屠杀那些敌人时，你的一个手下听到他们在谈论他们正在寻找的一名法师。他们建造了一座堡垒，并在陆地上散布了几个前哨站，以寻找他。虽然你对这样的消息不感兴趣，但有传言说他最后一次出现在西南树林深处的一个洞穴里，然后就消失了。",PLAYER0)
ENDIF
	
IF_ACTION_POINT(1,PLAYER0)
	QUICK_INFORMATION(3,"你的小鬼回来告诉你，他们在东边发现了破坏墙壁的魔法，但他们无法到达那里。",3)
	set_timer(PLAYER0,TIMER0)
ENDIF
	
IF(PLAYER0,TIMER0>=500)
	QUICK_INFORMATION(4,"我明白了！如果我们听说的那个法师是真的，也许他可以帮助我们获得访问权限！等等，你还记得他最后一次出现在哪里吗？",3)
ENDIF

IF(PLAYER0,TIMER0>=1000)
	QUICK_OBJECTIVE(20,"找到法师并寻求他的帮助，记住他很难找到。他很可能使用了一些魔法技巧来隐藏自己。",3)
ENDIF

IF(PLAYER0,TIME_MAGE>=1)
	ROOM_AVAILABLE(ALL_PLAYERS,RESEARCH,1,1)
	CHANGE_SLAB_OWNER(82,48,PLAYER0)
	set_timer(PLAYER0,TIMER1)
	QUICK_OBJECTIVE(5,"我们找到法师了！原来他确实会一些魔术。“我会给你你想要的通道，”他说。等着桥出现时，你面前发生了一件奇怪的事情！土壤变色了吗？“好吧，既然你确实问了通道，而我只是想恶作剧，所以我没有用简单的方法 ;-)！”他眨了眨眼。如果没有人能帮你拿到它，那么有时你必须接手并自己动手……也许你应该重新考虑每个人的工资！",3)
ENDIF

IF_SLAB_OWNER(20,71,PLAYER0)
	IF_SLAB_OWNER(78,49,PLAYER_GOOD)
		QUICK_OBJECTIVE(8,"嗯……死路一条……我们必须另辟蹊径。事实上，一定有人发明了某种魔法来破坏墙壁之类的东西！对于像你这样的懒惰守护者来说，生活会轻松得多。",2)
	ENDIF
	IF_AVAILABLE(PLAYER0,POWER_DESTROY_WALLS>0)
		QUICK_OBJECTIVE(7,"干得好，我们找到了破坏墙壁的魔法！进入这座堡垒，这样我们就可以一劳永逸地结束这一切！",2)
	ENDIF
ENDIF		

IF_SLAB_OWNER(22,71,PLAYER0)
	QUICK_INFORMATION(15,"不！！你把你的魔法书掉进熔岩里了，你这个蠢货……！",2)
	MAGIC_AVAILABLE(ALL_PLAYERS,POWER_DESTROY_WALLS,0,0)
ENDIF

IF(PLAYER0,TEMPLE>=1)
	ROOM_AVAILABLE(ALL_PLAYERS,TEMPLE,1,1)
	set_timer(Player0,TIMER2)
ENDIF

IF(PLAYER0,GRAVEYARD>=1)
	ROOM_AVAILABLE(ALL_PLAYERS,GRAVEYARD,1,1)
ENDIF

IF(PLAYER0,TORTURE>=1)
	ROOM_AVAILABLE(ALL_PLAYERS,TORTURE,1,1)
ENDIF

IF(PLAYER0,PRISON>=1)
	ROOM_AVAILABLE(ALL_PLAYERS,PRISON,1,1)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SNEAKY_SMALL,-1,2)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SNEAKY_SMALL,-2,2)
ENDIF

IF(PLAYER0,MANUFACTURE_GOLD>=10000)
	QUICK_INFORMATION(9,"“今天，仅限今天，您梦想中的大门可享受 50% 的折扣！”……守护者，您真的没有其他事情要做吗？这个王国不是商店……",Player0)
ENDIF

IF(PLAYER0,MANUFACTURE_GOLD>=20000)
	QUICK_INFORMATION(10,"好吧，看看你！终于成长为一个真正的商人了！我还以为你是世上最邪恶的人呢！好吧，但如果没有客户，商人还有什么用呢？",Player0)
	set_timer(PLAYER0,TIMER3)
ENDIF

IF(PLAYER0,MANUFACTURE_GOLD>=40000)
	QUICK_INFORMATION(19,"那是什么？又是黑色星期五？",Player0)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,CUSTOMERS,-1,3)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,CUSTOMERS,-2,1)
ENDIF		

IF(PLAYER0,TIMER1 >= 800)
	QUICK_INFORMATION(12,"很快就会有消息传开说你就是那个找到法师的人，他们会试图搜查你的地下城并把他干掉！",Player0)
ENDIF

IF(PLAYER0,TIMER1 >= 1500)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,MAGE,-1,1)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,MAGE,-3,1)
ENDIF

IF(PLAYER0,TIMER1 >= 2500)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SNEAKY,-2,1)
ENDIF
	
IF(PLAYER0,TIMER2 >= 250)
	QUICK_INFORMATION(14,"看来他们并不觉得好笑，他们确实想要恢复那座寺庙......想知道他们在崇拜什么吗？",Player0)
ENDIF

IF(PLAYER0,TIMER2 >= 600)	
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,TEMPLE,-2,1)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,TEMPLE,-3,1)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,TEMPLE,4,1)
ENDIF

IF(PLAYER0,TIMER2 >= 1250)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SNEAKY,-1,1)
ENDIF

IF(PLAYER0,TIMER2 >= 2750)
	QUICK_INFORMATION(13,"小心！似乎有什么东西从我们发现的那座黑暗神殿里升起！",Player0)
ENDIF

IF(PLAYER0,TIMER2 >= 2900)	
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,HORNIES,4,1)
ENDIF

IF(PLAYER0,TIMER3 >= 350)
	QUICK_INFORMATION(11,"黑色星期五已经开始了，连巨人都来了。大人物 = 大门 = 让守护者大赚一笔！哦等等……他们不是来买东西的！啊，他们只是嫉妒你的门……",Player0)
ENDIF

IF(PLAYER0,TIMER3 >= 750)	
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,CUSTOMERS,-1,1)
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,CUSTOMERS,-2,2)
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,CUSTOMERS,-3,1)
ENDIF

IF(PLAYER0,TIMER3 >= 3000)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SNEAKY,-1,1)
ENDIF
		
IF_ACTION_POINT(5,PLAYER0)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,LANDLORD,-4,1)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,TEMPLE,-4,1)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SNEAKY,-1,1)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SNEAKY,-5,1)
ENDIF

IF (PLAYER_GOOD,DUNGEON_DESTROYED == 1)
	SET_TIMER(PLAYER0,TIMER4)
	QUICK_OBJECTIVE(16,"敌人的心脏已摧毁！这就是你所能得到的一切吗？！",Player0)
ENDIF

IF (Player0,TIMER4>600)
	QUICK_INFORMATION(18,"哦不，这是什么声音？敌人？？？看来国王有几个兄弟姐妹，他们想要报仇！而且他们还带来了很多朋友！",Player0)
ENDIF

IF (Player0,TIMER4>1000)
ADD_PARTY_TO_LEVEL(PLAYER_GOOD,REVENGE,-2,1)
ADD_PARTY_TO_LEVEL(PLAYER_GOOD,TEMPLE,-3,1)
ADD_PARTY_TO_LEVEL(PLAYER_GOOD,ROOM2,-1,1)
ENDIF

IF(PLAYER0,TIMER4>1500)
ADD_PARTY_TO_LEVEL(PLAYER_GOOD,REVENGE,-3,1)
ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SNEAKY_SMALL,-1,2)
ADD_PARTY_TO_LEVEL(PLAYER_GOOD,ROOM2,-5,2)
ENDIF

IF(PLAYER0,TIMER4>2000)
ADD_PARTY_TO_LEVEL(PLAYER_GOOD,REVENGE,-4,1)
ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SNEAKY,-1,1)
ADD_PARTY_TO_LEVEL(PLAYER_GOOD,ROOM5,-4,1)
ADD_PARTY_TO_LEVEL(PLAYER_GOOD,ROOM5,-2,1)
ENDIF

IF_ACTION_POINT(8,PLAYER0)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,REVEAL,8,2)
ENDIF

IF_ACTION_POINT(9,PLAYER0)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,ROOM1,9,2)
ENDIF

IF_ACTION_POINT(10,PLAYER0)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,ROOM3,10,1)
ENDIF

IF_ACTION_POINT(11,PLAYER0)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,ROOM2,11,4)
ENDIF

IF_ACTION_POINT(12,PLAYER0)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,ROOM4,12,1)
ENDIF

IF_ACTION_POINT(13,PLAYER0)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,ROOM3,13,1)
ENDIF

IF_ACTION_POINT(14,PLAYER0)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,ROOM_HEAL,14,1)
ENDIF

IF_ACTION_POINT(15,PLAYER0)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,ROOM2,7,1)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SNEAKY_SMALL,-1,1)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SNEAKY_SMALL,-3,1)
ENDIF

IF (PLAYER_GOOD,DUNGEON_DESTROYED == 1)
	IF(Player0,TIMER4>2000)
		If(Player_Good,TOTAL_CREATURES == 0)
			QUICK_INFORMATION(30,"好吧，你成功了，这片土地属于你了，你与那些德鲁伊结成了新的联盟。这帮书呆子，他们确实帮了你很多！也许你从现在起应该多读书了。别忘了，德鲁伊确实邀请你参加下一次烧烤，这样他们就可以带你去他们居住的地方上方的树林！谢谢你的游玩，希望你喜欢我的第一张地图。由 Crokydriver 制作",Player0)
			WIN_GAME
		ENDIF	
	ENDIF
ENDIF

IF (PLAYER0,DUNGEON_DESTROYED == 1)
	LOSE_GAME
ENDIF