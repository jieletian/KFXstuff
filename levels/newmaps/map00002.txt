REM # General Settings
LEVEL_VERSION(1)
RUN_AFTER_VICTORY(1)

REM # Computer Settings
COMPUTER_PLAYER(PLAYER1,1)
COMPUTER_PLAYER(PLAYER2,0)
COMPUTER_PLAYER(PLAYER3,6)

REM # Map Rules
SET_GENERATE_SPEED(300)
START_MONEY(ALL_PLAYERS,30000)
MAX_CREATURES(ALL_PLAYERS,32)

REM # Creatures' Pool
ADD_CREATURE_TO_POOL(DARK_MISTRESS,32)
ADD_CREATURE_TO_POOL(HORNY,4)
ADD_CREATURE_TO_POOL(BUG,32)
ADD_CREATURE_TO_POOL(FLY,32)
ADD_CREATURE_TO_POOL(SPIDER,32)

REM # Creatures' Availablity
CREATURE_AVAILABLE(ALL_PLAYERS,DARK_MISTRESS,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,HORNY,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,BUG,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,FLY,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,SPIDER,1,1)

REM # Keeper's Rooms Availablity
ROOM_AVAILABLE(ALL_PLAYERS,RESEARCH,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,LAIR,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,GARDEN,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,TREASURE,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,TRAINING,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,TORTURE,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,GRAVEYARD,1,1)

REM # Keeper's Powers Availablity
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HAND,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SLAP,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_POSSESS,1,1)
MAGIC_AVAILABLE(PLAYER0,POWER_IMP,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CALL_TO_ARMS,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SPEED,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CONCEAL,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_PROTECT,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HEAL_CREATURE,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CHICKEN,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_DISEASE,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_LIGHTNING,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CAVE_IN,1,0)

REM # Setup Pandora's Box party for Heroic Assault and Backlash of Conquest - they can vary between map.
CREATE_PARTY(HRCSSLT)
ADD_TO_PARTY(HRCSSLT,KNIGHT,8,800,ATTACK_ENEMIES,0)
ADD_TO_PARTY(HRCSSLT,ARCHER,8,800,ATTACK_ENEMIES,0)
ADD_TO_PARTY(HRCSSLT,BARBARIAN,8,800,ATTACK_ENEMIES,0)
ADD_TO_PARTY(HRCSSLT,DWARFA,8,800,ATTACK_ENEMIES,0)
ADD_TO_PARTY(HRCSSLT,FAIRY,8,800,ATTACK_ENEMIES,0)
ADD_TO_PARTY(HRCSSLT,GIANT,8,800,ATTACK_ENEMIES,0)
ADD_TO_PARTY(HRCSSLT,WITCH,8,800,ATTACK_ENEMIES,0)
ADD_TO_PARTY(HRCSSLT,WIZARD,8,800,ATTACK_ENEMIES,0)

CREATE_PARTY(CNQSTA)
ADD_TO_PARTY(CNQSTA,KNIGHT,10,0,SNIPE_DUNGEON_HEART,0)
ADD_TO_PARTY(CNQSTA,ARCHER,10,0,SNIPE_DUNGEON_HEART,0)
ADD_TO_PARTY(CNQSTA,ARCHER,10,0,SNIPE_DUNGEON_HEART,0)
ADD_TO_PARTY(CNQSTA,ARCHER,10,0,SNIPE_DUNGEON_HEART,0)

CREATE_PARTY(CNQSTB)
ADD_TO_PARTY(CNQSTB,KNIGHT,10,0,SABOTAGE_ROOMS,0)
ADD_TO_PARTY(CNQSTB,BARBARIAN,10,0,SABOTAGE_ROOMS,0)
ADD_TO_PARTY(CNQSTB,BARBARIAN,10,0,SABOTAGE_ROOMS,0)
ADD_TO_PARTY(CNQSTB,BARBARIAN,10,0,SABOTAGE_ROOMS,0)

CREATE_PARTY(CNQSTC)
ADD_TO_PARTY(CNQSTC,KNIGHT,10,0,STEAL_GOLD,0)
ADD_TO_PARTY(CNQSTC,DWARFA,10,0,STEAL_GOLD,0)
ADD_TO_PARTY(CNQSTC,DWARFA,10,0,STEAL_GOLD,0)
ADD_TO_PARTY(CNQSTC,DWARFA,10,0,STEAL_GOLD,0)

CREATE_PARTY(CNQSTD)
ADD_TO_PARTY(CNQSTD,KNIGHT,10,0,ATTACK_DUNGEON_HEART,0)
ADD_TO_PARTY(CNQSTD,FAIRY,10,0,ATTACK_DUNGEON_HEART,0)
ADD_TO_PARTY(CNQSTD,FAIRY,10,0,ATTACK_DUNGEON_HEART,0)
ADD_TO_PARTY(CNQSTD,FAIRY,10,0,ATTACK_DUNGEON_HEART,0)

CREATE_PARTY(CNQSTE)
ADD_TO_PARTY(CNQSTE,KNIGHT,10,0,SNIPE_DUNGEON_HEART,0)
ADD_TO_PARTY(CNQSTE,GIANT,10,0,SNIPE_DUNGEON_HEART,0)
ADD_TO_PARTY(CNQSTE,GIANT,10,0,SNIPE_DUNGEON_HEART,0)
ADD_TO_PARTY(CNQSTE,GIANT,10,0,SNIPE_DUNGEON_HEART,0)

CREATE_PARTY(CNQSTF)
ADD_TO_PARTY(CNQSTF,KNIGHT,10,0,STEAL_SPELLS,0)
ADD_TO_PARTY(CNQSTF,MONK,10,0,STEAL_SPELLS,0)
ADD_TO_PARTY(CNQSTF,MONK,10,0,STEAL_SPELLS,0)
ADD_TO_PARTY(CNQSTF,MONK,10,0,STEAL_SPELLS,0)

CREATE_PARTY(CNQSTG)
ADD_TO_PARTY(CNQSTG,KNIGHT,10,0,ATTACK_DUNGEON_HEART,0)
ADD_TO_PARTY(CNQSTG,SAMURAI,10,0,ATTACK_DUNGEON_HEART,0)
ADD_TO_PARTY(CNQSTG,SAMURAI,10,0,ATTACK_DUNGEON_HEART,0)
ADD_TO_PARTY(CNQSTG,SAMURAI,10,0,ATTACK_DUNGEON_HEART,0)

CREATE_PARTY(CNQSTH)
ADD_TO_PARTY(CNQSTH,KNIGHT,10,0,STEAL_GOLD,0)
ADD_TO_PARTY(CNQSTH,THIEF,10,0,STEAL_GOLD,0)
ADD_TO_PARTY(CNQSTH,THIEF,10,0,STEAL_GOLD,0)
ADD_TO_PARTY(CNQSTH,THIEF,10,0,STEAL_GOLD,0)

CREATE_PARTY(CNQSTI)
ADD_TO_PARTY(CNQSTI,KNIGHT,10,0,SABOTAGE_ROOMS,0)
ADD_TO_PARTY(CNQSTI,WITCH,10,0,SABOTAGE_ROOMS,0)
ADD_TO_PARTY(CNQSTI,WITCH,10,0,SABOTAGE_ROOMS,0)
ADD_TO_PARTY(CNQSTI,WITCH,10,0,SABOTAGE_ROOMS,0)

CREATE_PARTY(CNQSTJ)
ADD_TO_PARTY(CNQSTJ,KNIGHT,10,0,STEAL_SPELLS,0)
ADD_TO_PARTY(CNQSTJ,WIZARD,10,0,STEAL_SPELLS,0)
ADD_TO_PARTY(CNQSTJ,WIZARD,10,0,STEAL_SPELLS,0)
ADD_TO_PARTY(CNQSTJ,WIZARD,10,0,STEAL_SPELLS,0)

REM # Set tooltip for each possible effect.
SET_BOX_TOOLTIP(0,"神秘宝盒：请把鼠标放在宝盒上面阅读它的信息。")
SET_BOX_TOOLTIP(1,"冰霜宝盒：所有敌人被冰冻。")
SET_BOX_TOOLTIP(2,"保护宝盒：所有友军获得保护术。")
SET_BOX_TOOLTIP(3,"护盾宝盒：所有友军获得反弹术。")
SET_BOX_TOOLTIP(4,"治疗宝盒：所有友军获得治疗。")
SET_BOX_TOOLTIP(5,"消失宝盒：所有友方小鬼获得隐身术。")
SET_BOX_TOOLTIP(6,"束缚宝盒：把所有敌人送回巢穴。")
SET_BOX_TOOLTIP(7,"救援宝盒：把所有友军送回巢穴。")
SET_BOX_TOOLTIP(8,"停战宝盒：把双方所有怪物送回巢穴。")
SET_BOX_TOOLTIP(9,"急速宝盒：所有友军获得速度术。")
SET_BOX_TOOLTIP(10,"蛛网宝盒：所有敌人被减速。")
SET_BOX_TOOLTIP(11,"发光宝盒：所有小鬼获得发光术。")
SET_BOX_TOOLTIP(12,"漂浮宝盒：所有友军获得飞空术。")
SET_BOX_TOOLTIP(13,"视野宝盒：所有友军获得邪眼术。")
SET_BOX_TOOLTIP(14,"瘟疫宝盒：所有敌军获得疾病术。")
SET_BOX_TOOLTIP(15,"妖术宝盒：所有敌军被变成小鸡。")
SET_BOX_TOOLTIP(16,"雷霆宝盒：对踏入友方领地的随机敌人释放闪电术，持续1分钟。")
SET_BOX_TOOLTIP(17,"落石宝盒：对踏入友方领地的随机敌人释放塌方术，持续1分钟。")
SET_BOX_TOOLTIP(18,"英雄宝盒：为你召唤一个随机的英雄。")
SET_BOX_TOOLTIP(19,"邪恶宝盒：为你召唤一个随机的怪物。")
SET_BOX_TOOLTIP(20,"死灵宝盒：根据你上一场战斗死亡的怪物数量，为你召唤同样数量的亡灵。")
SET_BOX_TOOLTIP(21,"愤怒宝盒：所有怪物立即开始生气。")
SET_BOX_TOOLTIP(22,"快乐宝盒：所有友军获得快乐。")
SET_BOX_TOOLTIP(23,"地下城宝盒：你的地下城心脏回复全部生命值。")
SET_BOX_TOOLTIP(24,"守护者宝盒：使所有友军获得全部增益效果。")
SET_BOX_TOOLTIP(25,"潘多拉魔盒：没人知道打开它会发生什么……")
SET_BOX_TOOLTIP(225,"失忆宝盒：你失去制造小鬼能力。")
SET_BOX_TOOLTIP(226,"惊叫宝盒：一个笨拙的小鬼把咖啡撒在了你身上，打所有小鬼一巴掌！")
SET_BOX_TOOLTIP(227,"入侵宝盒：随机召唤一队英雄前来进攻你。")
SET_BOX_TOOLTIP(228,"叛乱宝盒：所有友军立即开始生气，并且可能会造反。持续1分钟。")
SET_BOX_TOOLTIP(229,"天谴宝盒：对友方领地的随机友军释放闪电术，持续1分钟。")
SET_BOX_TOOLTIP(230,"崩塌宝盒：对友方领地的随机友军释放塌方术，持续1分钟。")
SET_BOX_TOOLTIP(231,"复仇之渊:为你召唤一名圣者，但是5分钟后他会叛变并召唤敌军。如果你再次使用此宝盒，则会召唤冥界使者前来攻击你。")
SET_BOX_TOOLTIP(232,"世界毁灭:消灭所有友军，并召唤等量的骑士，并使他们获得所有增益效果。但要注意：5分钟后他们会全数叛变！")

REM # There is an issue with these Mysterious Box, nothing happen when you first use them, this is a workaround.
SET_FLAG(PLAYER0,BOX0_ACTIVATED,1)
SET_FLAG(PLAYER0,BOX18_ACTIVATED,1)
SET_FLAG(PLAYER0,BOX19_ACTIVATED,1)
SET_FLAG(PLAYER0,BOX25_ACTIVATED,1)

REM # Get a random value to determine what effect the Mysterious Box hold.
IF(PLAYER0,BOX0_ACTIVATED > 0)
	NEXT_COMMAND_REUSABLE
	RANDOMISE_FLAG(PLAYER0,BOX100_ACTIVATED,25)
	IF(PLAYER0,BOX100_ACTIVATED == 1)
		NEXT_COMMAND_REUSABLE
		ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,LAST_EVENT,1,PLAYER0)
	ENDIF
	IF(PLAYER0,BOX100_ACTIVATED == 2)
		NEXT_COMMAND_REUSABLE
		ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,LAST_EVENT,2,PLAYER0)
	ENDIF
	IF(PLAYER0,BOX100_ACTIVATED == 3)
		NEXT_COMMAND_REUSABLE
		ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,LAST_EVENT,3,PLAYER0)
	ENDIF
	IF(PLAYER0,BOX100_ACTIVATED == 4)
		NEXT_COMMAND_REUSABLE
		ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,LAST_EVENT,4,PLAYER0)
	ENDIF
	IF(PLAYER0,BOX100_ACTIVATED == 5)
		NEXT_COMMAND_REUSABLE
		ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,LAST_EVENT,5,PLAYER0)
	ENDIF
	IF(PLAYER0,BOX100_ACTIVATED == 6)
		NEXT_COMMAND_REUSABLE
		ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,LAST_EVENT,6,PLAYER0)
	ENDIF
	IF(PLAYER0,BOX100_ACTIVATED == 7)
		NEXT_COMMAND_REUSABLE
		ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,LAST_EVENT,7,PLAYER0)
	ENDIF
	IF(PLAYER0,BOX100_ACTIVATED == 8)
		NEXT_COMMAND_REUSABLE
		ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,LAST_EVENT,8,PLAYER0)
	ENDIF
	IF(PLAYER0,BOX100_ACTIVATED == 9)
		NEXT_COMMAND_REUSABLE
		ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,LAST_EVENT,9,PLAYER0)
	ENDIF
	IF(PLAYER0,BOX100_ACTIVATED == 10)
		NEXT_COMMAND_REUSABLE
		ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,LAST_EVENT,10,PLAYER0)
	ENDIF
	IF(PLAYER0,BOX100_ACTIVATED == 11)
		NEXT_COMMAND_REUSABLE
		ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,LAST_EVENT,11,PLAYER0)
	ENDIF
	IF(PLAYER0,BOX100_ACTIVATED == 12)
		NEXT_COMMAND_REUSABLE
		ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,LAST_EVENT,12,PLAYER0)
	ENDIF
	IF(PLAYER0,BOX100_ACTIVATED == 13)
		NEXT_COMMAND_REUSABLE
		ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,LAST_EVENT,13,PLAYER0)
	ENDIF
	IF(PLAYER0,BOX100_ACTIVATED == 14)
		NEXT_COMMAND_REUSABLE
		ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,LAST_EVENT,14,PLAYER0)
	ENDIF
	IF(PLAYER0,BOX100_ACTIVATED == 15)
		NEXT_COMMAND_REUSABLE
		ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,LAST_EVENT,15,PLAYER0)
	ENDIF
	IF(PLAYER0,BOX100_ACTIVATED == 16)
		NEXT_COMMAND_REUSABLE
		ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,LAST_EVENT,16,PLAYER0)
	ENDIF
	IF(PLAYER0,BOX100_ACTIVATED == 17)
		NEXT_COMMAND_REUSABLE
		ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,LAST_EVENT,17,PLAYER0)
	ENDIF
	IF(PLAYER0,BOX100_ACTIVATED == 18)
		NEXT_COMMAND_REUSABLE
		ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,LAST_EVENT,18,PLAYER0)
	ENDIF
	IF(PLAYER0,BOX100_ACTIVATED == 19)
		NEXT_COMMAND_REUSABLE
		ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,LAST_EVENT,19,PLAYER0)
	ENDIF
	IF(PLAYER0,BOX100_ACTIVATED == 20)
		NEXT_COMMAND_REUSABLE
		ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,LAST_EVENT,20,PLAYER0)
	ENDIF
	IF(PLAYER0,BOX100_ACTIVATED == 21)
		NEXT_COMMAND_REUSABLE
		ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,LAST_EVENT,21,PLAYER0)
	ENDIF
	IF(PLAYER0,BOX100_ACTIVATED == 22)
		NEXT_COMMAND_REUSABLE
		ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,LAST_EVENT,22,PLAYER0)
	ENDIF
	IF(PLAYER0,BOX100_ACTIVATED == 23)
		NEXT_COMMAND_REUSABLE
		ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,LAST_EVENT,23,PLAYER0)
	ENDIF
	IF(PLAYER0,BOX100_ACTIVATED == 24)
		NEXT_COMMAND_REUSABLE
		ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,LAST_EVENT,24,PLAYER0)
	ENDIF
	IF(PLAYER0,BOX100_ACTIVATED == 25)
		NEXT_COMMAND_REUSABLE
		ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,LAST_EVENT,25,PLAYER0)
	ENDIF
	NEXT_COMMAND_REUSABLE
	SET_FLAG(PLAYER0,BOX0_ACTIVATED,0)
ENDIF

REM # "Frostbite Box: Cast Freeze on all hostile minions."
IF(PLAYER0,BOX1_ACTIVATED > 0)
	REM # For the demonstration each time the player use a special box, respawn it.
	NEXT_COMMAND_REUSABLE
	ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,LAST_EVENT,1,PLAYER0)
	REM # Remove the two lines above for a normal gameplay.
	NEXT_COMMAND_REUSABLE
	USE_SPELL_ON_PLAYERS_CREATURES(PLAYER1,ANY_CREATURE,SPELL_FREEZE)
	NEXT_COMMAND_REUSABLE
	USE_SPELL_ON_PLAYERS_CREATURES(PLAYER2,ANY_CREATURE,SPELL_FREEZE)
	NEXT_COMMAND_REUSABLE
	USE_SPELL_ON_PLAYERS_CREATURES(PLAYER3,ANY_CREATURE,SPELL_FREEZE)
	NEXT_COMMAND_REUSABLE
	USE_SPELL_ON_PLAYERS_CREATURES(PLAYER_GOOD,ANY_CREATURE,SPELL_FREEZE)
	NEXT_COMMAND_REUSABLE
	SET_FLAG(PLAYER0,BOX1_ACTIVATED,0)
ENDIF

REM # "Protect Box: Cast Armour on all friendly minions."
IF(PLAYER0,BOX2_ACTIVATED > 0)
	REM # For the demonstration each time the player use a special box, respawn it.
	NEXT_COMMAND_REUSABLE
	ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,LAST_EVENT,2,PLAYER0)
	REM # Remove the two lines above for a normal gameplay.
	NEXT_COMMAND_REUSABLE
	USE_SPELL_ON_PLAYERS_CREATURES(PLAYER0,ANY_CREATURE,SPELL_ARMOUR)
	NEXT_COMMAND_REUSABLE
	USE_SPELL_ON_PLAYERS_CREATURES(PLAYER0,IMP,SPELL_ARMOUR)
	NEXT_COMMAND_REUSABLE
	SET_FLAG(PLAYER0,BOX2_ACTIVATED,0)
ENDIF

REM # "Shell Box: Cast Rebound on all friendly minions."
IF(PLAYER0,BOX3_ACTIVATED > 0)
	REM # For the demonstration each time the player use a special box, respawn it.
	NEXT_COMMAND_REUSABLE
	ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,LAST_EVENT,3,PLAYER0)
	REM # Remove the two lines above for a normal gameplay.
	NEXT_COMMAND_REUSABLE
	USE_SPELL_ON_PLAYERS_CREATURES(PLAYER0,ANY_CREATURE,SPELL_REBOUND)
	NEXT_COMMAND_REUSABLE
	USE_SPELL_ON_PLAYERS_CREATURES(PLAYER0,IMP,SPELL_REBOUND)
	NEXT_COMMAND_REUSABLE
	SET_FLAG(PLAYER0,BOX3_ACTIVATED,0)
ENDIF

REM # "Cure Box: Cast Heal on all friendly minions."
IF(PLAYER0,BOX4_ACTIVATED > 0)
	REM # For the demonstration each time the player use a special box, respawn it.
	NEXT_COMMAND_REUSABLE
	ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,LAST_EVENT,4,PLAYER0)
	REM # Remove the two lines above for a normal gameplay.
	NEXT_COMMAND_REUSABLE
	USE_SPELL_ON_PLAYERS_CREATURES(PLAYER0,ANY_CREATURE,SPELL_HEAL)
	NEXT_COMMAND_REUSABLE
	USE_SPELL_ON_PLAYERS_CREATURES(PLAYER0,IMP,SPELL_HEAL)
	NEXT_COMMAND_REUSABLE
	SET_FLAG(PLAYER0,BOX4_ACTIVATED,0)
ENDIF

REM # "Vanish Box: Cast Invisibility on all friendly imps."
IF(PLAYER0,BOX5_ACTIVATED > 0)
	REM # For the demonstration each time the player use a special box, respawn it.
	NEXT_COMMAND_REUSABLE
	ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,LAST_EVENT,5,PLAYER0)
	REM # Remove the two lines above for a normal gameplay.
	NEXT_COMMAND_REUSABLE
	USE_SPELL_ON_PLAYERS_CREATURES(PLAYER0,IMP,SPELL_INVISIBILITY)
	NEXT_COMMAND_REUSABLE
	SET_FLAG(PLAYER0,BOX5_ACTIVATED,0)
ENDIF

REM # "Warp Box: Teleport all hostile minions to their lair."
IF(PLAYER0,BOX6_ACTIVATED > 0)
	REM # For the demonstration each time the player use a special box, respawn it.
	NEXT_COMMAND_REUSABLE
	ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,LAST_EVENT,6,PLAYER0)
	REM # Remove the two lines above for a normal gameplay.
	NEXT_COMMAND_REUSABLE
	USE_SPELL_ON_PLAYERS_CREATURES(PLAYER1,ANY_CREATURE,SPELL_TELEPORT)
	NEXT_COMMAND_REUSABLE
	USE_SPELL_ON_PLAYERS_CREATURES(PLAYER2,ANY_CREATURE,SPELL_TELEPORT)
	NEXT_COMMAND_REUSABLE
	USE_SPELL_ON_PLAYERS_CREATURES(PLAYER3,ANY_CREATURE,SPELL_TELEPORT)
	NEXT_COMMAND_REUSABLE
	USE_SPELL_ON_PLAYERS_CREATURES(PLAYER_GOOD,ANY_CREATURE,SPELL_TELEPORT)
	NEXT_COMMAND_REUSABLE
	SET_FLAG(PLAYER0,BOX6_ACTIVATED,0)
ENDIF

REM # "Rescue Box: Teleport your own minions to their lair."
IF(PLAYER0,BOX7_ACTIVATED > 0)
	REM # For the demonstration each time the player use a special box, respawn it.
	NEXT_COMMAND_REUSABLE
	ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,LAST_EVENT,7,PLAYER0)
	REM # Remove the two lines above for a normal gameplay.
	NEXT_COMMAND_REUSABLE
	USE_SPELL_ON_PLAYERS_CREATURES(PLAYER0,ANY_CREATURE,SPELL_TELEPORT)
	NEXT_COMMAND_REUSABLE
	SET_FLAG(PLAYER0,BOX7_ACTIVATED,0)
ENDIF

REM # "Cease-fire Box: Teleport all minions to their lair."
IF(PLAYER0,BOX8_ACTIVATED > 0)
	REM # For the demonstration each time the player use a special box, respawn it.
	NEXT_COMMAND_REUSABLE
	ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,LAST_EVENT,8,PLAYER0)
	REM # Remove the two lines above for a normal gameplay.
	NEXT_COMMAND_REUSABLE
	USE_SPELL_ON_PLAYERS_CREATURES(ALL_PLAYERS,ANY_CREATURE,SPELL_TELEPORT)
	NEXT_COMMAND_REUSABLE
	USE_SPELL_ON_PLAYERS_CREATURES(ALL_PLAYERS,IMP,SPELL_TELEPORT)
	NEXT_COMMAND_REUSABLE
	SET_FLAG(PLAYER0,BOX8_ACTIVATED,0)
ENDIF

REM # "Haste Box: Cast Speed on all friendly minions."
IF(PLAYER0,BOX9_ACTIVATED > 0)
	REM # For the demonstration each time the player use a special box, respawn it.
	NEXT_COMMAND_REUSABLE
	ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,LAST_EVENT,9,PLAYER0)
	REM # Remove the two lines above for a normal gameplay.
	NEXT_COMMAND_REUSABLE
	USE_SPELL_ON_PLAYERS_CREATURES(PLAYER0,ANY_CREATURE,SPELL_SPEED)
	NEXT_COMMAND_REUSABLE
	USE_SPELL_ON_PLAYERS_CREATURES(PLAYER0,IMP,SPELL_SPEED)
	NEXT_COMMAND_REUSABLE
	SET_FLAG(PLAYER0,BOX9_ACTIVATED,0)
ENDIF

REM # "Web Box: Cast Slow on all hostile minions."
IF(PLAYER0,BOX10_ACTIVATED > 0)
	REM # For the demonstration each time the player use a special box, respawn it.
	NEXT_COMMAND_REUSABLE
	ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,LAST_EVENT,10,PLAYER0)
	REM # Remove the two lines above for a normal gameplay.
	NEXT_COMMAND_REUSABLE
	USE_SPELL_ON_PLAYERS_CREATURES(PLAYER1,ANY_CREATURE,SPELL_SLOW)
	NEXT_COMMAND_REUSABLE
	USE_SPELL_ON_PLAYERS_CREATURES(PLAYER2,ANY_CREATURE,SPELL_SLOW)
	NEXT_COMMAND_REUSABLE
	USE_SPELL_ON_PLAYERS_CREATURES(PLAYER3,ANY_CREATURE,SPELL_SLOW)
	NEXT_COMMAND_REUSABLE
	USE_SPELL_ON_PLAYERS_CREATURES(PLAYER_GOOD,ANY_CREATURE,SPELL_SLOW)
	NEXT_COMMAND_REUSABLE
	SET_FLAG(PLAYER0,BOX10_ACTIVATED,0)
ENDIF

REM # "Illumination Box: Cast Light on all imps."
IF(PLAYER0,BOX11_ACTIVATED > 0)
	REM # For the demonstration each time the player use a special box, respawn it.
	NEXT_COMMAND_REUSABLE
	ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,LAST_EVENT,11,PLAYER0)
	REM # Remove the two lines above for a normal gameplay.
	NEXT_COMMAND_REUSABLE
	USE_SPELL_ON_PLAYERS_CREATURES(ALL_PLAYERS,IMP,SPELL_LIGHT)
	NEXT_COMMAND_REUSABLE
	SET_FLAG(PLAYER0,BOX11_ACTIVATED,0)
ENDIF

REM # "Float Box: Cast Flight on all friendly minions."
IF(PLAYER0,BOX12_ACTIVATED > 0)
	REM # For the demonstration each time the player use a special box, respawn it.
	NEXT_COMMAND_REUSABLE
	ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,LAST_EVENT,12,PLAYER0)
	REM # Remove the two lines above for a normal gameplay.
	NEXT_COMMAND_REUSABLE
	USE_SPELL_ON_PLAYERS_CREATURES(PLAYER0,ANY_CREATURE,SPELL_FLIGHT)
	NEXT_COMMAND_REUSABLE
	USE_SPELL_ON_PLAYERS_CREATURES(PLAYER0,IMP,SPELL_FLIGHT)
	NEXT_COMMAND_REUSABLE
	SET_FLAG(PLAYER0,BOX12_ACTIVATED,0)
ENDIF

REM # "Vision Box: Cast Sight on all friendly minions."
IF(PLAYER0,BOX13_ACTIVATED > 0)
	REM # For the demonstration each time the player use a special box, respawn it.
	NEXT_COMMAND_REUSABLE
	ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,LAST_EVENT,13,PLAYER0)
	REM # Remove the two lines above for a normal gameplay.
	NEXT_COMMAND_REUSABLE
	USE_SPELL_ON_PLAYERS_CREATURES(PLAYER0,ANY_CREATURE,SPELL_SIGHT)
	NEXT_COMMAND_REUSABLE
	USE_SPELL_ON_PLAYERS_CREATURES(PLAYER0,IMP,SPELL_SIGHT)
	NEXT_COMMAND_REUSABLE
	SET_FLAG(PLAYER0,BOX13_ACTIVATED,0)
ENDIF

REM # "Plague Box: Cast Disease on all hostile minions."
IF(PLAYER0,BOX14_ACTIVATED > 0)
	REM # For the demonstration each time the player use a special box, respawn it.
	NEXT_COMMAND_REUSABLE
	ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,LAST_EVENT,14,PLAYER0)
	REM # Remove the two lines above for a normal gameplay.
	NEXT_COMMAND_REUSABLE
	USE_SPELL_ON_PLAYERS_CREATURES(PLAYER1,ANY_CREATURE,SPELL_DISEASE)
	NEXT_COMMAND_REUSABLE
	USE_SPELL_ON_PLAYERS_CREATURES(PLAYER2,ANY_CREATURE,SPELL_DISEASE)
	NEXT_COMMAND_REUSABLE
	USE_SPELL_ON_PLAYERS_CREATURES(PLAYER3,ANY_CREATURE,SPELL_DISEASE)
	NEXT_COMMAND_REUSABLE
	USE_SPELL_ON_PLAYERS_CREATURES(PLAYER_GOOD,ANY_CREATURE,SPELL_DISEASE)
	NEXT_COMMAND_REUSABLE
	SET_FLAG(PLAYER0,BOX14_ACTIVATED,0)
ENDIF

REM # "Hex Box: Cast Chicken on all hostile minions."
IF(PLAYER0,BOX15_ACTIVATED > 0)
	REM # For the demonstration each time the player use a special box, respawn it.
	NEXT_COMMAND_REUSABLE
	ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,LAST_EVENT,15,PLAYER0)
	REM # Remove the two lines above for a normal gameplay.
	NEXT_COMMAND_REUSABLE
	USE_SPELL_ON_PLAYERS_CREATURES(PLAYER1,ANY_CREATURE,SPELL_CHICKEN)
	NEXT_COMMAND_REUSABLE
	USE_SPELL_ON_PLAYERS_CREATURES(PLAYER2,ANY_CREATURE,SPELL_CHICKEN)
	NEXT_COMMAND_REUSABLE
	USE_SPELL_ON_PLAYERS_CREATURES(PLAYER3,ANY_CREATURE,SPELL_CHICKEN)
	NEXT_COMMAND_REUSABLE
	USE_SPELL_ON_PLAYERS_CREATURES(PLAYER_GOOD,ANY_CREATURE,SPELL_CHICKEN)
	NEXT_COMMAND_REUSABLE
	SET_FLAG(PLAYER0,BOX15_ACTIVATED,0)
ENDIF

REM # "Wrath Box: Cast Lightning on your territory at random hostile minions, last 60 seconds."
IF(PLAYER0,BOX16_ACTIVATED > 0)
	REM # For the demonstration each time the player use a special box, respawn it.
	NEXT_COMMAND_REUSABLE
	ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,LAST_EVENT,16,PLAYER0)
	REM # Remove the two lines above for a normal gameplay.
	NEXT_COMMAND_REUSABLE
	ADD_TO_FLAG(PLAYER0,FLAG0,1200)
	NEXT_COMMAND_REUSABLE
	SET_FLAG(PLAYER0,BOX16_ACTIVATED,0)
ENDIF

REM # Wrath Box's effect happen every game turn until FLAG0 flag reach 0.
REM # To change duration see above and replace 1200 (60 seconds) with any value you want.
IF(PLAYER0,FLAG0 > 0)
	NEXT_COMMAND_REUSABLE
	USE_POWER_ON_CREATURE(PLAYER1,ANY_CREATURE,ON_ENEMY_GROUND,PLAYER0,POWER_LIGHTNING,1,1)
	NEXT_COMMAND_REUSABLE
	USE_POWER_ON_CREATURE(PLAYER2,ANY_CREATURE,ON_ENEMY_GROUND,PLAYER0,POWER_LIGHTNING,1,1)
	NEXT_COMMAND_REUSABLE
	USE_POWER_ON_CREATURE(PLAYER3,ANY_CREATURE,ON_ENEMY_GROUND,PLAYER0,POWER_LIGHTNING,1,1)
	NEXT_COMMAND_REUSABLE
	USE_POWER_ON_CREATURE(PLAYER_GOOD,ANY_CREATURE,ON_ENEMY_GROUND,PLAYER0,POWER_LIGHTNING,1,1)
	NEXT_COMMAND_REUSABLE
	USE_POWER_ON_CREATURE(PLAYER1,IMP,ON_ENEMY_GROUND,PLAYER0,POWER_LIGHTNING,1,1)
	NEXT_COMMAND_REUSABLE
	USE_POWER_ON_CREATURE(PLAYER2,IMP,ON_ENEMY_GROUND,PLAYER0,POWER_LIGHTNING,1,1)
	NEXT_COMMAND_REUSABLE
	USE_POWER_ON_CREATURE(PLAYER3,IMP,ON_ENEMY_GROUND,PLAYER0,POWER_LIGHTNING,1,1)
	NEXT_COMMAND_REUSABLE
	USE_POWER_ON_CREATURE(PLAYER_GOOD,IMP,ON_ENEMY_GROUND,PLAYER0,POWER_LIGHTNING,1,1)
	NEXT_COMMAND_REUSABLE
	ADD_TO_FLAG(PLAYER0,FLAG0,-1)
ENDIF

REM # "Rockfall Box: Cast Cave-In on your territory at random hostile minions, last 60 seconds."
IF(PLAYER0,BOX17_ACTIVATED > 0)
	REM # For the demonstration each time the player use a special box, respawn it.
	NEXT_COMMAND_REUSABLE
	ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,LAST_EVENT,17,PLAYER0)
	REM # Remove the two lines above for a normal gameplay.
	NEXT_COMMAND_REUSABLE
	ADD_TO_FLAG(PLAYER0,FLAG1,1200)
	NEXT_COMMAND_REUSABLE
	SET_FLAG(PLAYER0,BOX17_ACTIVATED,0)
ENDIF

REM # Rockfall Box's effect happen every game turn until FLAG1 flag reach 0.
REM # To change duration see above and replace 1200 (60 seconds) with any value you want.
IF(PLAYER0,FLAG1 > 0)
	NEXT_COMMAND_REUSABLE
	USE_POWER_ON_CREATURE(PLAYER1,ANY_CREATURE,ON_ENEMY_GROUND,PLAYER0,POWER_CAVE_IN,1,1)
	NEXT_COMMAND_REUSABLE
	USE_POWER_ON_CREATURE(PLAYER2,ANY_CREATURE,ON_ENEMY_GROUND,PLAYER0,POWER_CAVE_IN,1,1)
	NEXT_COMMAND_REUSABLE
	USE_POWER_ON_CREATURE(PLAYER3,ANY_CREATURE,ON_ENEMY_GROUND,PLAYER0,POWER_CAVE_IN,1,1)
	NEXT_COMMAND_REUSABLE
	USE_POWER_ON_CREATURE(PLAYER_GOOD,ANY_CREATURE,ON_ENEMY_GROUND,PLAYER0,POWER_CAVE_IN,1,1)
	NEXT_COMMAND_REUSABLE
	USE_POWER_ON_CREATURE(PLAYER1,IMP,ON_ENEMY_GROUND,PLAYER0,POWER_CAVE_IN,1,1)
	NEXT_COMMAND_REUSABLE
	USE_POWER_ON_CREATURE(PLAYER2,IMP,ON_ENEMY_GROUND,PLAYER0,POWER_CAVE_IN,1,1)
	NEXT_COMMAND_REUSABLE
	USE_POWER_ON_CREATURE(PLAYER3,IMP,ON_ENEMY_GROUND,PLAYER0,POWER_CAVE_IN,1,1)
	NEXT_COMMAND_REUSABLE
	USE_POWER_ON_CREATURE(PLAYER_GOOD,IMP,ON_ENEMY_GROUND,PLAYER0,POWER_CAVE_IN,1,1)
	NEXT_COMMAND_REUSABLE
	ADD_TO_FLAG(PLAYER0,FLAG1,-1)
ENDIF

REM # "Heroic Box: Summon a random hero to join your ranks."
REM # The pool of hero can vary between map.
IF(PLAYER0,BOX18_ACTIVATED > 0)
	REM # For the demonstration each time the player use a special box, respawn it.
	NEXT_COMMAND_REUSABLE
	ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,LAST_EVENT,18,PLAYER0)
	REM # Remove the two lines above for a normal gameplay.
	NEXT_COMMAND_REUSABLE
	RANDOMISE_FLAG(PLAYER0,BOX118_ACTIVATED,10)
	IF(PLAYER0,BOX118_ACTIVATED == 1)
		NEXT_COMMAND_REUSABLE
		ADD_CREATURE_TO_LEVEL(PLAYER0,ARCHER,LAST_EVENT,1,5,0)
	ENDIF
	IF(PLAYER0,BOX118_ACTIVATED == 2)
		NEXT_COMMAND_REUSABLE
		ADD_CREATURE_TO_LEVEL(PLAYER0,BARBARIAN,LAST_EVENT,1,5,0)
	ENDIF
	IF(PLAYER0,BOX118_ACTIVATED == 3)
		NEXT_COMMAND_REUSABLE
		ADD_CREATURE_TO_LEVEL(PLAYER0,DWARFA,LAST_EVENT,1,5,0)
	ENDIF
	IF(PLAYER0,BOX118_ACTIVATED == 4)
		NEXT_COMMAND_REUSABLE
		ADD_CREATURE_TO_LEVEL(PLAYER0,FAIRY,LAST_EVENT,1,5,0)
	ENDIF
	IF(PLAYER0,BOX118_ACTIVATED == 5)
		NEXT_COMMAND_REUSABLE
		ADD_CREATURE_TO_LEVEL(PLAYER0,GIANT,LAST_EVENT,1,5,0)
	ENDIF
	IF(PLAYER0,BOX118_ACTIVATED == 6)
		NEXT_COMMAND_REUSABLE
		ADD_CREATURE_TO_LEVEL(PLAYER0,MONK,LAST_EVENT,1,5,0)
	ENDIF
	IF(PLAYER0,BOX118_ACTIVATED == 7)
		NEXT_COMMAND_REUSABLE
		ADD_CREATURE_TO_LEVEL(PLAYER0,SAMURAI,LAST_EVENT,1,5,0)
	ENDIF
	IF(PLAYER0,BOX118_ACTIVATED == 8)
		NEXT_COMMAND_REUSABLE
		ADD_CREATURE_TO_LEVEL(PLAYER0,THIEF,LAST_EVENT,1,5,0)
	ENDIF
	IF(PLAYER0,BOX118_ACTIVATED == 9)
		NEXT_COMMAND_REUSABLE
		ADD_CREATURE_TO_LEVEL(PLAYER0,WITCH,LAST_EVENT,1,5,0)
	ENDIF
	IF(PLAYER0,BOX118_ACTIVATED == 10)
		NEXT_COMMAND_REUSABLE
		ADD_CREATURE_TO_LEVEL(PLAYER0,WIZARD,LAST_EVENT,1,5,0)
	ENDIF
	NEXT_COMMAND_REUSABLE
	SET_FLAG(PLAYER0,BOX18_ACTIVATED,0)
ENDIF

REM # "Evil Box: Summon a random creature to join your ranks."
REM # The pool of creature can vary between map.
IF(PLAYER0,BOX19_ACTIVATED > 0)
	REM # For the demonstration each time the player use a special box, respawn it.
	NEXT_COMMAND_REUSABLE
	ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,LAST_EVENT,19,PLAYER0)
	REM # Remove the two lines above for a normal gameplay.
	NEXT_COMMAND_REUSABLE
	RANDOMISE_FLAG(PLAYER0,BOX119_ACTIVATED,8)
	IF(PLAYER0,BOX119_ACTIVATED == 1)
		NEXT_COMMAND_REUSABLE
		ADD_CREATURE_TO_LEVEL(PLAYER0,BILE_DEMON,LAST_EVENT,1,5,0)
	ENDIF
	IF(PLAYER0,BOX119_ACTIVATED == 2)
		NEXT_COMMAND_REUSABLE
		ADD_CREATURE_TO_LEVEL(PLAYER0,DARK_MISTRESS,LAST_EVENT,1,5,0)
	ENDIF
	IF(PLAYER0,BOX119_ACTIVATED == 3)
		NEXT_COMMAND_REUSABLE
		ADD_CREATURE_TO_LEVEL(PLAYER0,DRAGON,LAST_EVENT,1,5,0)
	ENDIF
	IF(PLAYER0,BOX119_ACTIVATED == 4)
		NEXT_COMMAND_REUSABLE
		ADD_CREATURE_TO_LEVEL(PLAYER0,HELL_HOUND,LAST_EVENT,1,5,0)
	ENDIF
	IF(PLAYER0,BOX119_ACTIVATED == 5)
		NEXT_COMMAND_REUSABLE
		ADD_CREATURE_TO_LEVEL(PLAYER0,ORC,LAST_EVENT,1,5,0)
	ENDIF
	IF(PLAYER0,BOX119_ACTIVATED == 6)
		NEXT_COMMAND_REUSABLE
		ADD_CREATURE_TO_LEVEL(PLAYER0,SORCEROR,LAST_EVENT,1,5,0)
	ENDIF
	IF(PLAYER0,BOX119_ACTIVATED == 7)
		NEXT_COMMAND_REUSABLE
		ADD_CREATURE_TO_LEVEL(PLAYER0,TENTACLE,LAST_EVENT,1,5,0)
	ENDIF
	IF(PLAYER0,BOX119_ACTIVATED == 8)
		NEXT_COMMAND_REUSABLE
		ADD_CREATURE_TO_LEVEL(PLAYER0,TROLL,LAST_EVENT,1,5,0)
	ENDIF
	NEXT_COMMAND_REUSABLE
	SET_FLAG(PLAYER0,BOX19_ACTIVATED,0)
ENDIF

REM # "Necromancy Box: Raise a squad of undead at the site of the last battle to join your ranks."
REM # The type of undead can vary between map.
IF(PLAYER0,BOX20_ACTIVATED > 0)
	REM # For the demonstration each time the player use a special box, respawn it.
	NEXT_COMMAND_REUSABLE
	ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,LAST_EVENT,20,PLAYER0)
	REM # Remove the two lines above for a normal gameplay.
	NEXT_COMMAND_REUSABLE
	ADD_CREATURE_TO_LEVEL(PLAYER0,SKELETON,COMBAT,2,3,0)
	NEXT_COMMAND_REUSABLE
	ADD_CREATURE_TO_LEVEL(PLAYER0,GHOST,COMBAT,2,3,0)
	NEXT_COMMAND_REUSABLE
	ADD_CREATURE_TO_LEVEL(PLAYER0,VAMPIRE,COMBAT,1,3,0)
	NEXT_COMMAND_REUSABLE
	SET_FLAG(PLAYER0,BOX20_ACTIVATED,0)
ENDIF

REM # "Anger Box: All the minions get angry."
IF(PLAYER0,BOX21_ACTIVATED > 0)
	REM # For the demonstration each time the player use a special box, respawn it.
	NEXT_COMMAND_REUSABLE
	ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,LAST_EVENT,21,PLAYER0)
	REM # Remove the two lines above for a normal gameplay.
	NEXT_COMMAND_REUSABLE
	CHANGE_CREATURES_ANNOYANCE(ALL_PLAYERS,ANY_CREATURE,SET,10000)
	NEXT_COMMAND_REUSABLE
	SET_FLAG(PLAYER0,BOX21_ACTIVATED,0)
ENDIF

REM # "Happy Box: All friendly minions get happy."
IF(PLAYER0,BOX22_ACTIVATED > 0)
	REM # For the demonstration each time the player use a special box, respawn it.
	NEXT_COMMAND_REUSABLE
	ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,LAST_EVENT,22,PLAYER0)
	REM # Remove the two lines above for a normal gameplay.
	NEXT_COMMAND_REUSABLE
	CHANGE_CREATURES_ANNOYANCE(PLAYER0,ANY_CREATURE,SET,0)
	NEXT_COMMAND_REUSABLE
	SET_FLAG(PLAYER0,BOX22_ACTIVATED,0)
ENDIF

REM # "Dungeon Box: Fully restores your Dungeon Heart."
IF(PLAYER0,BOX23_ACTIVATED > 0)
	REM # For the demonstration each time the player use a special box, respawn it.
	NEXT_COMMAND_REUSABLE
	ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,LAST_EVENT,23,PLAYER0)
	REM # Remove the two lines above for a normal gameplay.
	NEXT_COMMAND_REUSABLE
	ADD_HEART_HEALTH(PLAYER0,30000,0)
	NEXT_COMMAND_REUSABLE
	SET_FLAG(PLAYER0,BOX23_ACTIVATED,0)
ENDIF

REM # "Keeper Box: Set all buff effects on all friendly minions."
IF(PLAYER0,BOX24_ACTIVATED > 0)
	REM # For the demonstration each time the player use a special box, respawn it.
	NEXT_COMMAND_REUSABLE
	ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,LAST_EVENT,24,PLAYER0)
	REM # Remove the two lines above for a normal gameplay.
	NEXT_COMMAND_REUSABLE
	USE_SPELL_ON_PLAYERS_CREATURES(PLAYER0,ANY_CREATURE,SPELL_ARMOUR)
	NEXT_COMMAND_REUSABLE
	USE_SPELL_ON_PLAYERS_CREATURES(PLAYER0,ANY_CREATURE,SPELL_REBOUND)
	NEXT_COMMAND_REUSABLE
	USE_SPELL_ON_PLAYERS_CREATURES(PLAYER0,ANY_CREATURE,SPELL_HEAL)
	NEXT_COMMAND_REUSABLE
	USE_SPELL_ON_PLAYERS_CREATURES(PLAYER0,ANY_CREATURE,SPELL_SPEED)
	NEXT_COMMAND_REUSABLE
	USE_SPELL_ON_PLAYERS_CREATURES(PLAYER0,ANY_CREATURE,SPELL_FLIGHT)
	NEXT_COMMAND_REUSABLE
	USE_SPELL_ON_PLAYERS_CREATURES(PLAYER0,ANY_CREATURE,SPELL_SIGHT)
	NEXT_COMMAND_REUSABLE
	USE_SPELL_ON_PLAYERS_CREATURES(PLAYER0,IMP,SPELL_ARMOUR)
	NEXT_COMMAND_REUSABLE
	USE_SPELL_ON_PLAYERS_CREATURES(PLAYER0,IMP,SPELL_REBOUND)
	NEXT_COMMAND_REUSABLE
	USE_SPELL_ON_PLAYERS_CREATURES(PLAYER0,IMP,SPELL_HEAL)
	NEXT_COMMAND_REUSABLE
	USE_SPELL_ON_PLAYERS_CREATURES(PLAYER0,IMP,SPELL_SPEED)
	NEXT_COMMAND_REUSABLE
	USE_SPELL_ON_PLAYERS_CREATURES(PLAYER0,IMP,SPELL_FLIGHT)
	NEXT_COMMAND_REUSABLE
	USE_SPELL_ON_PLAYERS_CREATURES(PLAYER0,IMP,SPELL_SIGHT)
	NEXT_COMMAND_REUSABLE
	SET_FLAG(PLAYER0,BOX24_ACTIVATED,0)
ENDIF

REM # "Pandora's Box: There's no telling what this will do."
REM # Can trigger any effect above, in addition it can trigger unique events such as:
REM # Note: if you use quick inf/obj you need to start with ID 33, as the ID from 1 to 32 is used here.
REM # "Amnesia?: You forgot how to summon new imps."
REM # "Oops!: A clumsy imp tripped and spilled his coffee on you, slap 'em all!"
REM # "Heroic Assault: Summon a squad of heroes to destroy you."
REM # "Rebellion: All friendly minions get angry. Your minions may turn against you, last 60 seconds."
REM # "Diverted Wrath: Cast Lightning on your territory at random friendly minions, last 60 seconds."
REM # "Collapse: Cast Cave-In on your territory at random friendly minions, last 60 seconds."
REM # "Backlash of Conquest: Summon an Avatar to fight by your side, after 300 seconds he betrays you and calls his army to arms. If you use this effect again before the betrayal, summon Horny at the site of the last battle to fight you."
REM # "Ragnarok: Kill all your minions then summon as many Knights to join your ranks, then set all buff effects on them PERMANENTLY. Warning: After 300 seconds or if you reactivate this effect, all the Knights will turn against you."
IF(PLAYER0,BOX25_ACTIVATED > 0)
	NEXT_COMMAND_REUSABLE
	RANDOMISE_FLAG(PLAYER0,BOX125_ACTIVATED,32)
	IF(PLAYER0,BOX125_ACTIVATED == 1)
		NEXT_COMMAND_REUSABLE
		QUICK_INFORMATION(1,"潘多拉魔盒变成了冰霜宝盒：所有敌人被冰冻。",PLAYER0)
		NEXT_COMMAND_REUSABLE
		SET_FLAG(PLAYER0,BOX1_ACTIVATED,1)
	ENDIF
	IF(PLAYER0,BOX125_ACTIVATED == 2)
		NEXT_COMMAND_REUSABLE
		QUICK_INFORMATION(2,"潘多拉魔盒变成了保护宝盒：所有友军获得保护术。",PLAYER0)
		NEXT_COMMAND_REUSABLE
		SET_FLAG(PLAYER0,BOX2_ACTIVATED,1)
	ENDIF
	IF(PLAYER0,BOX125_ACTIVATED == 3)
		NEXT_COMMAND_REUSABLE
		QUICK_INFORMATION(3,"潘多拉魔盒变成了护盾宝盒：所有友军获得反弹术。",PLAYER0)
		NEXT_COMMAND_REUSABLE
		SET_FLAG(PLAYER0,BOX3_ACTIVATED,1)
	ENDIF
	IF(PLAYER0,BOX125_ACTIVATED == 4)
		NEXT_COMMAND_REUSABLE
		QUICK_INFORMATION(4,"潘多拉魔盒变成了治疗宝盒：所有友军获得治疗。",PLAYER0)
		NEXT_COMMAND_REUSABLE
		SET_FLAG(PLAYER0,BOX4_ACTIVATED,1)
	ENDIF
	IF(PLAYER0,BOX125_ACTIVATED == 5)
		NEXT_COMMAND_REUSABLE
		QUICK_INFORMATION(5,"潘多拉魔盒变成了消失宝盒：所有友方小鬼获得隐身术。",PLAYER0)
		NEXT_COMMAND_REUSABLE
		SET_FLAG(PLAYER0,BOX5_ACTIVATED,1)
	ENDIF
	IF(PLAYER0,BOX125_ACTIVATED == 6)
		NEXT_COMMAND_REUSABLE
		QUICK_INFORMATION(6,"潘多拉魔盒变成了束缚宝盒：把所有敌人送回巢穴。",PLAYER0)
		NEXT_COMMAND_REUSABLE
		SET_FLAG(PLAYER0,BOX6_ACTIVATED,1)
	ENDIF
	IF(PLAYER0,BOX125_ACTIVATED == 7)
		NEXT_COMMAND_REUSABLE
		QUICK_INFORMATION(7,"潘多拉魔盒变成了救援宝盒：把所有友军送回巢穴。",PLAYER0)
		NEXT_COMMAND_REUSABLE
		SET_FLAG(PLAYER0,BOX7_ACTIVATED,1)
	ENDIF
	IF(PLAYER0,BOX125_ACTIVATED == 8)
		NEXT_COMMAND_REUSABLE
		QUICK_INFORMATION(8,"潘多拉魔盒变成了停战宝盒：把双方所有怪物送回巢穴。",PLAYER0)
		NEXT_COMMAND_REUSABLE
		SET_FLAG(PLAYER0,BOX8_ACTIVATED,1)
	ENDIF
	IF(PLAYER0,BOX125_ACTIVATED == 9)
		NEXT_COMMAND_REUSABLE
		QUICK_INFORMATION(9,"潘多拉魔盒变成了急速宝盒：所有友军获得速度术。",PLAYER0)
		NEXT_COMMAND_REUSABLE
		SET_FLAG(PLAYER0,BOX9_ACTIVATED,1)
	ENDIF
	IF(PLAYER0,BOX125_ACTIVATED == 10)
		NEXT_COMMAND_REUSABLE
		QUICK_INFORMATION(10,"潘多拉魔盒变成了蛛网宝盒：所有敌人被减速。",PLAYER0)
		NEXT_COMMAND_REUSABLE
		SET_FLAG(PLAYER0,BOX10_ACTIVATED,1)
	ENDIF
	IF(PLAYER0,BOX125_ACTIVATED == 11)
		NEXT_COMMAND_REUSABLE
		QUICK_INFORMATION(11,"潘多拉魔盒变成了发光宝盒：所有小鬼获得发光术。",PLAYER0)
		NEXT_COMMAND_REUSABLE
		SET_FLAG(PLAYER0,BOX11_ACTIVATED,1)
	ENDIF
	IF(PLAYER0,BOX125_ACTIVATED == 12)
		NEXT_COMMAND_REUSABLE
		QUICK_INFORMATION(12,"潘多拉魔盒变成了漂浮宝盒：所有友军获得飞空术。",PLAYER0)
		NEXT_COMMAND_REUSABLE
		SET_FLAG(PLAYER0,BOX12_ACTIVATED,1)
	ENDIF
	IF(PLAYER0,BOX125_ACTIVATED == 13)
		NEXT_COMMAND_REUSABLE
		QUICK_INFORMATION(13,"潘多拉魔盒变成了视野宝盒：所有友军获得邪眼术。",PLAYER0)
		NEXT_COMMAND_REUSABLE
		SET_FLAG(PLAYER0,BOX13_ACTIVATED,1)
	ENDIF
	IF(PLAYER0,BOX125_ACTIVATED == 14)
		NEXT_COMMAND_REUSABLE
		QUICK_INFORMATION(14,"潘多拉魔盒变成了瘟疫宝盒：所有敌军获得疾病术。",PLAYER0)
		NEXT_COMMAND_REUSABLE
		SET_FLAG(PLAYER0,BOX14_ACTIVATED,1)
	ENDIF
	IF(PLAYER0,BOX125_ACTIVATED == 15)
		NEXT_COMMAND_REUSABLE
		QUICK_INFORMATION(15,"潘多拉魔盒变成了妖术宝盒：所有敌军被变成小鸡。",PLAYER0)
		NEXT_COMMAND_REUSABLE
		SET_FLAG(PLAYER0,BOX15_ACTIVATED,1)
	ENDIF
	IF(PLAYER0,BOX125_ACTIVATED == 16)
		NEXT_COMMAND_REUSABLE
		QUICK_INFORMATION(16,"潘多拉魔盒变成了雷霆宝盒：对踏入友方领地的随机敌人释放闪电术，持续1分钟。",PLAYER0)
		NEXT_COMMAND_REUSABLE
		SET_FLAG(PLAYER0,BOX16_ACTIVATED,1)
	ENDIF
	IF(PLAYER0,BOX125_ACTIVATED == 17)
		NEXT_COMMAND_REUSABLE
		QUICK_INFORMATION(17,"潘多拉魔盒变成了落石宝盒：对踏入友方领地的随机敌人释放塌方术，持续1分钟。",PLAYER0)
		NEXT_COMMAND_REUSABLE
		SET_FLAG(PLAYER0,BOX17_ACTIVATED,1)
	ENDIF
	IF(PLAYER0,BOX125_ACTIVATED == 18)
		NEXT_COMMAND_REUSABLE
		QUICK_INFORMATION(18,"潘多拉魔盒变成了英雄宝盒：为你召唤一个随机的英雄。",PLAYER0)
		NEXT_COMMAND_REUSABLE
		SET_FLAG(PLAYER0,BOX18_ACTIVATED,1)
	ENDIF
	IF(PLAYER0,BOX125_ACTIVATED == 19)
		NEXT_COMMAND_REUSABLE
		QUICK_INFORMATION(19,"潘多拉魔盒变成了邪恶宝盒：为你召唤一个随机的怪物。",PLAYER0)
		NEXT_COMMAND_REUSABLE
		SET_FLAG(PLAYER0,BOX19_ACTIVATED,1)
	ENDIF
	IF(PLAYER0,BOX125_ACTIVATED == 20)
		NEXT_COMMAND_REUSABLE
		QUICK_INFORMATION(20,"潘多拉魔盒变成了死灵宝盒：根据你上一场战斗死亡的怪物数量，为你召唤同样数量的亡灵。",PLAYER0)
		NEXT_COMMAND_REUSABLE
		SET_FLAG(PLAYER0,BOX20_ACTIVATED,1)
	ENDIF
	IF(PLAYER0,BOX125_ACTIVATED == 21)
		NEXT_COMMAND_REUSABLE
		QUICK_INFORMATION(21,"潘多拉魔盒变成了愤怒宝盒：所有怪物立即开始生气。",PLAYER0)
		NEXT_COMMAND_REUSABLE
		SET_FLAG(PLAYER0,BOX21_ACTIVATED,1)
	ENDIF
	IF(PLAYER0,BOX125_ACTIVATED == 22)
		NEXT_COMMAND_REUSABLE
		QUICK_INFORMATION(22,"潘多拉魔盒变成了快乐宝盒：所有友军获得快乐。",PLAYER0)
		NEXT_COMMAND_REUSABLE
		SET_FLAG(PLAYER0,BOX22_ACTIVATED,1)
	ENDIF
	IF(PLAYER0,BOX125_ACTIVATED == 23)
		NEXT_COMMAND_REUSABLE
		QUICK_INFORMATION(23,"潘多拉魔盒变成了地下城宝盒：你的地下城心脏回复全部生命值。",PLAYER0)
		NEXT_COMMAND_REUSABLE
		SET_FLAG(PLAYER0,BOX23_ACTIVATED,1)
	ENDIF
	IF(PLAYER0,BOX125_ACTIVATED == 24)
		NEXT_COMMAND_REUSABLE
		QUICK_INFORMATION(24,"潘多拉魔盒变成了守护者宝盒：使所有友军获得全部增益效果。",PLAYER0)
		NEXT_COMMAND_REUSABLE
		SET_FLAG(PLAYER0,BOX24_ACTIVATED,1)
	ENDIF
	IF(PLAYER0,BOX125_ACTIVATED == 25)
		NEXT_COMMAND_REUSABLE
		QUICK_INFORMATION(25,"潘多拉魔盒变成了失忆宝盒：你失去制造小鬼能力。",PLAYER0)
		NEXT_COMMAND_REUSABLE
		SET_FLAG(PLAYER0,BOX225_ACTIVATED,1)
	ENDIF
	IF(PLAYER0,BOX125_ACTIVATED == 26)
		NEXT_COMMAND_REUSABLE
		QUICK_INFORMATION(26,"潘多拉魔盒变成了惊叫宝盒：一个笨拙的小鬼把咖啡撒在了你身上，打所有小鬼一巴掌！",PLAYER0)
		NEXT_COMMAND_REUSABLE
		SET_FLAG(PLAYER0,BOX226_ACTIVATED,1)
	ENDIF
	IF(PLAYER0,BOX125_ACTIVATED == 27)
		NEXT_COMMAND_REUSABLE
		QUICK_INFORMATION(27,"潘多拉魔盒变成了入侵宝盒：随机召唤一队英雄前来进攻你。",PLAYER0)
		NEXT_COMMAND_REUSABLE
		SET_FLAG(PLAYER0,BOX227_ACTIVATED,1)
	ENDIF
	IF(PLAYER0,BOX125_ACTIVATED == 28)
		NEXT_COMMAND_REUSABLE
		QUICK_INFORMATION(28,"潘多拉魔盒变成了叛乱宝盒：所有友军立即开始生气，并且可能会造反。持续1分钟。",PLAYER0)
		NEXT_COMMAND_REUSABLE
		SET_FLAG(PLAYER0,BOX228_ACTIVATED,1)
	ENDIF
	IF(PLAYER0,BOX125_ACTIVATED == 29)
		NEXT_COMMAND_REUSABLE
		QUICK_INFORMATION(29,"潘多拉魔盒变成了天谴宝盒：对友方领地的随机友军释放闪电术，持续1分钟。",PLAYER0)
		NEXT_COMMAND_REUSABLE
		SET_FLAG(PLAYER0,BOX229_ACTIVATED,1)
	ENDIF
	IF(PLAYER0,BOX125_ACTIVATED == 30)
		NEXT_COMMAND_REUSABLE
		QUICK_INFORMATION(30,"潘多拉魔盒变成了崩塌宝盒：对友方领地的随机友军释放塌方术，持续1分钟。",PLAYER0)
		NEXT_COMMAND_REUSABLE
		SET_FLAG(PLAYER0,BOX230_ACTIVATED,1)
	ENDIF
	IF(PLAYER0,BOX125_ACTIVATED == 31)
		NEXT_COMMAND_REUSABLE
		QUICK_INFORMATION(31,"潘多拉魔盒变成了复仇之渊:为你召唤一名圣者，但是5分钟后他会叛变并召唤敌军。如果你再次使用此宝盒，则会召唤冥界使者前来攻击你。",PLAYER0)
		NEXT_COMMAND_REUSABLE
		SET_FLAG(PLAYER0,BOX231_ACTIVATED,1)
	ENDIF
	IF(PLAYER0,BOX125_ACTIVATED == 32)
		NEXT_COMMAND_REUSABLE
		QUICK_INFORMATION(32,"潘多拉魔盒变成了世界毁灭:消灭所有友军，并召唤等量的骑士，并使他们获得所有增益效果。但要注意：5分钟后他们会全数叛变！",PLAYER0)
		NEXT_COMMAND_REUSABLE
		SET_FLAG(PLAYER0,BOX232_ACTIVATED,1)
	ENDIF
	REM # For the demonstration each time the player use a special box, respawn it.
	NEXT_COMMAND_REUSABLE
	ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,LAST_EVENT,25,PLAYER0)
	REM # Remove the two lines above for a normal gameplay.
	NEXT_COMMAND_REUSABLE
	SET_FLAG(PLAYER0,BOX25_ACTIVATED,0)
ENDIF

REM # "Amnesia?: You forgot how to summon new imps."
IF(PLAYER0,BOX225_ACTIVATED > 0)
	REM # For the demonstration each time the player use a special box, respawn it.
	NEXT_COMMAND_REUSABLE
	ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,LAST_EVENT,225,PLAYER0)
	REM # Remove the two lines above for a normal gameplay.
	NEXT_COMMAND_REUSABLE
	MAGIC_AVAILABLE(PLAYER0,POWER_IMP,1,0)
	NEXT_COMMAND_REUSABLE
	SET_FLAG(PLAYER0,BOX225_ACTIVATED,0)
ENDIF

REM # "Oops!: A clumsy imp tripped and spilled his coffee on you, slap 'em all!"
IF(PLAYER0,BOX226_ACTIVATED > 0)
	REM # For the demonstration each time the player use a special box, respawn it.
	NEXT_COMMAND_REUSABLE
	ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,LAST_EVENT,226,PLAYER0)
	REM # Remove the two lines above for a normal gameplay.
	NEXT_COMMAND_REUSABLE
	SET_FLAG(PLAYER0,BOX126_ACTIVATED,32)
	NEXT_COMMAND_REUSABLE
	SET_FLAG(PLAYER0,BOX226_ACTIVATED,0)
ENDIF

REM # Oops!'s effect happen every game turn until BOX126_ACTIVATED flag reach 0.
IF(PLAYER0,BOX126_ACTIVATED > 0)
	NEXT_COMMAND_REUSABLE
	USE_POWER_ON_CREATURE(PLAYER0,ANY_CREATURE,ANYWHERE,PLAYER0,POWER_SLAP,1,1)
	NEXT_COMMAND_REUSABLE
	USE_POWER_ON_CREATURE(PLAYER0,IMP,ANYWHERE,PLAYER0,POWER_SLAP,1,1)
	NEXT_COMMAND_REUSABLE
	ADD_TO_FLAG(PLAYER0,BOX126_ACTIVATED,-1)
ENDIF

REM # "Heroic Assault: Summon a squad of heroes to destroy you."
IF(PLAYER0,BOX227_ACTIVATED > 0)
	REM # For the demonstration each time the player use a special box, respawn it.
	NEXT_COMMAND_REUSABLE
	ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,LAST_EVENT,227,PLAYER0)
	REM # Remove the two lines above for a normal gameplay.
	NEXT_COMMAND_REUSABLE
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,HRCSSLT,LAST_EVENT,1)
	NEXT_COMMAND_REUSABLE
	SET_FLAG(PLAYER0,BOX227_ACTIVATED,0)
ENDIF

REM # "Rebellion: All friendly minions get angry. Your minions may turn against you, last 60 seconds."
IF(PLAYER0,BOX228_ACTIVATED > 0)
	REM # For the demonstration each time the player use a special box, respawn it.
	NEXT_COMMAND_REUSABLE
	ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,LAST_EVENT,228,PLAYER0)
	REM # Remove the two lines above for a normal gameplay.
	NEXT_COMMAND_REUSABLE
	CHANGE_CREATURES_ANNOYANCE(PLAYER0,ANY_CREATURE,INCREASE,6000)
	NEXT_COMMAND_REUSABLE
	ADD_TO_FLAG(PLAYER0,FLAG2,1200)
	NEXT_COMMAND_REUSABLE
	SET_FLAG(PLAYER0,BOX228_ACTIVATED,0)
ENDIF

REM # Until FLAG2 flag reach 0 there is small chance a minion turn against its owner.
IF(PLAYER0,FLAG2 > 0)
	NEXT_COMMAND_REUSABLE
	CHANGE_CREATURES_ANNOYANCE(PLAYER0,ANY_CREATURE,DECREASE,5)
	NEXT_COMMAND_REUSABLE
	ADD_TO_FLAG(PLAYER0,FLAG2,-1)
	NEXT_COMMAND_REUSABLE
	RANDOMISE_FLAG(PLAYER0,BOX128_ACTIVATED,255)
	IF(PLAYER0,BOX128_ACTIVATED > 250)
		NEXT_COMMAND_REUSABLE
		CHANGE_CREATURE_OWNER(PLAYER0,ANY_CREATURE,ANYWHERE,PLAYER_GOOD)	
	ENDIF
ENDIF

REM # "Diverted Wrath: Cast Lightning on your territory at random friendly minions, last 60 seconds."
IF(PLAYER0,BOX229_ACTIVATED > 0)
	REM # For the demonstration each time the player use a special box, respawn it.
	NEXT_COMMAND_REUSABLE
	ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,LAST_EVENT,229,PLAYER0)
	REM # Remove the two lines above for a normal gameplay.
	NEXT_COMMAND_REUSABLE
	ADD_TO_FLAG(PLAYER0,FLAG3,1200)
	NEXT_COMMAND_REUSABLE
	SET_FLAG(PLAYER0,BOX229_ACTIVATED,0)
ENDIF

REM # Diverted Wrath's effect happen every game turn until FLAG3 flag reach 0.
REM # To change duration see above and replace 1200 (60 seconds) with any value you want.
IF(PLAYER0,FLAG3 > 0)
	NEXT_COMMAND_REUSABLE
	USE_POWER_ON_CREATURE(PLAYER0,ANY_CREATURE,ON_FRIENDLY_GROUND,PLAYER0,POWER_LIGHTNING,1,1)
	NEXT_COMMAND_REUSABLE
	USE_POWER_ON_CREATURE(PLAYER0,IMP,ON_FRIENDLY_GROUND,PLAYER0,POWER_LIGHTNING,1,1)
	NEXT_COMMAND_REUSABLE
	ADD_TO_FLAG(PLAYER0,FLAG3,-1)
ENDIF

REM # "Collapse: Cast Cave-In on your territory at random friendly minions, last 60 seconds."
IF(PLAYER0,BOX230_ACTIVATED > 0)
	REM # For the demonstration each time the player use a special box, respawn it.
	NEXT_COMMAND_REUSABLE
	ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,LAST_EVENT,230,PLAYER0)
	REM # Remove the two lines above for a normal gameplay.
	NEXT_COMMAND_REUSABLE
	ADD_TO_FLAG(PLAYER0,FLAG4,1200)
	NEXT_COMMAND_REUSABLE
	SET_FLAG(PLAYER0,BOX230_ACTIVATED,0)
ENDIF

REM # Collapse's effect happen every game turn until FLAG4 flag reach 0.
REM # To change duration see above and replace 1200 (60 seconds) with any value you want.
IF(PLAYER0,FLAG4 > 0)
	NEXT_COMMAND_REUSABLE
	USE_POWER_ON_CREATURE(PLAYER0,ANY_CREATURE,ON_FRIENDLY_GROUND,PLAYER0,POWER_CAVE_IN,1,1)
	NEXT_COMMAND_REUSABLE
	USE_POWER_ON_CREATURE(PLAYER0,IMP,ON_FRIENDLY_GROUND,PLAYER0,POWER_CAVE_IN,1,1)
	NEXT_COMMAND_REUSABLE
	ADD_TO_FLAG(PLAYER0,FLAG4,-1)
ENDIF

REM # "Backlash of Conquest: Summon an Avatar to fight by your side, after 300 seconds he betrays you and calls his army to arms. If you use this effect again before the betrayal, summon Horny at the site of the last battle to fight you."
IF(PLAYER0,BOX231_ACTIVATED > 0)
	IF(PLAYER0,BOX31_ACTIVATED == 0)
		NEXT_COMMAND_REUSABLE
		ADD_CREATURE_TO_LEVEL(PLAYER0,AVATAR,LAST_EVENT,1,10,0)
		NEXT_COMMAND_REUSABLE
		SET_FLAG(PLAYER0,BOX31_ACTIVATED,1)
		NEXT_COMMAND_REUSABLE
		SET_FLAG(PLAYER0,FLAG5,6000)
	ENDIF
	IF(PLAYER0,BOX31_ACTIVATED > 0)
		REM # For the demonstration each time the player use a special box, respawn it.
		NEXT_COMMAND_REUSABLE
		ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,LAST_EVENT,231,PLAYER0)
		REM # Remove the two lines above for a normal gameplay.
		NEXT_COMMAND_REUSABLE
		ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,HORNY,COMBAT,1,10,0)
		NEXT_COMMAND_REUSABLE
		SET_FLAG(PLAYER0,BOX231_ACTIVATED,0)
	ENDIF
ENDIF

REM # Backlash of Conquest's effect countdown.
REM # When FLAG5 flag reach 0, the Avatar change ownership to PLAYER_GOOD and calls to arms his army.
IF(PLAYER0,FLAG5 > 0)
	IF_CONTROLS(PLAYER0,AVATAR > 0)
		NEXT_COMMAND_REUSABLE
		ADD_TO_FLAG(PLAYER0,FLAG5,-1)
	ENDIF
	IF_CONTROLS(PLAYER0,AVATAR < 1)
		NEXT_COMMAND_REUSABLE
		SET_FLAG(PLAYER0,BOX31_ACTIVATED,0)
		NEXT_COMMAND_REUSABLE
		SET_FLAG(PLAYER0,BOX231_ACTIVATED,0)
		NEXT_COMMAND_REUSABLE
		SET_FLAG(PLAYER0,FLAG5,0)
	ENDIF
ENDIF

REM # Backlash of Conquest's effect betrayal.
IF(PLAYER0,BOX31_ACTIVATED > 0)
	IF(PLAYER0,FLAG5 < 1)
		NEXT_COMMAND_REUSABLE
		MOVE_CREATURE(PLAYER0,AVATAR,MOST_EXPERIENCED,1,COMBAT,0)
		NEXT_COMMAND_REUSABLE
		CHANGE_CREATURE_OWNER(PLAYER0,AVATAR,MOST_EXPERIENCED,PLAYER_GOOD)
		NEXT_COMMAND_REUSABLE
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,CNQSTA,COMBAT,1)
		NEXT_COMMAND_REUSABLE
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,CNQSTB,COMBAT,1)
		NEXT_COMMAND_REUSABLE
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,CNQSTC,COMBAT,1)
		NEXT_COMMAND_REUSABLE
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,CNQSTD,COMBAT,1)
		NEXT_COMMAND_REUSABLE
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,CNQSTE,COMBAT,1)
		NEXT_COMMAND_REUSABLE
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,CNQSTF,COMBAT,1)
		NEXT_COMMAND_REUSABLE
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,CNQSTG,COMBAT,1)
		NEXT_COMMAND_REUSABLE
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,CNQSTH,COMBAT,1)
		NEXT_COMMAND_REUSABLE
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,CNQSTI,COMBAT,1)
		NEXT_COMMAND_REUSABLE
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,CNQSTJ,COMBAT,1)
		NEXT_COMMAND_REUSABLE
		SET_FLAG(PLAYER0,BOX31_ACTIVATED,0)
	ENDIF
ENDIF

REM # "Ragnarok: Kill all your minions then summon as many Knights to join your ranks, then set all buff effects on them PERMANENTLY. Warning: After 300 seconds or if you reactivate this effect, all the Knights will turn against you."
IF(PLAYER0,BOX232_ACTIVATED > 0)
	IF(PLAYER0,BOX32_ACTIVATED == 0)
		REM # For the demonstration each time the player use a special box, respawn it.
		NEXT_COMMAND_REUSABLE
		ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,LAST_EVENT,232,PLAYER0)
		REM # Remove the two lines above for a normal gameplay.
		NEXT_COMMAND_REUSABLE
		COMPUTE_FLAG(PLAYER0,BOX131_ACTIVATED,SET,PLAYER0,TOTAL_CREATURES,1)
		NEXT_COMMAND_REUSABLE
		SET_FLAG(PLAYER0,BOX32_ACTIVATED,1)
		NEXT_COMMAND_REUSABLE
		SET_FLAG(PLAYER0,FLAG6,6000)
	ENDIF
	REM # Ragnarok's effect permanent buff to Knight.
	IF(PLAYER0,BOX32_ACTIVATED > 0)
		NEXT_COMMAND_REUSABLE
		USE_SPELL_ON_PLAYERS_CREATURES(PLAYER0,KNIGHT,SPELL_ARMOUR)
		NEXT_COMMAND_REUSABLE
		USE_SPELL_ON_PLAYERS_CREATURES(PLAYER0,KNIGHT,SPELL_REBOUND)
		NEXT_COMMAND_REUSABLE
		USE_SPELL_ON_PLAYERS_CREATURES(PLAYER0,KNIGHT,SPELL_HEAL)
		NEXT_COMMAND_REUSABLE
		USE_SPELL_ON_PLAYERS_CREATURES(PLAYER0,KNIGHT,SPELL_SPEED)
		NEXT_COMMAND_REUSABLE
		USE_SPELL_ON_PLAYERS_CREATURES(PLAYER0,KNIGHT,SPELL_FLIGHT)
		NEXT_COMMAND_REUSABLE
		USE_SPELL_ON_PLAYERS_CREATURES(PLAYER0,KNIGHT,SPELL_SIGHT)
	ENDIF
ENDIF

REM # Ragnarok's effect betrayal. If the player trigger a second time Ragnarok, all the Knights change ownership to PLAYER_GOOD.
IF(PLAYER0,BOX232_ACTIVATED > 1)
	IF(PLAYER0,BOX32_ACTIVATED > 1)
		IF_CONTROLS(PLAYER0,KNIGHT > 0)
			NEXT_COMMAND_REUSABLE
			CHANGE_CREATURE_OWNER(PLAYER0,KNIGHT,MOST_EXPERIENCED,PLAYER_GOOD)
		ENDIF
		IF_CONTROLS(PLAYER0,KNIGHT < 1)
			REM # For the demonstration each time the player use a special box, respawn it.
			NEXT_COMMAND_REUSABLE
			ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,COMBAT,232,PLAYER0)
			REM # Remove the two lines above for a normal gameplay.
			NEXT_COMMAND_REUSABLE
			SET_FLAG(PLAYER0,BOX32_ACTIVATED,0)
			NEXT_COMMAND_REUSABLE
			SET_FLAG(PLAYER0,BOX232_ACTIVATED,0)
		ENDIF
	ENDIF
ENDIF

REM # Ragnarok's effect to kill all your minions and summon a Knight for each.
IF(PLAYER0,BOX131_ACTIVATED > 0)
	NEXT_COMMAND_REUSABLE
	KILL_CREATURE(PLAYER0,ANY_CREATURE,ANYWHERE,1)
	NEXT_COMMAND_REUSABLE
	ADD_TO_FLAG(PLAYER0,BOX132_ACTIVATED,1)
	NEXT_COMMAND_REUSABLE
	ADD_TO_FLAG(PLAYER0,BOX131_ACTIVATED,-1)
ENDIF
IF(PLAYER0,BOX132_ACTIVATED > 0)
	IF(PLAYER0,BOX131_ACTIVATED < 1)
		NEXT_COMMAND_REUSABLE
		ADD_CREATURE_TO_LEVEL(PLAYER0,KNIGHT,LAST_EVENT,1,10,0)
		NEXT_COMMAND_REUSABLE
		ADD_TO_FLAG(PLAYER0,BOX32_ACTIVATED,1)
		NEXT_COMMAND_REUSABLE
		ADD_TO_FLAG(PLAYER0,BOX132_ACTIVATED,-1)
	ENDIF
ENDIF

REM # Ragnarok's effect countdown.
IF(PLAYER0,FLAG6 > 0)
	IF_CONTROLS(PLAYER0,KNIGHT > 0)
		NEXT_COMMAND_REUSABLE
		ADD_TO_FLAG(PLAYER0,FLAG6,-1)
	ENDIF
	IF(PLAYER0,BOX132_ACTIVATED < 1)
		IF_CONTROLS(PLAYER0,KNIGHT < 1)
			REM # For the demonstration each time the player use a special box, respawn it.
			NEXT_COMMAND_REUSABLE
			ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,COMBAT,232,PLAYER0)
			REM # Remove the two lines above for a normal gameplay.
			NEXT_COMMAND_REUSABLE
			SET_FLAG(PLAYER0,BOX32_ACTIVATED,0)
			NEXT_COMMAND_REUSABLE
			SET_FLAG(PLAYER0,BOX232_ACTIVATED,0)
		ENDIF
	ENDIF
ENDIF

REM # When FLAG6 flag reach 0, all the Knights change ownership to PLAYER_GOOD.
IF(PLAYER0,BOX32_ACTIVATED > 0)
	IF(PLAYER0,FLAG6 < 1)
		NEXT_COMMAND_REUSABLE
		SET_FLAG(PLAYER0,BOX32_ACTIVATED,2)
		NEXT_COMMAND_REUSABLE
		SET_FLAG(PLAYER0,BOX232_ACTIVATED,2)
	ENDIF
ENDIF

REM # Map's specific behavior starts from this point.
QUICK_OBJECTIVE(33,"欢迎来到神秘宝盒展示关卡。这里有多达32种不同效果的宝盒可供选择。每个宝盒都有自己的信息提示，点击即可使用，使用后会自动刷新，让您可以反复使用。要想获胜，您必须摧毁其他所有敌人的地下城心脏。",PLAYER0)
QUICK_INFORMATION(34,"使用潘多拉魔盒时要小心，这是一种除了原始效果外，还会产生已应用效果的魔盒。",PLAYER0)
QUICK_INFORMATION(35,"在十字型区域的每个端点都有一个宝盒，使用后可以触发一次英雄入侵。",PLAYER0)

REM # Loop to spawn minion for White.
SET_TIMER(PLAYER_GOOD,TIMER0)
IF(PLAYER_GOOD,TIMER0 >= 20)
	IF(PLAYER_GOOD,SKELETON < 4)
		NEXT_COMMAND_REUSABLE
		ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,SKELETON,1,1,4,0)
	ENDIF
	IF(PLAYER_GOOD,ARCHER < 1)
		NEXT_COMMAND_REUSABLE
		ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,ARCHER,7,1,3,0)
	ENDIF
	IF(PLAYER_GOOD,BARBARIAN < 1)
		NEXT_COMMAND_REUSABLE
		ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,BARBARIAN,8,1,3,0)
	ENDIF
	IF(PLAYER_GOOD,DWARFA < 1)
		NEXT_COMMAND_REUSABLE
		ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,DWARFA,9,1,3,0)
	ENDIF
	IF(PLAYER_GOOD,THIEF < 1)
		NEXT_COMMAND_REUSABLE
		ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,THIEF,10,1,3,0)
	ENDIF
	NEXT_COMMAND_REUSABLE
	SET_TIMER(PLAYER_GOOD,TIMER0)
ENDIF

REM # Loop to spawn minion for Red.
SET_TIMER(PLAYER_GOOD,TIMER1)
IF(PLAYER_GOOD,TIMER1 >= 20)
	IF(PLAYER0,SKELETON < 2)
		NEXT_COMMAND_REUSABLE
		ADD_CREATURE_TO_LEVEL(PLAYER0,SKELETON,2,1,2,0)
	ENDIF
	IF(PLAYER0,SORCEROR < 1)
		NEXT_COMMAND_REUSABLE
		ADD_CREATURE_TO_LEVEL(PLAYER0,SORCEROR,3,1,5,0)
	ENDIF
	IF(PLAYER0,DEMONSPAWN < 1)
		NEXT_COMMAND_REUSABLE
		ADD_CREATURE_TO_LEVEL(PLAYER0,DEMONSPAWN,4,1,2,0)
	ENDIF
	IF(PLAYER0,SPIDER < 1)
		NEXT_COMMAND_REUSABLE
		ADD_CREATURE_TO_LEVEL(PLAYER0,SPIDER,5,1,2,0)
	ENDIF
	IF(PLAYER0,TENTACLE < 1)
		NEXT_COMMAND_REUSABLE
		ADD_CREATURE_TO_LEVEL(PLAYER0,TENTACLE,6,1,2,0)
	ENDIF
	NEXT_COMMAND_REUSABLE
	SET_TIMER(PLAYER_GOOD,TIMER1)
ENDIF

REM # This loop spawn Imp every 5 seconds for each player until they have 8.
SET_TIMER(PLAYER_GOOD,TIMER2)
IF(PLAYER_GOOD,TIMER2 >= 100)
	IF(PLAYER0,DUNGEON_DESTROYED == 0)
		IF_CONTROLS(PLAYER0,IMP < 8)
			NEXT_COMMAND_REUSABLE
			ADD_CREATURE_TO_LEVEL(PLAYER0,IMP,PLAYER0,1,10,0)
		ENDIF
	ENDIF
	IF(PLAYER1,DUNGEON_DESTROYED == 0)
		IF_CONTROLS(PLAYER1,IMP < 8)
			NEXT_COMMAND_REUSABLE
			ADD_CREATURE_TO_LEVEL(PLAYER1,IMP,PLAYER1,1,10,0)
		ENDIF
	ENDIF
	IF(PLAYER2,DUNGEON_DESTROYED == 0)
		IF_CONTROLS(PLAYER2,IMP < 8)
			NEXT_COMMAND_REUSABLE
			ADD_CREATURE_TO_LEVEL(PLAYER2,IMP,PLAYER2,1,10,0)
		ENDIF
	ENDIF
	IF(PLAYER3,DUNGEON_DESTROYED == 0)
		IF_CONTROLS(PLAYER3,IMP < 8)
			NEXT_COMMAND_REUSABLE
			ADD_CREATURE_TO_LEVEL(PLAYER3,IMP,PLAYER3,1,10,0)
		ENDIF
	ENDIF
	NEXT_COMMAND_REUSABLE
	SET_TIMER(PLAYER_GOOD,TIMER2)
ENDIF

REM # Win condition.
IF(PLAYER0,ALL_DUNGEONS_DESTROYED == 1)
	WIN_GAME
ENDIF