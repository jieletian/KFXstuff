REM # General Settings.

LEVEL_VERSION(1)
RUN_AFTER_VICTORY(1)

REM # Custom special for testing, to REM # before release.
REM # ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,PLAYER0,249)
REM # ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,103,201)
REM # ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,104,202)
REM # ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,105,203)
REM # ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,106,204)
REM # ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,107,205)
REM # ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,108,206)
REM # ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,109,207)
REM # ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,110,208)

REM # Map Settings.
SET_GENERATE_SPEED(1200)
SET_FLAG(PLAYER0,BOX53_ACTIVATED,1)
SET_FLAG(PLAYER0,BOX153_ACTIVATED,1)
SET_FLAG(PLAYER0,BOX253_ACTIVATED,1)
SET_FLAG(PLAYER0,BOX111_ACTIVATED,25)
SET_FLAG(PLAYER0,BOX112_ACTIVATED,50)
SET_FLAG(PLAYER0,BOX113_ACTIVATED,75)
SET_FLAG(PLAYER0,BOX114_ACTIVATED,100)
SET_FLAG(PLAYER0,BOX115_ACTIVATED,125)
SET_FLAG(PLAYER0,BOX116_ACTIVATED,150)
SET_FLAG(PLAYER0,BOX117_ACTIVATED,175)
SET_FLAG(PLAYER0,BOX118_ACTIVATED,200)
SET_FLAG(PLAYER0,BOX119_ACTIVATED,225)
SET_FLAG(PLAYER0,BOX120_ACTIVATED,250)
SET_FLAG(PLAYER0,BOX100_ACTIVATED,10)
SET_FLAG(PLAYER_GOOD,FLAG6,46340)
SET_TIMER(PLAYER_GOOD,TIMER0)

REM # Setup Game Rule.
SET_GAME_RULE(RoomSellGoldBackPercent,0)
SET_GAME_RULE(DoorSellValuePercent,0)
SET_GAME_RULE(TrapSellValuePercent,0)
SET_GAME_RULE(PlaceTrapsOnSubtiles,0)
SET_GAME_RULE(HungerHealthLoss,32767)
SET_GAME_RULE(GameTurnsPerHungerHealthLoss,1)

REM # Setup Object Configuration.
SET_OBJECT_CONFIGURATION(HERO_GATE,AnimationSpeed,128)
SET_OBJECT_CONFIGURATION(HERO_GATE,MaximumSize,150)

REM # Null All Instance.
SET_CREATURE_INSTANCE(IMP,3,NULL,0)
SET_CREATURE_INSTANCE(IMP,10,NULL,0)
SET_CREATURE_INSTANCE(FLY,5,NULL,0)
SET_CREATURE_INSTANCE(FLY,8,NULL,0)
SET_CREATURE_INSTANCE(BILE_DEMON,1,NULL,0)
SET_CREATURE_INSTANCE(BILE_DEMON,2,NULL,0)
SET_CREATURE_INSTANCE(BILE_DEMON,4,NULL,0)
SET_CREATURE_INSTANCE(BILE_DEMON,7,NULL,0)
SET_CREATURE_INSTANCE(HORNY,5,NULL,0)
SET_CREATURE_INSTANCE(HORNY,10,NULL,0)
SET_CREATURE_INSTANCE(FAIRY,1,NULL,0)
SET_CREATURE_INSTANCE(FAIRY,3,NULL,0)
SET_CREATURE_INSTANCE(FAIRY,4,NULL,0)
SET_CREATURE_INSTANCE(FAIRY,5,NULL,0)
SET_CREATURE_INSTANCE(FAIRY,6,NULL,0)
SET_CREATURE_INSTANCE(FAIRY,7,NULL,0)
SET_CREATURE_INSTANCE(FAIRY,8,NULL,0)
SET_CREATURE_INSTANCE(FAIRY,9,NULL,0)
SET_CREATURE_INSTANCE(FAIRY,10,NULL,0)
SET_CREATURE_INSTANCE(WIZARD,2,NULL,0)
SET_CREATURE_INSTANCE(WIZARD,3,NULL,0)
SET_CREATURE_INSTANCE(WIZARD,4,NULL,0)
SET_CREATURE_INSTANCE(WIZARD,5,NULL,0)
SET_CREATURE_INSTANCE(WIZARD,6,NULL,0)
SET_CREATURE_INSTANCE(WIZARD,7,NULL,0)
SET_CREATURE_INSTANCE(WIZARD,8,NULL,0)
SET_CREATURE_INSTANCE(WIZARD,9,NULL,0)
SET_CREATURE_INSTANCE(WIZARD,10,NULL,0)

REM # IMP Configuration.
SET_CREATURE_CONFIGURATION(IMP,Health,150)
SET_CREATURE_CONFIGURATION(IMP,HealThreshold,255)
SET_CREATURE_CONFIGURATION(IMP,HealRequirement,17)
SET_CREATURE_CONFIGURATION(IMP,Strength,0)
SET_CREATURE_CONFIGURATION(IMP,Armour,0)
SET_CREATURE_CONFIGURATION(IMP,Dexterity,0)
SET_CREATURE_CONFIGURATION(IMP,Defence,0)
SET_CREATURE_CONFIGURATION(IMP,Luck,0)
SET_CREATURE_CONFIGURATION(IMP,BaseSpeed,0)
SET_CREATURE_CONFIGURATION(IMP,GoldHold,0)
SET_CREATURE_CONFIGURATION(IMP,SlapsToKill,1)
SET_CREATURE_CONFIGURATION(IMP,DamageToBoulder,8)

REM # FLY Configuration.
SET_CREATURE_CONFIGURATION(FLY,Health,32767)
SET_CREATURE_CONFIGURATION(FLY,Strength,255)
SET_CREATURE_CONFIGURATION(FLY,Armour,200)
SET_CREATURE_CONFIGURATION(FLY,Dexterity,255)
SET_CREATURE_CONFIGURATION(FLY,Defence,255)
SET_CREATURE_CONFIGURATION(FLY,Luck,100)
SET_CREATURE_CONFIGURATION(FLY,BaseSpeed,128)
SET_CREATURE_CONFIGURATION(FLY,AttackPreference,MELEE)
SET_CREATURE_CONFIGURATION(FLY,SlapsToKill,0)
SET_CREATURE_CONFIGURATION(FLY,DamageToBoulder,8)
SET_CREATURE_INSTANCE(FLY,1,SWING_WEAPON_FIST,1)
SET_CREATURE_INSTANCE(FLY,10,TELEPORT,10)
SET_CREATURE_PROPERTY(FLY,NO_ENMHEART_ATTCK,1)

REM # BILE_DEMON Configuration.
SET_CREATURE_CONFIGURATION(BILE_DEMON,Strength,90)
SET_CREATURE_CONFIGURATION(BILE_DEMON,BaseSpeed,64)
SET_CREATURE_CONFIGURATION(BILE_DEMON,AttackPreference,MELEE)
SET_CREATURE_CONFIGURATION(BILE_DEMON,SlapsToKill,0)
SET_CREATURE_CONFIGURATION(BILE_DEMON,DamageToBoulder,128)
SET_CREATURE_INSTANCE(BILE_DEMON,1,SWING_WEAPON_SWORD,1)
SET_CREATURE_INSTANCE(BILE_DEMON,2,POISON_CLOUD,1)
SET_CREATURE_INSTANCE(BILE_DEMON,3,FART,1)
SET_CREATURE_PROPERTY(BILE_DEMON,NO_ENMHEART_ATTCK,1)

REM # HORNY Configuration.
SET_CREATURE_CONFIGURATION(HORNY,Strength,135)
SET_CREATURE_CONFIGURATION(HORNY,BaseSpeed,128)
SET_CREATURE_CONFIGURATION(HORNY,AttackPreference,MELEE)
SET_CREATURE_CONFIGURATION(HORNY,SlapsToKill,0)
SET_CREATURE_CONFIGURATION(HORNY,DamageToBoulder,128)
SET_CREATURE_INSTANCE(HORNY,1,SWING_WEAPON_SWORD,1)
SET_CREATURE_INSTANCE(HORNY,2,SWING_WEAPON_FIST,1)
SET_CREATURE_INSTANCE(HORNY,5,SPEED,5)
SET_CREATURE_INSTANCE(HORNY,10,TELEPORT,10)
SET_CREATURE_PROPERTY(HORNY,NO_ENMHEART_ATTCK,1)

REM # FAIRY Configuration.
SET_CREATURE_CONFIGURATION(FAIRY,BaseSpeed,80)
SET_CREATURE_CONFIGURATION(FAIRY,AttackPreference,RANGED)
SET_CREATURE_CONFIGURATION(FAIRY,SlapsToKill,0)
SET_CREATURE_CONFIGURATION(FAIRY,DamageToBoulder,32)
SET_CREATURE_INSTANCE(FAIRY,1,LIGHTNING,1)
SET_CREATURE_PROPERTY(FAIRY,NO_ENMHEART_ATTCK,1)

REM # WIZARD Configuration.
SET_CREATURE_CONFIGURATION(WIZARD,BaseSpeed,32)
SET_CREATURE_CONFIGURATION(WIZARD,AttackPreference,RANGED)
SET_CREATURE_CONFIGURATION(WIZARD,SlapsToKill,0)
SET_CREATURE_CONFIGURATION(WIZARD,DamageToBoulder,32)
SET_CREATURE_INSTANCE(WIZARD,1,HAILSTORM,1)
SET_CREATURE_PROPERTY(WIZARD,NO_ENMHEART_ATTCK,1)

REM # Room Configuration.
SET_ROOM_CONFIGURATION(DUNGEON_HEART,SymbolSprites,356,414)

REM # Boulder Trap Configuration.
SET_TRAP_CONFIGURATION(BOULDER,Shots,1)
SET_TRAP_CONFIGURATION(BOULDER,TimeBetweenShots,0)
SET_TRAP_CONFIGURATION(BOULDER,SellingValue,0)
SET_TRAP_CONFIGURATION(BOULDER,TriggerType,3)
SET_TRAP_CONFIGURATION(BOULDER,ActivationType,5)
SET_TRAP_CONFIGURATION(BOULDER,Hidden,0)
SET_TRAP_CONFIGURATION(BOULDER,Slappable,1)
SET_TRAP_CONFIGURATION(BOULDER,Destructible,0)
SET_TRAP_CONFIGURATION(BOULDER,Unstable,0)
SET_TRAP_CONFIGURATION(BOULDER,Health,256)

REM # Keeper's Powers Availablity.
MAGIC_AVAILABLE(PLAYER0,POWER_POSSESS,0,0)
MAGIC_AVAILABLE(PLAYER0,POWER_HAND,0,0)
MAGIC_AVAILABLE(PLAYER0,POWER_SLAP,1,1)

REM # Keeper's Traps Availablity.
TRAP_AVAILABLE(PLAYER0,BOULDER,1,1)

REM # Set tooltip for Ready!...
SET_BOX_TOOLTIP(0,"准备就绪！")
SET_BOX_TOOLTIP(201,"塌方： 每个游戏回合施放一次 “塌方”，持续 5 秒。可堆叠。")
SET_BOX_TOOLTIP(202,"连杀：每个游戏回合杀死一名小恶魔，持续 5 秒。可叠加。")
SET_BOX_TOOLTIP(203,"放屁专家： 产生两个巨魔，帮你杀死小鬼。")
SET_BOX_TOOLTIP(204,"疯狂精神病患者：产生一只镰刀魔，帮你杀死小鬼。。")
SET_BOX_TOOLTIP(205,"巨石风扇：提供 3 个巨石陷阱。")
SET_BOX_TOOLTIP(206,"大屠杀者：杀死所有小鬼。")
SET_BOX_TOOLTIP(207,"哼哈二将：生成一个仙女和一个巫师，帮你杀死小鬼。")
SET_BOX_TOOLTIP(208,"炸弹群：催生一群苍蝇，并对它们施放定时炸弹。")
SET_BOX_TOOLTIP(249,"调试模式！")

REM # "Cave-In: Casts Cave-In every game turn, last 5 seconds. Stackable."
IF(PLAYER0,BOX201_ACTIVATED > 0)
	REM # ADD_TO_FLAG(PLAYER0,FLAG1,2147483647)
	NEXT_COMMAND_REUSABLE
	ADD_TO_FLAG(PLAYER0,FLAG1,100)
	NEXT_COMMAND_REUSABLE
	SET_FLAG(PLAYER0,BOX201_ACTIVATED,0)
ENDIF
IF(PLAYER0,FLAG1 > 0)
	NEXT_COMMAND_REUSABLE
	USE_POWER_ON_CREATURE(PLAYER0,IMP,ANYWHERE,PLAYER0,POWER_CAVE_IN,9,1)
	NEXT_COMMAND_REUSABLE
	ADD_TO_FLAG(PLAYER0,FLAG1,-1)
ENDIF

REM # "Killing Spree: Kills one Imp every game turn, last 5 seconds. Stackable."
IF(PLAYER0,BOX202_ACTIVATED > 0)
	REM # ADD_TO_FLAG(PLAYER0,FLAG2,2147483647)
	NEXT_COMMAND_REUSABLE
	ADD_TO_FLAG(PLAYER0,FLAG2,100)
	NEXT_COMMAND_REUSABLE
	SET_FLAG(PLAYER0,BOX202_ACTIVATED,0)
ENDIF
IF(PLAYER0,FLAG2 > 0)
	NEXT_COMMAND_REUSABLE
	COMPUTE_FLAG(PLAYER0,FLAG3,SET,PLAYER0,IMP,0)
	NEXT_COMMAND_REUSABLE
	KILL_CREATURE(PLAYER0,IMP,ANYWHERE,1)
	NEXT_COMMAND_REUSABLE
	COMPUTE_FLAG(PLAYER0,FLAG3,DECREASE,PLAYER0,IMP,0)
	NEXT_COMMAND_REUSABLE
	COMPUTE_FLAG(PLAYER0,BOX200_ACTIVATED,INCREASE,PLAYER0,FLAG3,0)
	NEXT_COMMAND_REUSABLE
	COMPUTE_FLAG(PLAYER0,FLAG3,MULTIPLY,PLAYER0,BOX253_ACTIVATED,0)
	NEXT_COMMAND_REUSABLE
	COMPUTE_FLAG(PLAYER0,FLAG3,MULTIPLY,PLAYER0,BOX153_ACTIVATED,0)
	NEXT_COMMAND_REUSABLE
	COMPUTE_FLAG(PLAYER0,FLAG3,MULTIPLY,PLAYER0,BOX53_ACTIVATED,0)
	NEXT_COMMAND_REUSABLE
	COMPUTE_FLAG(PLAYER0,FLAG0,INCREASE,PLAYER0,FLAG3,0)
	NEXT_COMMAND_REUSABLE
	COMPUTE_FLAG(PLAYER0,BOX255_ACTIVATED,SET,PLAYER0,IMP,0)
	NEXT_COMMAND_REUSABLE
	SET_FLAG(PLAYER_GOOD,FLAG0,100)
	NEXT_COMMAND_REUSABLE
	COMPUTE_FLAG(PLAYER_GOOD,FLAG0,INCREASE,PLAYER0,IMP,0)
	NEXT_COMMAND_REUSABLE
	COMPUTE_FLAG(PLAYER_GOOD,FLAG0,MULTIPLY,PLAYER0,IMP,0)
	NEXT_COMMAND_REUSABLE
	COMPUTE_FLAG(PLAYER_GOOD,FLAG0,MULTIPLY,PLAYER0,IMP,0)
	NEXT_COMMAND_REUSABLE
	COMPUTE_FLAG(PLAYER_GOOD,FLAG0,DECREASE,PLAYER0,FLAG7,0)
	NEXT_COMMAND_REUSABLE
	ADD_TO_FLAG(PLAYER0,FLAG2,-1)
ENDIF

REM # "Farting Experts: Spawns two Bile Demon to help you kill Imps."
IF(PLAYER0,BOX203_ACTIVATED > 0)
	IF(PLAYER0,BOX3_ACTIVATED < 253)
		NEXT_COMMAND_REUSABLE
		ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,BILE_DEMON,LAST_EVENT,2,10,0)
		NEXT_COMMAND_REUSABLE
		ADD_TO_FLAG(PLAYER0,BOX203_ACTIVATED,-1)
	ENDIF
ENDIF
IF(PLAYER_GOOD,BILE_DEMON > 0)
	IF(PLAYER_GOOD,FLAG2 < 3600)
		NEXT_COMMAND_REUSABLE
		COMPUTE_FLAG(PLAYER_GOOD,FLAG2,INCREASE,PLAYER_GOOD,BILE_DEMON,0)
		NEXT_COMMAND_REUSABLE
		USE_POWER_ON_CREATURE(PLAYER_GOOD,BILE_DEMON,ANYWHERE,PLAYER_GOOD,POWER_SPEED,9,1)
	ENDIF
	IF(PLAYER_GOOD,FLAG2 >= 3600)
		NEXT_COMMAND_REUSABLE
		KILL_CREATURE(PLAYER_GOOD,BILE_DEMON,ANYWHERE,1)
		NEXT_COMMAND_REUSABLE
		SET_FLAG(PLAYER_GOOD,FLAG2,0)
	ENDIF
ENDIF

REM # "Mad Psycho: Spawn a Horned Reaper to help you kill Imps."
IF(PLAYER0,BOX204_ACTIVATED > 0)
	IF(PLAYER0,BOX3_ACTIVATED < 255)
		NEXT_COMMAND_REUSABLE
		ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,HORNY,LAST_EVENT,1,10,0)
		NEXT_COMMAND_REUSABLE
		ADD_TO_FLAG(PLAYER0,BOX204_ACTIVATED,-1)
	ENDIF
ENDIF
IF(PLAYER_GOOD,HORNY > 0)
	IF(PLAYER_GOOD,FLAG1 < 1200)
		NEXT_COMMAND_REUSABLE
		COMPUTE_FLAG(PLAYER_GOOD,FLAG1,INCREASE,PLAYER_GOOD,HORNY,0)
		NEXT_COMMAND_REUSABLE
		USE_POWER_ON_CREATURE(PLAYER_GOOD,HORNY,ANYWHERE,PLAYER_GOOD,POWER_SPEED,9,1)
	ENDIF
	IF(PLAYER_GOOD,FLAG1 >= 1200)
		NEXT_COMMAND_REUSABLE
		KILL_CREATURE(PLAYER_GOOD,HORNY,ANYWHERE,1)
		NEXT_COMMAND_REUSABLE
		SET_FLAG(PLAYER_GOOD,FLAG1,0)
	ENDIF
ENDIF

REM # "Boulder Fan: Gives 3 Boulder Trap."
IF(PLAYER0,BOX205_ACTIVATED > 0)
	NEXT_COMMAND_REUSABLE
	TRAP_AVAILABLE(PLAYER0,BOULDER,1,3)
	NEXT_COMMAND_REUSABLE
	SET_FLAG(PLAYER0,BOX205_ACTIVATED,0)
ENDIF

REM # "Mass Murderer: Kills all Imps."
IF(PLAYER0,BOX206_ACTIVATED > 0)
	NEXT_COMMAND_REUSABLE
	COMPUTE_FLAG(PLAYER0,FLAG4,SET,PLAYER0,IMP,0)
	NEXT_COMMAND_REUSABLE
	KILL_CREATURE(PLAYER0,IMP,ANYWHERE,255)
	NEXT_COMMAND_REUSABLE
	COMPUTE_FLAG(PLAYER0,FLAG4,DECREASE,PLAYER0,IMP,0)
	NEXT_COMMAND_REUSABLE
	COMPUTE_FLAG(PLAYER0,BOX200_ACTIVATED,INCREASE,PLAYER0,FLAG4,0)
	NEXT_COMMAND_REUSABLE
	COMPUTE_FLAG(PLAYER0,FLAG4,MULTIPLY,PLAYER0,BOX253_ACTIVATED,0)
	NEXT_COMMAND_REUSABLE
	COMPUTE_FLAG(PLAYER0,FLAG4,MULTIPLY,PLAYER0,BOX153_ACTIVATED,0)
	NEXT_COMMAND_REUSABLE
	COMPUTE_FLAG(PLAYER0,FLAG4,MULTIPLY,PLAYER0,BOX53_ACTIVATED,0)
	NEXT_COMMAND_REUSABLE
	COMPUTE_FLAG(PLAYER0,FLAG0,INCREASE,PLAYER0,FLAG4,0)
	NEXT_COMMAND_REUSABLE
	COMPUTE_FLAG(PLAYER0,BOX255_ACTIVATED,SET,PLAYER0,IMP,0)
	NEXT_COMMAND_REUSABLE
	SET_FLAG(PLAYER_GOOD,FLAG0,100)
	NEXT_COMMAND_REUSABLE
	COMPUTE_FLAG(PLAYER_GOOD,FLAG0,INCREASE,PLAYER0,IMP,0)
	NEXT_COMMAND_REUSABLE
	COMPUTE_FLAG(PLAYER_GOOD,FLAG0,MULTIPLY,PLAYER0,IMP,0)
	NEXT_COMMAND_REUSABLE
	COMPUTE_FLAG(PLAYER_GOOD,FLAG0,MULTIPLY,PLAYER0,IMP,0)
	NEXT_COMMAND_REUSABLE
	COMPUTE_FLAG(PLAYER_GOOD,FLAG0,DECREASE,PLAYER0,FLAG7,0)
	NEXT_COMMAND_REUSABLE
	SET_FLAG(PLAYER0,BOX206_ACTIVATED,0)
ENDIF

REM # "Dynamic Duo: Spawns a Fairy and a Wizard to help you kill Imps."
IF(PLAYER0,BOX207_ACTIVATED > 0)
	IF(PLAYER0,BOX3_ACTIVATED < 253)
		NEXT_COMMAND_REUSABLE
		ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,FAIRY,LAST_EVENT,1,10,0)
		NEXT_COMMAND_REUSABLE
		ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,WIZARD,LAST_EVENT,1,10,0)
		NEXT_COMMAND_REUSABLE
		ADD_TO_FLAG(PLAYER0,BOX207_ACTIVATED,-1)
	ENDIF
ENDIF
IF(PLAYER_GOOD,FAIRY > 0)
	IF(PLAYER_GOOD,FLAG3 < 1920)
		NEXT_COMMAND_REUSABLE
		COMPUTE_FLAG(PLAYER_GOOD,FLAG3,INCREASE,PLAYER_GOOD,FAIRY,0)
		NEXT_COMMAND_REUSABLE
		USE_POWER_ON_CREATURE(PLAYER_GOOD,FAIRY,ANYWHERE,PLAYER_GOOD,POWER_SPEED,9,1)
	ENDIF
	IF(PLAYER_GOOD,FLAG3 >= 1920)
		NEXT_COMMAND_REUSABLE
		KILL_CREATURE(PLAYER_GOOD,FAIRY,ANYWHERE,1)
		NEXT_COMMAND_REUSABLE
		SET_FLAG(PLAYER_GOOD,FLAG3,0)
	ENDIF
ENDIF
IF(PLAYER_GOOD,WIZARD > 0)
	IF(PLAYER_GOOD,FLAG4 < 1800)
		NEXT_COMMAND_REUSABLE
		COMPUTE_FLAG(PLAYER_GOOD,FLAG4,INCREASE,PLAYER_GOOD,WIZARD,0)
		NEXT_COMMAND_REUSABLE
		USE_POWER_ON_CREATURE(PLAYER_GOOD,WIZARD,ANYWHERE,PLAYER_GOOD,POWER_SPEED,9,1)
	ENDIF
	IF(PLAYER_GOOD,FLAG4 >= 1800)
		NEXT_COMMAND_REUSABLE
		KILL_CREATURE(PLAYER_GOOD,WIZARD,ANYWHERE,1)
		NEXT_COMMAND_REUSABLE
		SET_FLAG(PLAYER_GOOD,FLAG4,0)
	ENDIF
ENDIF

REM # "Bombing Swarm: Spawns a swarm of flies and casts Time Bomb on them."
IF(PLAYER0,BOX208_ACTIVATED > 0)
	NEXT_COMMAND_REUSABLE
	ADD_TO_FLAG(PLAYER3,FLAG3,32)
	NEXT_COMMAND_REUSABLE
	ADD_TO_FLAG(PLAYER0,BOX208_ACTIVATED,-1)
ENDIF
IF(PLAYER3,FLAG3 > 0)
	IF(PLAYER0,BOX3_ACTIVATED < 255)
		IF(PLAYER_GOOD,FLY < 8)
			NEXT_COMMAND_REUSABLE
			RANDOMISE_FLAG(PLAYER3,BOX3_ACTIVATED,8)
			IF(PLAYER3,BOX3_ACTIVATED == 1)
				NEXT_COMMAND_REUSABLE
				ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,FLY,103,1,10,0)
				NEXT_COMMAND_REUSABLE
				ADD_TO_FLAG(PLAYER3,FLAG3,-1)
			ENDIF
			IF(PLAYER3,BOX3_ACTIVATED == 2)
				NEXT_COMMAND_REUSABLE
				ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,FLY,104,1,10,0)
				NEXT_COMMAND_REUSABLE
				ADD_TO_FLAG(PLAYER3,FLAG3,-1)
			ENDIF
			IF(PLAYER3,BOX3_ACTIVATED == 3)
				NEXT_COMMAND_REUSABLE
				ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,FLY,105,1,10,0)
				NEXT_COMMAND_REUSABLE
				ADD_TO_FLAG(PLAYER3,FLAG3,-1)
			ENDIF
			IF(PLAYER3,BOX3_ACTIVATED == 4)
				NEXT_COMMAND_REUSABLE
				ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,FLY,106,1,10,0)
				NEXT_COMMAND_REUSABLE
				ADD_TO_FLAG(PLAYER3,FLAG3,-1)
			ENDIF
			IF(PLAYER3,BOX3_ACTIVATED == 5)
				NEXT_COMMAND_REUSABLE
				ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,FLY,107,1,10,0)
				NEXT_COMMAND_REUSABLE
				ADD_TO_FLAG(PLAYER3,FLAG3,-1)
			ENDIF
			IF(PLAYER3,BOX3_ACTIVATED == 6)
				NEXT_COMMAND_REUSABLE
				ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,FLY,108,1,10,0)
				NEXT_COMMAND_REUSABLE
				ADD_TO_FLAG(PLAYER3,FLAG3,-1)
			ENDIF
			IF(PLAYER3,BOX3_ACTIVATED == 7)
				NEXT_COMMAND_REUSABLE
				ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,FLY,109,1,10,0)
				NEXT_COMMAND_REUSABLE
				ADD_TO_FLAG(PLAYER3,FLAG3,-1)
			ENDIF
			IF(PLAYER3,BOX3_ACTIVATED == 8)
				NEXT_COMMAND_REUSABLE
				ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,FLY,110,1,10,0)
				NEXT_COMMAND_REUSABLE
				ADD_TO_FLAG(PLAYER3,FLAG3,-1)
			ENDIF
		ENDIF
	ENDIF
ENDIF
IF(PLAYER_GOOD,FLY > 0)
	NEXT_COMMAND_REUSABLE
	USE_POWER_ON_CREATURE(PLAYER_GOOD,FLY,ANYWHERE,PLAYER_GOOD,POWER_SPEED,9,1)
	NEXT_COMMAND_REUSABLE
	USE_POWER_ON_CREATURE(PLAYER_GOOD,FLY,ANYWHERE,PLAYER_GOOD,POWER_TIME_BOMB,9,1)
ENDIF

REM # "Score Up: Increases score gain by 1."
IF(PLAYER0,BOX209_ACTIVATED > 0)
	IF(PLAYER0,BOX253_ACTIVATED < 255)
		NEXT_COMMAND_REUSABLE
		ADD_TO_FLAG(PLAYER0,BOX253_ACTIVATED,1)
		NEXT_COMMAND_REUSABLE
		ADD_TO_FLAG(PLAYER0,BOX209_ACTIVATED,-1)
	ENDIF
	IF(PLAYER0,BOX253_ACTIVATED >= 255)
		IF(PLAYER0,BOX153_ACTIVATED < 255)
			NEXT_COMMAND_REUSABLE
			ADD_TO_FLAG(PLAYER0,BOX153_ACTIVATED,1)
			NEXT_COMMAND_REUSABLE
			ADD_TO_FLAG(PLAYER0,BOX209_ACTIVATED,-1)
		ENDIF
		IF(PLAYER0,BOX153_ACTIVATED >= 255)
			IF(PLAYER0,BOX53_ACTIVATED < 255)
				NEXT_COMMAND_REUSABLE
				ADD_TO_FLAG(PLAYER0,BOX53_ACTIVATED,1)
				NEXT_COMMAND_REUSABLE
				ADD_TO_FLAG(PLAYER0,BOX209_ACTIVATED,-1)
			ENDIF
		ENDIF
	ENDIF
ENDIF

REM # "Debug Mode!"
IF(PLAYER0,BOX249_ACTIVATED > 0)
	REM # SET_FLAG(PLAYER0,FLAG1,2147483647)
	REM # SET_FLAG(PLAYER0,FLAG2,2147483647)
	SET_FLAG(PLAYER3,FLAG3,2147483647)
ENDIF

REM # Score Computation & Display.
IF(PLAYER0,IMP < PLAYER0,BOX255_ACTIVATED)
	NEXT_COMMAND_REUSABLE
	COMPUTE_FLAG(PLAYER0,FLAG6,SET,PLAYER0,BOX255_ACTIVATED,0)
	NEXT_COMMAND_REUSABLE
	COMPUTE_FLAG(PLAYER0,FLAG6,DECREASE,PLAYER0,IMP,0)
	NEXT_COMMAND_REUSABLE
	COMPUTE_FLAG(PLAYER0,BOX200_ACTIVATED,INCREASE,PLAYER0,FLAG6,0)
	NEXT_COMMAND_REUSABLE
	COMPUTE_FLAG(PLAYER0,FLAG6,MULTIPLY,PLAYER0,BOX253_ACTIVATED,0)
	NEXT_COMMAND_REUSABLE
	COMPUTE_FLAG(PLAYER0,FLAG6,MULTIPLY,PLAYER0,BOX153_ACTIVATED,0)
	NEXT_COMMAND_REUSABLE
	COMPUTE_FLAG(PLAYER0,FLAG6,MULTIPLY,PLAYER0,BOX53_ACTIVATED,0)
	NEXT_COMMAND_REUSABLE
	COMPUTE_FLAG(PLAYER0,FLAG0,INCREASE,PLAYER0,FLAG6,0)
	NEXT_COMMAND_REUSABLE
	COMPUTE_FLAG(PLAYER0,BOX255_ACTIVATED,SET,PLAYER0,IMP,0)
	NEXT_COMMAND_REUSABLE
	SET_FLAG(PLAYER_GOOD,FLAG0,100)
	NEXT_COMMAND_REUSABLE
	COMPUTE_FLAG(PLAYER_GOOD,FLAG0,INCREASE,PLAYER0,IMP,0)
	NEXT_COMMAND_REUSABLE
	COMPUTE_FLAG(PLAYER_GOOD,FLAG0,MULTIPLY,PLAYER0,IMP,0)
	NEXT_COMMAND_REUSABLE
	COMPUTE_FLAG(PLAYER_GOOD,FLAG0,MULTIPLY,PLAYER0,IMP,0)
	NEXT_COMMAND_REUSABLE
	COMPUTE_FLAG(PLAYER_GOOD,FLAG0,DECREASE,PLAYER0,FLAG7,0)
ENDIF
IF(PLAYER0,FLAG0 > 10000)
	NEXT_COMMAND_REUSABLE
	ADD_GOLD_TO_PLAYER(PLAYER0,10000)
	NEXT_COMMAND_REUSABLE
	ADD_TO_FLAG(PLAYER0,FLAG0,-10000)
	NEXT_COMMAND_REUSABLE
	COMPUTE_FLAG(PLAYER0,FLAG7,SET,PLAYER0,MONEY,0)
	NEXT_COMMAND_REUSABLE
	COMPUTE_FLAG(PLAYER0,FLAG7,MULTIPLY,PLAYER0,MONEY,0)
ENDIF
IF(PLAYER0,FLAG0 > 1000)
	NEXT_COMMAND_REUSABLE
	ADD_GOLD_TO_PLAYER(PLAYER0,1000)
	NEXT_COMMAND_REUSABLE
	ADD_TO_FLAG(PLAYER0,FLAG0,-1000)
	NEXT_COMMAND_REUSABLE
	COMPUTE_FLAG(PLAYER0,FLAG7,SET,PLAYER0,MONEY,0)
	NEXT_COMMAND_REUSABLE
	COMPUTE_FLAG(PLAYER0,FLAG7,MULTIPLY,PLAYER0,MONEY,0)
ENDIF
IF(PLAYER0,FLAG0 > 100)
	NEXT_COMMAND_REUSABLE
	ADD_GOLD_TO_PLAYER(PLAYER0,100)
	NEXT_COMMAND_REUSABLE
	ADD_TO_FLAG(PLAYER0,FLAG0,-100)
	NEXT_COMMAND_REUSABLE
	COMPUTE_FLAG(PLAYER0,FLAG7,SET,PLAYER0,MONEY,0)
	NEXT_COMMAND_REUSABLE
	COMPUTE_FLAG(PLAYER0,FLAG7,MULTIPLY,PLAYER0,MONEY,0)
ENDIF
IF(PLAYER0,FLAG0 > 10)
	NEXT_COMMAND_REUSABLE
	ADD_GOLD_TO_PLAYER(PLAYER0,10)
	NEXT_COMMAND_REUSABLE
	ADD_TO_FLAG(PLAYER0,FLAG0,-10)
	NEXT_COMMAND_REUSABLE
	COMPUTE_FLAG(PLAYER0,FLAG7,SET,PLAYER0,MONEY,0)
	NEXT_COMMAND_REUSABLE
	COMPUTE_FLAG(PLAYER0,FLAG7,MULTIPLY,PLAYER0,MONEY,0)
ENDIF
IF(PLAYER0,FLAG0 > 0)
	NEXT_COMMAND_REUSABLE
	ADD_GOLD_TO_PLAYER(PLAYER0,1)
	NEXT_COMMAND_REUSABLE
	ADD_TO_FLAG(PLAYER0,FLAG0,-1)
	NEXT_COMMAND_REUSABLE
	COMPUTE_FLAG(PLAYER0,FLAG7,SET,PLAYER0,MONEY,0)
	NEXT_COMMAND_REUSABLE
	COMPUTE_FLAG(PLAYER0,FLAG7,MULTIPLY,PLAYER0,MONEY,0)
ENDIF

REM # Hidden messages for when PLAYER0 reaches the maximum value of FLAG0.
IF(PLAYER0,MONEY >= PLAYER_GOOD,FLAG6)
	HIDE_VARIABLE
	QUICK_INFORMATION(2,"哦，看看谁能抽出一点时间来仔细阅读这条消息。真令人印象深刻！",ALL_PLAYERS)
	NEXT_COMMAND_REUSABLE
	ADD_TO_FLAG(PLAYER_GOOD,FLAG7,1)
	NEXT_COMMAND_REUSABLE
	ADD_TO_FLAG(PLAYER_GOOD,FLAG6,5000)
	SET_FLAG(PLAYER_GOOD,FLAG6,50000)
ENDIF
IF(PLAYER0,MONEY >= 50000)
	DISPLAY_VARIABLE(PLAYER_GOOD,FLAG7)
	QUICK_INFORMATION(3,"好了，好了，好了，看看你，达到了 50,000 金大关！不如我们再加把劲，看看你能不能征服这个艰巨的新挑战？每5,000金，我们就把新旗帜升1级。祝你好运，我毫无防备的受害者 哇哈哈",ALL_PLAYERS)
ENDIF
IF(PLAYER0,MONEY >= 100000)
	QUICK_INFORMATION(4,"哦，看在一切邪恶和扭曲的份上！看看你，你这个卑鄙的主谋，沐浴在十万金的丑恶海洋中！真是卑鄙无耻 请允许我向你提出一个建议，我邪恶的同志：停止这种疯狂的行为吧，因为根本就没有任何理由去冒险超越这种令人发指的财富。哇哈哈！",ALL_PLAYERS)
ENDIF
IF(PLAYER0,MONEY >= 1000000)
	QUICK_INFORMATION(5,"哦，亲爱的，你那孱弱的小脑袋似乎已经失去了理智，在完全疯狂的云雾中飘走了。真的，你已经踏上了一段疯狂的旅程，进入了绝对疯狂的深渊。在理智的领域里徘徊，你的思想永远迷失在自己灭亡的辉煌混沌中，这对你来说该是多么令人愉快的扭曲和乐趣啊。",ALL_PLAYERS)
ENDIF
IF(PLAYER0,MONEY >= 999999999)
	QUICK_INFORMATION(6,"啊，你这个可怜虫 由于某种扭曲的运气，你设法凑齐了 这个王国最后的可怜残余。的确，你的成就令人惊叹......或者说，你在自欺欺人！唉，你把这一切都挥霍一空了 一切都变得一文不值 只留下一片荒凉 你说胜利？哈 不过是空洞可怜的宣言罢了！",ALL_PLAYERS)
	LOSE_GAME
ENDIF

REM # Moving Timer Reduction & Limits.
IF(PLAYER0,BOX200_ACTIVATED > PLAYER0,BOX100_ACTIVATED)
	NEXT_COMMAND_REUSABLE
	ADD_TO_FLAG(PLAYER0,BOX100_ACTIVATED,10)
	NEXT_COMMAND_REUSABLE
	TRAP_AVAILABLE(PLAYER0,BOULDER,1,1)
	NEXT_COMMAND_REUSABLE
	ADD_TO_FLAG(PLAYER0,BOX111_ACTIVATED,-1)
	NEXT_COMMAND_REUSABLE
	ADD_TO_FLAG(PLAYER0,BOX112_ACTIVATED,-2)
	NEXT_COMMAND_REUSABLE
	ADD_TO_FLAG(PLAYER0,BOX113_ACTIVATED,-3)
	NEXT_COMMAND_REUSABLE
	ADD_TO_FLAG(PLAYER0,BOX114_ACTIVATED,-4)
	NEXT_COMMAND_REUSABLE
	ADD_TO_FLAG(PLAYER0,BOX115_ACTIVATED,-5)
	NEXT_COMMAND_REUSABLE
	ADD_TO_FLAG(PLAYER0,BOX116_ACTIVATED,-6)
	NEXT_COMMAND_REUSABLE
	ADD_TO_FLAG(PLAYER0,BOX117_ACTIVATED,-7)
	NEXT_COMMAND_REUSABLE
	ADD_TO_FLAG(PLAYER0,BOX118_ACTIVATED,-8)
	NEXT_COMMAND_REUSABLE
	ADD_TO_FLAG(PLAYER0,BOX119_ACTIVATED,-9)
	NEXT_COMMAND_REUSABLE
	ADD_TO_FLAG(PLAYER0,BOX120_ACTIVATED,-10)
	NEXT_COMMAND_REUSABLE
	ADD_TO_FLAG(PLAYER0,BOX209_ACTIVATED,1)
	NEXT_COMMAND_REUSABLE
	SET_FLAG(PLAYER0,BOX200_ACTIVATED,0)
ENDIF
IF(PLAYER0,BOX111_ACTIVATED < 1)
	NEXT_COMMAND_REUSABLE
	SET_FLAG(PLAYER0,BOX111_ACTIVATED,1)
ENDIF
IF(PLAYER0,BOX112_ACTIVATED < 2)
	NEXT_COMMAND_REUSABLE
	SET_FLAG(PLAYER0,BOX112_ACTIVATED,2)
ENDIF
IF(PLAYER0,BOX113_ACTIVATED < 3)
	NEXT_COMMAND_REUSABLE
	SET_FLAG(PLAYER0,BOX113_ACTIVATED,3)
ENDIF
IF(PLAYER0,BOX114_ACTIVATED < 4)
	NEXT_COMMAND_REUSABLE
	SET_FLAG(PLAYER0,BOX114_ACTIVATED,4)
ENDIF
IF(PLAYER0,BOX115_ACTIVATED < 5)
	NEXT_COMMAND_REUSABLE
	SET_FLAG(PLAYER0,BOX115_ACTIVATED,5)
ENDIF
IF(PLAYER0,BOX116_ACTIVATED < 6)
	NEXT_COMMAND_REUSABLE
	SET_FLAG(PLAYER0,BOX116_ACTIVATED,6)
ENDIF
IF(PLAYER0,BOX117_ACTIVATED < 7)
	NEXT_COMMAND_REUSABLE
	SET_FLAG(PLAYER0,BOX117_ACTIVATED,7)
ENDIF
IF(PLAYER0,BOX118_ACTIVATED < 8)
	NEXT_COMMAND_REUSABLE
	SET_FLAG(PLAYER0,BOX118_ACTIVATED,8)
ENDIF
IF(PLAYER0,BOX119_ACTIVATED < 9)
	NEXT_COMMAND_REUSABLE
	SET_FLAG(PLAYER0,BOX119_ACTIVATED,9)
ENDIF
IF(PLAYER0,BOX120_ACTIVATED < 10)
	NEXT_COMMAND_REUSABLE
	SET_FLAG(PLAYER0,BOX120_ACTIVATED,10)
ENDIF
IF(PLAYER0,BOX111_ACTIVATED > 25)
	NEXT_COMMAND_REUSABLE
	SET_FLAG(PLAYER0,BOX111_ACTIVATED,25)
ENDIF
IF(PLAYER0,BOX112_ACTIVATED > 50)
	NEXT_COMMAND_REUSABLE
	SET_FLAG(PLAYER0,BOX112_ACTIVATED,50)
ENDIF
IF(PLAYER0,BOX113_ACTIVATED > 75)
	NEXT_COMMAND_REUSABLE
	SET_FLAG(PLAYER0,BOX113_ACTIVATED,75)
ENDIF
IF(PLAYER0,BOX114_ACTIVATED > 100)
	NEXT_COMMAND_REUSABLE
	SET_FLAG(PLAYER0,BOX114_ACTIVATED,100)
ENDIF
IF(PLAYER0,BOX115_ACTIVATED > 125)
	NEXT_COMMAND_REUSABLE
	SET_FLAG(PLAYER0,BOX115_ACTIVATED,125)
ENDIF
IF(PLAYER0,BOX116_ACTIVATED > 150)
	NEXT_COMMAND_REUSABLE
	SET_FLAG(PLAYER0,BOX116_ACTIVATED,150)
ENDIF
IF(PLAYER0,BOX117_ACTIVATED > 175)
	NEXT_COMMAND_REUSABLE
	SET_FLAG(PLAYER0,BOX117_ACTIVATED,175)
ENDIF
IF(PLAYER0,BOX118_ACTIVATED > 200)
	NEXT_COMMAND_REUSABLE
	SET_FLAG(PLAYER0,BOX118_ACTIVATED,200)
ENDIF
IF(PLAYER0,BOX119_ACTIVATED > 225)
	NEXT_COMMAND_REUSABLE
	SET_FLAG(PLAYER0,BOX119_ACTIVATED,225)
ENDIF
IF(PLAYER0,BOX120_ACTIVATED > 250)
	NEXT_COMMAND_REUSABLE
	SET_FLAG(PLAYER0,BOX120_ACTIVATED,250)
ENDIF

REM # Main Loop.
IF(PLAYER0,DUNGEON_DESTROYED == 0)
	IF(PLAYER0,BOX0_ACTIVATED > 0)
		SET_FLAG(PLAYER_GOOD,FLAG0,100)
		DISPLAY_VARIABLE(PLAYER0,FLAG7)
		NEXT_COMMAND_REUSABLE
		COMPUTE_FLAG(PLAYER0,BOX3_ACTIVATED,SET,PLAYER0,IMP,0)
		NEXT_COMMAND_REUSABLE
		COMPUTE_FLAG(PLAYER0,BOX3_ACTIVATED,INCREASE,PLAYER_GOOD,FLY,0)
		NEXT_COMMAND_REUSABLE
		COMPUTE_FLAG(PLAYER0,BOX3_ACTIVATED,INCREASE,PLAYER_GOOD,HORNY,0)
		NEXT_COMMAND_REUSABLE
		COMPUTE_FLAG(PLAYER0,BOX3_ACTIVATED,INCREASE,PLAYER_GOOD,BILE_DEMON,0)
		NEXT_COMMAND_REUSABLE
		COMPUTE_FLAG(PLAYER0,BOX3_ACTIVATED,INCREASE,PLAYER_GOOD,FAIRY,0)
		NEXT_COMMAND_REUSABLE
		COMPUTE_FLAG(PLAYER0,BOX3_ACTIVATED,INCREASE,PLAYER_GOOD,WIZARD,0)
		NEXT_COMMAND_REUSABLE
		ADD_TO_FLAG(PLAYER0,BOX101_ACTIVATED,1)
		NEXT_COMMAND_REUSABLE
		ADD_TO_FLAG(PLAYER0,BOX102_ACTIVATED,1)
		NEXT_COMMAND_REUSABLE
		ADD_TO_FLAG(PLAYER0,BOX103_ACTIVATED,1)
		NEXT_COMMAND_REUSABLE
		ADD_TO_FLAG(PLAYER0,BOX104_ACTIVATED,1)
		NEXT_COMMAND_REUSABLE
		ADD_TO_FLAG(PLAYER0,BOX105_ACTIVATED,1)
		NEXT_COMMAND_REUSABLE
		ADD_TO_FLAG(PLAYER0,BOX106_ACTIVATED,1)
		NEXT_COMMAND_REUSABLE
		ADD_TO_FLAG(PLAYER0,BOX107_ACTIVATED,1)
		NEXT_COMMAND_REUSABLE
		ADD_TO_FLAG(PLAYER0,BOX108_ACTIVATED,1)
		NEXT_COMMAND_REUSABLE
		ADD_TO_FLAG(PLAYER0,BOX109_ACTIVATED,1)
		NEXT_COMMAND_REUSABLE
		ADD_TO_FLAG(PLAYER0,BOX110_ACTIVATED,1)
		NEXT_COMMAND_REUSABLE
		ADD_TO_FLAG(PLAYER_GOOD,FLAG0,-1)
		IF(PLAYER_GOOD,FLAG0 <= 0)
			NEXT_COMMAND_REUSABLE
			RANDOMISE_FLAG(PLAYER_GOOD,BOX255_ACTIVATED,5)
			IF(PLAYER_GOOD,BOX255_ACTIVATED == 1)
				NEXT_COMMAND_REUSABLE
				ADD_CREATURE_TO_LEVEL(PLAYER0,IMP,10,1,10,0)
				NEXT_COMMAND_REUSABLE
				USE_SPELL_ON_PLAYERS_CREATURES(PLAYER0,IMP,SPELL_FREEZE)
				NEXT_COMMAND_REUSABLE
				COMPUTE_FLAG(PLAYER0,BOX255_ACTIVATED,SET,PLAYER0,IMP,0)
				NEXT_COMMAND_REUSABLE
				SET_FLAG(PLAYER_GOOD,FLAG0,100)
				NEXT_COMMAND_REUSABLE
				COMPUTE_FLAG(PLAYER_GOOD,FLAG0,INCREASE,PLAYER0,IMP,0)
				NEXT_COMMAND_REUSABLE
				COMPUTE_FLAG(PLAYER_GOOD,FLAG0,MULTIPLY,PLAYER0,IMP,0)
				NEXT_COMMAND_REUSABLE
				COMPUTE_FLAG(PLAYER_GOOD,FLAG0,MULTIPLY,PLAYER0,IMP,0)
				NEXT_COMMAND_REUSABLE
				COMPUTE_FLAG(PLAYER_GOOD,FLAG0,DECREASE,PLAYER0,FLAG7,0)
			ENDIF
			IF(PLAYER_GOOD,BOX255_ACTIVATED == 2)
				NEXT_COMMAND_REUSABLE
				ADD_CREATURE_TO_LEVEL(PLAYER0,IMP,30,1,10,0)
				NEXT_COMMAND_REUSABLE
				USE_SPELL_ON_PLAYERS_CREATURES(PLAYER0,IMP,SPELL_FREEZE)
				NEXT_COMMAND_REUSABLE
				COMPUTE_FLAG(PLAYER0,BOX255_ACTIVATED,SET,PLAYER0,IMP,0)
				NEXT_COMMAND_REUSABLE
				SET_FLAG(PLAYER_GOOD,FLAG0,100)
				NEXT_COMMAND_REUSABLE
				COMPUTE_FLAG(PLAYER_GOOD,FLAG0,INCREASE,PLAYER0,IMP,0)
				NEXT_COMMAND_REUSABLE
				COMPUTE_FLAG(PLAYER_GOOD,FLAG0,MULTIPLY,PLAYER0,IMP,0)
				NEXT_COMMAND_REUSABLE
				COMPUTE_FLAG(PLAYER_GOOD,FLAG0,MULTIPLY,PLAYER0,IMP,0)
				NEXT_COMMAND_REUSABLE
				COMPUTE_FLAG(PLAYER_GOOD,FLAG0,DECREASE,PLAYER0,FLAG7,0)
			ENDIF
			IF(PLAYER_GOOD,BOX255_ACTIVATED == 3)
				NEXT_COMMAND_REUSABLE
				ADD_CREATURE_TO_LEVEL(PLAYER0,IMP,50,1,10,0)
				NEXT_COMMAND_REUSABLE
				USE_SPELL_ON_PLAYERS_CREATURES(PLAYER0,IMP,SPELL_FREEZE)
				NEXT_COMMAND_REUSABLE
				COMPUTE_FLAG(PLAYER0,BOX255_ACTIVATED,SET,PLAYER0,IMP,0)
				NEXT_COMMAND_REUSABLE
				SET_FLAG(PLAYER_GOOD,FLAG0,100)
				NEXT_COMMAND_REUSABLE
				COMPUTE_FLAG(PLAYER_GOOD,FLAG0,INCREASE,PLAYER0,IMP,0)
				NEXT_COMMAND_REUSABLE
				COMPUTE_FLAG(PLAYER_GOOD,FLAG0,MULTIPLY,PLAYER0,IMP,0)
				NEXT_COMMAND_REUSABLE
				COMPUTE_FLAG(PLAYER_GOOD,FLAG0,MULTIPLY,PLAYER0,IMP,0)
				NEXT_COMMAND_REUSABLE
				COMPUTE_FLAG(PLAYER_GOOD,FLAG0,DECREASE,PLAYER0,FLAG7,0)
			ENDIF
			IF(PLAYER_GOOD,BOX255_ACTIVATED == 4)
				NEXT_COMMAND_REUSABLE
				ADD_CREATURE_TO_LEVEL(PLAYER0,IMP,70,1,10,0)
				NEXT_COMMAND_REUSABLE
				USE_SPELL_ON_PLAYERS_CREATURES(PLAYER0,IMP,SPELL_FREEZE)
				NEXT_COMMAND_REUSABLE
				COMPUTE_FLAG(PLAYER0,BOX255_ACTIVATED,SET,PLAYER0,IMP,0)
				NEXT_COMMAND_REUSABLE
				SET_FLAG(PLAYER_GOOD,FLAG0,100)
				NEXT_COMMAND_REUSABLE
				COMPUTE_FLAG(PLAYER_GOOD,FLAG0,INCREASE,PLAYER0,IMP,0)
				NEXT_COMMAND_REUSABLE
				COMPUTE_FLAG(PLAYER_GOOD,FLAG0,MULTIPLY,PLAYER0,IMP,0)
				NEXT_COMMAND_REUSABLE
				COMPUTE_FLAG(PLAYER_GOOD,FLAG0,MULTIPLY,PLAYER0,IMP,0)
				NEXT_COMMAND_REUSABLE
				COMPUTE_FLAG(PLAYER_GOOD,FLAG0,DECREASE,PLAYER0,FLAG7,0)
			ENDIF
			IF(PLAYER_GOOD,BOX255_ACTIVATED == 5)
				NEXT_COMMAND_REUSABLE
				ADD_CREATURE_TO_LEVEL(PLAYER0,IMP,90,1,10,0)
				NEXT_COMMAND_REUSABLE
				USE_SPELL_ON_PLAYERS_CREATURES(PLAYER0,IMP,SPELL_FREEZE)
				NEXT_COMMAND_REUSABLE
				COMPUTE_FLAG(PLAYER0,BOX255_ACTIVATED,SET,PLAYER0,IMP,0)
				NEXT_COMMAND_REUSABLE
				SET_FLAG(PLAYER_GOOD,FLAG0,100)
				NEXT_COMMAND_REUSABLE
				COMPUTE_FLAG(PLAYER_GOOD,FLAG0,INCREASE,PLAYER0,IMP,0)
				NEXT_COMMAND_REUSABLE
				COMPUTE_FLAG(PLAYER_GOOD,FLAG0,MULTIPLY,PLAYER0,IMP,0)
				NEXT_COMMAND_REUSABLE
				COMPUTE_FLAG(PLAYER_GOOD,FLAG0,MULTIPLY,PLAYER0,IMP,0)
				NEXT_COMMAND_REUSABLE
				COMPUTE_FLAG(PLAYER_GOOD,FLAG0,DECREASE,PLAYER0,FLAG7,0)
			ENDIF
		ENDIF
		SET_TIMER(PLAYER0,TIMER0)
		IF(PLAYER0,TIMER0 >= 1200)
			NEXT_COMMAND_REUSABLE
			RANDOMISE_FLAG(PLAYER_GOOD,BOX249_ACTIVATED,8)
			IF(PLAYER_GOOD,BOX249_ACTIVATED == 1)
				NEXT_COMMAND_REUSABLE
				ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,103,201)
				NEXT_COMMAND_REUSABLE
				SET_TIMER(PLAYER0,TIMER0)
			ENDIF
			IF(PLAYER_GOOD,BOX249_ACTIVATED == 2)
				NEXT_COMMAND_REUSABLE
				ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,104,202)
				NEXT_COMMAND_REUSABLE
				SET_TIMER(PLAYER0,TIMER0)
			ENDIF
			IF(PLAYER_GOOD,BOX249_ACTIVATED == 3)
				NEXT_COMMAND_REUSABLE
				ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,105,203)
				NEXT_COMMAND_REUSABLE
				SET_TIMER(PLAYER0,TIMER0)
			ENDIF
			IF(PLAYER_GOOD,BOX249_ACTIVATED == 4)
				NEXT_COMMAND_REUSABLE
				ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,106,204)
				NEXT_COMMAND_REUSABLE
				SET_TIMER(PLAYER0,TIMER0)
			ENDIF
			IF(PLAYER_GOOD,BOX249_ACTIVATED == 5)
				NEXT_COMMAND_REUSABLE
				ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,107,205)
				NEXT_COMMAND_REUSABLE
				SET_TIMER(PLAYER0,TIMER0)
			ENDIF
			IF(PLAYER_GOOD,BOX249_ACTIVATED == 6)
				NEXT_COMMAND_REUSABLE
				ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,108,206)
				NEXT_COMMAND_REUSABLE
				SET_TIMER(PLAYER0,TIMER0)
			ENDIF
			IF(PLAYER_GOOD,BOX249_ACTIVATED == 7)
				NEXT_COMMAND_REUSABLE
				ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,109,207)
				NEXT_COMMAND_REUSABLE
				SET_TIMER(PLAYER0,TIMER0)
			ENDIF
			IF(PLAYER_GOOD,BOX249_ACTIVATED == 8)
				NEXT_COMMAND_REUSABLE
				ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,110,208)
				NEXT_COMMAND_REUSABLE
				SET_TIMER(PLAYER0,TIMER0)
			ENDIF
		ENDIF
	ENDIF
ENDIF

REM # Freeze Loop.
IF(PLAYER_GOOD,TIMER0 >= 20)
	NEXT_COMMAND_REUSABLE
	USE_SPELL_ON_PLAYERS_CREATURES(PLAYER0,IMP,SPELL_FREEZE)
	NEXT_COMMAND_REUSABLE
	SET_TIMER(PLAYER_GOOD,TIMER0)
ENDIF

REM # Moving Loop.
IF(PLAYER0,BOX101_ACTIVATED >= PLAYER0,BOX111_ACTIVATED)
	NEXT_COMMAND_REUSABLE
	MOVE_CREATURE(PLAYER0,IMP,AT_ACTION_POINT[11],1,1,0)
	NEXT_COMMAND_REUSABLE
	MOVE_CREATURE(PLAYER0,IMP,AT_ACTION_POINT[31],1,21,0)
	NEXT_COMMAND_REUSABLE
	MOVE_CREATURE(PLAYER0,IMP,AT_ACTION_POINT[51],1,41,0)
	NEXT_COMMAND_REUSABLE
	MOVE_CREATURE(PLAYER0,IMP,AT_ACTION_POINT[71],1,61,0)
	NEXT_COMMAND_REUSABLE
	MOVE_CREATURE(PLAYER0,IMP,AT_ACTION_POINT[91],1,81,0)
	NEXT_COMMAND_REUSABLE
	USE_SPELL_ON_PLAYERS_CREATURES(PLAYER0,IMP,SPELL_FREEZE)
	NEXT_COMMAND_REUSABLE
	SET_FLAG(PLAYER0,BOX101_ACTIVATED,0)
ENDIF
IF(PLAYER0,BOX102_ACTIVATED >= PLAYER0,BOX112_ACTIVATED)
	NEXT_COMMAND_REUSABLE
	MOVE_CREATURE(PLAYER0,IMP,AT_ACTION_POINT[12],1,2,0)
	NEXT_COMMAND_REUSABLE
	MOVE_CREATURE(PLAYER0,IMP,AT_ACTION_POINT[32],1,22,0)
	NEXT_COMMAND_REUSABLE
	MOVE_CREATURE(PLAYER0,IMP,AT_ACTION_POINT[52],1,42,0)
	NEXT_COMMAND_REUSABLE
	MOVE_CREATURE(PLAYER0,IMP,AT_ACTION_POINT[72],1,62,0)
	NEXT_COMMAND_REUSABLE
	MOVE_CREATURE(PLAYER0,IMP,AT_ACTION_POINT[92],1,82,0)
	NEXT_COMMAND_REUSABLE
	USE_SPELL_ON_PLAYERS_CREATURES(PLAYER0,IMP,SPELL_FREEZE)
	NEXT_COMMAND_REUSABLE
	SET_FLAG(PLAYER0,BOX102_ACTIVATED,0)
ENDIF
IF(PLAYER0,BOX103_ACTIVATED >= PLAYER0,BOX113_ACTIVATED)
	NEXT_COMMAND_REUSABLE
	MOVE_CREATURE(PLAYER0,IMP,AT_ACTION_POINT[13],1,3,0)
	NEXT_COMMAND_REUSABLE
	MOVE_CREATURE(PLAYER0,IMP,AT_ACTION_POINT[33],1,23,0)
	NEXT_COMMAND_REUSABLE
	MOVE_CREATURE(PLAYER0,IMP,AT_ACTION_POINT[53],1,43,0)
	NEXT_COMMAND_REUSABLE
	MOVE_CREATURE(PLAYER0,IMP,AT_ACTION_POINT[73],1,63,0)
	NEXT_COMMAND_REUSABLE
	MOVE_CREATURE(PLAYER0,IMP,AT_ACTION_POINT[93],1,83,0)
	NEXT_COMMAND_REUSABLE
	USE_SPELL_ON_PLAYERS_CREATURES(PLAYER0,IMP,SPELL_FREEZE)
	NEXT_COMMAND_REUSABLE
	SET_FLAG(PLAYER0,BOX103_ACTIVATED,0)
ENDIF
IF(PLAYER0,BOX104_ACTIVATED >= PLAYER0,BOX114_ACTIVATED)
	NEXT_COMMAND_REUSABLE
	MOVE_CREATURE(PLAYER0,IMP,AT_ACTION_POINT[14],1,4,0)
	NEXT_COMMAND_REUSABLE
	MOVE_CREATURE(PLAYER0,IMP,AT_ACTION_POINT[34],1,24,0)
	NEXT_COMMAND_REUSABLE
	MOVE_CREATURE(PLAYER0,IMP,AT_ACTION_POINT[54],1,44,0)
	NEXT_COMMAND_REUSABLE
	MOVE_CREATURE(PLAYER0,IMP,AT_ACTION_POINT[74],1,64,0)
	NEXT_COMMAND_REUSABLE
	MOVE_CREATURE(PLAYER0,IMP,AT_ACTION_POINT[94],1,84,0)
	NEXT_COMMAND_REUSABLE
	USE_SPELL_ON_PLAYERS_CREATURES(PLAYER0,IMP,SPELL_FREEZE)
	NEXT_COMMAND_REUSABLE
	SET_FLAG(PLAYER0,BOX104_ACTIVATED,0)
ENDIF
IF(PLAYER0,BOX105_ACTIVATED >= PLAYER0,BOX115_ACTIVATED)
	NEXT_COMMAND_REUSABLE
	MOVE_CREATURE(PLAYER0,IMP,AT_ACTION_POINT[15],1,5,0)
	NEXT_COMMAND_REUSABLE
	MOVE_CREATURE(PLAYER0,IMP,AT_ACTION_POINT[35],1,25,0)
	NEXT_COMMAND_REUSABLE
	MOVE_CREATURE(PLAYER0,IMP,AT_ACTION_POINT[55],1,45,0)
	NEXT_COMMAND_REUSABLE
	MOVE_CREATURE(PLAYER0,IMP,AT_ACTION_POINT[75],1,65,0)
	NEXT_COMMAND_REUSABLE
	MOVE_CREATURE(PLAYER0,IMP,AT_ACTION_POINT[95],1,85,0)
	NEXT_COMMAND_REUSABLE
	USE_SPELL_ON_PLAYERS_CREATURES(PLAYER0,IMP,SPELL_FREEZE)
	NEXT_COMMAND_REUSABLE
	SET_FLAG(PLAYER0,BOX105_ACTIVATED,0)
ENDIF
IF(PLAYER0,BOX106_ACTIVATED >= PLAYER0,BOX116_ACTIVATED)
	NEXT_COMMAND_REUSABLE
	MOVE_CREATURE(PLAYER0,IMP,AT_ACTION_POINT[16],1,6,0)
	NEXT_COMMAND_REUSABLE
	MOVE_CREATURE(PLAYER0,IMP,AT_ACTION_POINT[36],1,26,0)
	NEXT_COMMAND_REUSABLE
	MOVE_CREATURE(PLAYER0,IMP,AT_ACTION_POINT[56],1,46,0)
	NEXT_COMMAND_REUSABLE
	MOVE_CREATURE(PLAYER0,IMP,AT_ACTION_POINT[76],1,66,0)
	NEXT_COMMAND_REUSABLE
	MOVE_CREATURE(PLAYER0,IMP,AT_ACTION_POINT[96],1,86,0)
	NEXT_COMMAND_REUSABLE
	USE_SPELL_ON_PLAYERS_CREATURES(PLAYER0,IMP,SPELL_FREEZE)
	NEXT_COMMAND_REUSABLE
	SET_FLAG(PLAYER0,BOX106_ACTIVATED,0)
ENDIF
IF(PLAYER0,BOX107_ACTIVATED >= PLAYER0,BOX117_ACTIVATED)
	NEXT_COMMAND_REUSABLE
	MOVE_CREATURE(PLAYER0,IMP,AT_ACTION_POINT[17],1,7,0)
	NEXT_COMMAND_REUSABLE
	MOVE_CREATURE(PLAYER0,IMP,AT_ACTION_POINT[37],1,27,0)
	NEXT_COMMAND_REUSABLE
	MOVE_CREATURE(PLAYER0,IMP,AT_ACTION_POINT[57],1,47,0)
	NEXT_COMMAND_REUSABLE
	MOVE_CREATURE(PLAYER0,IMP,AT_ACTION_POINT[77],1,67,0)
	NEXT_COMMAND_REUSABLE
	MOVE_CREATURE(PLAYER0,IMP,AT_ACTION_POINT[97],1,87,0)
	NEXT_COMMAND_REUSABLE
	USE_SPELL_ON_PLAYERS_CREATURES(PLAYER0,IMP,SPELL_FREEZE)
	NEXT_COMMAND_REUSABLE
	SET_FLAG(PLAYER0,BOX107_ACTIVATED,0)
ENDIF
IF(PLAYER0,BOX108_ACTIVATED >= PLAYER0,BOX118_ACTIVATED)
	NEXT_COMMAND_REUSABLE
	MOVE_CREATURE(PLAYER0,IMP,AT_ACTION_POINT[18],1,8,0)
	NEXT_COMMAND_REUSABLE
	MOVE_CREATURE(PLAYER0,IMP,AT_ACTION_POINT[38],1,28,0)
	NEXT_COMMAND_REUSABLE
	MOVE_CREATURE(PLAYER0,IMP,AT_ACTION_POINT[58],1,48,0)
	NEXT_COMMAND_REUSABLE
	MOVE_CREATURE(PLAYER0,IMP,AT_ACTION_POINT[78],1,68,0)
	NEXT_COMMAND_REUSABLE
	MOVE_CREATURE(PLAYER0,IMP,AT_ACTION_POINT[98],1,88,0)
	NEXT_COMMAND_REUSABLE
	USE_SPELL_ON_PLAYERS_CREATURES(PLAYER0,IMP,SPELL_FREEZE)
	NEXT_COMMAND_REUSABLE
	SET_FLAG(PLAYER0,BOX108_ACTIVATED,0)
ENDIF
IF(PLAYER0,BOX109_ACTIVATED >= PLAYER0,BOX119_ACTIVATED)
	NEXT_COMMAND_REUSABLE
	MOVE_CREATURE(PLAYER0,IMP,AT_ACTION_POINT[19],1,9,0)
	NEXT_COMMAND_REUSABLE
	MOVE_CREATURE(PLAYER0,IMP,AT_ACTION_POINT[39],1,29,0)
	NEXT_COMMAND_REUSABLE
	MOVE_CREATURE(PLAYER0,IMP,AT_ACTION_POINT[59],1,49,0)
	NEXT_COMMAND_REUSABLE
	MOVE_CREATURE(PLAYER0,IMP,AT_ACTION_POINT[79],1,69,0)
	NEXT_COMMAND_REUSABLE
	MOVE_CREATURE(PLAYER0,IMP,AT_ACTION_POINT[99],1,89,0)
	NEXT_COMMAND_REUSABLE
	USE_SPELL_ON_PLAYERS_CREATURES(PLAYER0,IMP,SPELL_FREEZE)
	NEXT_COMMAND_REUSABLE
	SET_FLAG(PLAYER0,BOX109_ACTIVATED,0)
ENDIF
IF(PLAYER0,BOX110_ACTIVATED >= PLAYER0,BOX120_ACTIVATED)
	NEXT_COMMAND_REUSABLE
	MOVE_CREATURE(PLAYER0,IMP,AT_ACTION_POINT[20],1,101,0)
	NEXT_COMMAND_REUSABLE
	MOVE_CREATURE(PLAYER0,IMP,AT_ACTION_POINT[40],1,101,0)
	NEXT_COMMAND_REUSABLE
	MOVE_CREATURE(PLAYER0,IMP,AT_ACTION_POINT[60],1,101,0)
	NEXT_COMMAND_REUSABLE
	MOVE_CREATURE(PLAYER0,IMP,AT_ACTION_POINT[80],1,101,0)
	NEXT_COMMAND_REUSABLE
	MOVE_CREATURE(PLAYER0,IMP,AT_ACTION_POINT[100],1,101,0)
	NEXT_COMMAND_REUSABLE
	USE_SPELL_ON_PLAYERS_CREATURES(PLAYER0,IMP,SPELL_FREEZE)
	NEXT_COMMAND_REUSABLE
	COUNT_CREATURES_AT_ACTION_POINT(102,PLAYER0,IMP,PLAYER0,BOX250_ACTIVATED)
	NEXT_COMMAND_REUSABLE
	SET_FLAG(PLAYER0,BOX110_ACTIVATED,0)
ENDIF
IF(PLAYER0,BOX250_ACTIVATED > 0)
	NEXT_COMMAND_REUSABLE
	RANDOMISE_FLAG(PLAYER_GOOD,BOX250_ACTIVATED,5)
	IF(PLAYER_GOOD,BOX250_ACTIVATED == 1)
		NEXT_COMMAND_REUSABLE
		MOVE_CREATURE(PLAYER0,IMP,AT_ACTION_POINT[102],1,10,47)
		NEXT_COMMAND_REUSABLE
		USE_SPELL_ON_PLAYERS_CREATURES(PLAYER0,IMP,SPELL_FREEZE)
		NEXT_COMMAND_REUSABLE
		COUNT_CREATURES_AT_ACTION_POINT(102,PLAYER0,IMP,PLAYER0,BOX250_ACTIVATED)
		NEXT_COMMAND_REUSABLE
		CREATE_EFFECT(14,101,3)
		NEXT_COMMAND_REUSABLE
		ADD_HEART_HEALTH(PLAYER0,-3750,1)
	ENDIF
	IF(PLAYER_GOOD,BOX250_ACTIVATED == 2)
		NEXT_COMMAND_REUSABLE
		MOVE_CREATURE(PLAYER0,IMP,AT_ACTION_POINT[102],1,30,47)
		NEXT_COMMAND_REUSABLE
		USE_SPELL_ON_PLAYERS_CREATURES(PLAYER0,IMP,SPELL_FREEZE)
		NEXT_COMMAND_REUSABLE
		COUNT_CREATURES_AT_ACTION_POINT(102,PLAYER0,IMP,PLAYER0,BOX250_ACTIVATED)
		NEXT_COMMAND_REUSABLE
		CREATE_EFFECT(14,101,3)
		NEXT_COMMAND_REUSABLE
		ADD_HEART_HEALTH(PLAYER0,-3750,1)
	ENDIF
	IF(PLAYER_GOOD,BOX250_ACTIVATED == 3)
		NEXT_COMMAND_REUSABLE
		MOVE_CREATURE(PLAYER0,IMP,AT_ACTION_POINT[102],1,50,47)
		NEXT_COMMAND_REUSABLE
		USE_SPELL_ON_PLAYERS_CREATURES(PLAYER0,IMP,SPELL_FREEZE)
		NEXT_COMMAND_REUSABLE
		COUNT_CREATURES_AT_ACTION_POINT(102,PLAYER0,IMP,PLAYER0,BOX250_ACTIVATED)
		NEXT_COMMAND_REUSABLE
		CREATE_EFFECT(14,101,3)
		NEXT_COMMAND_REUSABLE
		ADD_HEART_HEALTH(PLAYER0,-3750,1)
	ENDIF
	IF(PLAYER_GOOD,BOX250_ACTIVATED == 4)
		NEXT_COMMAND_REUSABLE
		MOVE_CREATURE(PLAYER0,IMP,AT_ACTION_POINT[102],1,70,47)
		NEXT_COMMAND_REUSABLE
		USE_SPELL_ON_PLAYERS_CREATURES(PLAYER0,IMP,SPELL_FREEZE)
		NEXT_COMMAND_REUSABLE
		COUNT_CREATURES_AT_ACTION_POINT(102,PLAYER0,IMP,PLAYER0,BOX250_ACTIVATED)
		NEXT_COMMAND_REUSABLE
		CREATE_EFFECT(14,101,3)
		NEXT_COMMAND_REUSABLE
		ADD_HEART_HEALTH(PLAYER0,-3750,1)
	ENDIF
	IF(PLAYER_GOOD,BOX250_ACTIVATED == 5)
		NEXT_COMMAND_REUSABLE
		MOVE_CREATURE(PLAYER0,IMP,AT_ACTION_POINT[102],1,90,47)
		NEXT_COMMAND_REUSABLE
		USE_SPELL_ON_PLAYERS_CREATURES(PLAYER0,IMP,SPELL_FREEZE)
		NEXT_COMMAND_REUSABLE
		COUNT_CREATURES_AT_ACTION_POINT(102,PLAYER0,IMP,PLAYER0,BOX250_ACTIVATED)
		NEXT_COMMAND_REUSABLE
		CREATE_EFFECT(14,101,3)
		NEXT_COMMAND_REUSABLE
		ADD_HEART_HEALTH(PLAYER0,-3750,1)
	ENDIF
ENDIF

REM # Initial Objective.
QUICK_OBJECTIVE(0,"A你好，邪恶的守护者！在这个罪恶的国度里，你可以尽情享受无情地扇耳光的扭曲快感，赚取丰厚的分数，直到你不可避免地死去。别担心，你可以向其他 “掌掴 ”爱好者炫耀你的可怜战绩。胜利就在前方等着你，只要残忍地击打你的爪牙，获得至少 1,000,000 分。愿邪恶降临到你身上，愿你的扭曲旅程充满魔鬼般的欢笑和邪恶的财富。",ALL_PLAYERS)

REM # Quick Information.
IF(PLAYER_GOOD,GAME_TURN >= 20)
	QUICK_INFORMATION(1,"要想真正掌握主动权，我建议你将视角放大，采用邪恶的视角。当你觉得自己真正邪恶并做好了准备时，轻点 “准备就绪！”方框，让毫无戒备的人坠入地狱。",53)
ENDIF

REM # Win Condition.
IF(PLAYER0,MONEY >= 999)
	WIN_GAME
ENDIF