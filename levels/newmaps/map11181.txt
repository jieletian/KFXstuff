LEVEL_VERSION(1)
SET_GENERATE_SPEED(200)
START_MONEY(ALL_PLAYERS,2500)
MAX_CREATURES(PLAYER0,15)
MAX_CREATURES(PLAYER1,60)
MAX_CREATURES(PLAYER2,30)
MAX_CREATURES(PLAYER3,30)
MAX_CREATURES(PLAYER4,30)
MAX_CREATURES(PLAYER5,30)
MAX_CREATURES(PLAYER6,30)

SET_GAME_RULE(AlliesShareCta,1)
SET_GAME_RULE(AlliesShareVision,1)
SET_GAME_RULE(AlliesShareDrop,1)
SET_CREATURE_PROPERTY(KNIGHT,LORD,0)
SET_CREATURE_MAX_LEVEL(PLAYER0,ANY_CREATURE,4)
SET_GAME_RULE(ImpWorkExperience,400)
SET_GAME_RULE(TortureConvertChance,100)
SET_CREATURE_PROPERTY(IMP,NO_IMPRISONMENT,1)
SET_GAME_RULE(TortureConvertChance,100)
SET_GAME_RULE(DisplayPortalLimit,1)

QUICK_OBJECTIVE(1,"欢迎来到这个巨大的王国，主人，危险无处不在。建立自己的地下城，发展壮大，并征服这个王国！但要小心，因为你的怪物一开始被限制在4级，直到你能证明自己是强大的领导者！明智地选择你的道路，有些可能比其他更危险！")

ALLY_PLAYERS(PLAYER6,PLAYER3,3)
ALLY_PLAYERS(PLAYER3,PLAYER6,3)
ALLY_PLAYERS(PLAYER4,PLAYER2,3)
ALLY_PLAYERS(PLAYER2,PLAYER4,3)
ALLY_PLAYERS(PLAYER1,PLAYER_GOOD,3)
ALLY_PLAYERS(PLAYER_GOOD,PLAYER1,3)


COMPUTER_PLAYER(PLAYER1,0)
COMPUTER_PLAYER(PLAYER2,0)
COMPUTER_PLAYER(PLAYER3,0)
COMPUTER_PLAYER(PLAYER4,0)
COMPUTER_PLAYER(PLAYER5,0)
COMPUTER_PLAYER(PLAYER6,0)

ADD_CREATURE_TO_POOL(TROLL,100)
ADD_CREATURE_TO_POOL(SPIDER,100)
ADD_CREATURE_TO_POOL(TENTACLE,100)
ADD_CREATURE_TO_POOL(SORCEROR,100)
ADD_CREATURE_TO_POOL(ORC,100)
ADD_CREATURE_TO_POOL(BILE_DEMON,100)
ADD_CREATURE_TO_POOL(DRAGON,100)
ADD_CREATURE_TO_POOL(DARK_MISTRESS,100)
ADD_CREATURE_TO_POOL(THIEF,100)
ADD_CREATURE_TO_POOL(DWARFA,100)
ADD_CREATURE_TO_POOL(ARCHER,100)
ADD_CREATURE_TO_POOL(BARBARIAN,100)
ADD_CREATURE_TO_POOL(MONK,100)
ADD_CREATURE_TO_POOL(FAIRY,100)
ADD_CREATURE_TO_POOL(WITCH,100)
ADD_CREATURE_TO_POOL(WIZARD,100)
ADD_CREATURE_TO_POOL(GIANT,100)
ADD_CREATURE_TO_POOL(SAMURAI,100)

rem Human Player Creatures
CREATURE_AVAILABLE(PLAYER0,TROLL,1,0)
CREATURE_AVAILABLE(PLAYER0,SORCEROR,1,0)
CREATURE_AVAILABLE(PLAYER0,ORC,1,0)
CREATURE_AVAILABLE(PLAYER0,DRAGON,1,0)
CREATURE_AVAILABLE(PLAYER0,BARBARIAN,1,0)
CREATURE_AVAILABLE(PLAYER0,GIANT,1,0)

rem Player Black Creatures
CREATURE_AVAILABLE(PLAYER5,SPIDER,1,0)
CREATURE_AVAILABLE(PLAYER5,TENTACLE,1,0)
CREATURE_AVAILABLE(PLAYER5,BILE_DEMON,1,0)
CREATURE_AVAILABLE(PLAYER5,DARK_MISTRESS,1,0)
CREATURE_AVAILABLE(PLAYER5,FAIRY,1,0)
CREATURE_AVAILABLE(PLAYER5,WITCH,1,0)
CREATURE_AVAILABLE(PLAYER5,WIZARD,1,0)
CREATURE_AVAILABLE(PLAYER5,DRAGON,1,0)
CREATURE_AVAILABLE(PLAYER5,TROLL,1,0)

rem Orange&Yellow Player Creatures

CREATURE_AVAILABLE(PLAYER3,DRAGON,1,0)
CREATURE_AVAILABLE(PLAYER3,DARK_MISTRESS,1,0)
CREATURE_AVAILABLE(PLAYER3,BILE_DEMON,1,0)
CREATURE_AVAILABLE(PLAYER3,TROLL,1,0)
CREATURE_AVAILABLE(PLAYER3,SPIDER,1,0)
CREATURE_AVAILABLE(PLAYER3,DARK_MISTRESS,1,0)
CREATURE_AVAILABLE(PLAYER3,ORC,1,0)

CREATURE_AVAILABLE(PLAYER6,DRAGON,1,0)
CREATURE_AVAILABLE(PLAYER6,DARK_MISTRESS,1,0)
CREATURE_AVAILABLE(PLAYER6,BILE_DEMON,1,0)
CREATURE_AVAILABLE(PLAYER6,TROLL,1,0)
CREATURE_AVAILABLE(PLAYER6,SPIDER,1,0)
CREATURE_AVAILABLE(PLAYER6,DARK_MISTRESS,1,0)
CREATURE_AVAILABLE(PLAYER6,ORC,1,0)

rem Purple&Green Creatures
CREATURE_AVAILABLE(PLAYER2,SAMURAI,1,0)
CREATURE_AVAILABLE(PLAYER2,THIEF,1,0)
CREATURE_AVAILABLE(PLAYER2,DWARFA,1,0)
CREATURE_AVAILABLE(PLAYER2,ARCHER,1,0)
CREATURE_AVAILABLE(PLAYER2,MONK,1,0)
CREATURE_AVAILABLE(PLAYER2,WITCH,1,0)
CREATURE_AVAILABLE(PLAYER2,TIME_MAGE,1,0)
CREATURE_AVAILABLE(PLAYER2,FAIRY,1,0)
CREATURE_AVAILABLE(PLAYER2,ARCHER,1,0)
CREATURE_AVAILABLE(PLAYER2,GIANT,1,0)
CREATURE_AVAILABLE(PLAYER2,BARBARIAN,1,0)

CREATURE_AVAILABLE(PLAYER4,SAMURAI,1,0)
CREATURE_AVAILABLE(PLAYER4,THIEF,1,0)
CREATURE_AVAILABLE(PLAYER4,DWARFA,1,0)
CREATURE_AVAILABLE(PLAYER4,ARCHER,1,0)
CREATURE_AVAILABLE(PLAYER4,MONK,1,0)
CREATURE_AVAILABLE(PLAYER4,WITCH,1,0)
CREATURE_AVAILABLE(PLAYER4,TIME_MAGE,1,0)
CREATURE_AVAILABLE(PLAYER4,FAIRY,1,0)
CREATURE_AVAILABLE(PLAYER4,ARCHER,1,0)
CREATURE_AVAILABLE(PLAYER4,GIANT,1,0)
CREATURE_AVAILABLE(PLAYER4,BARBARIAN,1,0)

rem blue player creatures

CREATURE_AVAILABLE(player1,DARK_MISTRESS,1,0)
CREATURE_AVAILABLE(player1,DRAGON,1,0)
CREATURE_AVAILABLE(player1,BILE_DEMON,1,0)
CREATURE_AVAILABLE(player1,TROLL,1,0)
CREATURE_AVAILABLE(player1,BARBARIAN,1,0)
CREATURE_AVAILABLE(player1,GIANT,1,0)
CREATURE_AVAILABLE(player1,THIEF,1,0)
CREATURE_AVAILABLE(player1,FAIRY,1,0)
CREATURE_AVAILABLE(player1,WITCH,1,0)
CREATURE_AVAILABLE(player1,WIZARD,1,0)
CREATURE_AVAILABLE(player1,DWARFA,1,0)
CREATURE_AVAILABLE(player1,ORC,1,0)
CREATURE_AVAILABLE(player1,TROLL,1,0)
CREATURE_AVAILABLE(player1,MONK,1,0)
CREATURE_AVAILABLE(player1,TIME_MAGE,1,0)



rem Rooms

ROOM_AVAILABLE(ALL_PLAYERS,TREASURE,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,LAIR,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,GARDEN,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,TRAINING,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,RESEARCH,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,GUARD_POST,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,WORKSHOP,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,PRISON,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,TORTURE,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,BARRACKS,1,0)
ROOM_AVAILABLE(PLAYER5,SCAVENGER,1,1)
ROOM_AVAILABLE(PLAYER3,SCAVENGER,1,1)

Rem Magic

MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HAND,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SLAP,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_POSSESS,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_IMP,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SIGHT,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SPEED,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_OBEY,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CALL_TO_ARMS,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HEAL_CREATURE,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_PROTECT,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_LIGHTNING,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CHICKEN,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_DISEASE,1,0)

rem human magic
MAGIC_AVAILABLE(PLAYER0,POWER_LIGHTNING,0,0)
MAGIC_AVAILABLE(PLAYER0,POWER_CHICKEN,0,0)
MAGIC_AVAILABLE(PLAYER0,POWER_DISEASE,0,0)
MAGIC_AVAILABLE(PLAYER0,POWER_DESTROY_WALLS,1,1)
MAGIC_AVAILABLE(PLAYER0,POWER_SIGHT,0,0)

TRAP_AVAILABLE(ALL_PLAYERS,LIGHTNING,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,WORD_OF_POWER,1,0)

DOOR_AVAILABLE(ALL_PLAYERS,BRACED,1,0)
DOOR_AVAILABLE(ALL_PLAYERS,STEEL,1,0)
DOOR_AVAILABLE(ALL_PLAYERS,MAGIC,1,0)


REM its PARTY TIME


CREATE_PARTY(weaker)
	ADD_TO_PARTY(weaker,ARCHER,4,100,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(weaker,BARBARIAN,4,100,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(weaker,BARBARIAN,4,100,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(weaker,ARCHER,4,100,ATTACK_DUNGEON_HEART,0)

CREATE_PARTY(middle)
	ADD_TO_PARTY(middle,ARCHER,5,100,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(middle,BARBARIAN,5,100,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(middle,THIEF,5,100,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(middle,WITCH,5,100,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(middle,ARCHER,5,100,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(middle,ARCHER,5,100,ATTACK_DUNGEON_HEART,0)

CREATE_PARTY(strong)
	ADD_TO_PARTY(strong,ARCHER,8,100,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(strong,BARBARIAN,8,100,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(strong,THIEF,8,100,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(strong,WITCH,8,100,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(strong,ARCHER,8,100,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(strong,SAMURAI,8,100,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(strong,SAMURAI,8,100,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(strong,WITCH,8,100,ATTACK_DUNGEON_HEART,0)

CREATE_PARTY(annoy)
	ADD_TO_PARTY(annoy,ARCHER,7,100,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(annoy,BARBARIAN,7,100,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(annoy,BARBARIAN,7,100,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(annoy,THIEF,7,100,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(annoy,FAIRY,7,100,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(annoy,ARCHER,7,100,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(annoy,FAIRY,7,100,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(annoy,FAIRY,7,100,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(annoy,SAMURAI,7,100,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(annoy,SAMURAI,7,100,ATTACK_DUNGEON_HEART,0)

CREATE_PARTY(defense)
	ADD_TO_PARTY(defense,KNIGHT,10,100,DEFEND_LOCATION,0)
	ADD_TO_PARTY(defense,BARBARIAN,10,100,DEFEND_LOCATION,0)
	ADD_TO_PARTY(defense,BARBARIAN,10,100,DEFEND_LOCATION,0)
	ADD_TO_PARTY(defense,WITCH,10,100,DEFEND_LOCATION,0)
	ADD_TO_PARTY(defense,WITCH,10,100,DEFEND_LOCATION,0)
	ADD_TO_PARTY(defense,WIZARD,10,100,DEFEND_LOCATION,0)
	ADD_TO_PARTY(defense,WIZARD,10,100,DEFEND_LOCATION,0)
	ADD_TO_PARTY(defense,ARCHER,10,100,DEFEND_LOCATION,0)
	ADD_TO_PARTY(defense,ARCHER,10,100,DEFEND_LOCATION,0)
	ADD_TO_PARTY(defense,SAMURAI,10,100,DEFEND_LOCATION,0)
	ADD_TO_PARTY(defense,SAMURAI,10,100,DEFEND_LOCATION,0)

CREATE_PARTY(laststand)
	ADD_TO_PARTY(laststand,AVATAR,10,100,DEFEND_HEART,0)
	ADD_TO_PARTY(laststand,KNIGHT,10,100,DEFEND_HEART,0)
	ADD_TO_PARTY(laststand,KNIGHT,10,100,DEFEND_HEART,0)
	ADD_TO_PARTY(laststand,HORNY,10,100,DEFEND_HEART,0)
	ADD_TO_PARTY(laststand,HORNY,10,100,DEFEND_HEART,0)
	ADD_TO_PARTY(laststand,WITCH,10,100,DEFEND_HEART,0)
	ADD_TO_PARTY(laststand,WITCH,10,100,DEFEND_HEART,0)
	ADD_TO_PARTY(laststand,FAIRY,10,100,DEFEND_HEART,0)
	ADD_TO_PARTY(laststand,FAIRY,10,100,DEFEND_HEART,0)
	ADD_TO_PARTY(laststand,WIZARD,10,100,DEFEND_HEART,0)
	ADD_TO_PARTY(laststand,WIZARD,10,100,DEFEND_HEART,0)
	ADD_TO_PARTY(laststand,SAMURAI,10,100,DEFEND_HEART,0)
	ADD_TO_PARTY(laststand,SAMURAI,10,100,DEFEND_HEART,0)
	ADD_TO_PARTY(laststand,DRAGON,10,100,DEFEND_HEART,0)
	ADD_TO_PARTY(laststand,DRAGON,10,100,DEFEND_HEART,0)
	ADD_TO_PARTY(laststand,DARK_MISTRESS,10,100,DEFEND_HEART,0)
	ADD_TO_PARTY(laststand,DARK_MISTRESS,10,100,DEFEND_HEART,0)
	ADD_TO_PARTY(laststand,MONK,10,100,DEFEND_HEART,0)
	ADD_TO_PARTY(laststand,MONK,10,100,DEFEND_HEART,0)

CREATE_PARTY(inbetween)
	ADD_TO_PARTY(inbetween,ARCHER,9,100,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(inbetween,BARBARIAN,9,100,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(inbetween,THIEF,9,100,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(inbetween,WITCH,9,100,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(inbetween,ARCHER,9,100,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(inbetween,MONK,9,100,ATTACK_DUNGEON_HEART,0)



rem ACtion points

IF_ACTION_POINT(3,PLAYER0)

	QUICK_INFORMATION(2,"请谨慎行动！这条路通向巨大的宝藏，但伴随着遭遇守卫的危险。",3)
	ADD_TO_FLAG(PLAYER0,FLAG0,1)
ENDIF

if(PLAYER0,FLAG0 == 0)
	IF_ACTION_POINT(2,PLAYER0)
		QUICK_INFORMATION(3,"也许前往这个方向还为时过早...尝试先征服北方吧。",2)
	ENDIF
ENDIF

if(PLAYER0,FLAG0 == 1)
	IF_ACTION_POINT(2,PLAYER0)
		QUICK_INFORMATION(4,"前方是另一个黑暗守护者的地牢，你必须确保自己足够强大才能与他战斗！",2)
	ENDIF
ENDIF

IF_ACTION_POINT(4,PLAYER0)
	QUICK_INFORMATION(5,"干得好，我的主人，你成功解决了这些讨厌的守卫，并提升了你的怪物的力量。现在它们可以训练到 8 级以上了。获得更多的力量，以释放你的爪牙们的全部潜力！",4)
	SET_CREATURE_MAX_LEVEL(PLAYER0,ANY_CREATURE,8)
ENDIF

IF_ACTION_POINT(11,PLAYER0)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,weaker,11,1)
ENDIF

IF_ACTION_POINT(5,PLAYER0)
	SET_TIMER(PLAYER0,TIMER0)
	QUICK_INFORMATION(6,"您到达了一个适合防守的好地方……但是国王知道了你的成功，并会试图通过随时间派遣部队来阻止你！危险仍在前方！",5)
ENDIF

IF_ACTION_POINT(1,PLAYER0)
	QUICK_INFORMATION(7,"你成功击败了黑暗守护者并打开了这条道路......你的邪恶之手现在能抓取更多的怪物了，并且另一条秘道已经显现，就在前方！",1)
	CHANGE_SLAB_TYPE(76,74,PATH,NONE)
	CHANGE_SLAB_TYPE(77,74,PATH,NONE)
	CHANGE_SLAB_TYPE(78,74,PATH,NONE)
	CHANGE_SLAB_TYPE(79,74,PATH,NONE)
	CHANGE_SLAB_TYPE(80,74,PATH,NONE)
	CHANGE_SLAB_TYPE(80,75,PATH,NONE)
	CHANGE_SLAB_TYPE(81,75,PATH,NONE)
	CHANGE_SLAB_TYPE(81,76,PATH,NONE)
	CHANGE_SLAB_TYPE(82,76,PATH,NONE)
	CHANGE_SLAB_TYPE(82,77,PATH,NONE)
	CHANGE_SLAB_TYPE(82,78,PATH,NONE)
	CHANGE_SLAB_TYPE(83,78,PATH,NONE)
	CHANGE_SLAB_TYPE(83,79,PATH,NONE)
	CHANGE_SLAB_TYPE(83,80,PATH,NONE)
	CHANGE_SLAB_TYPE(83,81,PATH,NONE)
	CHANGE_SLAB_TYPE(87,81,DIRT,NONE)
	SET_GAME_RULE(MaxThingsInHand,12)
ENDIF

if(PLAYER5,DUNGEON_DESTROYED == 1)
	QUICK_INFORMATION(8,"主人，您干得好，成功地开辟了一条通往这个领域的道路，并释放了您的生物的全部潜力！",1)
	CHANGE_SLAB_TYPE(76,68,PATH,NONE)
	SET_CREATURE_MAX_LEVEL(PLAYER0,ANY_CREATURE,10)
ENDIF

if(PLAYER3,DUNGEON_DESTROYED == 1)
	ADD_TO_FLAG(PLAYER0,FLAG2,1)
	ADD_TO_FLAG(PLAYER0,FLAG4,1)
ENDIF

if(PLAYER6,DUNGEON_DESTROYED == 1)
	ADD_TO_FLAG(PLAYER0,FLAG2,1)
	ADD_TO_FLAG(PLAYER0,FLAG4,1)
ENDIF

if(PLAYER0,FLAG2 == 2)
	QUICK_INFORMATION(9,"你又打败了两个笨蛋。现在你成功地在中间开辟了另一条道路。",1)
	CHANGE_SLAB_TYPE(75,74,PATH,NONE)
	CHANGE_SLAB_TYPE(74,74,PATH,NONE)
	CHANGE_SLAB_TYPE(73,74,PATH,NONE)
	CHANGE_SLAB_TYPE(72,74,PATH,NONE)
	CHANGE_SLAB_TYPE(72,75,PATH,NONE)
	CHANGE_SLAB_TYPE(71,75,PATH,NONE)
	CHANGE_SLAB_TYPE(71,76,PATH,NONE)
	CHANGE_SLAB_TYPE(70,76,PATH,NONE)
	CHANGE_SLAB_TYPE(70,77,PATH,NONE)
	CHANGE_SLAB_TYPE(69,77,PATH,NONE)
	CHANGE_SLAB_TYPE(69,78,PATH,NONE)
	CHANGE_SLAB_TYPE(69,79,PATH,NONE)
	CHANGE_SLAB_TYPE(69,80,PATH,NONE)
	CHANGE_SLAB_TYPE(69,81,PATH,NONE)
ENDIF


if(PLAYER2,DUNGEON_DESTROYED == 1)
	ADD_TO_FLAG(PLAYER0,FLAG3,1)
ENDIF

if(PLAYER4,DUNGEON_DESTROYED == 1)
	ADD_TO_FLAG(PLAYER0,FLAG3,1)
ENDIF

if(PLAYER0,FLAG3 == 2)
	QUICK_INFORMATION(10,"这是展示更多力量的绝佳方式！现在你找到了建造桥梁并占领这片土地的方法。但要小心，因为蓝色守护者与这个卑鄙的守护者结盟了，而且非常强大！",PLAYER0)
	ROOM_AVAILABLE(PLAYER0,BRIDGE,1,1)
ENDIF

if(PLAYER1,DUNGEON_DESTROYED == 1)
	MAGIC_AVAILABLE(PLAYER0,POWER_DESTROY_WALLS,0,0)
	MAGIC_AVAILABLE(PLAYER0,POWER_SIGHT,0,0)
	QUICK_INFORMATION(11,"最后的安全机制启动了……他们成功压制了你破坏墙壁和视线的力量！找到守护者城堡的入口，向他证明没有什么可以阻止你！",PLAYER0)
ENDIF

IF_ACTION_POINT(8,PLAYER0)
	QUICK_INFORMATION(12,"恭喜你找到了城堡入口！但要小心，因为这个卑鄙的蠢货可能已经设置了一些陷阱……",PLAYER0)
ENDIF

IF_ACTION_POINT(9,PLAYER0)
	QUICK_INFORMATION(13,"嗯……该往哪儿走？只有一条路是正确的……",PLAYER0)
ENDIF

IF_ACTION_POINT(14,PLAYER0)
	QUICK_INFORMATION(14,"哇，你又一次找到了破坏墙壁的魔法！现在这有什么用呢？",14)
	MAGIC_AVAILABLE(PLAYER0,POWER_DESTROY_WALLS,1,1)
ENDIF

IF_ACTION_POINT(13,PLAYER0)
	QUICK_INFORMATION(15,"啊啊啊……你的破坏墙壁的力量又消失了……",13)
	MAGIC_AVAILABLE(PLAYER0,POWER_DESTROY_WALLS,0,0)
ENDIF

IF_ACTION_POINT(7,PLAYER0)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,defense,6,1)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,laststand,6,1)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,inbetween,5,2)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,inbetween,4,1)
	MAGIC_AVAILABLE(PLAYER0,POWER_DESTROY_WALLS,1,1)
ENDIF


rem Imp limitation AI

IF_CONTROLS(PLAYER1,IMP < 16)
  NEXT_COMMAND_REUSABLE
  MAGIC_AVAILABLE(PLAYER1,POWER_IMP,1,1)
 ENDIF

IF_CONTROLS(PLAYER1,IMP >= 16)
  NEXT_COMMAND_REUSABLE
  MAGIC_AVAILABLE(PLAYER1,POWER_IMP,0,0)
ENDIF

IF_CONTROLS(PLAYER2,IMP < 16)
  NEXT_COMMAND_REUSABLE
  MAGIC_AVAILABLE(PLAYER2,POWER_IMP,1,1)
 ENDIF

IF_CONTROLS(PLAYER2,IMP >= 16)
  NEXT_COMMAND_REUSABLE
  MAGIC_AVAILABLE(PLAYER2,POWER_IMP,0,0)
ENDIF
IF_CONTROLS(PLAYER3,IMP < 16)
  NEXT_COMMAND_REUSABLE
  MAGIC_AVAILABLE(PLAYER3,POWER_IMP,1,1)
 ENDIF

IF_CONTROLS(PLAYER3,IMP >= 16)
  NEXT_COMMAND_REUSABLE
  MAGIC_AVAILABLE(PLAYER3,POWER_IMP,0,0)
ENDIF

IF_CONTROLS(PLAYER4,IMP < 16)
  NEXT_COMMAND_REUSABLE
  MAGIC_AVAILABLE(PLAYER4,POWER_IMP,1,1)
 ENDIF

IF_CONTROLS(PLAYER4,IMP >= 16)
  NEXT_COMMAND_REUSABLE
  MAGIC_AVAILABLE(PLAYER4,POWER_IMP,0,0)
ENDIF
IF_CONTROLS(PLAYER5,IMP < 16)
  NEXT_COMMAND_REUSABLE
  MAGIC_AVAILABLE(PLAYER5,POWER_IMP,1,1)
 ENDIF

IF_CONTROLS(PLAYER5,IMP >= 16)
  NEXT_COMMAND_REUSABLE
  MAGIC_AVAILABLE(PLAYER5,POWER_IMP,0,0)
ENDIF

IF_CONTROLS(PLAYER6,IMP < 16)
  NEXT_COMMAND_REUSABLE
  MAGIC_AVAILABLE(PLAYER6,POWER_IMP,1,1)
 ENDIF

IF_CONTROLS(PLAYER6,IMP >= 16)
  NEXT_COMMAND_REUSABLE
  MAGIC_AVAILABLE(PLAYER6,POWER_IMP,0,0)
ENDIF

rem max creature limit player

IF_CONTROLS(PLAYER0,TOTAL_CREATURES >= 50)
	NEXT_COMMAND_REUSABLE
	SET_CREATURE_TENDENCIES(PLAYER1,IMPRISON,0)
	NEXT_COMMAND_REUSABLE
	SET_CREATURE_TENDENCIES(PLAYER2,IMPRISON,0)
	NEXT_COMMAND_REUSABLE
	SET_CREATURE_TENDENCIES(PLAYER3,IMPRISON,0)
	NEXT_COMMAND_REUSABLE
	SET_CREATURE_TENDENCIES(PLAYER4,IMPRISON,0)
	NEXT_COMMAND_REUSABLE
	SET_CREATURE_TENDENCIES(PLAYER5,IMPRISON,0)
	NEXT_COMMAND_REUSABLE
	SET_CREATURE_TENDENCIES(PLAYER6,IMPRISON,0)
	NEXT_COMMAND_REUSABLE
	SET_CREATURE_TENDENCIES(PLAYER_GOOD,IMPRISON,0)
	NEXT_COMMAND_REUSABLE
	SET_GAME_RULE(TortureConvertChance,0)
	NEXT_COMMAND_REUSABLE
	SET_GAME_RULE(PrisonSkeletonChance,0)
	NEXT_COMMAND_REUSABLE
	SET_GAME_RULE(BodiesForVampire,100)
	QUICK_INFORMATION(16,"似乎有什么东西使你无法控制超过 50 个怪物...是某种古老的诅咒造成的吗？",PLAYER0)
	NEXT_COMMAND_REUSABLE
	SET_GAME_RULE(TortureDeathChance,0)
	NEXT_COMMAND_REUSABLE
	SET_GAME_RULE(TortureHealthLoss,0)
ENDIF

IF_CONTROLS(PLAYER0,TOTAL_CREATURES < 50)
	NEXT_COMMAND_REUSABLE
	SET_CREATURE_TENDENCIES(PLAYER1,IMPRISON,1)
	NEXT_COMMAND_REUSABLE
	SET_CREATURE_TENDENCIES(PLAYER2,IMPRISON,1)
	NEXT_COMMAND_REUSABLE
	SET_CREATURE_TENDENCIES(PLAYER3,IMPRISON,1)
	NEXT_COMMAND_REUSABLE
	SET_CREATURE_TENDENCIES(PLAYER4,IMPRISON,1)
	NEXT_COMMAND_REUSABLE
	SET_CREATURE_TENDENCIES(PLAYER5,IMPRISON,1)
	NEXT_COMMAND_REUSABLE
	SET_CREATURE_TENDENCIES(PLAYER6,IMPRISON,1)
	NEXT_COMMAND_REUSABLE
	SET_CREATURE_TENDENCIES(PLAYER_GOOD,IMPRISON,1)
	NEXT_COMMAND_REUSABLE
	SET_GAME_RULE(TortureConvertChance,100)
	NEXT_COMMAND_REUSABLE
	SET_GAME_RULE(PrisonSkeletonChance,15)
	NEXT_COMMAND_REUSABLE
	SET_GAME_RULE(BodiesForVampire,8)
	NEXT_COMMAND_REUSABLE
	SET_GAME_RULE(TortureDeathChance,10)
	NEXT_COMMAND_REUSABLE
	SET_GAME_RULE(TortureHealthLoss,5)
ENDIF





rem TImers

If(PLAYER0,FLAG4 == 0)
	if(PLAYER0,TIMER0 >= 10000)
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,middle,5,1)
		SET_TIMER(PLAYER0,TIMER1)
		SET_TIMER(PLAYER0,TIMER2)
	ENDIF

	if(PLAYER0,TIMER1 >= 8500)
		NEXT_COMMAND_REUSABLE
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,strong,5,1)
		NEXT_COMMAND_REUSABLE
		SET_TIMER(PLAYER0,TIMER1)
	ENDIF

	IF(PLAYER0,TIMER2 >= 13000)
		NEXT_COMMAND_REUSABLE
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,annoy,5,1)
		NEXT_COMMAND_REUSABLE
		SET_TIMER(PLAYER0,TIMER2)
	ENDIF
ENDIF

rem timer stop condition

IF(PLAYER0,FLAG4 >= 1)
	QUICK_INFORMATION(17,"你再一次证明了自己，国王已经没有任何兵力可以派遣了……所以在进入下一个王国之前，先深呼吸放松一下吧！",LAST_EVENT)
ENDIF


rem winning condition

IF(PLAYER0,ALL_DUNGEONS_DESTROYED == 1)
	WIN_GAME
ENDIF
