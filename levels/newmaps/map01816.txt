LEVEL_VERSION(1)
SET_GENERATE_SPEED(400)
START_MONEY(ALL_PLAYERS,2000)
MAX_CREATURES(ALL_PLAYERS,25)

REM Play custom track
SET_MUSIC("map01816_start.ogg")

SET_PLAYER_COLOR(PLAYER0, BLUE)

ROOM_AVAILABLE(ALL_PLAYERS,TREASURE,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,LAIR,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,GARDEN,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,TRAINING,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,RESEARCH,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,WORKSHOP,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,BARRACKS,1,0)

MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HAND,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SLAP,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_POSSESS,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_IMP,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SIGHT,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SPEED,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CALL_TO_ARMS,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CONCEAL,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HOLD_AUDIENCE,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CAVE_IN,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_LIGHTNING,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_PROTECT,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CHICKEN,1,0)

TRAP_AVAILABLE(ALL_PLAYERS,LIGHTNING,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,WORD_OF_POWER,1,0)

DOOR_AVAILABLE(ALL_PLAYERS,STEEL,1,0)

QUICK_OBJECTIVE(1,"这片被熔岩淹没的土地和这里的居民曾经由一位像你一样的守护者控制。英雄们摧毁了他的领地，没有给你留下传送门，你需要注意你的资源并进行探索。首先考虑向北走。但不要偷懒，敌人的侦察兵已经在路上了。以最不尊重的态度对待他们。")

REM Memo: 20 turns -> 1s

REM Intro message when player has creatures and basic rooms
IF (PLAYER0, TOTAL_CREATURES > 0)
    IF (PLAYER0, LAIR > 0)
        IF (PLAYER0, TRAINING > 0)
            SET_TIMER(PLAYER0,TIMER1)
            REM For debugging the timer
            REM DISPLAY_TIMER(PLAYER0, TIMER1, 0)
        ENDIF
    ENDIF
ENDIF

IF(PLAYER0,TIMER1 >= 750)
	QUICK_INFORMATION(4,"英雄们不会一直等着你，你应该尽早摧毁他们的心脏。你可不想在他们最后一击的时候还在他们身边。时间在流逝。")
ENDIF

REM All parties

    REM Party North 1
    CREATE_PARTY(party1)
        ADD_TO_PARTY(party1,FAIRY,2,400,ATTACK_DUNGEON_HEART,0)
        ADD_TO_PARTY(party1,DEMONSPAWN,2,0,ATTACK_DUNGEON_HEART,0)
        ADD_TO_PARTY(party1,DEMONSPAWN,2,0,ATTACK_DUNGEON_HEART,0)
        ADD_TO_PARTY(party1,DEMONSPAWN,2,0,ATTACK_DUNGEON_HEART,0)

    REM Party South 1
    CREATE_PARTY(party2)
        ADD_TO_PARTY(party2,MONK,1,200,ATTACK_DUNGEON_HEART,0)
        ADD_TO_PARTY(party2,MONK,1,200,ATTACK_DUNGEON_HEART,0)
        ADD_TO_PARTY(party2,MONK,1,200,ATTACK_DUNGEON_HEART,0)
        ADD_TO_PARTY(party2,MONK,1,200,ATTACK_DUNGEON_HEART,0)

    REM Party South 2
    CREATE_PARTY(party3)
        ADD_TO_PARTY(party3,DEMONSPAWN,3,200,ATTACK_DUNGEON_HEART,0)
        ADD_TO_PARTY(party3,DEMONSPAWN,3,200,ATTACK_DUNGEON_HEART,0)
        ADD_TO_PARTY(party3,DEMONSPAWN,3,200,ATTACK_DUNGEON_HEART,0)
        ADD_TO_PARTY(party3,WIZARD,2,200,ATTACK_DUNGEON_HEART,0)
        ADD_TO_PARTY(party3,WIZARD,2,200,ATTACK_DUNGEON_HEART,0)
        ADD_TO_PARTY(party3,WIZARD,2,200,ATTACK_DUNGEON_HEART,0)
        ADD_TO_PARTY(party3,DRAGON,1,200,ATTACK_DUNGEON_HEART,0)
        ADD_TO_PARTY(party3,DRAGON,1,200,ATTACK_DUNGEON_HEART,0)
        ADD_TO_PARTY(party3,DRAGON,1,200,ATTACK_DUNGEON_HEART,0)

    REM Party North 2
    CREATE_PARTY(party4)
        ADD_TO_PARTY(party4,FAIRY,3,400,ATTACK_DUNGEON_HEART,0)
        ADD_TO_PARTY(party4,HELL_HOUND,2,200,ATTACK_DUNGEON_HEART,0)
        ADD_TO_PARTY(party4,HELL_HOUND,2,200,ATTACK_DUNGEON_HEART,0)
        ADD_TO_PARTY(party4,HELL_HOUND,2,200,ATTACK_DUNGEON_HEART,0)

    REM Party South 3
    CREATE_PARTY(party5)
        ADD_TO_PARTY(party5,BARBARIAN,3,200,ATTACK_DUNGEON_HEART,0)
        ADD_TO_PARTY(party5,WIZARD,2,200,ATTACK_DUNGEON_HEART,0)
        ADD_TO_PARTY(party5,WIZARD,2,200,ATTACK_DUNGEON_HEART,0)
        ADD_TO_PARTY(party5,WIZARD,2,200,ATTACK_DUNGEON_HEART,0)

    REM BREAK TIME

    REM Wave 5

    REM Party South 4
    CREATE_PARTY(party6)
        ADD_TO_PARTY(party6,GIANT,10,200,ATTACK_DUNGEON_HEART,0)
        ADD_TO_PARTY(party6,DWARFA,1,200,ATTACK_DUNGEON_HEART,0)

    REM Party North 3
    CREATE_PARTY(party7)
        ADD_TO_PARTY(party7,DEMONSPAWN,7,200,ATTACK_DUNGEON_HEART,0)
        ADD_TO_PARTY(party7,DRAGON,4,200,ATTACK_DUNGEON_HEART,0)
        ADD_TO_PARTY(party7,DRAGON,4,200,ATTACK_DUNGEON_HEART,0)
        ADD_TO_PARTY(party7,FAIRY,4,400,ATTACK_DUNGEON_HEART,0)

    REM Wave 6

    REM Party South 5
    CREATE_PARTY(party8)
        ADD_TO_PARTY(party8,ARCHER,7,200,ATTACK_DUNGEON_HEART,0)
        ADD_TO_PARTY(party8,ARCHER,3,200,ATTACK_DUNGEON_HEART,0)
        ADD_TO_PARTY(party8,ARCHER,3,200,ATTACK_DUNGEON_HEART,0)
        ADD_TO_PARTY(party8,ARCHER,3,200,ATTACK_DUNGEON_HEART,0)
        ADD_TO_PARTY(party8,ARCHER,4,200,ATTACK_DUNGEON_HEART,0)
        ADD_TO_PARTY(party8,ARCHER,4,200,ATTACK_DUNGEON_HEART,0)
        ADD_TO_PARTY(party8,WIZARD,8,200,ATTACK_DUNGEON_HEART,0)

    REM Party North 4
    CREATE_PARTY(party9)
        ADD_TO_PARTY(party9,DRAGON,8,200,ATTACK_DUNGEON_HEART,0)
        ADD_TO_PARTY(party9,FAIRY,8,200,ATTACK_DUNGEON_HEART,0)

    REM SECOND BREAK

    REM Wave 7

    REM Party South 6
    CREATE_PARTY(party10)
        ADD_TO_PARTY(party10,MONK,7,200,ATTACK_DUNGEON_HEART,0)
        ADD_TO_PARTY(party10,DWARFA,7,200,ATTACK_DUNGEON_HEART,0)
        ADD_TO_PARTY(party10,WIZARD,7,200,ATTACK_DUNGEON_HEART,0)

    REM Party North 5
    CREATE_PARTY(party11)
        ADD_TO_PARTY(party11,DRAGON,9,200,ATTACK_DUNGEON_HEART,0)
        ADD_TO_PARTY(party11,DEMONSPAWN,9,200,ATTACK_DUNGEON_HEART,0)
        ADD_TO_PARTY(party11,DEMONSPAWN,9,200,ATTACK_DUNGEON_HEART,0)
        ADD_TO_PARTY(party11,DEMONSPAWN,9,200,ATTACK_DUNGEON_HEART,0)
        ADD_TO_PARTY(party11,HELL_HOUND,7,200,ATTACK_DUNGEON_HEART,0)
        ADD_TO_PARTY(party11,HELL_HOUND,7,200,ATTACK_DUNGEON_HEART,0)
        ADD_TO_PARTY(party11,HELL_HOUND,7,200,ATTACK_DUNGEON_HEART,0)

REM Manage groups spawning

	IF(PLAYER0,TIMER1 >= 3500)
        SET_MUSIC("map01816_main.ogg")
		QUICK_INFORMATION(3,"第一波攻势已经抵达北部 开始行动，消灭这些胆敢挑战你的蠢货。")
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,party1,-1,1)
	ENDIF

	IF(PLAYER0,TIMER1 >= 9500)
		QUICK_INFORMATION(5,"准备好，第二波浪潮的脚步正在逼近。")
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,party1,-1,1)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,party2,-2,1)
	ENDIF

	IF(PLAYER0,TIMER1 >= 15500)
		QUICK_INFORMATION(6,"看来第三波攻击将从南面发起 希望你一直在训练，守卫者。")
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,party3,-2,1)
	ENDIF

    IF(PLAYER0,TIMER1 >= 20500)
		QUICK_INFORMATION(7,"双方再进行一次攻击。在这之后，你应该可以轻松休息一段时间了。不过也别偷懒 还有很多值得寻找的东西...")
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,party4,-1,1)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,party5,-2,1)
	ENDIF

    REM Don't spawn enemies for a while - half an hour has passed

    IF(PLAYER0,TIMER1 >= 34500)
		QUICK_INFORMATION(8,"第五波来了。敌人越来越强大，你们要时刻保持警惕。")
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,party7,-1,1)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,party6,-2,1)
	ENDIF

    IF(PLAYER0,TIMER1 >= 39500)
		QUICK_INFORMATION(9,"第六波来了，小心点。下一波会很痛，你应该尽快完成这里的任务。好好利用这一轮之后的时间，摧毁他们在北部要塞的心脏。")
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,party9,-1,1)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,party8,-2,1)
	ENDIF

    REM Another long break

    IF(PLAYER0,TIMER1 >= 55500)
		QUICK_INFORMATION(10,"敌人近在眼前。你们最好做好准备，及时收手。")
	ENDIF

    IF(PLAYER0,TIMER1 >= 61000)
		QUICK_INFORMATION(11,"最后一波已经到来。如果你还没有摧毁地牢之心，祝你好运。")
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,party11,-1,1)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,party10,-2,3)
	ENDIF

REM Victory Condition

    IF(PLAYER_GOOD,DUNGEON_DESTROYED == 1)
        QUICK_OBJECTIVE(2,"击溃这些蠢货，干得漂亮。在我们的征战中，他们将不再打扰我们。")
        WIN_GAME
    ENDIF