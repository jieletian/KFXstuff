REM ********************************************
REM
REM             Script for Level 350
REM
REM ********************************************

REM ********************************************
REM  Faqhuin the Game Master - Chess
REM  by dayokay
REM  Version 0.2
REM ********************************************

LEVEL_VERSION(1)

REM	SET_GENERATE_SPEED(250)

QUICK_OBJECTIVE(10,"欢迎来到游戏大师法昆的王国，守护者！我相信你一定听说过这款游戏，但我同样相信你从未像现在这样玩过这款游戏！",PLAYER0)

IF(PLAYER0,TIMER0>500)
	QUICK_OBJECTIVE(11,"利用现在的时间，用附身魔法移动你的怪物。你可以建造桥梁，但数量有限...",ALL_PLAYERS)
ENDIF

IF(PLAYER0,TIMER0>1000)
	QUICK_OBJECTIVE(12,"一旦游戏开始，你将不能再抓起你的“棋子”。你只能用巴掌将它们拍入战场！",ALL_PLAYERS)
ENDIF

REM	REVEAL_MAP_RECT(PLAYER0,127,127,254,254)

START_MONEY(PLAYER0,20000)
START_MONEY(PLAYER3,20000)

COMPUTER_PLAYER(PLAYER3,9)

REM	ROOM_AVAILABLE(ALL_PLAYERS,TREASURE,1,1)
REM	ROOM_AVAILABLE(ALL_PLAYERS,RESEARCH,1,1)
REM	ROOM_AVAILABLE(ALL_PLAYERS,GARDEN,1,1)
REM	ROOM_AVAILABLE(ALL_PLAYERS,LAIR,1,1)
REM	ROOM_AVAILABLE(ALL_PLAYERS,TORTURE,1,0)
REM	ROOM_AVAILABLE(ALL_PLAYERS,TRAINING,1,0)
REM	ROOM_AVAILABLE(ALL_PLAYERS,TEMPLE,1,0)
REM	ROOM_AVAILABLE(ALL_PLAYERS,GRAVEYARD,0,0)
REM	ROOM_AVAILABLE(ALL_PLAYERS,BRIDGE,1,1)

REM	MAGIC_AVAILABLE(ALL_PLAYERS,POWER_OBEY,0,0)
REM	MAGIC_AVAILABLE(ALL_PLAYERS,POWER_IMP,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SLAP,1,1)
REM	MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CALL_TO_ARMS,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HEAL_CREATURE,1,1)
REM	MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HOLD_AUDIENCE,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SPEED,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CONCEAL,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_PROTECT,1,1)

ALLY_PLAYERS(PLAYER0,PLAYER_GOOD,1)
ALLY_PLAYERS(RED,YELLOW,1)
ALLY_PLAYERS(PLAYER3,PLAYER_GOOD,1)

SET_TIMER(PLAYER0,TIMER0)

BONUS_LEVEL_TIME(3000)

IF(PLAYER0,TIMER0<=3000)
	IF(PLAYER3,BATTLES_LOST>=1)
		QUICK_OBJECTIVE(18,"喂！游戏还没开始，你就杀了对方的怪物，这是作弊行为！",ALL_PLAYERS)
		LOSE_GAME
	ENDIF	
ENDIF

IF(PLAYER3,DUNGEON_DESTROYED==1)
	QUICK_OBJECTIVE(21,"想通过偷掉心脏来直接获胜吗？不讲武德的家伙！",ALL_PLAYERS)
	LOSE_GAME
ENDIF	

IF(PLAYER0,TIMER0>3000)
	MAGIC_AVAILABLE(PLAYER0,POWER_HAND,0,0)
	MAGIC_AVAILABLE(PLAYER3,POWER_HAND,0,0)
	MAGIC_AVAILABLE(PLAYER0,POWER_POSSESS,0,0)
	MAGIC_AVAILABLE(PLAYER3,POWER_POSSESS,0,0)
	QUICK_INFORMATION(13,"你不能再使用手和附身魔法了，守护者 :-(",ALL_PLAYERS)
	ALLY_PLAYERS(RED,YELLOW,0)
	QUICK_OBJECTIVE(14,"那么，游戏――开始！",ALL_PLAYERS)
	BONUS_LEVEL_TIME(0)
ENDIF


IF(PLAYER0,BRIDGE>4)
	NEXT_COMMAND_REUSABLE
	ROOM_AVAILABLE(PLAYER0,BRIDGE,0,0)
ENDIF

IF(PLAYER0,BRIDGE<3)
	NEXT_COMMAND_REUSABLE
	ROOM_AVAILABLE(PLAYER0,BRIDGE,1,1)
ENDIF

IF(PLAYER0,TIMER0>6000)
	QUICK_OBJECTIVE(15,"法昆把棋盘的尺寸缩小了？！",PLAYER0)
	CHANGE_SLAB_TYPE(22,19,LAVA)
	CHANGE_SLAB_TYPE(32,19,LAVA)
	CHANGE_SLAB_TYPE(22,33,LAVA)
	CHANGE_SLAB_TYPE(32,33,LAVA)
ENDIF

IF(PLAYER0,TIMER0>9000)
	QUICK_OBJECTIVE(16,"棋盘再次缩小了！",PLAYER0)
	CHANGE_SLAB_TYPE(20,19,LAVA)
	CHANGE_SLAB_TYPE(30,19,LAVA)
	CHANGE_SLAB_TYPE(20,33,LAVA)
	CHANGE_SLAB_TYPE(30,33,LAVA)
ENDIF

REM	IF(PLAYER0,BATTLES_WON>1)
	REM	NEXT_COMMAND_REUSABLE
	REM	CHANGE_SLAB_OWNER(22,23,PLAYER0)
REM	ENDIF

IF(PLAYER0, AVATAR<1)
	LOSE_GAME
	ALLY_PLAYERS(WHITE,YELLOW,0)
	ALLY_PLAYERS(RED,WHITE,0)
ENDIF

IF(PLAYER3, AVATAR<1)
	WIN_GAME
	QUICK_OBJECTIVE(3,"你战胜了你那拙劣的对手，但法昆需要为下一盘棋清理棋盘！",PLAYER0)
REM	ALLY_PLAYERS(RED,YELLOW,1)
	ALLY_PLAYERS(RED,WHITE,0)
	ALLY_PLAYERS(WHITE,YELLOW,0)
ENDIF


