LEVEL_VERSION(1)
SET_GENERATE_SPEED(400)
START_MONEY(PLAYER0,10000)
MAX_CREATURES(PLAYER0,30)
RUN_AFTER_VICTORY(1)

ADD_CREATURE_TO_POOL(FLY,20)
ADD_CREATURE_TO_POOL(BUG,80)
ADD_CREATURE_TO_POOL(DEMONSPAWN,50)
ADD_CREATURE_TO_POOL(TROLL,30)
ADD_CREATURE_TO_POOL(SORCEROR,30)
ADD_CREATURE_TO_POOL(ORC,30)
ADD_CREATURE_TO_POOL(TUNNELLER,30)

CREATURE_AVAILABLE(PLAYER0,FLY,1,0)
CREATURE_AVAILABLE(PLAYER0,BUG,1,0)
CREATURE_AVAILABLE(PLAYER0,DEMONSPAWN,1,0)
CREATURE_AVAILABLE(PLAYER0,TROLL,1,0)
CREATURE_AVAILABLE(PLAYER0,SORCEROR,1,0)
CREATURE_AVAILABLE(PLAYER0,ORC,1,0)

ROOM_AVAILABLE(PLAYER0,TREASURE,1,1)
ROOM_AVAILABLE(PLAYER0,LAIR,1,1)
ROOM_AVAILABLE(PLAYER0,GARDEN,1,1)
ROOM_AVAILABLE(PLAYER0,TRAINING,1,1)
ROOM_AVAILABLE(PLAYER0,GUARD_POST,1,0)
ROOM_AVAILABLE(PLAYER0,WORKSHOP,1,0)
ROOM_AVAILABLE(PLAYER0,BARRACKS,1,0)
ROOM_AVAILABLE(PLAYER0,TEMPLE,1,0)

MAGIC_AVAILABLE(PLAYER0,POWER_HAND,1,1)
MAGIC_AVAILABLE(PLAYER0,POWER_SLAP,1,1)
MAGIC_AVAILABLE(PLAYER0,POWER_POSSESS,1,1)
MAGIC_AVAILABLE(PLAYER0,POWER_IMP,1,1)
MAGIC_AVAILABLE(PLAYER0,POWER_SPEED,1,0)
MAGIC_AVAILABLE(PLAYER0,POWER_OBEY,1,0)
MAGIC_AVAILABLE(PLAYER0,POWER_CONCEAL,1,0)
MAGIC_AVAILABLE(PLAYER0,POWER_HOLD_AUDIENCE,1,0)
MAGIC_AVAILABLE(PLAYER0,POWER_LIGHTNING,1,0)
MAGIC_AVAILABLE(PLAYER0,POWER_PROTECT,1,0)

TRAP_AVAILABLE(PLAYER0,POISON_GAS,1,0)
TRAP_AVAILABLE(PLAYER0,LIGHTNING,1,0)
TRAP_AVAILABLE(PLAYER0,WORD_OF_POWER,1,0)

DOOR_AVAILABLE(PLAYER0,STEEL,1,0)
DOOR_AVAILABLE(PLAYER0,MAGIC,1,0)

SET_CREATURE_HEALTH(WIZARD,7895)
SET_CREATURE_ARMOUR(WIZARD,80)
SET_CREATURE_INSTANCE(WIZARD,2,LIGHTNING,10)
SET_CREATURE_INSTANCE(WIZARD,3,CAST_SPELL_TIME_BOMB,10)
SET_CREATURE_FEARSOME_FACTOR(WIZARD,80)

SET_FLAG(PLAYER_GOOD,FLAG0,0)
SET_FLAG(PLAYER_GOOD,FLAG1,0)
SET_FLAG(PLAYER_GOOD,FLAG2,0)
SET_FLAG(PLAYER_GOOD,FLAG3,0)
SET_FLAG(PLAYER_GOOD,FLAG4,0)
SET_FLAG(PLAYER0,FLAG1,0)
SET_FLAG(PLAYER0,FLAG2,0)
SET_FLAG(PLAYER0,FLAG3,0)
SET_FLAG(PLAYER0,FLAG4,0)
SET_FLAG(PLAYER0,FLAG5,0)
SET_FLAG(PLAYER0,FLAG6,0)
SET_FLAG(PLAYER0,FLAG7,0)
SET_FLAG(PLAYER0,CAMPAIGN_FLAG0,0)

CREATE_PARTY(DWARVES)
	ADD_TO_PARTY(DWARVES,DWARFA,7,500,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(DWARVES,DWARFA,7,500,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(DWARVES,DWARFA,7,500,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(DWARVES,DWARFA,7,500,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(DWARVES,DWARFA,7,500,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(DWARVES,DWARFA,7,500,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(DWARVES,DWARFA,7,500,ATTACK_ENEMIES,0)

CREATE_PARTY(THIEVES)
	ADD_TO_PARTY(THIEVES,THIEF,10,500,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(THIEVES,THIEF,10,500,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(THIEVES,THIEF,10,500,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(THIEVES,THIEF,10,500,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(THIEVES,THIEF,10,500,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(THIEVES,THIEF,10,500,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(THIEVES,THIEF,10,500,ATTACK_ENEMIES,0)

CREATE_PARTY(SAMURAI)
	ADD_TO_PARTY(SAMURAI,SAMURAI,7,0,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(SAMURAI,SAMURAI,7,0,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(SAMURAI,SAMURAI,7,0,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(SAMURAI,SAMURAI,7,0,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(SAMURAI,SAMURAI,7,0,ATTACK_DUNGEON_HEART,0)
	ADD_TO_PARTY(SAMURAI,SAMURAI,7,0,ATTACK_DUNGEON_HEART,0)

SET_TIMER(PLAYER_GOOD,TIMER0)
SET_TIMER(PLAYER0,TIMER4)

CHANGE_SLAB_TYPE(72, 55, DOOR_MAGIC2)
CHANGE_SLAB_TYPE(75, 28, DOOR_MAGIC2)

SET_BOX_TOOLTIP(2,"U")
SET_BOX_TOOLTIP(3,"D")
SET_BOX_TOOLTIP(4,"L")
SET_BOX_TOOLTIP(5,"R")
SET_BOX_TOOLTIP(6,"B")
SET_BOX_TOOLTIP(7,"A")
SET_BOX_TOOLTIP(9,"魔法炸药:发射大量魔法炸弹，直击敌人心脏。")
SET_BOX_TOOLTIP(10,"获得提示：获得一个任务提示。")

SET_CREATURE_CONFIGURATION(TUNNELLER,HurtByLava,6)

QUICK_OBJECTIVE(23,"这里住着一个狂妄自大的巫师。当地的居民会被他愚蠢无力的火花表演和其他雕虫小技的魔法所吸引。你的力量一定会更令他们印象深刻.....",ALL_PLAYERS)

IF_ACTION_POINT(1,PLAYER0)
	CHANGE_SLAB_OWNER(72,56,PLAYER0)
	CHANGE_SLAB_TYPE(72,55,PRETTY_PATH)
	PLAY_MESSAGE(PLAYER0,SOUND,943)
ENDIF

IF_ACTION_POINT(2,PLAYER0)
	CHANGE_SLAB_TYPE(6,55,GEMS)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,DWARVES,2,1)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,DWARVES,3,1)
	IF_CONTROLS(PLAYER_GOOD,DWARFA==0)
		CREATURE_AVAILABLE(PLAYER0,TUNNELLER,1,0)
		QUICK_INFORMATION(33,"这些打算将黄金据为己有的矮人同意帮助您推翻巫师，作为回报，他们将撤走矿井的卫兵。",ALL_PLAYERS)
	ENDIF
ENDIF

IF_ACTION_POINT(4,PLAYER0)
	IF_CONTROLS(PLAYER_GOOD,WIZARD>0)
		QUICK_MESSAGE(16, "穆鲁姆――艾瑞奇！", WIZARD)
		CHANGE_SLAB_TYPE(41,15,DRAPE_WALL)
		CHANGE_SLAB_TYPE(42,15,DRAPE_WALL)
		CHANGE_SLAB_TYPE(43,15,DRAPE_WALL)
		CHANGE_SLAB_TYPE(44,15,DRAPE_WALL)
		CHANGE_SLAB_TYPE(45,15,DRAPE_WALL)
		CHANGE_SLAB_TYPE(46,15,DRAPE_WALL)
		CHANGE_SLAB_TYPE(47,15,DRAPE_WALL)
		CHANGE_SLAB_TYPE(48,15,DRAPE_WALL)
		CHANGE_SLAB_TYPE(49,15,DRAPE_WALL)
		QUICK_INFORMATION(11,"巫师为了保护自己，变出了一堵高墙……",4)
		SET_TIMER(PLAYER_GOOD,TIMER1)
		IF(PLAYER0,TOTAL_CREATURES<5)
			IF(PLAYER0,BATTLES_LOST>40)
				QUICK_INFORMATION(25,"这个巫师显然比我们想象的要强大得多。我们必须想办法来打破他的防御....。",4)
			ENDIF
		ENDIF
		NEXT_COMMAND_REUSABLE
	ENDIF
	NEXT_COMMAND_REUSABLE
	RESET_ACTION_POINT(4)
ENDIF

IF_ACTION_POINT(5,PLAYER0)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,THIEVES,5,3)
ENDIF

IF_ACTION_POINT(6,PLAYER0)
	ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,FAIRY,6,15,7,1000)
ENDIF

IF_ACTION_POINT(7,PLAYER0)
	ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,WITCH,7,15,6,1000)
ENDIF

IF_ACTION_POINT(8,PLAYER0)
	ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,GIANT,8,15,8,1000)
ENDIF

IF_ACTION_POINT(9,PLAYER0)
	ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,SAMURAI,9,10,7,1000)
ENDIF

IF_ACTION_POINT(10,PLAYER0)
	QUICK_INFORMATION(2,"传说雪洞里有一座废弃的金矿....",ALL_PLAYERS)
ENDIF

IF_ACTION_POINT(11,PLAYER0)
	ROOM_AVAILABLE(PLAYER0,TORTURE,1,1)
	QUICK_INFORMATION(13,"你发现了这个守护者收藏的刑具。这些应该会有用。",ALL_PLAYERS)
ENDIF

IF_ACTION_POINT(12,PLAYER0)
	SET_FLAG(PLAYER_GOOD,FLAG1,1)
	REVEAL_MAP_LOCATION(PLAYER0,13,5)
	QUICK_INFORMATION(3,"你从书架上拿起一本书阅读。书中记载了你巫师在心脏周围施展的保护魔法的来源，以及如何解除它。",13)
ENDIF

IF_ACTION_POINT(13,PLAYER0)
	SET_FLAG(PLAYER_GOOD,FLAG0,1)
	PLAY_MESSAGE(PLAYER0,SOUND,944)
	QUICK_INFORMATION(4,"你已经解除了巫师在心脏周围施展的保护魔法！",ALL_PLAYERS)
ENDIF

IF_ACTION_POINT(14,PLAYER0)
	IF(PLAYER0,FLAG2==0)
		NEXT_COMMAND_REUSABLE
		PLAY_MESSAGE(PLAYER0,SOUND,959)
		NEXT_COMMAND_REUSABLE
		SET_FLAG(PLAYER0,FLAG2,1)
		
	ENDIF
	NEXT_COMMAND_REUSABLE
	SET_TIMER(PLAYER0,TIMER0)
	NEXT_COMMAND_REUSABLE
	CHANGE_SLAB_TYPE(22,57,HARD)
	NEXT_COMMAND_REUSABLE
	CHANGE_SLAB_TYPE(23,57,HARD)
	QUICK_INFORMATION(9,"书中似乎没有提到魔法可以侦测入侵者并具有自卫机制。看来你得另辟蹊径进入密室了.....",ALL_PLAYERS)
	NEXT_COMMAND_REUSABLE
	RESET_ACTION_POINT(14)
ENDIF

IF_ACTION_POINT(15,PLAYER0)
	IF_CONTROLS(PLAYER_GOOD,WIZARD>0)
		QUICK_MESSAGE(18, "玛吉卡――因喷地门图姆！", WIZARD)
		SET_CREATURE_INSTANCE(SORCEROR,2,NULL,2)
		SET_CREATURE_INSTANCE(SORCEROR,3,NULL,3)
		SET_CREATURE_INSTANCE(SORCEROR,4,NULL,4)
		SET_CREATURE_INSTANCE(SORCEROR,5,NULL,5)
		SET_CREATURE_INSTANCE(SORCEROR,6,NULL,6)
		SET_CREATURE_INSTANCE(SORCEROR,7,NULL,7)
		SET_CREATURE_INSTANCE(SORCEROR,8,NULL,8)
		SET_CREATURE_INSTANCE(SORCEROR,9,NULL,9)
		SET_CREATURE_INSTANCE(SORCEROR,10,NULL,10)
		QUICK_INFORMATION(5,"哦，不！巫师吸走了你所有魔术师的力量！真该死！",ALL_PLAYERS)
		SET_FLAG(PLAYER_GOOD,FLAG2,1)
	ENDIF
ENDIF

IF_ACTION_POINT(16,PLAYER0)
	QUICK_INFORMATION(34,"守护者，我觉得这扇门有些古怪.....",16)
ENDIF

IF_ACTION_POINT(17,PLAYER0)
	ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,SAMURAI,-2,14,7,500)
ENDIF

IF_ACTION_POINT(18,PLAYER0)
	IF(PLAYER_GOOD,FLAG1==1)
		CHANGE_SLAB_TYPE(16,57,PATH)
		PLAY_MESSAGE(PLAYER0,SOUND,856)
		IF(PLAYER0,DEMONSPAWN<5)
			NEXT_COMMAND_REUSABLE
			ADD_CREATURE_TO_LEVEL(PLAYER0,DEMONSPAWN,18,1,1,0)
		ENDIF
	ENDIF
	NEXT_COMMAND_REUSABLE
	RESET_ACTION_POINT(18)
ENDIF

IF_ACTION_POINT(19,PLAYER0)
	QUICK_INFORMATION(12,"巫师就住在北面。在你充分准备好之前，不要贸然挑战他.....。",ALL_PLAYERS)
	MAX_CREATURES(PLAYER0,50)
ENDIF

IF_ACTION_POINT(21,PLAYER0)
	SET_FLAG(PLAYER0,FLAG3,1)
ENDIF

IF_ACTION_POINT(22,PLAYER0)
	SET_FLAG(PLAYER0,FLAG3,1)
ENDIF

IF_ACTION_POINT(23,PLAYER0)
	SET_FLAG(PLAYER_GOOD,FLAG3,1)
ENDIF

IF_ACTION_POINT(24,PLAYER0)
	SET_FLAG(PLAYER_GOOD,FLAG3,1)
ENDIF

IF_ACTION_POINT(25,PLAYER0)
	CHANGE_SLAB_OWNER(32,42,PLAYER0)
	CHANGE_SLAB_OWNER(33,42,PLAYER0)
	CHANGE_SLAB_OWNER(34,42,PLAYER0)
	QUICK_INFORMATION(32,"巫师把一个背叛了他的腐朽的战士关进了地牢。如果你释放他，他一定会成为您的得力助手。",25)
	REVEAL_MAP_RECT(PLAYER0,89,130,3,3)
ENDIF

IF_ACTION_POINT(26,PLAYER0)
	CHANGE_SLAB_OWNER(43,24,PLAYER0)
	CHANGE_SLAB_TYPE(43,23,PRETTY_PATH)
ENDIF

IF_ACTION_POINT(27,PLAYER0)
	CHANGE_SLAB_OWNER(74,27,PLAYER0)
	CHANGE_SLAB_TYPE(75,28,PRETTY_PATH)
	CHANGE_SLAB_OWNER(76,31,PLAYER0)
	CHANGE_SLAB_OWNER(77,26,PLAYER0)
	CHANGE_SLAB_OWNER(73,25,PLAYER0)
	CHANGE_SLAB_OWNER(72,32,PLAYER0)
	CHANGE_SLAB_OWNER(77,33,PLAYER0)
	CHANGE_SLAB_OWNER(79,24,PLAYER0)
	CHANGE_SLAB_OWNER(72,23,PLAYER0)
	CHANGE_SLAB_OWNER(70,29,PLAYER0)
	CHANGE_SLAB_OWNER(71,35,PLAYER0)
	CHANGE_SLAB_OWNER(80,35,PLAYER0)
	CHANGE_SLAB_OWNER(81,22,PLAYER0)
	PLAY_MESSAGE(PLAYER0,SOUND,943)
ENDIF

IF_ACTION_POINT(28,PLAYER0)
	IF_CONTROLS(PLAYER_GOOD,WIZARD>0)
		NEXT_COMMAND_REUSABLE
		QUICK_MESSAGE(15, "雷电听我号令！", WIZARD)
		NEXT_COMMAND_REUSABLE
		PLAY_MESSAGE(PLAYER0,SOUND,974)
		NEXT_COMMAND_REUSABLE
		USE_POWER_AT_POS(PLAYER1,13,145,POWER_LIGHTNING,9,1)
	ENDIF
	NEXT_COMMAND_REUSABLE
	RESET_ACTION_POINT(28)
ENDIF

IF_ACTION_POINT(29,PLAYER0)
	NEXT_COMMAND_REUSABLE
	PLAY_MESSAGE(PLAYER0,SOUND,42)
	NEXT_COMMAND_REUSABLE
	CHANGE_SLAB_TYPE(2,32,TWINS_WALL)
	NEXT_COMMAND_REUSABLE
	CHANGE_SLAB_OWNER(2,32,PLAYER_NEUTRAL)
	NEXT_COMMAND_REUSABLE
	RESET_ACTION_POINT(29)
ENDIF

IF_ACTION_POINT(30,PLAYER0)
	IF(PLAYER0,FLAG6!=1)
		NEXT_COMMAND_REUSABLE
		PLAY_MESSAGE(PLAYER0,SOUND,856)
		NEXT_COMMAND_REUSABLE
		CHANGE_SLAB_TYPE(81,20,PATH)
		NEXT_COMMAND_REUSABLE
		SET_FLAG(PLAYER0,FLAG6,1)
	ENDIF
	NEXT_COMMAND_REUSABLE
	RESET_ACTION_POINT(30)
ENDIF

IF_ACTION_POINT(31,PLAYER0)
	IF(PLAYER0,FLAG6!=0)
		NEXT_COMMAND_REUSABLE
		PLAY_MESSAGE(PLAYER0,SOUND,959)
		NEXT_COMMAND_REUSABLE
		CHANGE_SLAB_TYPE(81,20,HARD)
		NEXT_COMMAND_REUSABLE
		SET_FLAG(PLAYER0,FLAG6,0)
	ENDIF
	NEXT_COMMAND_REUSABLE
	RESET_ACTION_POINT(31)
ENDIF

IF(PLAYER_GOOD,TIMER0>=100)
	NEXT_COMMAND_REUSABLE
	USE_SPELL_ON_CREATURE(PLAYER_GOOD,WIZARD,ANYWHERE,SPELL_REBOUND,0)
	NEXT_COMMAND_REUSABLE
	USE_SPELL_ON_CREATURE(PLAYER_GOOD,WIZARD,ANYWHERE,SPELL_HEAL,10)
	NEXT_COMMAND_REUSABLE
	USE_SPELL_ON_CREATURE(PLAYER_GOOD,WIZARD,ANYWHERE,SPELL_HEAL,10)
	NEXT_COMMAND_REUSABLE
	USE_SPELL_ON_CREATURE(PLAYER_GOOD,WIZARD,ANYWHERE,SPELL_HEAL,10)
	NEXT_COMMAND_REUSABLE
	USE_SPELL_ON_CREATURE(PLAYER_GOOD,WIZARD,ANYWHERE,SPELL_ARMOUR,10)
	NEXT_COMMAND_REUSABLE
	SET_TIMER(PLAYER_GOOD,TIMER0)
ENDIF

IF(PLAYER_GOOD,DUNGEON_DESTROYED==1)
	IF_CONTROLS(PLAYER_GOOD,TOTAL_CREATURES == 0)
		QUICK_OBJECTIVE(31,"你胜利了！巫师被打败了，他的力量消失了，这里的居民们都在你的面前瑟瑟发抖，如你所愿。",PLAYER_GOOD)
		WIN_GAME
	ENDIF
ENDIF

IF(PLAYER0,BOX0_ACTIVATED > 0)
  	RANDOMISE_FLAG(PLAYER0,FLAG1,4)
	IF(PLAYER0,FLAG1==1)
		PLAY_MESSAGE(PLAYER0,SOUND,47)
		SET_HEART_HEALTH(PLAYER0,0)
		QUICK_OBJECTIVE(1,"砰！你死掉了。糟糕……",PLAYER0)
	ENDIF
	IF(PLAYER0,FLAG1==2)
		PLAY_MESSAGE(PLAYER0,SOUND,47)
		KILL_CREATURE(PLAYER0,IMP,ANYWHERE,255)
		QUICK_INFORMATION(7,"如果你以为这里所有的魔法都能带来好处，那可真是愚蠢....",ALL_PLAYERS)
	ENDIF
	IF(PLAYER0,FLAG1==3)
		PLAY_MESSAGE(PLAYER0,SOUND,860)
		ADD_CREATURE_TO_LEVEL(PLAYER0,DRAGON,20,10,10,0)
	ENDIF
	IF(PLAYER0,FLAG1==4)
		QUICK_INFORMATION(8,"嗯……什么也么有发生。",ALL_PLAYERS)
	ENDIF
ENDIF

IF(PLAYER0,BOX1_ACTIVATED > 0)
	PLAY_MESSAGE(PLAYER0,SOUND,856)
  	CHANGE_SLAB_TYPE(3,59,DIRT)
	CHANGE_SLAB_TYPE(1,59,DIRT)
	CHANGE_SLAB_TYPE(2,59,DIRT)
	CHANGE_SLAB_TYPE(4,59,DIRT)
	CHANGE_SLAB_TYPE(5,59,DIRT)
	QUICK_INFORMATION(35,"哦，看哪，一个入口打开了！",2)
ENDIF

IF(PLAYER0,BOX2_ACTIVATED == 1)
	IF(PLAYER0,CAMPAIGN_FLAG0 == 0)
  		ADD_TO_FLAG(PLAYER0,FLAG4,1)
		PLAY_MESSAGE(PLAYER0,SOUND,76)
	ENDIF
ENDIF

IF(PLAYER0,BOX2_ACTIVATED == 2)
	IF(PLAYER0,FLAG4 < 1)
  		SET_FLAG(PLAYER0,CAMPAIGN_FLAG0,1)
	ENDIF
	IF(PLAYER0,CAMPAIGN_FLAG0 == 0)
		IF(PLAYER0,FLAG4 == 1)
  			ADD_TO_FLAG(PLAYER0,FLAG4,1)
			PLAY_MESSAGE(PLAYER0,SOUND,76)
		ENDIF
	ENDIF
ENDIF

IF(PLAYER0,BOX3_ACTIVATED == 1)
	IF(PLAYER0,FLAG4 < 2)
  		SET_FLAG(PLAYER0,CAMPAIGN_FLAG0,1)
	ENDIF
	IF(PLAYER0,CAMPAIGN_FLAG0 == 0)
		IF(PLAYER0,FLAG4 == 2)
  			ADD_TO_FLAG(PLAYER0,FLAG4,1)
			PLAY_MESSAGE(PLAYER0,SOUND,76)
		ENDIF
	ENDIF
ENDIF

IF(PLAYER0,BOX3_ACTIVATED == 2)
	IF(PLAYER0,FLAG4 < 3)
  		SET_FLAG(PLAYER0,CAMPAIGN_FLAG0,1)
	ENDIF
	IF(PLAYER0,CAMPAIGN_FLAG0 == 0)
		IF(PLAYER0,FLAG4 == 3)
  			ADD_TO_FLAG(PLAYER0,FLAG4,1)
			PLAY_MESSAGE(PLAYER0,SOUND,76)
		ENDIF
	ENDIF
ENDIF

IF(PLAYER0,BOX4_ACTIVATED == 1)
	IF(PLAYER0,FLAG4 < 4)
  		SET_FLAG(PLAYER0,CAMPAIGN_FLAG0,1)
	ENDIF
	IF(PLAYER0,CAMPAIGN_FLAG0 == 0)
		IF(PLAYER0,FLAG4 == 4)
  			ADD_TO_FLAG(PLAYER0,FLAG4,1)
			PLAY_MESSAGE(PLAYER0,SOUND,76)
		ENDIF
	ENDIF
ENDIF

IF(PLAYER0,BOX5_ACTIVATED == 1)
	IF(PLAYER0,FLAG4 < 5)
  		SET_FLAG(PLAYER0,CAMPAIGN_FLAG0,1)
	ENDIF
	IF(PLAYER0,CAMPAIGN_FLAG0 == 0)
		IF(PLAYER0,FLAG4 == 5)
  			ADD_TO_FLAG(PLAYER0,FLAG4,1)
			PLAY_MESSAGE(PLAYER0,SOUND,76)
		ENDIF
	ENDIF
ENDIF

IF(PLAYER0,BOX4_ACTIVATED == 2)
	IF(PLAYER0,FLAG4 < 6)
  		SET_FLAG(PLAYER0,CAMPAIGN_FLAG0,1)
	ENDIF
	IF(PLAYER0,CAMPAIGN_FLAG0 == 0)
		IF(PLAYER0,FLAG4 == 6)
  			ADD_TO_FLAG(PLAYER0,FLAG4,1)
			PLAY_MESSAGE(PLAYER0,SOUND,76)
		ENDIF
	ENDIF
ENDIF

IF(PLAYER0,BOX5_ACTIVATED == 2)
	IF(PLAYER0,FLAG4 < 7)
  		SET_FLAG(PLAYER0,CAMPAIGN_FLAG0,1)
	ENDIF
	IF(PLAYER0,CAMPAIGN_FLAG0 == 0)
		IF(PLAYER0,FLAG4 == 7)
  			ADD_TO_FLAG(PLAYER0,FLAG4,1)
			PLAY_MESSAGE(PLAYER0,SOUND,76)
		ENDIF
	ENDIF
ENDIF

IF(PLAYER0,BOX6_ACTIVATED == 1)
	IF(PLAYER0,FLAG4 < 8)
  		SET_FLAG(PLAYER0,CAMPAIGN_FLAG0,1)
	ENDIF
	IF(PLAYER0,CAMPAIGN_FLAG0 == 0)
		IF(PLAYER0,FLAG4 == 8)
  			ADD_TO_FLAG(PLAYER0,FLAG4,1)
			PLAY_MESSAGE(PLAYER0,SOUND,76)
		ENDIF
	ENDIF
ENDIF

IF(PLAYER0,BOX7_ACTIVATED == 1)
	IF(PLAYER0,FLAG4 < 9)
  		SET_FLAG(PLAYER0,CAMPAIGN_FLAG0,1)
	ENDIF
	IF(PLAYER0,CAMPAIGN_FLAG0 == 0)
		IF(PLAYER0,FLAG4 == 9)
  			ADD_TO_FLAG(PLAYER0,FLAG4,1)
			PLAY_MESSAGE(PLAYER0,SOUND,944)
			CHANGE_SLAB_TYPE(31, 43, DIRT)
			USE_POWER_AT_POS(PLAYER0,94,130,POWER_DESTROY_WALLS,4,1)
		ENDIF
	ENDIF
ENDIF

IF(PLAYER0,BOX8_ACTIVATED == 1)
	ADD_GOLD_TO_PLAYER(PLAYER0,10000000)
	PLAY_MESSAGE(PLAYER0,SOUND,34)
ENDIF

IF(PLAYER0,BOX9_ACTIVATED == 1)
	SET_TIMER(PLAYER0,TIMER1)
	QUICK_INFORMATION(21, "你点燃了引线……", ALL_PLAYERS)
	DISPLAY_COUNTDOWN(PLAYER0, TIMER1, 1000, 1)
ENDIF

IF(PLAYER0,BOX10_ACTIVATED == 1)
	QUICK_INFORMATION(36, "飞行的能力很有用吧……", ALL_PLAYERS)
ENDIF

IF(PLAYER0,BOX10_ACTIVATED == 2)
	QUICK_INFORMATION(37, "......但你并不需要飞行怪物的飞行能力。", ALL_PLAYERS)
ENDIF

IF(PLAYER0,BOX10_ACTIVATED == 3)
	QUICK_INFORMATION(38, "如果你无法打破一扇门，也许你应该试着穿过它？", ALL_PLAYERS)
ENDIF

IF(PLAYER0,BOX10_ACTIVATED == 4)
	QUICK_INFORMATION(39, "恶鬼不会无缘无故在这里孵化...", 18)
ENDIF

IF(PLAYER0,TIMER0>=600)
	IF(PLAYER0,FLAG2==1)
		NEXT_COMMAND_REUSABLE
		PLAY_MESSAGE(PLAYER0,SOUND,856)
		NEXT_COMMAND_REUSABLE
		CHANGE_SLAB_TYPE(22,57,LAVA)
		NEXT_COMMAND_REUSABLE
		CHANGE_SLAB_TYPE(23,57,LAVA)
		QUICK_INFORMATION(22, "入口再一次打开了！", 13)
		NEXT_COMMAND_REUSABLE
		SET_FLAG(PLAYER0,FLAG2,0)
	ENDIF
ENDIF

IF(PLAYER0,TIMER1>=1000)
	IF_CONTROLS(PLAYER_GOOD,WIZARD>0)
		IF(PLAYER_GOOD,DUNGEON_DESTROYED==0)
			QUICK_MESSAGE(19, "爆炸吧！", WIZARD)
			PLAY_MESSAGE(PLAYER0,SOUND,926)
			SET_TIMER(PLAYER0,TIMER2)
			SET_FLAG(PLAYER0,FLAG7,1)
		ENDIF
	ENDIF
	IF_CONTROLS(PLAYER_GOOD,WIZARD==0)
		IF(PLAYER_GOOD,DUNGEON_DESTROYED==0)
			SET_TIMER(PLAYER_GOOD,TIMER2)
			SET_FLAG(PLAYER0,FLAG7,1)
		ENDIF
	ENDIF
	IF(PLAYER_GOOD,DUNGEON_DESTROYED==1)
		IF(PLAYER0,FLAG7==0)
			QUICK_INFORMATION(40, "炸药唯一能找到的目标是：你的心脏！", PLAYER0)
			SET_TIMER(PLAYER0,TIMER2)
			SET_FLAG(PLAYER0,FLAG7,1)
		ENDIF
	ENDIF
ENDIF

IF(PLAYER0,TIMER2>=20)
	SET_FLAG(PLAYER0,FLAG7,1)
	IF(PLAYER0,FLAG5<=10)
		NEXT_COMMAND_REUSABLE
		CREATE_EFFECT_AT_POS(EFFECT_EXPLOSION_1,124,223)
		NEXT_COMMAND_REUSABLE
		CREATE_EFFECT_AT_POS(EFFECT_EXPLOSION_2,123,222)
		NEXT_COMMAND_REUSABLE
		CREATE_EFFECT_AT_POS(EFFECT_EXPLOSION_3,124,222)
		NEXT_COMMAND_REUSABLE
		CREATE_EFFECT_AT_POS(EFFECT_EXPLOSION_4,125,222)
		NEXT_COMMAND_REUSABLE
		CREATE_EFFECT_AT_POS(EFFECT_EXPLOSION_5,123,223)
		NEXT_COMMAND_REUSABLE
		CREATE_EFFECT_AT_POS(EFFECT_EXPLOSION_6,125,223)
		NEXT_COMMAND_REUSABLE
		CREATE_EFFECT_AT_POS(EFFECT_EXPLOSION_7,123,224)
		NEXT_COMMAND_REUSABLE
		CREATE_EFFECT_AT_POS(EFFECT_EXPLOSION_1,124,224)
		NEXT_COMMAND_REUSABLE
		CREATE_EFFECT_AT_POS(EFFECT_EXPLOSION_1,125,224)
		NEXT_COMMAND_REUSABLE
		ADD_HEART_HEALTH(PLAYER0,-2500,1)
		NEXT_COMMAND_REUSABLE
		ADD_TO_FLAG(PLAYER0,FLAG5,1)
		NEXT_COMMAND_REUSABLE
		SET_TIMER(PLAYER0,TIMER2)
	ENDIF
	IF(PLAYER0,FLAG5>10)
		IF(PLAYER0,HEART_HEALTH>0)
			IF_CONTROLS(PLAYER_GOOD,WIZARD>0)
				IF(PLAYER_GOOD,DUNGEON_DESTROYED==0)
					QUICK_INFORMATION(20, "呼！真是千钧一发啊。看来我们低估了他的力量...", PLAYER0)
				ENDIF
			ENDIF
		ENDIF
	ENDIF
ENDIF

IF(PLAYER0,TIMER3>=2000)
	SET_CREATURE_INSTANCE(SORCEROR,2,FIREBALL,2)
	SET_CREATURE_INSTANCE(SORCEROR,3,HEAL,3)
	SET_CREATURE_INSTANCE(SORCEROR,4,FIRE_BOMB,4)
	SET_CREATURE_INSTANCE(SORCEROR,5,INVISIBILITY,5)
	SET_CREATURE_INSTANCE(SORCEROR,6,NAVIGATING_MISSILE,6)
	SET_CREATURE_INSTANCE(SORCEROR,7,SIGHT,7)
	SET_CREATURE_INSTANCE(SORCEROR,8,WIND,8)
	SET_CREATURE_INSTANCE(SORCEROR,9,WORD_OF_POWER,9)
	ADD_CREATURE_TO_LEVEL(PLAYER0,SORCEROR,PLAYER0,10,10,0)
	QUICK_INFORMATION(6,"这些魔术师秘密地利用图书馆的综合资源扭转了巫师的诅咒，并发誓要向巫师复仇。",ALL_PLAYERS)
ENDIF

IF(PLAYER_GOOD,TIMER1>=2500)
	IF_CONTROLS(PLAYER_GOOD,WIZARD>0)
		QUICK_MESSAGE(17, "忏悔吧！厄里哥特――旁提姆！", WIZARD)
		USE_POWER_AT_POS(PLAYER1,124,190,POWER_DESTROY_WALLS,9,1)
		CHANGE_SLAB_TYPE(41,57,BRIDGE_FRAME)
		CHANGE_SLAB_TYPE(41,58,BRIDGE_FRAME)
		CHANGE_SLAB_TYPE(41,59,BRIDGE_FRAME)
		CHANGE_SLAB_TYPE(41,60,BRIDGE_FRAME)
		CHANGE_SLAB_TYPE(41,61,BRIDGE_FRAME)
		CHANGE_SLAB_TYPE(41,62,BRIDGE_FRAME)
		CHANGE_SLAB_OWNER(41,57,PLAYER_GOOD)
		ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,SAMURAI,-1,DUNGEON_HEART,0,9,0)
	ENDIF	
ENDIF

IF(PLAYER_GOOD,TIMER1>=5000)
	IF_CONTROLS(PLAYER_GOOD,WIZARD>0)
		ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,FAIRY,-1,10,10,0)
	ENDIF	
ENDIF

IF(PLAYER_GOOD,TIMER1>=7500)
	IF_CONTROLS(PLAYER_GOOD,WIZARD>0)
		ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,MONK,-1,15,10,0)
	ENDIF	
ENDIF

IF(PLAYER_GOOD,TIMER1>=10000)
	IF_CONTROLS(PLAYER_GOOD,WIZARD>0)
		ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,DWARFA,-1,25,10,0)
	ENDIF	
ENDIF

IF(PLAYER_GOOD,TIMER2>=20)
	IF(PLAYER_GOOD,DUNGEON_DESTROYED==0)
		SET_FLAG(PLAYER0,FLAG7,1)
		IF(PLAYER_GOOD,FLAG4<=10)
			NEXT_COMMAND_REUSABLE
			CREATE_EFFECT_AT_POS(EFFECT_EXPLOSION_1,138,22)
			NEXT_COMMAND_REUSABLE
			CREATE_EFFECT_AT_POS(EFFECT_EXPLOSION_2,137,21)
			NEXT_COMMAND_REUSABLE
			CREATE_EFFECT_AT_POS(EFFECT_EXPLOSION_3,138,21)
			NEXT_COMMAND_REUSABLE
			CREATE_EFFECT_AT_POS(EFFECT_EXPLOSION_4,139,21)
			NEXT_COMMAND_REUSABLE
			CREATE_EFFECT_AT_POS(EFFECT_EXPLOSION_5,137,22)
			NEXT_COMMAND_REUSABLE
			CREATE_EFFECT_AT_POS(EFFECT_EXPLOSION_6,139,22)
			NEXT_COMMAND_REUSABLE
			CREATE_EFFECT_AT_POS(EFFECT_EXPLOSION_7,137,23)
			NEXT_COMMAND_REUSABLE
			CREATE_EFFECT_AT_POS(EFFECT_EXPLOSION_1,138,23)
			NEXT_COMMAND_REUSABLE
			CREATE_EFFECT_AT_POS(EFFECT_EXPLOSION_1,139,23)
			IF(PLAYER_GOOD,FLAG0>0)
				NEXT_COMMAND_REUSABLE
				ADD_HEART_HEALTH(PLAYER_GOOD,-2500,0)
			ENDIF
			IF(PLAYER_GOOD,FLAG0==0)
				NEXT_COMMAND_REUSABLE
				ADD_HEART_HEALTH(PLAYER0,-2500,1)
			ENDIF
			NEXT_COMMAND_REUSABLE
			ADD_TO_FLAG(PLAYER_GOOD,FLAG4,1)
			NEXT_COMMAND_REUSABLE
			SET_TIMER(PLAYER_GOOD,TIMER2)
		ENDIF
	ENDIF
ENDIF

IF(PLAYER0,FLAG3==1)
	QUICK_INFORMATION(10,"在你面前，是那些敢于挑战这个巫师的守护者们的地下城的废墟。看来传言还是有一定道理的.....。",ALL_PLAYERS)
ENDIF

IF(PLAYER_GOOD,FLAG3==1)
	SET_DOOR(UNLOCKED,42,4)
	ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,SAMURAI,-3,30,10,1000)
ENDIF

IF(PLAYER_GOOD,HEART_HEALTH < 30000)
	IF(PLAYER_GOOD,FLAG0==0)
		NEXT_COMMAND_REUSABLE
		SET_HEART_HEALTH(PLAYER_GOOD, 30000)
		NEXT_COMMAND_REUSABLE
		ADD_HEART_HEALTH(PLAYER0, -25, 0)
		NEXT_COMMAND_REUSABLE
		RANDOMISE_FLAG(PLAYER0,FLAG0,50)
		IF(PLAYER0,FLAG0==1)
			PLAY_MESSAGE(PLAYER0,SPEECH,8)
			QUICK_INFORMATION(0,"这是怎么回事？奇怪的魔法把对巫师的心脏的伤害转移到了你自己的心脏身上！",PLAYER0)
		ENDIF
	ENDIF
ENDIF

IF(PLAYER0,GAME_TURN>=500)
	QUICK_INFORMATION(26,"一种奇怪的魔法阻止了你在这里建造新的图书馆。也许这附近有一个我们可以占领的？你必须要有一个图书馆才能征服这片土地。",ALL_PLAYERS)
ENDIF

IF(PLAYER0,GAME_TURN>=2500)
	QUICK_INFORMATION(28,"北方潜伏着强大的对手。小心。",ALL_PLAYERS)
ENDIF

IF(PLAYER0,GAME_TURN>=5000)
	QUICK_INFORMATION(24,"有传言说，尽管这个巫师是个同性恋，但那些反对他或反对他那可笑的公平、平等和机会观念的人都很害怕他。但这只是些所谓的谣言，对吧？",ALL_PLAYERS)
ENDIF

IF(PLAYER0,GAME_TURN>=7500)
	QUICK_INFORMATION(29,"报告显示，洞穴里藏有强大的魔法文物。我们应该去寻找它们。",ALL_PLAYERS)
ENDIF

IF(PLAYER0,RESEARCH>0)
	QUICK_INFORMATION(27,"您控制了一座黑暗图书馆，巫师曾将其尘封并加以看守，以阻止黑暗魔法的研究。",ALL_PLAYERS)
ENDIF

IF_CONTROLS(PLAYER_GOOD,WIZARD==0)
	IF(PLAYER_GOOD,DUNGEON_DESTROYED==0)
		QUICK_OBJECTIVE(30,"巫师已经被打败了，但我还能感觉到他残留的魔力.....。",PLAYER_GOOD)
	ENDIF
ENDIF

IF(PLAYER0,RESEARCH>81)
	IF(PLAYER_GOOD,FLAG2==1)
		SET_TIMER(PLAYER0,TIMER3)
	ENDIF
ENDIF
