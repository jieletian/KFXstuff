REM ***********************************************************************
REM   Script for Level  459.Yami
REM ***********************************************************************
REM 
REM ***********************************************************************
REM 地图的大概情况与参考建议：
REM 1、人类很多骑士，最后一波很猛烈，利用好熔岩。
REM 2、 守护者有时候也发展很好，塌方对付小鬼、隐身魔法抢地，一面紫金不够钱塌方用就用工厂和精灵、。
REM 3、紫金外围没有泥，不能做围墙，守护者会搭桥过来抢，要小心 。
REM 4、 寺庙、陵墓占了就可以建造，但审讯室和监狱不能，如果要用它们就不要卖。
REM 5、守护者图书馆里多了治疗、闪电等魔法 。
REM 6、人类地盘都有监狱、寺庙、审讯室、陵墓，但不容易占 。
REM 7、敌人会招募你的蜘蛛等怪，骷髅估计也会招募，所以建议先攻守护者。如果先攻人类，就要用隐身魔法
REM     去占人类的审讯室，如果只有监狱没有审讯室估计也难打。
REM ***********************************************************************
REM ***************************各种基本设置 *****************************
SET_GENERATE_SPEED(700)
START_MONEY(PLAYER0,10000)
START_MONEY(PLAYER1,100000)

MAX_CREATURES(PLAYER0,17)
MAX_CREATURES(PLAYER1,32)

COMPUTER_PLAYER(PLAYER1,0)
SET_COMPUTER_PROCESS(PLAYER1,"BUILD A SCAVENGER ROOM",0,5,4,9,0)
SET_COMPUTER_PROCESS(PLAYER1,"BUILD A GRAVEYARD ROOM",0,5,6,11,0)

REM *******************************添加怪物到入口 *********************************
REM   (添加到入口的生物名字,数量)
ADD_CREATURE_TO_POOL(TROLL,30)
ADD_CREATURE_TO_POOL(DARK_MISTRESS,20)
ADD_CREATURE_TO_POOL(BILE_DEMON,30)
ADD_CREATURE_TO_POOL(SORCEROR,6)
ADD_CREATURE_TO_POOL(ORC,15)
ADD_CREATURE_TO_POOL(HELL_HOUND,10)
ADD_CREATURE_TO_POOL(SPIDER,8)
ADD_CREATURE_TO_POOL(BUG,5)
ADD_CREATURE_TO_POOL(DEMONSPAWN,10)
ADD_CREATURE_TO_POOL(SKELETON,25)
ADD_CREATURE_TO_POOL(GHOST,3)
ADD_CREATURE_TO_POOL(VAMPIRE,2)
ADD_CREATURE_TO_POOL(DRAGON,3)


REM *******************************指定阵营（玩家）允许/禁止从入口（怪物池）中招指定生物*********************************
REM  (玩家,生物名称,1（只能是1）,[1允许招募/0禁止招募])
CREATURE_AVAILABLE(ALL_PLAYERS,SPIDER,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,SORCEROR,1,1)

CREATURE_AVAILABLE(PLAYER1,SKELETON,1,1)
CREATURE_AVAILABLE(PLAYER1,GHOST,1,1)

CREATURE_AVAILABLE(PLAYER0,TROLL,1,1)
CREATURE_AVAILABLE(PLAYER0,BILE_DEMON,1,1)
CREATURE_AVAILABLE(PLAYER0,DARK_MISTRESS,1,1)
CREATURE_AVAILABLE(PLAYER0,ORC,1,1)
CREATURE_AVAILABLE(PLAYER0,BUG,1,1)
CREATURE_AVAILABLE(PLAYER0,DEMONSPAWN,1,1)
CREATURE_AVAILABLE(PLAYER0,HELL_HOUND,1,1)

REM ********************指定阵营（玩家）允许/禁止研究和建造指定房子（建筑）**********************************
REM  (玩家,房子名称,是否可研究或建造[1是允许；0是禁止],是否已经可建造[1已经可建/0待研究])
ROOM_AVAILABLE(ALL_PLAYERS,TREASURE,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,LAIR,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,GARDEN,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,RESEARCH,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,BRIDGE,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,GUARD_POST,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,WORKSHOP,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,SCAVENGER,1,0)

ROOM_AVAILABLE(PLAYER0,TRAINING,1,1)
ROOM_AVAILABLE(PLAYER0,BARRACKS,1,0)

ROOM_AVAILABLE(PLAYER1,TORTURE,1,1)
ROOM_AVAILABLE(PLAYER1,PRISON,1,1)
ROOM_AVAILABLE(PLAYER1,TEMPLE,1,0)
ROOM_AVAILABLE(PLAYER1,GRAVEYARD,1,0)

REM ********************指定阵营（玩家）允许/禁止研究和使用指定魔法**********************************
REM  (玩家,魔法名称,是否可研究或使用[1是允许；0是禁止],是否已经可使用[1已经可建/0待研究])
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HAND,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_IMP,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SLAP,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CALL_TO_ARMS,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_PROTECT,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CONCEAL,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CHICKEN,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CAVE_IN,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SPEED,1,0)

MAGIC_AVAILABLE(PLAYER1,POWER_SIGHT,1,0)
MAGIC_AVAILABLE(PLAYER1,POWER_HEAL_CREATURE,1,0)
MAGIC_AVAILABLE(PLAYER1,POWER_LIGHTNING,1,0)

REM *********************指定阵营（玩家）允许/禁止制造指定的门*********************************
REM  (玩家,门的名称,[1是允许；0是禁止],1[0是允许；数字是已经制造出来/赠送的数量]
DOOR_AVAILABLE(ALL_PLAYERS,STEEL,1,0)
DOOR_AVAILABLE(ALL_PLAYERS,MAGIC,1,0)

REM *********************指定阵营（玩家）允许/禁止制造指定陷阱*********************************
REM  (玩家,陷阱名称,[1是允许；0是禁止],1[0是允许；数字是已经制造出来/赠送的数量]
TRAP_AVAILABLE(ALL_PLAYERS,POISON_GAS,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,LIGHTNING,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,WORD_OF_POWER,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,LAVA,1,0)
TRAP_AVAILABLE(PLAYER0,ALARM,1,0)

REM CREATE_PARTY =>组建队伍                (队伍名称)   
REM ADD_TO_PARTY=>添加生物到队伍中   (队伍名称,生物名称,生物等级,携带金钱数量,任务,延时)
CREATE_PARTY(ONE)
    ADD_TO_PARTY(ONE,THIEF,5,200,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(ONE,THIEF,4,150,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(ONE,THIEF,4,300,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(ONE,ARCHER,4,250,DEFEND_PARTY,0)
    ADD_TO_PARTY(ONE,DWARFA,3,200,STEAL_GOLD,0)
CREATE_PARTY(TWO)
    ADD_TO_PARTY(TWO,BARBARIAN,4,300,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(TWO,BARBARIAN,5,350,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(TWO,ARCHER,4,250,DEFEND_PARTY,0)
    ADD_TO_PARTY(TWO,ARCHER,5,300,DEFEND_PARTY,0)
CREATE_PARTY(THREE)
    ADD_TO_PARTY(THREE,MONK,5,400,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(THREE,MONK,6,500,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(THREE,WITCH,4,300,DEFEND_PARTY,0)
    ADD_TO_PARTY(THREE,WITCH,5,300,DEFEND_PARTY,0)
CREATE_PARTY(FOUR)
    ADD_TO_PARTY(FOUR,KNIGHT,7,500,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(FOUR,KNIGHT,7,750,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(FOUR,WIZARD,7,400,DEFEND_PARTY,0)
CREATE_PARTY(BRUTES)
    ADD_TO_PARTY(BRUTES,GIANT,6,400,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(BRUTES,GIANT,6,400,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(BRUTES,GIANT,6,400,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(BRUTES,GIANT,6,400,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(BRUTES,GIANT,6,400,DEFEND_PARTY,0)
    ADD_TO_PARTY(BRUTES,GIANT,6,400,DEFEND_PARTY,0)
CREATE_PARTY(SCOUTS)
    ADD_TO_PARTY(SCOUTS,DWARFA,10,500,STEAL_GOLD,0)
    ADD_TO_PARTY(SCOUTS,THIEF,9,600,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(SCOUTS,THIEF,9,500,DEFEND_PARTY,0)
    ADD_TO_PARTY(SCOUTS,THIEF,8,400,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(SCOUTS,ARCHER,9,300,DEFEND_PARTY,0)
    ADD_TO_PARTY(SCOUTS,ARCHER,8,200,DEFEND_PARTY,0)
    ADD_TO_PARTY(SCOUTS,ARCHER,8,200,DEFEND_PARTY,0)
CREATE_PARTY(MYSTIC)
    ADD_TO_PARTY(MYSTIC,MONK,7,500,DEFEND_PARTY,0)
    ADD_TO_PARTY(MYSTIC,WIZARD,8,750,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(MYSTIC,FAIRY,10,1000,DEFEND_PARTY,0)
    ADD_TO_PARTY(MYSTIC,WITCH,8,500,STEAL_SPELLS,0)
CREATE_PARTY(ELITES)
    ADD_TO_PARTY(ELITES,KNIGHT,9,1000,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(ELITES,WIZARD,8,1250,STEAL_SPELLS,0)
    ADD_TO_PARTY(ELITES,BARBARIAN,8,800,DEFEND_PARTY,0)
    ADD_TO_PARTY(ELITES,GIANT,7,800,ATTACK_ENEMIES,0)
CREATE_PARTY(SHOGUN)
    ADD_TO_PARTY(SHOGUN,SAMURAI,10,2500,ATTACK_DUNGEON_HEART,1)
    ADD_TO_PARTY(SHOGUN,WIZARD,9,1000,STEAL_SPELLS,1)
    ADD_TO_PARTY(SHOGUN,ARCHER,8,500,DEFEND_PARTY,1)
    ADD_TO_PARTY(SHOGUN,MONK,9,750,DEFEND_PARTY,1)
    ADD_TO_PARTY(SHOGUN,KNIGHT,8,1000,ATTACK_ENEMIES,1)
    ADD_TO_PARTY(SHOGUN,BARBARIAN,8,800,ATTACK_ENEMIES,1)



REM ****************************脚本主体(主要内容、核心内容）**********************************
SET_CREATURE_STRENGTH(SAMURAI,150)
SET_CREATURE_HEALTH(SAMURAI,2000)
SET_CREATURE_ARMOUR(SAMURAI,120)

REM QUICK_OBJECTIVE(0,"This realm is ruled by the Shogun, a legendary Samurai warrior with great swordfighting skills. I say it's time we put these so-called skills to the test! Be warned, the Shogun may decide to pre-emptively attack us if we become too powerful.",PLAYER0)
QUICK_OBJECTIVE(0,"这个王国由幕府将军统治，这武士是一个具有伟大剑术技能的传奇人物。我说是时候把这些所谓的技能放在考验中了！小心，幕府将军可能会决定先发制人地攻击我们，如果我们变得太强大。",PLAYER0)

REM 寺庙和陵墓
IF(PLAYER0,TEMPLE > 0)
    ROOM_AVAILABLE(ALL_PLAYERS,GRAVEYARD,1,1)
ENDIF
IF(PLAYER0,GRAVEYARD > 0)
    ROOM_AVAILABLE(ALL_PLAYERS,TEMPLE,1,1)
ENDIF

REM 限制工厂数量
IF(PLAYER2,WORKSHOP >= 72)
    NEXT_COMMAND_REUSABLE
    ROOM_AVAILABLE(PLAYER2,WORKSHOP,0,0)
ENDIF
IF(PLAYER2,WORKSHOP < 72)
    NEXT_COMMAND_REUSABLE
    ROOM_AVAILABLE(PLAYER2,WORKSHOP,1,1)
ENDIF

REM *************触发点事件***********************
IF_ACTION_POINT(3,PLAYER0)
    SET_FLAG(PLAYER_GOOD,FLAG1,1)
    SET_TIMER(PLAYER_GOOD,TIMER1)
    SET_TIMER(PLAYER_GOOD,TIMER2)
ENDIF
IF(PLAYER_GOOD,FLAG1 == 1)
    IF(PLAYER_GOOD,TIMER1 >= 1000)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,BRUTES,-4,1)
    ENDIF
    IF(PLAYER_GOOD,TIMER1 >= 1133)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SCOUTS,-1,1)
    ENDIF
    IF(PLAYER_GOOD,TIMER1 >= 1366)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,MYSTIC,-6,1)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,ELITES,-8,1)
    ENDIF
    IF(PLAYER_GOOD,TIMER1 >= 1466)
        REM QUICK_OBJECTIVE(1,"The Shogun himself has arrived... string him up!",PLAYER0)
        QUICK_OBJECTIVE(1,"幕府将军亲自来了…把他捆起来！",PLAYER0)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SHOGUN,-7,1)
    ENDIF
    IF(PLAYER_GOOD,TIMER1 >= 1600)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,BRUTES,-6,1)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,MYSTIC,-5,1)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,ELITES,8,1)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SCOUTS,-8,1)
    ENDIF
    IF(PLAYER_GOOD,TIMER2 >= 2500)
        IF(PLAYER_GOOD,SAMURAI == 1)
            SET_FLAG(PLAYER_GOOD,FLAG2,1)
            NEXT_COMMAND_REUSABLE
            ADD_PARTY_TO_LEVEL(PLAYER_GOOD,FOUR,-7,1)
            NEXT_COMMAND_REUSABLE
            SET_TIMER(PLAYER_GOOD,TIMER2)
        ENDIF
    ENDIF
ENDIF
IF_ACTION_POINT(4,PLAYER0)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,ONE,2,1)
ENDIF
IF_ACTION_POINT(5,PLAYER0)
    SET_TIMER(PLAYER0,TIMER0)
ENDIF
IF(PLAYER0,TIMER0 >= 1400)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,TWO,-1,1)
ENDIF
IF(PLAYER0,TIMER0 >= 3000)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,THREE,-2,1)
ENDIF
IF_ACTION_POINT(7,PLAYER0)
    SET_TIMER(PLAYER0,TIMER1)
ENDIF
IF(PLAYER0,TIMER1 >= 1400)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,ONE,1,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,TWO,-3,1)
ENDIF
IF(PLAYER0,TIMER1 >= 2500)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,THREE,-4,1)
ENDIF
IF(PLAYER0,TIMER1 >= 4200)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,FOUR,-3,1)
ENDIF


REM 人类心脏被打爆而守护者心脏未打爆，末日审判可研究
IF(PLAYER_GOOD,DUNGEON_DESTROYED == 1)
    IF(PLAYER1,DUNGEON_DESTROYED == 0)
        MAGIC_AVAILABLE(PLAYER0,POWER_ARMAGEDDON,1,0)
        TUTORIAL_FLASH_BUTTON(33,PLAYER0)
        MAX_CREATURES(PLAYER1,35)
        ADD_CREATURE_TO_LEVEL(PLAYER1,VAMPIRE,PLAYER1,1,10,1500)
        CREATURE_AVAILABLE(PLAYER1,SORCEROR,1,0)
        CREATURE_AVAILABLE(PLAYER1,VAMPIRE,1,1)
        CREATURE_AVAILABLE(PLAYER0,DRAGON,1,1)
    ENDIF
ENDIF

REM ****************************胜利/失败条件**********************************
REM 踩到人类心脏附近的触发点3触发时钟刷敌兵后，所有敌人被消灭
IF(PLAYER0,ALL_DUNGEONS_DESTROYED == 1)
    IF(PLAYER_GOOD,TOTAL_CREATURES == 0)
        IF(PLAYER_GOOD,FLAG2 == 1)
            WIN_GAME
        ENDIF
    ENDIF
ENDIF

REM ****************************提示信息**********************************










































