REM ****************************************************************************
REM  即时战略游戏《地下城守护者1》复刻版整合项目的战役
REM ***************************************************************************
REM  战役名: 更深的地下城
REM  作者：基于牛蛙脚本
REM  地下城守护者爱好者 CCP 团队
REM  版权:
REM   此脚本是免费的；你可以重新发布和/或修改它， 根据自由软件基金会公布的GNU
REM   通用公共许可证条款；许可证的版本2，或由你选择的任何后期版本。
REM ****************************************************************************
REM  更深地下城之脚本，地图名：84.Caddis Fell
REM  日期: 1996年12月17 -  2014年9月9
REM ****************************************************************************
REM 地图的大概情况与参考建议：
REM 1、守护者相对强大，但由于它监狱大于九格后强制设置监禁，因此还是能利用人类去消灭它。
REM 2、在熔岩上反复搭桥卖桥，把一波波人类进攻“收集”到守护者附近，最后搭桥连通守护者。
REM 3、因为守护者家里才有监狱，除非你先守护者，否则招降不到人类，招降的意义也不大。
REM 4、 。
REM 5、 。
REM ****************************************************************************
REM ***************************各种基本设置 *****************************
SET_GENERATE_SPEED(500)
COMPUTER_PLAYER(PLAYER1,1)
START_MONEY(PLAYER0,250)
START_MONEY(PLAYER1,10000)

MAX_CREATURES(PLAYER0,20)
MAX_CREATURES(PLAYER1,30)


REM *******************************添加怪物到入口 *********************************
REM   (添加到入口的生物名字,数量)
ADD_CREATURE_TO_POOL(SORCEROR,10)
ADD_CREATURE_TO_POOL(BILE_DEMON,10)
ADD_CREATURE_TO_POOL(DRAGON,10)
ADD_CREATURE_TO_POOL(BUG,15)
ADD_CREATURE_TO_POOL(SPIDER,15)
ADD_CREATURE_TO_POOL(DARK_MISTRESS,10)
ADD_CREATURE_TO_POOL(TROLL,5)
ADD_CREATURE_TO_POOL(ORC,10)
ADD_CREATURE_TO_POOL(VAMPIRE,5)
ADD_CREATURE_TO_POOL(FLY,10)

REM *******************************指定阵营（玩家）允许/禁止从入口（怪物池）中招指定生物*********************************
REM  (玩家,生物名称,1（只能是1）,[1允许招募/0禁止招募])

CREATURE_AVAILABLE(ALL_PLAYERS,SORCEROR,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,ORC,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,BILE_DEMON,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,BUG,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,SPIDER,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,DARK_MISTRESS,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,TROLL,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,FLY,1,0)
CREATURE_AVAILABLE(PLAYER1,VAMPIRE,1,0)
CREATURE_AVAILABLE(PLAYER1,DRAGON,1,0)

REM ********************指定阵营（玩家）允许/禁止研究和建造指定房子（建筑）**********************************
REM  (玩家,房子名称,是否可研究或建造[1是允许；0是禁止],是否已经可建造[1已经可建/0待研究])
ROOM_AVAILABLE(ALL_PLAYERS,LAIR,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,TREASURE,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,GUARD_POST,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,BARRACKS,1,0) 
ROOM_AVAILABLE(ALL_PLAYERS,TORTURE,1,0) 
ROOM_AVAILABLE(ALL_PLAYERS,WORKSHOP,1,0) 
ROOM_AVAILABLE(ALL_PLAYERS,TEMPLE,1,0) 
ROOM_AVAILABLE(ALL_PLAYERS,GRAVEYARD,1,0) 
ROOM_AVAILABLE(PLAYER1,BRIDGE,1,0)
ROOM_AVAILABLE(PLAYER1,TRAINING,1,0)
ROOM_AVAILABLE(PLAYER1,PRISON,1,0) 

REM ********************指定阵营（玩家）允许/禁止研究和使用指定魔法**********************************
REM  (玩家,魔法名称,是否可研究或使用[1是允许；0是禁止],是否已经可使用[1已经可建/0待研究])
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HAND,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SLAP,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_IMP,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_OBEY,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CALL_TO_ARMS,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HEAL_CREATURE,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SPEED,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CONCEAL,1,0)
REM MAGIC_AVAILABLE(PLAYER0,POWER_DESTROY_WALLS,1,0)

REM *********************指定阵营（玩家）允许/禁止制造指定的门*********************************
REM  (玩家,门的名称,[1是允许；0是禁止],1[0是允许；数字是已经制造出来/赠送的数量]
DOOR_AVAILABLE(ALL_PLAYERS,BRACED,1,0)
DOOR_AVAILABLE(ALL_PLAYERS,STEEL,1,0)
DOOR_AVAILABLE(ALL_PLAYERS,MAGIC,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,POISON_GAS,1,0)

REM *********************指定阵营（玩家）允许/禁止制造指定陷阱*********************************
REM  (玩家,陷阱名称,[1是允许；0是禁止],1[0是允许；数字是已经制造出来/赠送的数量]
TRAP_AVAILABLE(ALL_PLAYERS,LAVA,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,BOULDER,1,0)

REM ****************************增援部队**********************************
REM CREATE_PARTY =>组建队伍                (队伍名称)   
REM ADD_TO_PARTY=>添加生物到队伍中   (队伍名称,生物名称,生物等级,携带金钱数量,任务,延时)
CREATE_PARTY(PARTY1)
    ADD_TO_PARTY(PARTY1,SAMURAI,5,100,ATTACK_ENEMIES,0)
CREATE_PARTY(PARTY2)
    ADD_TO_PARTY(PARTY2,ARCHER,5,100,ATTACK_ENEMIES,0)
CREATE_PARTY(PARTY3)
    ADD_TO_PARTY(PARTY3,MONK,6,100,ATTACK_ENEMIES,0)
CREATE_PARTY(PARTY4)
    ADD_TO_PARTY(PARTY4,SAMURAI,5,100,ATTACK_ENEMIES,0)
CREATE_PARTY(PARTY5)
    ADD_TO_PARTY(PARTY5,FLY,5,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(PARTY5,FLY,5,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(PARTY5,FLY,5,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(PARTY5,FLY,5,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(PARTY5,FLY,5,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(PARTY5,FLY,5,0,ATTACK_ENEMIES,0)
CREATE_PARTY(PARTY6)
    ADD_TO_PARTY(PARTY6,WIZARD,5,100,ATTACK_ENEMIES,0)
CREATE_PARTY(PARTY7)
    ADD_TO_PARTY(PARTY7,FLY,7,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(PARTY7,FLY,7,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(PARTY7,FLY,7,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(PARTY7,FLY,7,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(PARTY7,FLY,5,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(PARTY7,FLY,5,0,ATTACK_ENEMIES,0)
CREATE_PARTY(PARTY8)
    ADD_TO_PARTY(PARTY8,SAMURAI,5,100,ATTACK_ENEMIES,0)
CREATE_PARTY(PARTY9)
    ADD_TO_PARTY(PARTY9,MONK,10,500,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(PARTY9,MONK,10,500,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(PARTY9,GIANT,10,500,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(PARTY9,GIANT,10,500,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(PARTY9,MONK,10,500,ATTACK_ENEMIES,0)
CREATE_PARTY(PARTY10)
    ADD_TO_PARTY(PARTY10,MONK,10,500,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(PARTY10,MONK,10,500,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(PARTY10,THIEF,10,500,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(PARTY10,GIANT,10,500,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(PARTY10,GIANT,10,500,ATTACK_ENEMIES,0)
CREATE_PARTY(PARTY11)
    ADD_TO_PARTY(PARTY11,BARBARIAN,10,1000,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(PARTY11,THIEF,10,100,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(PARTY11,GIANT,10,100,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(PARTY11,MONK,10,100,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(PARTY11,WITCH,10,100,ATTACK_ENEMIES,0)
CREATE_PARTY(PARTY12)
    ADD_TO_PARTY(PARTY12,FLY,7,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(PARTY12,FLY,5,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(PARTY12,FLY,5,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(PARTY12,FLY,5,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(PARTY12,FLY,5,0,ATTACK_ENEMIES,0)
CREATE_PARTY(PARTY15)
    ADD_TO_PARTY(PARTY15,KNIGHT,10,500,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(PARTY15,SAMURAI,10,100,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(PARTY15,SAMURAI,10,100,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(PARTY15,SAMURAI,10,100,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(PARTY15,WIZARD,10,100,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(PARTY15,WIZARD,10,100,ATTACK_ENEMIES,0)


REM ****************************脚本主体(主要内容、核心内容）**********************************
SET_CREATURE_FEAR_WOUNDED(FLY,30)

REM  桥、训练室、监狱、陵墓占领后就可建造
IF(PLAYER0,BRIDGE > 0)
    ROOM_AVAILABLE(PLAYER0,BRIDGE,1,1)
ENDIF
IF(PLAYER0,TRAINING > 0)
    ROOM_AVAILABLE(PLAYER0,TRAINING,1,1)
ENDIF
IF(PLAYER0,PRISON > 0)
    ROOM_AVAILABLE(PLAYER0,PRISON,1,1)
ENDIF
IF(PLAYER0,GRAVEYARD > 0)
    ROOM_AVAILABLE(PLAYER0,GRAVEYARD,1,1)
ENDIF

REM 孵化室、图书馆可占领但不可建造
IF(PLAYER0,GARDEN > 0)
    ROOM_AVAILABLE(PLAYER0,GARDEN,0,0)
ENDIF
IF(PLAYER0,RESEARCH > 0)
    ROOM_AVAILABLE(PLAYER0,RESEARCH,0,0)
ENDIF

IF(PLAYER1,PRISON >= 9)
    SET_CREATURE_TENDENCIES(PLAYER1,IMPRISON,1)
ENDIF


REM 触发点1、2、3、4、6都在你的大陆上。触发点7在守护者大陆上，12在东南，其它都有人类心脏附近。
IF_ACTION_POINT(1,PLAYER0)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY1,1,1)
ENDIF
IF_ACTION_POINT(2,PLAYER0)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY2,2,1)
ENDIF
IF_ACTION_POINT(3,PLAYER0)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY3,3,1)
ENDIF
IF_ACTION_POINT(4,PLAYER0)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY4,4,1)
ENDIF
IF_ACTION_POINT(5,PLAYER0)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY5,4,4)
ENDIF
IF_ACTION_POINT(6,PLAYER0)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY6,6,1)
ENDIF
IF_ACTION_POINT(7,PLAYER0)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY7,4,4)
ENDIF
IF_ACTION_POINT(8,PLAYER0)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY8,8,1)
ENDIF
IF_ACTION_POINT(9,PLAYER0)
    ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY9,9,ACTION_POINT,16,5,500)
ENDIF
IF_ACTION_POINT(10,PLAYER0)
    ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY10,10,ACTION_POINT,16,5,500)
ENDIF
IF_ACTION_POINT(11,PLAYER0)
    ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY11,11,ACTION_POINT,16,5,500)
ENDIF
IF_ACTION_POINT(12,PLAYER0)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY12,4,2)
ENDIF
IF_ACTION_POINT(15,PLAYER0)
    REM "A black shadow is creeping over this land. Now is the time to strike. Take control of your Vampire..."
    REM "一片黑影在这片土地上蔓延，现在是进攻的时候了，控制你的吸血鬼。"
    DISPLAY_OBJECTIVE(933,PLAYER0)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PARTY15,16,1)
    SET_FLAG(PLAYER0,FLAG0,1)
ENDIF

IF(PLAYER1,DUNGEON_DESTROYED == 1)
    IF(PLAYER_GOOD,DUNGEON_DESTROYED == 0)
        REM "Congratulations Keeper, you have a powerful army indeed. Do not grow complacent though, you still have another challenger to the south ..."
        REM "恭喜守护者，你确实有一支强大的军队，别自满，你还有一个南方挑战者。"
        DISPLAY_OBJECTIVE(915,PLAYER0)
    ENDIF
ENDIF

IF(PLAYER1,DUNGEON_DESTROYED == 0)
    IF(PLAYER_GOOD,DUNGEON_DESTROYED == 1)
        REM "恭喜守护者，你确实有一支强大的军队，别自满，你还有一个北方挑战者。"
        REM "Congratulations Keeper, you have a powerful army indeed. Do not grow complacent though, you still have another challenger to the north ..."
        DISPLAY_OBJECTIVE(914,PLAYER0)
    ENDIF
ENDIF

REM ****************************胜利/失败条件**********************************
REM 所有心脏被打爆，到达触发点15，杀死骑士才胜利。
IF(PLAYER1,DUNGEON_DESTROYED == 1)
    IF(PLAYER0,FLAG0 == 1)
        IF(PLAYER_GOOD,DUNGEON_DESTROYED == 1)
            IF_CONTROLS(PLAYER_GOOD,KNIGHT <= 0)

                REM 以下修改游戏，增加胜利后显示全图、送钱开放建造限制，让你回味一下胜利的喜悦！
                LEVEL_VERSION(1)
                REVEAL_MAP_RECT(PLAYER0,127,127,254,254)
                ADD_GOLD_TO_PLAYER (PLAYER0,9999999)

                ROOM_AVAILABLE(PLAYER0,TREASURE,1,1)
                ROOM_AVAILABLE(PLAYER0,GARDEN,1,1)
                ROOM_AVAILABLE(PLAYER0,LAIR,1,1)
                ROOM_AVAILABLE(PLAYER0,RESEARCH,1,1)
                ROOM_AVAILABLE(PLAYER0,TRAINING,1,1)
                ROOM_AVAILABLE(PLAYER0,BRIDGE,1,1)
                ROOM_AVAILABLE(PLAYER0,GUARD_POST,1,1)
                ROOM_AVAILABLE(PLAYER0,WORKSHOP,1,1)
                ROOM_AVAILABLE(PLAYER0,BARRACKS,1,1)
                ROOM_AVAILABLE(PLAYER0,TEMPLE,1,1)
                ROOM_AVAILABLE(PLAYER0,PRISON,1,1)
                ROOM_AVAILABLE(PLAYER0,TORTURE,1,1)
                ROOM_AVAILABLE(PLAYER0,SCAVENGER,1,1)
                ROOM_AVAILABLE(PLAYER0,GRAVEYARD,1,1)
                
                MAGIC_AVAILABLE(PLAYER0,POWER_IMP,1,1)
                MAGIC_AVAILABLE(PLAYER0,POWER_CALL_TO_ARMS,1,1
                MAGIC_AVAILABLE(PLAYER0,POWER_SPEED,1,1)
                MAGIC_AVAILABLE(PLAYER0,POWER_CONCEAL,1,1)
                MAGIC_AVAILABLE(PLAYER0,POWER_HEAL_CREATURE,1,1)
                MAGIC_AVAILABLE(PLAYER0,POWER_DESTROY_WALLS,1,1)

                DOOR_AVAILABLE(PLAYER0,MAGIC,1,200)
                TRAP_AVAILABLE(PLAYER0,LIGHTNING,1,0)
                TRAP_AVAILABLE(PLAYER0,BOULDER,1,200)
                QUICK_INFORMATION(15,"亮图送钱，让你回味一下胜利的喜悦！",PLAYER0) 

                WIN_GAME
            ENDIF
        ENDIF
    ENDIF
ENDIF

REM ****************************提示信息**********************************
REM "Choices, choices Keeper. Choose your path wisely. Some routes are more perilous than others ..."
REM "选择，选择守护者。明智地选择你的路。有些路线比其他路线更危险…"
DISPLAY_OBJECTIVE(913,PLAYER0)

IF(PLAYER0,ENTRANCE > 0)
    IF(PLAYER0,TOTAL_GOLD_MINED > 0)
        REM "The Lord of the Land is a pathetic individual who breeds fluffy bunnies. Pound him into the ground and..."
        REM "那土地领主是一个可怜的人，孕育着毛茸茸的兔子。把他打在地上…"
        DISPLAY_OBJECTIVE(916,PLAYER0)
    ENDIF
ENDIF



























