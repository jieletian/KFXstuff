REM ***********************************************************************
REM   Script for Level  374(74).2nd Armageddon 世界末日
REM ***********************************************************************
REM 
REM 
REM ***********************************************************************
REM 地图的大概情况与参考建议：
REM 1、开始必须附身苍蝇去打门，引发圆石碾碎哨所，在苍蝇打门的时候，可以退出付设计一下。
REM 2、打开门后要引两个垂直的圆石相撞，留一相水平的圆石去碾碎哨所，才可以建桥。
REM 3、得到造桥术后马上建桥去抢末日审判魔法书，后面更不好跨越战区去抢。
REM 4、在附身苍蝇之前可以尝试开了电脑辅助，让电脑帮你做一些建设，不过有一定风险。
REM 5、没钱的时候，要建工厂，建议多考虑招精灵。
REM 6、不要让地型破坏得差厉害，后面人类会由挖掘者过来。
REM ***********************************************************************
SET_GENERATE_SPEED(400)
COMPUTER_PLAYER(PLAYER1,0)
COMPUTER_PLAYER(PLAYER2,0)
COMPUTER_PLAYER(PLAYER3,0)

START_MONEY(PLAYER0,10000)
START_MONEY(PLAYER1,10000)
START_MONEY(PLAYER2,10000)
START_MONEY(PLAYER3,10000)
MAX_CREATURES(ALL_PLAYERS,20)

REM *******************************添加怪物到入口 *********************************
REM   (添加到入口的生物名字,数量)
REM 增加弓箭手、骑士、巨人、忍者、精灵、龙、恶鬼、苍蝇、黑暗女王、魔术师、巨魔、蟑螂、蜘蛛、地狱犬、兽人各100到入口。
ADD_CREATURE_TO_POOL(ARCHER,100)
ADD_CREATURE_TO_POOL(KNIGHT,100)
ADD_CREATURE_TO_POOL(GIANT,100)
ADD_CREATURE_TO_POOL(SAMURAI,100)
ADD_CREATURE_TO_POOL(TROLL,100)
ADD_CREATURE_TO_POOL(DRAGON,100)
ADD_CREATURE_TO_POOL(DEMONSPAWN,100)
ADD_CREATURE_TO_POOL(FLY,100)
ADD_CREATURE_TO_POOL(DARK_MISTRESS,100)
ADD_CREATURE_TO_POOL(SORCEROR,100)
ADD_CREATURE_TO_POOL(BILE_DEMON,100)
ADD_CREATURE_TO_POOL(BUG,100)
ADD_CREATURE_TO_POOL(SPIDER,100)
ADD_CREATURE_TO_POOL(HELL_HOUND,100)
ADD_CREATURE_TO_POOL(ORC,100)

REM *******************************指定阵营（玩家）允许/禁止从入口（怪物池）中招指定生物*********************************
REM  (玩家,生物名称,1（只能是1）,[1允许招募/0禁止招募])

CREATURE_AVAILABLE(ALL_PLAYERS,ARCHER,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,KNIGHT,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,GIANT,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,SAMURAI,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,TROLL,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,DRAGON,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,DEMONSPAWN,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,FLY,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,DARK_MISTRESS,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,SORCEROR,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,BILE_DEMON,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,BUG,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,SPIDER,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,HELL_HOUND,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,ORC,1,1)

REM ********************指定阵营（玩家）允许/禁止研究和建造指定房子（建筑）**********************************
REM  (玩家,房子名称,是否可研究或建造[1是允许；0是禁止],是否已经可建造[1已经可建/0待研究])
ROOM_AVAILABLE(ALL_PLAYERS,TREASURE,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,GARDEN,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,LAIR,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,RESEARCH,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,TRAINING,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,PRISON,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,TORTURE,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,WORKSHOP,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,SCAVENGER,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,TEMPLE,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,GRAVEYARD,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,BARRACKS,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,GUARD_POST,1,0)
REM ROOM_AVAILABLE(ALL_PLAYERS, BRIDGE, 1, 0)
ROOM_AVAILABLE(PLAYER1,BRIDGE,1,1)
ROOM_AVAILABLE(PLAYER2,BRIDGE,1,1)
ROOM_AVAILABLE(PLAYER3,BRIDGE,1,1)

REM ********************指定阵营（玩家）允许/禁止研究和使用指定魔法**********************************
REM  (玩家,魔法名称,是否可研究或使用[1是允许；0是禁止],是否已经可使用[1已经可建/0待研究])
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_IMP,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_OBEY,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CALL_TO_ARMS,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CAVE_IN,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HEAL_CREATURE,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HOLD_AUDIENCE,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_LIGHTNING,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SPEED,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_PROTECT,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CONCEAL,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_DISEASE,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CHICKEN,1,0)
REM MAGIC_AVAILABLE(ALL_PLAYERS, POWER_DESTROY_WALLS, 1, 0)
REM MAGIC_AVAILABLE(ALL_PLAYERS, POWER_ARMAGEDDON, 1, 0)

REM *********************指定阵营（玩家）允许/禁止制造指定的门*********************************
REM  (玩家,门的名称,[1是允许；0是禁止],1[0是允许；数字是已经制造出来/赠送的数量]
DOOR_AVAILABLE(ALL_PLAYERS,WOOD,1,0)
REM DOOR_AVAILABLE(ALL_PLAYERS, BRACED, 1, 0)
DOOR_AVAILABLE(ALL_PLAYERS,STEEL,1,0)
DOOR_AVAILABLE(ALL_PLAYERS,MAGIC,1,0)

REM *********************指定阵营（玩家）允许/禁止制造指定陷阱*********************************
REM  (玩家,陷阱名称,[1是允许；0是禁止],1[0是允许；数字是已经制造出来/赠送的数量]
TRAP_AVAILABLE(ALL_PLAYERS,BOULDER,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,ALARM,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,POISON_GAS,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,LIGHTNING,1,0)
REM TRAP_AVAILABLE(ALL_PLAYERS, WORD_OF_POWER, 1, 0)
TRAP_AVAILABLE(ALL_PLAYERS,LAVA,1,0)

REM ****************************增援部队**********************************
REM CREATE_PARTY =>组建队伍                (队伍名称)   
REM ADD_TO_PARTY=>添加生物到队伍中   (队伍名称,生物名称,生物等级,携带金钱数量,任务,延时)
REM 英雄
CREATE_PARTY(HEROES)
    ADD_TO_PARTY(HEROES,KNIGHT,5,2000,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(HEROES,ARCHER,7,1000,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(HEROES,THIEF,5,2000,STEAL_GOLD,0)
REM 增加给蓝色的部队
CREATE_PARTY(BLUE)
    ADD_TO_PARTY(BLUE,GIANT,7,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(BLUE,SAMURAI,6,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(BLUE,ORC,7,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(BLUE,DEMONSPAWN,7,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(BLUE,WIZARD,7,0,ATTACK_DUNGEON_HEART,0)
CREATE_PARTY(BLUE2)
    ADD_TO_PARTY(BLUE2,GIANT,6,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(BLUE2,SAMURAI,6,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(BLUE2,THIEF,5,0,ATTACK_DUNGEON_HEART,0)
REM 增加给绿色的部队
CREATE_PARTY(GREEN)
    ADD_TO_PARTY(GREEN,BILE_DEMON,7,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(GREEN,DRAGON,7,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(GREEN,TENTACLE,7,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(GREEN,GIANT,7,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(GREEN,SORCEROR,7,0,ATTACK_DUNGEON_HEART,0)
CREATE_PARTY(GREEN2)
    ADD_TO_PARTY(GREEN2,TENTACLE,7,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(GREEN2,HELL_HOUND,7,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(GREEN2,SPIDER,5,0,ATTACK_DUNGEON_HEART,0)
REM 增加给黄色的部队
CREATE_PARTY(YELLOW)
    ADD_TO_PARTY(YELLOW,ORC,7,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(YELLOW,DARK_MISTRESS,7,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(YELLOW,ARCHER,7,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(YELLOW,FAIRY,7,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(YELLOW,MONK,7,0,ATTACK_DUNGEON_HEART,0)
CREATE_PARTY(YELLOW2)
    ADD_TO_PARTY(YELLOW2,DARK_MISTRESS,6,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(YELLOW2,ARCHER,7,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(YELLOW2,TROLL,5,0,ATTACK_DUNGEON_HEART,0)

REM ****************************脚本主体(主要内容、核心内容）**********************************
SET_TIMER(PLAYER0,TIMER0)
SET_TIMER(PLAYER1,TIMER0)
SET_TIMER(PLAYER2,TIMER0)
SET_TIMER(PLAYER3,TIMER0)

SET_FLAG(PLAYER0,FLAG0,0)
SET_FLAG(PLAYER0,FLAG1,0)
SET_FLAG(PLAYER0,FLAG2,0)

REM ***********************************************************************
REM 东南角，末日送审魔法处，触发点1、哨所被碾粹后就可以建桥。
IF_ACTION_POINT(1,PLAYER0)
    SET_FLAG(PLAYER0,FLAG2,1)
ENDIF
IF(PLAYER0,FLAG2 == 1)
    IF(PLAYER_GOOD,GUARD_POST == 0)
        ROOM_AVAILABLE(PLAYER0,BRIDGE,1,1)
    ENDIF
ENDIF

IF(PLAYER0,TIMER0 >= 17280)
    ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,HEROES,-1,DUNGEON_HEART,0,5,0)
    ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,HEROES,-2,DUNGEON_HEART,0,5,0)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,HEROES,-3,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,HEROES,-4,1)
ENDIF

IF(PLAYER1,DUNGEON_DESTROYED == 1)
    SET_FLAG(PLAYER0,FLAG1,1)
ENDIF
IF(PLAYER2,DUNGEON_DESTROYED == 1)
    SET_FLAG(PLAYER0,FLAG1,1)
ENDIF
IF(PLAYER3,DUNGEON_DESTROYED == 1)
    SET_FLAG(PLAYER0,FLAG1,1)
ENDIF

REM BLUE 时钟大于7680后，蓝色心脏未爆前
IF(PLAYER1,TIMER0 >= 7680)
    IF(PLAYER1,DUNGEON_DESTROYED != 1)
        IF(PLAYER1,TOTAL_CREATURES < 25)
　　　　REM 未有其它守护者心脏被打爆
            IF(PLAYER0,FLAG1 == 0)
                NEXT_COMMAND_REUSABLE
                ADD_PARTY_TO_LEVEL(PLAYER1,BLUE,-6,1)
                NEXT_COMMAND_REUSABLE
                SET_TIMER(PLAYER1,TIMER0)
            ENDIF
　　　　　　 REM 已有其它守护者心脏被打爆
            IF(PLAYER0,FLAG1 == 1)
　　　　　REM 未得到末日审判魔法
                IF_AVAILABLE(PLAYER0,POWER_ARMAGEDDON == 0)
                    NEXT_COMMAND_REUSABLE
                    ADD_PARTY_TO_LEVEL(PLAYER1,BLUE,-6,1)
                    NEXT_COMMAND_REUSABLE
                    ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER1,BLUE2,-2,DUNGEON_HEART,4,5,0)
                    NEXT_COMMAND_REUSABLE
                    SET_TIMER(PLAYER1,TIMER0)
                ENDIF
　　　　　REM 已得到末日审判魔法
                IF_AVAILABLE(PLAYER0,POWER_ARMAGEDDON == 1)
                    NEXT_COMMAND_REUSABLE
                    ADD_PARTY_TO_LEVEL(PLAYER1,BLUE,-6,1)
                    NEXT_COMMAND_REUSABLE
                    ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER1,BLUE2,-2,DUNGEON_HEART,0,5,0)
                    NEXT_COMMAND_REUSABLE
                    SET_TIMER(PLAYER1,TIMER0)
                ENDIF
            ENDIF
        ENDIF
    ENDIF
ENDIF

REM GREEN  时钟大于7680后，绿色心脏未爆前
IF(PLAYER2,TIMER0 >= 7680)
    IF(PLAYER2,DUNGEON_DESTROYED != 1)
        IF(PLAYER2,TOTAL_CREATURES < 25)
　　　　REM 未有其它守护者心脏被打爆
            IF(PLAYER0,FLAG1 == 0)
                NEXT_COMMAND_REUSABLE
                ADD_PARTY_TO_LEVEL(PLAYER2,GREEN,-8,1)
                NEXT_COMMAND_REUSABLE
                SET_TIMER(PLAYER2,TIMER0)
            ENDIF
　　　　REM 已有其它守护者心脏被打爆
            IF(PLAYER0,FLAG1 == 1)
　　　　　　     REM 未得到末日审判魔法
                IF_AVAILABLE(PLAYER0,POWER_ARMAGEDDON == 0)
                    NEXT_COMMAND_REUSABLE
                    ADD_PARTY_TO_LEVEL(PLAYER2,GREEN,-8,1)
                    NEXT_COMMAND_REUSABLE
                    ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER2,GREEN2,-1,DUNGEON_HEART,4,5,0)
                    NEXT_COMMAND_REUSABLE
                    SET_TIMER(PLAYER2,TIMER0)
                ENDIF
　　　　　REM 已得到末日审判魔法
                IF_AVAILABLE(PLAYER0,POWER_ARMAGEDDON == 1)
                    NEXT_COMMAND_REUSABLE
                    ADD_PARTY_TO_LEVEL(PLAYER2,GREEN,-8,1)
                    NEXT_COMMAND_REUSABLE
                    ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER2,GREEN2,-1,DUNGEON_HEART,0,5,0)
                    NEXT_COMMAND_REUSABLE
                    SET_TIMER(PLAYER2,TIMER0)
                ENDIF
            ENDIF
        ENDIF
    ENDIF
ENDIF

REM YELLOW 时钟大于7680后，黄色心脏未爆前
IF(PLAYER3,TIMER0 >= 7680)
    IF(PLAYER3,DUNGEON_DESTROYED != 1)
        IF(PLAYER3,TOTAL_CREATURES < 25)
　　　  REM 未有其它守护者心脏被打爆
            IF(PLAYER0,FLAG1 == 0)
                NEXT_COMMAND_REUSABLE
                ADD_PARTY_TO_LEVEL(PLAYER3,YELLOW,-7,1)
                NEXT_COMMAND_REUSABLE
                SET_TIMER(PLAYER3,TIMER0)
            ENDIF
　　　　　　 REM 已有其它守护者心脏被打爆
            IF(PLAYER0,FLAG1 == 1)
　　　　　REM 未得到末日审判魔法
                IF_AVAILABLE(PLAYER0,POWER_ARMAGEDDON == 0)
                    NEXT_COMMAND_REUSABLE
                    ADD_PARTY_TO_LEVEL(PLAYER3,YELLOW,-7,1)
                    NEXT_COMMAND_REUSABLE
                    ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER3,YELLOW2,-4,DUNGEON_HEART,4,5,0)
                    NEXT_COMMAND_REUSABLE
                    SET_TIMER(PLAYER3,TIMER0)
                ENDIF
　　　　　REM 已得到末日审判魔法
                IF_AVAILABLE(PLAYER0,POWER_ARMAGEDDON == 1)
                    NEXT_COMMAND_REUSABLE
                    ADD_PARTY_TO_LEVEL(PLAYER3,YELLOW,-7,1)
                    NEXT_COMMAND_REUSABLE
                    ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER3,YELLOW2,-4,DUNGEON_HEART,0,5,0)
                    NEXT_COMMAND_REUSABLE
                    SET_TIMER(PLAYER3,TIMER0)
                ENDIF
            ENDIF
        ENDIF
    ENDIF
ENDIF

REM ****************************胜利/失败条件**********************************
IF(PLAYER_GOOD,DUNGEON_DESTROYED == 1)
    IF(PLAYER0,ALL_DUNGEONS_DESTROYED == 1)

        REM 以下修改游戏，增加胜利后可以建造其它房子，让你回味一下胜利的喜悦！
        LEVEL_VERSION(1)
        REVEAL_MAP_RECT(PLAYER0,127,127,254,254)
        ADD_GOLD_TO_PLAYER (PLAYER0,9999999)

        ROOM_AVAILABLE(PLAYER0,TREASURE,1,1)
        ROOM_AVAILABLE(PLAYER0,GARDEN,1,1)
        ROOM_AVAILABLE(PLAYER0,LAIR,1,1)
        ROOM_AVAILABLE(PLAYER0,RESEARCH,1,1)
        ROOM_AVAILABLE(PLAYER0,TRAINING,1,1)
        ROOM_AVAILABLE(PLAYER0,BRIDGE,1,1)
        ROOM_AVAILABLE(PLAYER0,GUARD_POST,1,1)
        ROOM_AVAILABLE(PLAYER0,WORKSHOP,1,1)
        ROOM_AVAILABLE(PLAYER0,BARRACKS,1,1)
        ROOM_AVAILABLE(PLAYER0,TEMPLE,1,1)
        ROOM_AVAILABLE(PLAYER0,PRISON,1,1)
        ROOM_AVAILABLE(PLAYER0,TORTURE,1,1)
        ROOM_AVAILABLE(PLAYER0,SCAVENGER,1,1)
        ROOM_AVAILABLE(PLAYER0,GRAVEYARD,1,1)
        
        MAGIC_AVAILABLE(PLAYER0,POWER_IMP,1,1)
        MAGIC_AVAILABLE(PLAYER0,POWER_CALL_TO_ARMS,1,1
        MAGIC_AVAILABLE(PLAYER0,POWER_SPEED,1,1)
        MAGIC_AVAILABLE(PLAYER0,POWER_HEAL_CREATURE,1,1)
        MAGIC_AVAILABLE(PLAYER0,POWER_DESTROY_WALLS,1,1)

        DOOR_AVAILABLE(PLAYER0,MAGIC,1,200)
        TRAP_AVAILABLE(PLAYER0,LIGHTNING,1,0)
        TRAP_AVAILABLE(PLAYER0,BOULDER,1,200)
        QUICK_INFORMATION(15,"送钱亮图，让你回味一下胜利的喜悦！",PLAYER0)

        WIN_GAME
    ENDIF
    REM 人类心脏已被打爆而你还没得到末日审判魔法。
    IF_AVAILABLE(PLAYER0,POWER_ARMAGEDDON == 0)
        LOSE_GAME
    ENDIF
ENDIF

REM ****************************提示信息**********************************
QUICK_OBJECTIVE(1,"参考提示：1、人类心脏被打爆之前要得到末日审判魔法，否则你就完蛋了；2、其它守护者守里都有紫金矿，你很快就要面对这个问题了；3、敌人的增援部队源源不断，除非你打爆它们的心脏。",PLAYER0)  
IF(PLAYER0,GAME_TURN > 500)
    QUICK_INFORMATION(2,"战祸连连，上天要惩罚那些作恶多端的家伙，所以你必须及早抢到末日审判的魔法书，审判那些邪恶的家伙们，否则很快你就失败了！",PLAYER0) 
ENDIF
IF(PLAYER0,GAME_TURN > 1500)
    QUICK_INFORMATION(3,"那些万恶的家伙都有紫金矿，而你什么都没有，你要胜利就得解决这个经济问题！",PLAYER0) 
ENDIF
IF(PLAYER0,GAME_TURN > 2500)
    QUICK_INFORMATION(4,"敌人的增援部队源源不断，打爆他们的心脏，增援部队才停止。",PLAYER0) 
ENDIF
IF(PLAYER0,GAME_TURN > 3500)
    QUICK_INFORMATION(5,"利用好岩浆陷阱，它可有效阻往敌人的进攻，另外，那些家伙都有招募室，小心他们招募你们兵！",PLAYER0) 
ENDIF
IF(PLAYER0,GAME_TURN > 4000)
    QUICK_INFORMATION(6,"这回，粉碎战祸、审判罪恶的任务就靠你了，祝你好运，守护者！",PLAYER0) 
ENDIF














