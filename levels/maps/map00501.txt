REM ***********************************************************************
REM   Script for Level 501.BETA Level 01
REM ***********************************************************************
REM 
REM 
REM ***********************************************************************
REM 地图的大概情况与参考建议：
REM 1、 。
REM 2、 。
REM 3、 。
REM 4、 。
REM 5、 。
REM ***********************************************************************
REM ***************************各种基本设置 *****************************
SET_GENERATE_SPEED(500)
MAX_CREATURES(PLAYER0,10)
START_MONEY(PLAYER0,1000)

REM *******************************添加怪物到入口 *********************************
REM   (添加到入口的生物名字,数量)
ADD_CREATURE_TO_POOL(BUG,20)

REM *******************************指定阵营（玩家）允许/禁止从入口（怪物池）中招指定生物*********************************
REM  (玩家,生物名称,1（只能是1）,[1允许招募/0禁止招募])
CREATURE_AVAILABLE(PLAYER0,BUG,1,1)

REM ********************指定阵营（玩家）允许/禁止研究和建造指定房子（建筑）**********************************
REM  (玩家,房子名称,是否可研究或建造[1是允许；0是禁止],是否已经可建造[1已经可建/0待研究])**********
ROOM_AVAILABLE(PLAYER0,TREASURE,1,1)

REM ********************指定阵营（玩家）允许/禁止研究和使用指定魔法**********************************
REM  (玩家,魔法名称,是否可研究或使用[1是允许；0是禁止],是否已经可使用[1已经可建/0待研究])
MAGIC_AVAILABLE(PLAYER0,POWER_IMP,1,1)
MAGIC_AVAILABLE(PLAYER0,POWER_OBEY,1,1)

REM 7增援部队添加
REM ****************************增援部队**********************************
REM CREATE_PARTY =>组建队伍                (队伍名称)   
REM ADD_TO_PARTY=>添加生物到队伍中   (队伍名称,生物名称,生物等级,携带金钱数量,任务,延时)
CREATE_PARTY(GROUP1)
    ADD_TO_PARTY(GROUP1,THIEF,1,100,ATTACK_DUNGEON_HEART,0)
CREATE_PARTY(GROUP2)
    ADD_TO_PARTY(GROUP2,THIEF,1,100,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(GROUP2,THIEF,1,100,ATTACK_DUNGEON_HEART,0)
CREATE_PARTY(GROUP3)
    ADD_TO_PARTY(GROUP3,KNIGHT,1,500,ATTACK_DUNGEON_HEART,0)



REM ****************************脚本主体(主要内容、核心内容）**********************************

SET_CREATURE_STRENGTH(KNIGHT,30)
SET_CREATURE_ARMOUR(KNIGHT,30)
SET_CREATURE_STRENGTH(TUNNELLER,20)
SET_CREATURE_ARMOUR(TUNNELLER,20)
SET_CREATURE_FEAR(TUNNELLER,0)
SET_CREATURE_FEAR(THIEF,0)
SET_CREATURE_FEAR(KNIGHT,0)

IF(PLAYER0,TREASURE >= 1)
    ROOM_AVAILABLE(PLAYER0,LAIR,1,1)
    ROOM_AVAILABLE(PLAYER0,GARDEN,1,1)
ENDIF

REM "建造一个金库并装入2000金子."
QUICK_OBJECTIVE(0,"Build a Treasure Room and fill it with 2000 gold.",PLAYER0)
IF(PLAYER0,MONEY >= 2000)
    REM "well done. now build a lair and hatchery and attract 5 creatures into your dungeon."
    QUICK_OBJECTIVE(1,"做得好！现在，建造一个巢穴和孵化场，吸引5个生物进入你的地下城.",PLAYER0)
ENDIF

IF(PLAYER0,TOTAL_CREATURES >= 5)
    REM "excellent.  now continue to mine the area for gold,but beware,your success will have attracted the unwanted attentions of local heros. defend youre dungeon from these do-gooders."
    QUICK_OBJECTIVE(2,"好极了！现在继续开采该地区的黄金，但要小心，你的成功会引起当地英雄们的不必要关注。保护你的地下城不受这些坏人的伤害.",PLAYER0)
    SET_TIMER(PLAYER0,TIMER0)
ENDIF
IF(PLAYER0,TIMER0 >= 250)
    ADD_TUNNELLER_TO_LEVEL(PLAYER_GOOD,1,DUNGEON_HEART,0,1,250)
    SET_FLAG(PLAYER0,FLAG0,1)
ENDIF

IF(PLAYER_GOOD,TOTAL_CREATURES == 0)
    IF(PLAYER0,FLAG0 == 1)
        ADD_TUNNELLER_TO_LEVEL(PLAYER_GOOD,2,DUNGEON_HEART,0,1,250)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,GROUP1,2,1)
        SET_FLAG(PLAYER0,FLAG0,2)
    ENDIF
ENDIF

IF(PLAYER_GOOD,TOTAL_CREATURES == 0)
    IF(PLAYER0,FLAG0 == 2)
        ADD_TUNNELLER_TO_LEVEL(PLAYER_GOOD,3,DUNGEON_HEART,0,1,250)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,GROUP2,3,1)
        SET_FLAG(PLAYER0,FLAG0,3)
    ENDIF
ENDIF

IF(PLAYER_GOOD,TOTAL_CREATURES == 0)
    IF(PLAYER0,FLAG0 == 3)
        REM "your continued success has attracted the attentions of the local lord. he must be defeated so you can claim this realm as your own."
        QUICK_OBJECTIVE(3,"你的持续成功引起了当地领主的注意，他一定会被打败，这样你就可以宣称这个王国是你自己的。",PLAYER0)
        SET_FLAG(PLAYER0,FLAG0,4)
    ENDIF
ENDIF
IF(PLAYER_GOOD,TOTAL_CREATURES == 0)
    IF(PLAYER0,FLAG0 == 4)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,GROUP3,4,1)
        SET_FLAG(PLAYER0,FLAG0,5)
    ENDIF
ENDIF

REM ****************************胜利/失败条件**********************************
IF(PLAYER_GOOD,KNIGHT == 0)
    IF(PLAYER0,FLAG0 == 5)
        REM "great work. by defeating the lord you have claimed this realm as your own."
        QUICK_OBJECTIVE(4,"做得好！你打败了主，就把这个王国称为你自己的。",PLAYER0)

        REM 以下修改游戏，增加胜利后可以建造其它房子，让你回味一下胜利的喜悦！
        LEVEL_VERSION(1)
        REVEAL_MAP_RECT(PLAYER0,127,127,254,254)
        ADD_GOLD_TO_PLAYER (PLAYER0,9999999)

        ROOM_AVAILABLE(PLAYER0,TREASURE,1,1)
        ROOM_AVAILABLE(PLAYER0,GARDEN,1,1)
        ROOM_AVAILABLE(PLAYER0,LAIR,1,1)
        ROOM_AVAILABLE(PLAYER0,RESEARCH,1,1)
        ROOM_AVAILABLE(PLAYER0,TRAINING,1,1)
        ROOM_AVAILABLE(PLAYER0,BRIDGE,1,1)
        ROOM_AVAILABLE(PLAYER0,GUARD_POST,1,1)
        ROOM_AVAILABLE(PLAYER0,WORKSHOP,1,1)
        ROOM_AVAILABLE(PLAYER0,BARRACKS,1,1)
        ROOM_AVAILABLE(PLAYER0,TEMPLE,1,1)
        ROOM_AVAILABLE(PLAYER0,PRISON,1,1)
        ROOM_AVAILABLE(PLAYER0,TORTURE,1,1)
        ROOM_AVAILABLE(PLAYER0,SCAVENGER,1,1)
        ROOM_AVAILABLE(PLAYER0,GRAVEYARD,1,1)
                
        MAGIC_AVAILABLE(PLAYER0,POWER_IMP,1,1)
        MAGIC_AVAILABLE(PLAYER0,POWER_CALL_TO_ARMS,1,1
        MAGIC_AVAILABLE(PLAYER0,POWER_SPEED,1,1)
        MAGIC_AVAILABLE(PLAYER0,POWER_HEAL_CREATURE,1,1)
        MAGIC_AVAILABLE(PLAYER0,POWER_DESTROY_WALLS,1,1)

        DOOR_AVAILABLE(PLAYER0,MAGIC,1,200)
        TRAP_AVAILABLE(PLAYER0,LIGHTNING,1,0)
        TRAP_AVAILABLE(PLAYER0,BOULDER,1,200)
        QUICK_INFORMATION(15,"送钱亮图，让你回味一下胜利的喜悦！",PLAYER0)

        WIN_GAME
    ENDIF
ENDIF
IF(PLAYER0,DUNGEON_DESTROYED == 1)
    REM "you have been defeated.better luck next time"
    QUICK_OBJECTIVE(5,"你被打败了…下次祝你好运！.",PLAYER0)
    LOSE_GAME
ENDIF
REM ****************************提示信息**********************************

















