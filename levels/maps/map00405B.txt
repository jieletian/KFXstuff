REM ***********************************************************************
REM   Script for Level 405.The Realm of Salaar 萨拉尔王国
REM ***********************************************************************
REM ***********************************************************************
REM   修改版 B版
REM   使用方法：把此文件名由map00405B.txt改为map00405.txt即可。
REM ***********************************************************************
REM 地图的大概情况与参考建议：
REM 1、此图和远古守护者99%相似,估计是远古守护者利用此图修改而成.主要区别在于此图白方心脏第一樘门 。
REM      是没有锁上的,也就是说只要你一打开兵营后那樘门镰刀就有镰刀和仙女冲过来,增加了一些难度，幸好招募中心是可以招怪物池中的怪。
REM 2、地图设计者想告诉新人没有训练室，可以让你的怪与敌兵PK与能升级（甚至可以附身怪砍自己的其它怪也能升级）；敌兵最好是血厚抗打的怪。
REM 3、此图桥建造限制规定：消灭1骑士可造1格桥，消灭3骑士可造3格桥，消灭所有6个骑士可任你造桥。
REM 4、 此图可以利用招募室把怪物从怪物池中招募出来，原版但不能建造房子，人口多了就没地方住，这样怪物受伤后不能恢复，易怒。
REM 5、此修改版增加了一些特定条件可以建造房子的语句，只要你招降、招募敌兵达到一定数量就可以一些房子 。
REM 6、前面3个骑士不得不杀，后面3个骑士最好俘虏招降过来，其它白方敌兵最好能引到外面，后面可以招募过来，不好用的兵俘虏过来饿成俘虏挡子弹 。
REM 7、后面几个镰刀可以想办法招募过来 ，让它们去招募室、寺庙或兵营，否则它们会造反。
REM 8、圣者可以升级龙，但它的杀伤力大，不适合升级其它怪，其它怪可以利用白方的骑士来升级，在FX0.46下的405图，骑士经常使用强力咒语，在此
REM       环境下也不能用它来升级小鬼，要升级小鬼只能附身小鬼砍自己的龙和招降过来的骑士等血厚怪。
REM ***********************************************************************
REM ***************************各种基本设置 *****************************
START_MONEY(PLAYER0,10000)
SET_FLAG(PLAYER0,FLAG0,0)

REM *******************************添加怪物到入口 *********************************
REM   (添加到入口的生物名字,数量)
ADD_CREATURE_TO_POOL(DRAGON,8)

REM *******************************指定阵营（玩家）允许/禁止从入口（怪物池）中招指定生物*********************************
REM  (玩家,生物名称,1（只能是1）,[1允许招募/0禁止招募])
CREATURE_AVAILABLE(PLAYER0,DRAGON,1,1)

REM ********************指定阵营（玩家）允许/禁止研究和使用指定魔法**********************************
REM  (玩家,魔法名称,是否可研究或使用[1是允许；0是禁止],是否已经可使用[1已经可建/0待研究])
MAGIC_AVAILABLE(PLAYER0,POWER_IMP,1,1)

REM *********************指定阵营（玩家）允许/禁止制造指定的门*********************************
REM  (玩家,门的名称,[1是允许；0是禁止],1[0是允许；数字是已经制造出来/赠送的数量]
DOOR_AVAILABLE(PLAYER0,MAGIC,1,0)

REM ****************************脚本主体(主要内容、核心内容）**********************************
REM DISPLAY_OBJECTIVE(179,PLAYER0)
REM QUICK_OBJECTIVE(1,"Behold, the abandoned realm of Salaar, one of the Great Ancient Keepers. Paranoid heroes guard this realm with care!",PLAYER0)
QUICK_INFORMATION(8,"瞧，这个被遗弃的萨拉王国，伟大的古代守护者之一，顽固疯狂的英雄们小心地守卫这王国！",PLAYER0)

REM *************触发点事件***********************
IF_ACTION_POINT(1,PLAYER0)
    REM DISPLAY_OBJECTIVE(180,PLAYER0)
    REM QUICK_OBJECTIVE(2,"Salaar was truly Evil! Discover and gaze in awe at His training room! Are you skillful enough to use it?",PLAYER0)
    QUICK_INFORMATION(9,"萨拉尔真的很邪恶！发现并敬畏地注视着他的训练室！你有足够的技巧使用它吗？",PLAYER0)
ENDIF
IF_ACTION_POINT(2,PLAYER0)
    REM DISPLAY_OBJECTIVE(181,PLAYER0)
    REM QUICK_OBJECTIVE(3,"Conquer the hero castles in this realm, and you will be a worthy successor to Salaar. Be bold for He is watching from below!",PLAYER0)
    QUICK_INFORMATION(10,"征服这个王国的英雄城堡，你将成为萨拉尔值得继承的人。大胆点，因为他从后面看着！",PLAYER0)
ENDIF

REM 地图有6个骑士，消灭了1个骑士就能建造1格桥 
IF(PLAYER_GOOD,KNIGHT == 5)
    SET_FLAG(PLAYER0,FLAG0,1)
    REM DISPLAY_OBJECTIVE(182,PLAYER0)
    REM QUICK_OBJECTIVE(4,"Salaar had a limit to the bridges he could own at one time. He sees no reason why you should have more!",PLAYER0)
    QUICK_OBJECTIVE(11,"萨拉尔对你有一次能拥有的桥梁有限制,除非你更多的能量（你已经消灭了一个英雄骑士，现在可造一格桥，想要更多的桥就得消灭更多的骑士）。!",PLAYER0)

    REM 修改增加，建筑面板和桥闪烁，提示有更新
    TUTORIAL_FLASH_BUTTON(2,-1)
    TUTORIAL_FLASH_BUTTON(18,-1)
ENDIF
IF(PLAYER0,FLAG0 == 1)
    IF(PLAYER0,BRIDGE < 1)
        NEXT_COMMAND_REUSABLE
        ROOM_AVAILABLE(PLAYER0,BRIDGE,1,1)
    ENDIF
ENDIF
IF(PLAYER0,FLAG0 == 1)
    IF(PLAYER0,BRIDGE > 0)
        NEXT_COMMAND_REUSABLE
        ROOM_AVAILABLE(PLAYER0,BRIDGE,0,0)
    ENDIF
ENDIF

REM 地图有6个骑士，消灭了3个骑士就能建造2格桥 
IF(PLAYER_GOOD,KNIGHT == 3)
    SET_FLAG(PLAYER0,FLAG0,2)
    QUICK_INFORMATION(12,"你已经消灭了三个英雄骑士，萨拉尔看你时面色不再难看,对你造桥的限制也放宽了，想要更多的桥就得消灭更多的骑士！",PLAYER0) 

    REM 修改增加，建筑面板和桥闪烁，提示有更新
    TUTORIAL_FLASH_BUTTON(2,-1)
    TUTORIAL_FLASH_BUTTON(18,-1)
ENDIF
IF(PLAYER0,FLAG0 == 2)
    IF(PLAYER0,BRIDGE < 2)
        NEXT_COMMAND_REUSABLE
        ROOM_AVAILABLE(PLAYER0,BRIDGE,1,1)
    ENDIF
ENDIF
IF(PLAYER0,FLAG0 == 2)
    IF(PLAYER0,BRIDGE > 1)
        NEXT_COMMAND_REUSABLE
        ROOM_AVAILABLE(PLAYER0,BRIDGE,0,0)
    ENDIF
ENDIF
REM 地图有6个骑士，骑士全部消灭就能任意建造桥 
IF(PLAYER_GOOD,KNIGHT == 0)
    SET_FLAG(PLAYER0,FLAG0,3)
    ROOM_AVAILABLE(PLAYER0,BRIDGE,1,1)
    QUICK_INFORMATION(13,"你已经消灭了所有英雄骑士，萨拉尔对你有点小欣赏,不再限制你造桥了.",PLAYER0) 

    REM 修改增加，建筑面板和桥闪烁，提示有更新
    TUTORIAL_FLASH_BUTTON(2,-1)
    TUTORIAL_FLASH_BUTTON(18,-1)
ENDIF

REM ****************************胜利/失败条件**********************************
IF(PLAYER_GOOD,TOTAL_CREATURES == 0)
    
    REM 以下修改游戏，增加胜利后可以建造其它房子，让你回味一下胜利的喜悦！
    LEVEL_VERSION(1)
    REVEAL_MAP_RECT(PLAYER0,127,127,254,254)
    ADD_GOLD_TO_PLAYER (PLAYER0,9999999)

    ROOM_AVAILABLE(PLAYER0,TREASURE,1,1)
    ROOM_AVAILABLE(PLAYER0,GARDEN,1,1)
    ROOM_AVAILABLE(PLAYER0,LAIR,1,1)
    ROOM_AVAILABLE(PLAYER0,RESEARCH,1,1)
    ROOM_AVAILABLE(PLAYER0,TRAINING,1,1)
    ROOM_AVAILABLE(PLAYER0,BRIDGE,1,1)
    ROOM_AVAILABLE(PLAYER0,GUARD_POST,1,1)
    ROOM_AVAILABLE(PLAYER0,WORKSHOP,1,1)
    ROOM_AVAILABLE(PLAYER0,BARRACKS,1,1)
    ROOM_AVAILABLE(PLAYER0,TEMPLE,1,1)
    ROOM_AVAILABLE(PLAYER0,PRISON,1,1)
    ROOM_AVAILABLE(PLAYER0,TORTURE,1,1)
    ROOM_AVAILABLE(PLAYER0,SCAVENGER,1,1)
    ROOM_AVAILABLE(PLAYER0,GRAVEYARD,1,1)
    
    MAGIC_AVAILABLE(PLAYER0,POWER_IMP,1,1)
    MAGIC_AVAILABLE(PLAYER0,POWER_CALL_TO_ARMS,1,1
    MAGIC_AVAILABLE(PLAYER0,POWER_SPEED,1,1)
    MAGIC_AVAILABLE(PLAYER0,POWER_HEAL_CREATURE,1,1)
    MAGIC_AVAILABLE(PLAYER0,POWER_DESTROY_WALLS,1,1)

    DOOR_AVAILABLE(PLAYER0,MAGIC,1,200)
    TRAP_AVAILABLE(PLAYER0,LIGHTNING,1,0)
    TRAP_AVAILABLE(PLAYER0,BOULDER,1,200)
    QUICK_INFORMATION(15,"送钱亮图，让你回味一下胜利的喜悦！",PLAYER0)

    WIN_GAME
ENDIF
IF(PLAYER0,DUNGEON_DESTROYED == 1)
    LOSE_GAME
ENDIF
REM 得到龙后如果龙死光了就失败（远古第五关里规定生物数量大于0然后小于0就失败）
IF(PLAYER0,DRAGON == 2)
    SET_FLAG(PLAYER0,FLAG1,1)
ENDIF
IF(PLAYER0,FLAG1 == 1)
    IF(PLAYER0,TOTAL_CREATURES < 1)
        LOSE_GAME
        REM DISPLAY_OBJECTIVE(183,PLAYER0)
        REM QUICK_OBJECTIVE(5,"That noise is Salaar's mocking laugh! You have failed and you are dismissed!",PLAYER0)
        QUICK_OBJECTIVE(16,"那声音是萨拉尔的嘲笑！你失败了，你被解雇了！",PLAYER0)
    ENDIF
ENDIF

REM ****************************提示信息**********************************
QUICK_OBJECTIVE(1,"参考提示：1、没有训练室的情况下，让你的怪和敌人PK能升级；2、消灭（招降）敌人骑士越多，造桥的限制就越少，如果你想用龙俘虏敌人，就不能一直用龙的一号喷火技能；3、招募室能招募野外的怪和死了的怪，死了的怪招募进来是1级的；4、规则修改过，你招降招募的怪越多，会有更多惊喜，不过记得升级你的小鬼，否则发不起工资。",PLAYER0)  
IF(PLAYER0,GAME_TURN > 200)
    QUICK_INFORMATION(3,"让你的怪和敌人PK能升级，不过要耗些时间，圣者、骑士、龙等有治疗技能、血多、攻击速度慢的怪是最理想的PK升级对象。",PLAYER0) 
ENDIF
IF(PLAYER0,GAME_TURN > 500)
    QUICK_INFORMATION(4,"这个王国共有6个骑士，消灭一个骑士能造一格桥，消灭三个骑士能造两格桥，消灭所以骑士可以随意造不再有限制。",PLAYER0) 
ENDIF
IF(PLAYER0,GAME_TURN > 1000)
    QUICK_INFORMATION(5,"招募室能招募野外敌人的怪和中立的怪，在这个王国里还能招募死了的怪，不过死了的怪招募进来是1级的；如果你想用龙俘虏敌人。",PLAYER0) 
ENDIF
IF(PLAYER0,GAME_TURN > 1500)
    QUICK_INFORMATION(6,"当把敌人喷得差不多没血的时候就就经改用手榴弹、强烈咒语等其它技能把敌人打晕，一号技能只会杀死敌人。",PLAYER0) 
ENDIF
IF(PLAYER0,GAME_TURN > 2000)
    QUICK_INFORMATION(7,"祝你好运，守护者！",PLAYER0) 
ENDIF

REM ********************************以下是修改B版修改的内容*************************************************
REM 地图上2条龙，怪物池中有8条龙，如果全部招募出来共10条龙。
IF(PLAYER0,dragon > 7)
    ROOM_AVAILABLE(PLAYER0,TREASURE,1,1)
    ROOM_AVAILABLE(PLAYER0,LAIR,1,1)
    ROOM_AVAILABLE(PLAYER0,GARDEN,1,1)

    TUTORIAL_FLASH_BUTTON(2,-1)
    QUICK_INFORMATION(17,"这个大陆之前混乱的大战导致各种房子设计图纸被人瓜分散落在大地上，很多怪都得到一两张变得残缺不全图纸，现在你的几条龙各自携带的图纸终于合在组成了几套完整的图纸，你终于掌握了这些房子的建造技术。",PLAYER0) 
ENDIF
REM 地图上6个忍者，如果玩家有4个以上忍者证明玩家利用了招募室。
IF(PLAYER0,SAMURAI > 4)
    ROOM_AVAILABLE(PLAYER0,TRAINING,1,1)
    ROOM_AVAILABLE(PLAYER0,WORKSHOP,1,1)
    ROOM_AVAILABLE(PLAYER0,BARRACKS,1,1)

    TUTORIAL_FLASH_BUTTON(2,-1)
    QUICK_INFORMATION(18,"把投降的敌兵各自携带的残图放在一起组合成了几套完整的设计图纸，你掌握了这些房子的建造技术。",PLAYER0) 
ENDIF

REM 地图上6个骑士，如果玩家有2个及以上骑士证明玩家有意识俘虏招降骑士。
IF(PLAYER0,KNIGHT > 1)
    ROOM_AVAILABLE(PLAYER0,TEMPLE,1,1)
    ROOM_AVAILABLE(PLAYER0,PRISON,1,1)
    ROOM_AVAILABLE(PLAYER0,TORTURE,1,1)
    ROOM_AVAILABLE(PLAYER0,SCAVENGER,1,1)
    ROOM_AVAILABLE(PLAYER0,GRAVEYARD,1,1)

    TUTORIAL_FLASH_BUTTON(2,-1)
    QUICK_INFORMATION(18,"把投降的敌兵各自携带的残图放在一起组合成了几套完整的设计图纸，你掌握了这些房子的建造技术。",PLAYER0) 
ENDIF

REM 地图上6个男巫，如果玩家有6个以上男巫证明玩家利用了招募室。
IF(PLAYER0,WIZARD > 4)
    ROOM_AVAILABLE(PLAYER0,RESEARCH,1,1)
    MAGIC_AVAILABLE(PLAYER0,POWER_IMP,1,1)
    MAGIC_AVAILABLE(PLAYER0,POWER_CALL_TO_ARMS,1,1)
    MAGIC_AVAILABLE(PLAYER0,POWER_SPEED,1,1)
    MAGIC_AVAILABLE(PLAYER0,POWER_HEAL_CREATURE,1,1)
    MAGIC_AVAILABLE(PLAYER0,POWER_DESTROY_WALLS,1,1)

    TUTORIAL_FLASH_BUTTON(3,-1)
    QUICK_INFORMATION(19,"这几个巫师身上不但携带房子设计图纸，还有几本魔法书。",PLAYER0) 
ENDIF































