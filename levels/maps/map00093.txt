REM ****************************************************************************
REM  即时战略游戏《地下城守护者1》复刻版整合项目的战役
REM ***************************************************************************
REM  战役名: 更深的地下城
REM  作者：基于牛蛙脚本
REM  地下城守护者爱好者 CCP 团队
REM  版权:
REM   此脚本是免费的；你可以重新发布和/或修改它， 根据自由软件基金会公布的GNU
REM   通用公共许可证条款；许可证的版本2，或由你选择的任何后期版本。
REM ****************************************************************************
REM  更深地下城之脚本，地图名：93.Batezek
REM  日期: 1996年12月17 -  2014年9月9
REM ****************************************************************************
REM 地图的大概情况与参考建议：
REM 1、。
REM 2、 。
REM 3、 。
REM 4、 。
REM 5、 。
REM  
REM ****************************************************************************
REM ***************************各种基本设置 *****************************
SET_GENERATE_SPEED(500)
START_MONEY(PLAYER0,25000)
MAX_CREATURES(PLAYER0,20)

REM *******************************添加怪物到入口 *********************************
REM   (添加到入口的生物名字,数量)
ADD_CREATURE_TO_POOL(HELL_HOUND,20)
ADD_CREATURE_TO_POOL(TENTACLE,20)
ADD_CREATURE_TO_POOL(SORCEROR,20)
ADD_CREATURE_TO_POOL(BILE_DEMON,20)
ADD_CREATURE_TO_POOL(DARK_MISTRESS,20)
ADD_CREATURE_TO_POOL(DRAGON,20)
ADD_CREATURE_TO_POOL(VAMPIRE,20)
ADD_CREATURE_TO_POOL(ORC,20)
ADD_CREATURE_TO_POOL(BUG,10)
ADD_CREATURE_TO_POOL(FLY,5)
ADD_CREATURE_TO_POOL(SPIDER,20)
ADD_CREATURE_TO_POOL(TROLL,20)

REM *******************************指定阵营（玩家）允许/禁止从入口（怪物池）中招指定生物*********************************
REM  (玩家,生物名称,1（只能是1）,[1允许招募/0禁止招募])

CREATURE_AVAILABLE(PLAYER0,HELL_HOUND,1,0)
CREATURE_AVAILABLE(PLAYER0,TENTACLE,1,0)
CREATURE_AVAILABLE(PLAYER0,SORCEROR,1,0)
CREATURE_AVAILABLE(PLAYER0,BILE_DEMON,1,0)
CREATURE_AVAILABLE(PLAYER0,DARK_MISTRESS,1,0)
CREATURE_AVAILABLE(PLAYER0,DRAGON,1,0)
CREATURE_AVAILABLE(PLAYER0,VAMPIRE,1,0)
CREATURE_AVAILABLE(PLAYER0,ORC,1,0)
CREATURE_AVAILABLE(PLAYER0,FLY,1,0)
CREATURE_AVAILABLE(PLAYER0,SPIDER,1,0)
CREATURE_AVAILABLE(PLAYER0,BUG,1,0)
CREATURE_AVAILABLE(PLAYER0,TROLL,1,0)

REM ********************指定阵营（玩家）允许/禁止研究和建造指定房子（建筑）**********************************
REM  (玩家,房子名称,是否可研究或建造[1是允许；0是禁止],是否已经可建造[1已经可建/0待研究])
ROOM_AVAILABLE(PLAYER0,TREASURE,1,1)
ROOM_AVAILABLE(PLAYER0,LAIR,1,1)
ROOM_AVAILABLE(PLAYER0,GARDEN,1,1)
ROOM_AVAILABLE(PLAYER0,TRAINING,1,1)
ROOM_AVAILABLE(PLAYER0,RESEARCH,1,1)
ROOM_AVAILABLE(PLAYER0,WORKSHOP,1,0)
ROOM_AVAILABLE(PLAYER0,BARRACKS,1,0)
ROOM_AVAILABLE(PLAYER0,GUARD_POST,1,0)
ROOM_AVAILABLE(PLAYER0,TORTURE,1,0)
ROOM_AVAILABLE(PLAYER0,TEMPLE,1,0)
ROOM_AVAILABLE(PLAYER0,GRAVEYARD,1,0)

REM ********************指定阵营（玩家）允许/禁止研究和使用指定魔法**********************************
REM  (玩家,魔法名称,是否可研究或使用[1是允许；0是禁止],是否已经可使用[1已经可建/0待研究])
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HAND,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_IMP,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SLAP,1,1)
MAGIC_AVAILABLE(PLAYER1,POWER_SIGHT,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HEAL_CREATURE,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SPEED,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_PROTECT,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CONCEAL,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CAVE_IN,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CHICKEN,1,0)

REM *********************指定阵营（玩家）允许/禁止制造指定的门*********************************
REM  (玩家,门的名称,[1是允许；0是禁止],1[0是允许；数字是已经制造出来/赠送的数量]
DOOR_AVAILABLE(ALL_PLAYERS,STEEL,1,0)
DOOR_AVAILABLE(ALL_PLAYERS,MAGIC,1,0)

REM *********************指定阵营（玩家）允许/禁止制造指定陷阱*********************************
REM  (玩家,陷阱名称,[1是允许；0是禁止],1[0是允许；数字是已经制造出来/赠送的数量]
TRAP_AVAILABLE(ALL_PLAYERS,ALARM,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,POISON_GAS,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,LIGHTNING,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,WORD_OF_POWER,1,0)

REM ****************************增援部队**********************************
REM CREATE_PARTY =>组建队伍                (队伍名称)   
REM ADD_TO_PARTY=>添加生物到队伍中   (队伍名称,生物名称,生物等级,携带金钱数量,任务,延时)
CREATE_PARTY(ONE)
    ADD_TO_PARTY(ONE,THIEF,2,400,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(ONE,THIEF,2,400,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(ONE,ARCHER,2,400,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(ONE,ARCHER,2,400,ATTACK_DUNGEON_HEART,0)
CREATE_PARTY(TWO)
    ADD_TO_PARTY(TWO,ARCHER,2,600,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(TWO,ARCHER,2,600,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(TWO,DWARFA,2,600,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(TWO,DWARFA,2,600,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(TWO,DWARFA,2,600,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(TWO,DWARFA,2,600,ATTACK_DUNGEON_HEART,0)
CREATE_PARTY(THREE)
    ADD_TO_PARTY(THREE,BARBARIAN,3,500,STEAL_GOLD,0)
    ADD_TO_PARTY(THREE,BARBARIAN,3,500,STEAL_GOLD,0)
    ADD_TO_PARTY(THREE,BARBARIAN,3,500,STEAL_GOLD,0)
    ADD_TO_PARTY(THREE,THIEF,3,500,STEAL_GOLD,0)
CREATE_PARTY(FOUR)
    ADD_TO_PARTY(FOUR,WITCH,3,550,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(FOUR,WITCH,3,550,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(FOUR,DWARFA,3,550,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(FOUR,DWARFA,3,550,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(FOUR,DWARFA,4,650,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(FOUR,DWARFA,4,650,ATTACK_ENEMIES,0)
CREATE_PARTY(FIVE)
    ADD_TO_PARTY(FIVE,FAIRY,4,700,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(FIVE,FAIRY,4,700,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(FIVE,FAIRY,4,700,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(FIVE,FAIRY,4,700,ATTACK_ENEMIES,0)
CREATE_PARTY(SIX)
    ADD_TO_PARTY(SIX,THIEF,4,500,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(SIX,THIEF,4,500,ATTACK_DUNGEON_HEART,0)    
    ADD_TO_PARTY(SIX,THIEF,4,500,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(SIX,THIEF,4,500,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(SIX,WIZARD,5,600,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(SIX,WIZARD,5,600,ATTACK_DUNGEON_HEART,0)
CREATE_PARTY(SEVEN)
    ADD_TO_PARTY(SEVEN,GIANT,5,1750,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(SEVEN,GIANT,5,1750,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(SEVEN,SAMURAI,5,1850,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(SEVEN,SAMURAI,5,1850,ATTACK_ENEMIES,0)
CREATE_PARTY(DEFENDA)
    ADD_TO_PARTY(DEFENDA,ARCHER,5,800,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(DEFENDA,ARCHER,5,800,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(DEFENDA,ARCHER,5,800,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(DEFENDA,ARCHER,5,800,ATTACK_ENEMIES,0)
CREATE_PARTY(DEFENDB)
    ADD_TO_PARTY(DEFENDB,ARCHER,6,900,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(DEFENDB,ARCHER,6,900,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(DEFENDB,WIZARD,7,920,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(DEFENDB,WIZARD,7,920,ATTACK_ENEMIES,0)
CREATE_PARTY(DEFENDC)
    ADD_TO_PARTY(DEFENDC,ARCHER,7,900,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(DEFENDC,ARCHER,7,900,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(DEFENDC,ARCHER,7,900,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(DEFENDC,ARCHER,7,900,ATTACK_ENEMIES,0)
CREATE_PARTY(DEFENDD)
    ADD_TO_PARTY(DEFENDD,ARCHER,7,900,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(DEFENDD,ARCHER,7,900,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(DEFENDD,WITCH,7,1100,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(DEFENDD,WITCH,7,1100,ATTACK_ENEMIES,0)
CREATE_PARTY(LANDLORD)
    ADD_TO_PARTY(LANDLORD,BARBARIAN,9,1500,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(LANDLORD,BARBARIAN,9,1500,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(LANDLORD,BARBARIAN,9,1500,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(LANDLORD,KNIGHT,10,5000,ATTACK_DUNGEON_HEART,0)
CREATE_PARTY(DEFENDER)
    ADD_TO_PARTY(DEFENDER,GIANT,8,1200,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(DEFENDER,GIANT,8,1200,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(DEFENDER,GIANT,8,1200,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(DEFENDER,GIANT,8,1200,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(DEFENDER,GIANT,8,1200,ATTACK_ENEMIES,0)
CREATE_PARTY(MISSIONA)
    ADD_TO_PARTY(MISSIONA,THIEF,9,1500,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(MISSIONA,DWARFA,10,1500,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(MISSIONA,THIEF,9,1500,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(MISSIONA,DWARFA,10,1500,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(MISSIONA,THIEF,9,1500,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(MISSIONA,DWARFA,10,1500,ATTACK_ENEMIES,0)
CREATE_PARTY(MISSIONB)
    ADD_TO_PARTY(MISSIONB,BARBARIAN,9,1500,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(MISSIONB,GIANT,10,1200,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(MISSIONB,BARBARIAN,9,1700,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(MISSIONB,BARBARIAN,9,1700,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(MISSIONB,GIANT,10,2000,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(MISSIONB,BARBARIAN,9,1500,ATTACK_ENEMIES,0)
CREATE_PARTY(MISSIONC)
    ADD_TO_PARTY(MISSIONC,SAMURAI,9,2000,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(MISSIONC,SAMURAI,9,2000,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(MISSIONC,SAMURAI,9,2000,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(MISSIONC,SAMURAI,9,2000,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(MISSIONC,SAMURAI,9,2000,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(MISSIONC,WITCH,10,2200,ATTACK_ENEMIES,0)



REM ****************************脚本主体(主要内容、核心内容）**********************************
REM "You are surrounded on all sides by valiant heroes so show them the true meaning of evil and terror."
REM "你四面都是英勇的英雄，所以要向他们展示邪恶和恐怖的真正含义。"
DISPLAY_OBJECTIVE(909,PLAYER0)

REM **************************
IF(PLAYER0,SCAVENGER >= 1)
    ROOM_AVAILABLE(PLAYER0,SCAVENGER,1,1)
ENDIF
IF(PLAYER0,GRAVEYARD >= 1)
    ROOM_AVAILABLE(PLAYER0,GRAVEYARD,1,1)
ENDIF
IF(PLAYER0,PRISON >= 1)
    ROOM_AVAILABLE(PLAYER0,PRISON,1,1)
ENDIF

REM **************************
REM 触发点13处得到监狱
IF_ACTION_POINT(13,PLAYER0)
    ROOM_AVAILABLE(PLAYER0,PRISON,1,1)
ENDIF

REM **************************
IF(PLAYER0,GAME_TURN >= 30000)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,BARBARIAN,2,8,10,1000)
ENDIF
IF(PLAYER0,GAME_TURN >= 45000)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,MONK,2,8,8,1200)
ENDIF

REM **************************
IF_ACTION_POINT(1,PLAYER0)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,DEFENDA,-2,1)
ENDIF
IF_ACTION_POINT(2,PLAYER0)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,DEFENDB,-3,1)
ENDIF
IF_ACTION_POINT(3,PLAYER0)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,DEFENDC,4,1)
ENDIF
IF_ACTION_POINT(5,PLAYER0)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,DEFENDD,6,1)
ENDIF
IF_ACTION_POINT(7,PLAYER0)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,DEFENDD,-1,1)
IF_ACTION_POINT(8,PLAYER0)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,DEFENDER,-5,1)
    SET_TIMER(PLAYER_GOOD,TIMER1)
ENDIF
IF(PLAYER_GOOD,TIMER1 >= 5500)
    NEXT_COMMAND_REUSABLE
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SIX,-5,1)
    NEXT_COMMAND_REUSABLE
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SEVEN,-4,1)
    NEXT_COMMAND_REUSABLE
    SET_TIMER(PLAYER_GOOD,TIMER1)
ENDIF
ENDIF
IF_ACTION_POINT(9,PLAYER0)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,DEFENDB,-4,1)
ENDIF
IF_ACTION_POINT(10,PLAYER0)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,DEFENDER,11,1)
ENDIF
IF_ACTION_POINT(12,PLAYER0)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,DEFENDER,-4,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,THREE,-4,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,FOUR,-4,1)
    SET_TIMER(PLAYER0,TIMER3)
    SET_TIMER(PLAYER0,TIMER4)
ENDIF
IF_ACTION_POINT(12,PLAYER0)
    IF(PLAYER0,FLAG3 == 0)
        IF(PLAYER0,TIMER4 >= 200)
            IF(PLAYER0,TIMER4 <= 600)
                NEXT_COMMAND_REUSABLE
                RESET_ACTION_POINT(12)
            ENDIF
        ENDIF
        IF(PLAYER0,TIMER4 >= 750)
            NEXT_COMMAND_REUSABLE
            SET_FLAG(PLAYER0,FLAG3,1)
        ENDIF
    ENDIF
ENDIF
IF(PLAYER0,FLAG3 == 1)
    NEXT_COMMAND_REUSABLE
    SET_TIMER(PLAYER0,TIMER4)
    NEXT_COMMAND_REUSABLE
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,THREE,-4,1)
    NEXT_COMMAND_REUSABLE
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,FOUR,-4,1)
    NEXT_COMMAND_REUSABLE
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,DEFENDER,-4,1)
    NEXT_COMMAND_REUSABLE
    SET_FLAG(PLAYER0,FLAG3,0)
ENDIF
IF(PLAYER0,TIMER3 >= 20000)
    SET_FLAG(PLAYER0,FLAG3,2)
ENDIF

REM **************************
SET_TIMER(PLAYER_GOOD,TIMER0)
IF(PLAYER_GOOD,TIMER0 >= 8000)
    ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,ONE,-1,DUNGEON,0,1,250)
ENDIF
IF(PLAYER_GOOD,TIMER0 >= 11000)
    ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,TWO,-2,DUNGEON,0,1,250)
ENDIF
IF(PLAYER_GOOD,TIMER0 >= 16000)
    ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,THREE,-3,DUNGEON,0,1,250)
ENDIF
IF(PLAYER_GOOD,TIMER0 >= 19000)
    ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,FOUR,-1,DUNGEON,0,1,250)
ENDIF
IF(PLAYER_GOOD,TIMER0 >= 22000)
    ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,FIVE,-2,DUNGEON,0,1,250)
ENDIF
IF(PLAYER_GOOD,TIMER0 >= 27000)
    ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,SIX,-3,DUNGEON,0,1,250)
ENDIF
IF(PLAYER_GOOD,TIMER0 >= 30000)
    ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,DEFENDA,-1,DUNGEON,0,1,250)
ENDIF
IF(PLAYER_GOOD,TIMER0 >= 35000)
    ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,DEFENDB,-3,DUNGEON,0,1,250)
ENDIF
IF(PLAYER_GOOD,TIMER0 >= 38000)
    ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,DEFENDC,-2,DUNGEON,0,1,250)
ENDIF
IF(PLAYER_GOOD,TIMER0 >= 41000)
    ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,DEFENDA,-1,DUNGEON,0,1,250)
    REM Make sure the continuous parties are disabled now - otherwise it may be too hard
    REM 确保已禁用连续参与方-否则可能太难
    SET_FLAG(PLAYER0,FLAG3,2)
ENDIF
IF(PLAYER_GOOD,TIMER0 >= 45000)
    ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,THREE,-2,DUNGEON,0,1,250)
    ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,FOUR,-2,DUNGEON,0,1,250)
ENDIF
IF(PLAYER_GOOD,TIMER0 >= 49000)
    ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,FIVE,-3,DUNGEON,0,1,250)
    ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,FOUR,-3,DUNGEON,0,1,250)
ENDIF
IF(PLAYER_GOOD,TIMER0 >= 60000)
    IF_CONTROLS(PLAYER_GOOD,TOTAL_CREATURES <= 0)
        REM "The Lord of the Land has sent his fiercest warriors against you. Show them no mercy and flay them alive."
        REM "这地的领主差遣他最凶猛的勇士来攻击你，不要怜悯他们，要活剥他们的皮。"
        DISPLAY_OBJECTIVE(910,PLAYER0)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,DEFENDER,-1,1)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,DEFENDER,-3,1)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,DEFENDER,-4,1)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,DEFENDA,-4,1)
        SET_TIMER(PLAYER0,TIMER5)
    ENDIF
ENDIF
IF(PLAYER0,TIMER5 >= 500)
    ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,MISSIONA,1,DUNGEON,0,5,500)
    ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,MISSIONB,2,DUNGEON,0,5,500)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,MISSIONC,-2,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,MISSIONC,-3,1)
ENDIF
IF(PLAYER0,TIMER5 >= 750)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,DEFENDER,-6,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,DEFENDD,-6,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,DEFENDB,-6,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,LANDLORD,-6,1)
ENDIF

REM ****************************胜利/失败条件**********************************
REM 游戏开始后就启动TIMER0，刷14波英雄，TIMER0到达时就启动TIMER5，之后就刷3波英雄，干掉所有骑士就胜利。
IF(PLAYER0,TIMER5 >= 950)
    IF_CONTROLS(PLAYER_GOOD,KNIGHT <= 0)

        REM 以下修改游戏，增加胜利后显示全图、送钱开放建造限制，让你回味一下胜利的喜悦！
        LEVEL_VERSION(1)
        REVEAL_MAP_RECT(PLAYER0,127,127,254,254)
        ADD_GOLD_TO_PLAYER (PLAYER0,9999999)

        ROOM_AVAILABLE(PLAYER0,TREASURE,1,1)
        ROOM_AVAILABLE(PLAYER0,GARDEN,1,1)
        ROOM_AVAILABLE(PLAYER0,LAIR,1,1)
        ROOM_AVAILABLE(PLAYER0,RESEARCH,1,1)
        ROOM_AVAILABLE(PLAYER0,TRAINING,1,1)
        ROOM_AVAILABLE(PLAYER0,BRIDGE,1,1)
        ROOM_AVAILABLE(PLAYER0,GUARD_POST,1,1)
        ROOM_AVAILABLE(PLAYER0,WORKSHOP,1,1)
        ROOM_AVAILABLE(PLAYER0,BARRACKS,1,1)
        ROOM_AVAILABLE(PLAYER0,TEMPLE,1,1)
        ROOM_AVAILABLE(PLAYER0,PRISON,1,1)
        ROOM_AVAILABLE(PLAYER0,TORTURE,1,1)
        ROOM_AVAILABLE(PLAYER0,SCAVENGER,1,1)
        ROOM_AVAILABLE(PLAYER0,GRAVEYARD,1,1)
        
        MAGIC_AVAILABLE(PLAYER0,POWER_IMP,1,1)
        MAGIC_AVAILABLE(PLAYER0,POWER_CALL_TO_ARMS,1,1
        MAGIC_AVAILABLE(PLAYER0,POWER_SPEED,1,1)
        MAGIC_AVAILABLE(PLAYER0,POWER_CONCEAL,1,1)
        MAGIC_AVAILABLE(PLAYER0,POWER_HEAL_CREATURE,1,1)
        MAGIC_AVAILABLE(PLAYER0,POWER_DESTROY_WALLS,1,1)

        DOOR_AVAILABLE(PLAYER0,MAGIC,1,200)
        TRAP_AVAILABLE(PLAYER0,LIGHTNING,1,0)
        TRAP_AVAILABLE(PLAYER0,BOULDER,1,200)
        QUICK_INFORMATION(15,"亮图送钱，让你回味一下胜利的喜悦！",PLAYER0) 

        WIN_GAME
    ENDIF
ENDIF

REM ****************************提示信息**********************************
























