REM ***********************************************************************
REM   Script for Level 210.Cold War 冷战
REM ***********************************************************************
REM 
REM 
REM ***********************************************************************
REM 地图的大概情况与参考建议：
REM 1、 。
REM 2、 。
REM 3、 。
REM 4、 。
REM 5、 。
REM ***********************************************************************
REM ***************************各种基本设置 *****************************
SET_GENERATE_SPEED(1023)
START_MONEY(PLAYER0,5000)
START_MONEY(PLAYER3,32767)
START_MONEY(PLAYER2,32767)
START_MONEY(PLAYER1,32767)
START_MONEY(PLAYER_GOOD,9999999)
MAX_CREATURES(PLAYER0,22)
MAX_CREATURES(PLAYER2,34)
MAX_CREATURES(PLAYER1,34)

ALLY_PLAYERS(PLAYER1,PLAYER2)
ALLY_PLAYERS(PLAYER_GOOD,PLAYER2)
ALLY_PLAYERS(PLAYER_GOOD,PLAYER1)
ALLY_PLAYERS(PLAYER_GOOD,PLAYER3)

COMPUTER_PLAYER(PLAYER1,1)
COMPUTER_PLAYER(PLAYER2,1)
COMPUTER_PLAYER(PLAYER3,0)
COMPUTER_PLAYER(PLAYER_GOOD,0)

SET_COMPUTER_CHECKS(PLAYER1,"CHECK FOR NEUTRAL PLACES",15000,0,0,0,65000)
SET_COMPUTER_CHECKS(PLAYER2,"CHECK FOR NEUTRAL PLACES",15000,0,0,0,65000)
SET_COMPUTER_CHECKS(PLAYER1,"CHECK FOR QUICK ATTACK",700,70,3000,8,12500)
SET_COMPUTER_PROCESS(PLAYER1,"ATTACK SAFE ATTACK",0,70,8,70,0)
SET_COMPUTER_CHECKS(PLAYER2,"CHECK FOR QUICK ATTACK",700,70,3000,8,12500)
SET_COMPUTER_PROCESS(PLAYER2,"ATTACK SAFE ATTACK",0,70,8,70,0)
SET_COMPUTER_PROCESS(PLAYER1,"ATTACK PLAN 1",-1,100,17,100,0)
SET_COMPUTER_PROCESS(PLAYER2,"ATTACK PLAN 1",-1,100,17,100,0)

REM *******************************添加怪物到入口 *********************************
REM   (添加到入口的生物名字,数量)
ADD_CREATURE_TO_POOL(IMP,25)
ADD_CREATURE_TO_POOL(FLY,25)
ADD_CREATURE_TO_POOL(BUG,25)
ADD_CREATURE_TO_POOL(SPIDER,25)
ADD_CREATURE_TO_POOL(DEMONSPAWN,25)
ADD_CREATURE_TO_POOL(SORCEROR,25)
ADD_CREATURE_TO_POOL(BILE_DEMON,25)
ADD_CREATURE_TO_POOL(TROLL,25)
ADD_CREATURE_TO_POOL(ORC,25)
ADD_CREATURE_TO_POOL(DARK_MISTRESS,25)
ADD_CREATURE_TO_POOL(SKELETON,25)
ADD_CREATURE_TO_POOL(HELL_HOUND,25)
ADD_CREATURE_TO_POOL(VAMPIRE,25)
ADD_CREATURE_TO_POOL(TENTACLE,25)
ADD_CREATURE_TO_POOL(HORNY,25)
ADD_CREATURE_TO_POOL(BARBARIAN,25)
ADD_CREATURE_TO_POOL(SAMURAI,25)

REM *******************************指定阵营（玩家）允许/禁止从入口（怪物池）中招指定生物*********************************
REM  (玩家,生物名称,1（只能是1）,[1允许招募/0禁止招募])

CREATURE_AVAILABLE(PLAYER1,FLY,1,1)
CREATURE_AVAILABLE(PLAYER1,BUG,1,1)
CREATURE_AVAILABLE(PLAYER1,DEMONSPAWN,1,1)
CREATURE_AVAILABLE(PLAYER1,SORCEROR,1,1)
CREATURE_AVAILABLE(PLAYER1,BILE_DEMON,1,1)
CREATURE_AVAILABLE(PLAYER1,TROLL,1,1)
CREATURE_AVAILABLE(PLAYER1,GHOST,1,1)
CREATURE_AVAILABLE(PLAYER1,TENTACLE,1,1)
CREATURE_AVAILABLE(PLAYER1,HORNY,1,1)

CREATURE_AVAILABLE(PLAYER2,BUG,1,1)
CREATURE_AVAILABLE(PLAYER2,SPIDER,1,1)
CREATURE_AVAILABLE(PLAYER2,TROLL,1,1)
CREATURE_AVAILABLE(PLAYER2,SKELETON,1,1)
CREATURE_AVAILABLE(PLAYER2,GHOST,1,1)
CREATURE_AVAILABLE(PLAYER2,HELL_HOUND,1,1)
CREATURE_AVAILABLE(PLAYER2,VAMPIRE,1,1)
CREATURE_AVAILABLE(PLAYER2,TENTACLE,1,1)
CREATURE_AVAILABLE(PLAYER2,HORNY,1,1)

CREATURE_AVAILABLE(PLAYER0,DEMONSPAWN,1,1)
CREATURE_AVAILABLE(PLAYER0,DARK_MISTRESS,1,1)
CREATURE_AVAILABLE(PLAYER0,ORC,1,1)
CREATURE_AVAILABLE(PLAYER0,HORNY,1,1)

REM ********************可研究和建筑的房子**********************************
ROOM_AVAILABLE(ALL_PLAYERS,TREASURE,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,LAIR,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,GARDEN,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,TRAINING,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,RESEARCH,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,BARRACKS,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,TORTURE,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,TEMPLE,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,SCAVENGER,1,0)

ROOM_AVAILABLE(PLAYER0,BRIDGE,1,0)
ROOM_AVAILABLE(PLAYER1,WORKSHOP,1,0)
ROOM_AVAILABLE(PLAYER2,BRIDGE,1,1)
ROOM_AVAILABLE(PLAYER2,PRISON,1,1)
ROOM_AVAILABLE(PLAYER2,GRAVEYARD,1,1)

REM ********************指定阵营（玩家）允许/禁止研究和使用指定魔法**********************************
REM  (玩家,魔法名称,是否可研究或使用[1是允许；0是禁止],是否已经可使用[1已经可建/0待研究])
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HAND,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SLAP,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SIGHT,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SPEED,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CHICKEN,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_DISEASE,1,0)

MAGIC_AVAILABLE(PLAYER0,POWER_IMP,1,1)
MAGIC_AVAILABLE(PLAYER1,POWER_IMP,1,1)
MAGIC_AVAILABLE(PLAYER2,POWER_IMP,1,1)

MAGIC_AVAILABLE(PLAYER0,POWER_CALL_TO_ARMS,1,0)
MAGIC_AVAILABLE(PLAYER1,POWER_CALL_TO_ARMS,1,1)
MAGIC_AVAILABLE(PLAYER2,POWER_CALL_TO_ARMS,1,1)

MAGIC_AVAILABLE(PLAYER0,POWER_CONCEAL,1,0)
MAGIC_AVAILABLE(PLAYER0,POWER_HEAL_CREATURE,1,0)
MAGIC_AVAILABLE(PLAYER0,POWER_ARMAGEDDON,1,0)

MAGIC_AVAILABLE(PLAYER1,POWER_DESTROY_WALLS,1,0)
MAGIC_AVAILABLE(PLAYER2,POWER_DESTROY_WALLS,1,0)
MAGIC_AVAILABLE(PLAYER3,POWER_DESTROY_WALLS,1,0)

REM *********************指定阵营（玩家）允许/禁止制造指定的门*********************************
REM  (玩家,门的名称,[1是允许；0是禁止],1[0是允许；数字是已经制造出来/赠送的数量]
DOOR_AVAILABLE(ALL_PLAYERS,WOOD,1,12)
DOOR_AVAILABLE(PLAYER0,STEEL,1,0)
DOOR_AVAILABLE(PLAYER1,STEEL,1,8)
DOOR_AVAILABLE(PLAYER2,STEEL,1,8)
DOOR_AVAILABLE(PLAYER3,STEEL,1,8)

REM *********************指定阵营（玩家）允许/禁止制造指定陷阱*********************************
REM  (玩家,陷阱名称,[1是允许；0是禁止],1[0是允许；数字是已经制造出来/赠送的数量]
TRAP_AVAILABLE(PLAYER3,LIGHTNING,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,ALARM,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,POISON_GAS,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,BOULDER,1,0)

REM ****************************增援部队**********************************
REM CREATE_PARTY =>组建队伍                (队伍名称)   
REM ADD_TO_PARTY=>添加生物到队伍中   (队伍名称,生物名称,生物等级,携带金钱数量,任务,延时)
CREATE_PARTY(CHAOS)
    ADD_TO_PARTY(CHAOS,WIZARD,8,800,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(CHAOS,BARBARIAN,8,400,DEFEND_PARTY,0)
    ADD_TO_PARTY(CHAOS,MONK,9,200,DEFEND_PARTY,0)
    ADD_TO_PARTY(CHAOS,MONK,9,200,DEFEND_PARTY,0)
    ADD_TO_PARTY(CHAOS,BARBARIAN,8,400,DEFEND_PARTY,0)
    ADD_TO_PARTY(CHAOS,DWARFA,7,400,ATTACK_ROOMS,0)
CREATE_PARTY(STEAL)
    ADD_TO_PARTY(STEAL,SAMURAI,7,800,DEFEND_PARTY,0)
    ADD_TO_PARTY(STEAL,SAMURAI,7,800,DEFEND_PARTY,0)
    ADD_TO_PARTY(STEAL,SAMURAI,7,800,DEFEND_PARTY,0)
    ADD_TO_PARTY(STEAL,THIEF,10,0,STEAL_SPELLS,0)
    ADD_TO_PARTY(STEAL,THIEF,7,0,STEAL_SPELLS,0)
    ADD_TO_PARTY(STEAL,DWARFA,4,100,ATTACK_ROOMS,0)
CREATE_PARTY(LOW)
    ADD_TO_PARTY(LOW,SAMURAI,6,400,DEFEND_PARTY,0)
    ADD_TO_PARTY(LOW,SAMURAI,6,400,DEFEND_PARTY,0)
    ADD_TO_PARTY(LOW,SAMURAI,6,400,DEFEND_PARTY,0)
    ADD_TO_PARTY(LOW,SAMURAI,6,400,DEFEND_PARTY,0)
    ADD_TO_PARTY(LOW,MONK,7,200,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(LOW,WIZARD,7,800,ATTACK_DUNGEON_HEART,0)
CREATE_PARTY(TEN)
    ADD_TO_PARTY(TEN,SAMURAI,9,1000,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(TEN,SAMURAI,9,1000,DEFEND_PARTY,0)
    ADD_TO_PARTY(TEN,BARBARIAN,6,400,DEFEND_PARTY,0)
    ADD_TO_PARTY(TEN,MONK,10,200,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(TEN,GIANT,10,400,DEFEND_PARTY,0)
    ADD_TO_PARTY(TEN,WIZARD,10,800,ATTACK_ENEMIES,0)
CREATE_PARTY(SPROOKJES)
    ADD_TO_PARTY(SPROOKJES,DWARFA,4,100,STEAL_GOLD,0)
    ADD_TO_PARTY(SPROOKJES,DWARFA,7,200,STEAL_GOLD,0)
    ADD_TO_PARTY(SPROOKJES,FAIRY,5,300,STEAL_GOLD,0)
    ADD_TO_PARTY(SPROOKJES,FAIRY,6,400,STEAL_GOLD,0)
    ADD_TO_PARTY(SPROOKJES,FAIRY,7,500,STEAL_GOLD,0)
    ADD_TO_PARTY(SPROOKJES,FAIRY,10,600,STEAL_GOLD,0)

REM ****************************脚本主体(主要内容、核心内容）**********************************
SET_CREATURE_STRENGTH(DEMONSPAWN,127)
SET_CREATURE_HEALTH(DEMONSPAWN,1023)
SET_CREATURE_ARMOUR(DEMONSPAWN,71)
SET_CREATURE_FEAR(DEMONSPAWN,7)

SET_CREATURE_STRENGTH(KNIGHT,255)
SET_CREATURE_HEALTH(KNIGHT,4095)
SET_CREATURE_ARMOUR(KNIGHT,255)
SET_CREATURE_FEAR(KNIGHT,7)

REM 桥、哨所、工厂、陵墓
IF(PLAYER1,BRIDGE > 0)
    ROOM_AVAILABLE(PLAYER1,BRIDGE,1,1)
ENDIF
IF(PLAYER2,BRIDGE > 0)
    ROOM_AVAILABLE(PLAYER2,BRIDGE,1,1)
ENDIF
IF(PLAYER0,GUARD_POST > 0)
    ROOM_AVAILABLE(PLAYER0,GUARD_POST,1,1)
ENDIF
IF(PLAYER0,WORKSHOP > 0)
    ROOM_AVAILABLE(PLAYER0,WORKSHOP,1,1)
ENDIF
IF(PLAYER0,PRISON > 0)
    ROOM_AVAILABLE(PLAYER0,PRISON,1,1)
ENDIF
IF(PLAYER0,GRAVEYARD > 0)
    ROOM_AVAILABLE(PLAYER0,GRAVEYARD,1,1)
ENDIF

REM 守护者1(蓝色)和守护者2(绿色)心脏被打爆后，就让守护者3(黄色)发展，并启动时钟刷几批人类出来
IF(PLAYER2,DUNGEON_DESTROYED == 1)
    IF(PLAYER1,DUNGEON_DESTROYED == 1)
        MAX_CREATURES(PLAYER3,34)
        ROOM_AVAILABLE(PLAYER3,TREASURE,1,1)
        ROOM_AVAILABLE(PLAYER3,LAIR,1,1)
        ROOM_AVAILABLE(PLAYER3,GARDEN,1,1)
        ROOM_AVAILABLE(PLAYER3,TRAINING,1,1)
        ROOM_AVAILABLE(PLAYER3,RESEARCH,1,1)
        ROOM_AVAILABLE(PLAYER3,BRIDGE,1,1)

        CREATURE_AVAILABLE(PLAYER3,SAMURAI,1,1)
        CREATURE_AVAILABLE(PLAYER3,SORCEROR,1,1)
        CREATURE_AVAILABLE(PLAYER3,ORC,1,1)
        CREATURE_AVAILABLE(PLAYER3,BARBARIAN,1,1)

        MAGIC_AVAILABLE(PLAYER3,POWER_IMP,1,1)
        MAGIC_AVAILABLE(PLAYER3,POWER_CALL_TO_ARMS,1,1)
        MAGIC_AVAILABLE(PLAYER_GOOD,POWER_SPEED,1,1)
        MAGIC_AVAILABLE(PLAYER_GOOD,POWER_SPEED,1,1)

        SET_TIMER(PLAYER0,TIMER0)
        IF(PLAYER3,TOTAL_CREATURES == 0)
            ADD_PARTY_TO_LEVEL(PLAYER_GOOD,TEN,-1,1)
            IF(PLAYER3,DUNGEON_DESTROYED == 0)
                ADD_CREATURE_TO_LEVEL(PLAYER3,IMP,1,8,3,500)
                ADD_CREATURE_TO_LEVEL(PLAYER3,SAMURAI,1,8,8,400)
            ENDIF
        ENDIF
    ENDIF
ENDIF

IF(PLAYER0,TIMER0 > 2400)
    ADD_CREATURE_TO_LEVEL(PLAYER3,IMP,1,8,3,500)
    ADD_CREATURE_TO_LEVEL(PLAYER3,SAMURAI,1,5,9,400)
    ADD_CREATURE_TO_LEVEL(PLAYER3,MONK,1,4,8,400)
ENDIF

IF(PLAYER0,TIMER0 > 9600)
    ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,TEN,-2,DUNGEON,0,10,0)
ENDIF

IF(PLAYER0,TIMER0 > 13400)
    ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,TEN,3,DUNGEON,0,10,0)
    ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,TEN,4,DUNGEON,0,10,0)
ENDIF

IF(PLAYER0,TIMER0 > 20000)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,TEN,-1,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SPROOKJES,-3,2)
ENDIF
REM 守护者2(绿色)心脏被打爆后，就刷挖掘者带队的敌人出来，让人来有隐身魔法(意义不大)
IF(PLAYER2,DUNGEON_DESTROYED == 1)
    ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,SPROOKJES,-3,DUNGEON,0,5,0)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,STEAL,8,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,LOW,-1,1)
    MAGIC_AVAILABLE(PLAYER_GOOD,POWER_CONCEAL,1,1)
ENDIF
REM 守护者1(蓝色)和守护者2(绿色)心脏被打爆后，就刷挖掘者带队的敌人出来，让人来有加速魔法
IF(PLAYER2,DUNGEON_DESTROYED == 0)
    IF(PLAYER1,DUNGEON_DESTROYED == 1)
        ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,TEN,-2,DUNGEON,0,10,0)
        ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,TEN,3,DUNGEON,0,10,0)
        ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,TEN,4,DUNGEON,0,10,0)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,TEN,-1,1)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SPROOKJES,-3,1)
        MAGIC_AVAILABLE(PLAYER_GOOD,POWER_SPEED,1,1)
    ENDIF
ENDIF

REM ***********************************************************************
IF_ACTION_POINT(2,PLAYER0)
    MAGIC_AVAILABLE(PLAYER_GOOD,POWER_CHICKEN,1,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,CHAOS,5,2)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,TEN,5,2)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SPROOKJES,-3,1)
ENDIF

IF_ACTION_POINT(5,PLAYER0)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,CHAOS,2,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,TEN,3,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,LOW,5,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,STEAL,5,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,TEN,-2,1)
    ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,TEN,-2,DUNGEON,0,10,0)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,KNIGHT,4,1,10,2400)
ENDIF

IF_ACTION_POINT(6,PLAYER0)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SPROOKJES,-1,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,STEAL,8,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,LOW,-3,1)
ENDIF

IF_ACTION_POINT(7,PLAYER0)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,STEAL,8,1)
ENDIF

IF_ACTION_POINT(3,PLAYER0)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,STEAL,8,1)
ENDIF

IF_ACTION_POINT(4,PLAYER0)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,STEAL,8,1)
ENDIF

IF_ACTION_POINT(9,PLAYER0)
    ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,CHAOS,-3,DUNGEON,0,7,0)
ENDIF

REM ****************************胜利/失败条件**********************************
REM 人类心脏被打爆，人类部队被消灭就胜利。
IF(PLAYER_GOOD,DUNGEON_DESTROYED == 1)
    IF(PLAYER_GOOD,TOTAL_CREATURES == 0)

        REM 以下修改游戏，增加胜利后可以建造其它房子，让你回味一下胜利的喜悦！
        LEVEL_VERSION(1)
        REVEAL_MAP_RECT(PLAYER0,127,127,254,254)
        ADD_GOLD_TO_PLAYER (PLAYER0,9999999)

        ROOM_AVAILABLE(PLAYER0,TREASURE,1,1)
        ROOM_AVAILABLE(PLAYER0,GARDEN,1,1)
        ROOM_AVAILABLE(PLAYER0,LAIR,1,1)
        ROOM_AVAILABLE(PLAYER0,RESEARCH,1,1)
        ROOM_AVAILABLE(PLAYER0,TRAINING,1,1)
        ROOM_AVAILABLE(PLAYER0,BRIDGE,1,1)
        ROOM_AVAILABLE(PLAYER0,GUARD_POST,1,1)
        ROOM_AVAILABLE(PLAYER0,WORKSHOP,1,1)
        ROOM_AVAILABLE(PLAYER0,BARRACKS,1,1)
        ROOM_AVAILABLE(PLAYER0,TEMPLE,1,1)
        ROOM_AVAILABLE(PLAYER0,PRISON,1,1)
        ROOM_AVAILABLE(PLAYER0,TORTURE,1,1)
        ROOM_AVAILABLE(PLAYER0,SCAVENGER,1,1)
        ROOM_AVAILABLE(PLAYER0,GRAVEYARD,1,1)
        
        MAGIC_AVAILABLE(PLAYER0,POWER_IMP,1,1)
        MAGIC_AVAILABLE(PLAYER0,POWER_CALL_TO_ARMS,1,1
        MAGIC_AVAILABLE(PLAYER0,POWER_SPEED,1,1)
        MAGIC_AVAILABLE(PLAYER0,POWER_HEAL_CREATURE,1,1)
        MAGIC_AVAILABLE(PLAYER0,POWER_DESTROY_WALLS,1,1)

        DOOR_AVAILABLE(PLAYER0,MAGIC,1,200)
        TRAP_AVAILABLE(PLAYER0,LIGHTNING,1,0)
        TRAP_AVAILABLE(PLAYER0,BOULDER,1,200)
        QUICK_INFORMATION(15,"送钱亮图，让你回味一下胜利的喜悦！",PLAYER0)

       WIN_GAME
    ENDIF
ENDIF
IF(PLAYER0,DUNGEON_DESTROYED == 1)
    LOSE_GAME
ENDIF


REM ****************************提示信息**********************************
QUICK_OBJECTIVE(1,"参考提示：1、人类心脏被打爆，人类部队被消灭你就胜利；2、重剑无锋，大巧不工；3、东边的家伙有监狱和陵墓，西边的家伙有哨所和工厂;4、利用好熔岩、疾病魔法等。",PLAYER0)  
IF(PLAYER0,GAME_TURN > 200)
    QUICK_INFORMATION(2,"重剑无锋，大巧不工，杀杀杀可能效率更高。",PLAYER0) 
ENDIF
IF(PLAYER0,GAME_TURN > 500)
    QUICK_INFORMATION(3,"人类心脏被打爆，人类部队被消灭你就胜利，如果你偷到破墙魔法书也许可以偷偷挖到人类心脏干掉人类。",PLAYER0) 
ENDIF
IF(PLAYER0,GAME_TURN > 1000)
    QUICK_INFORMATION(4,"东边的家伙有监狱和陵墓，西边的家伙有哨所和工厂，占领了你就能建造。",PLAYER0) 
ENDIF
IF(PLAYER0,GAME_TURN > 1500)
    QUICK_INFORMATION(5,"利用好熔岩、疾病魔法也许效果更好！",PLAYER0) 
ENDIF
IF(PLAYER0,GAME_TURN > 2000)
    QUICK_INFORMATION(6,"祝你好运，守护者！",PLAYER0) 
ENDIF







