REM ***********************************************************************
REM   Script for Level  478.Chaerith - Post Undead Keeper
REM ***********************************************************************
REM Made by Duke Ragereaver
REM 已经收录到FX的Post Undead Keeper战役中。（308）
REM ***********************************************************************
REM 地图的大概情况与参考建议：
REM 1、 。
REM 2、 。
REM 3、 。
REM 4、 。
REM 5、 。
REM ***********************************************************************
REM ***************************各种基本设置 *****************************
SET_GENERATE_SPEED(700)
COMPUTER_PLAYER(PLAYER3,0)
START_MONEY(PLAYER0,10000)
START_MONEY(PLAYER3,100000)
MAX_CREATURES(PLAYER0,17)
MAX_CREATURES(PLAYER3,32)


REM *******************************添加怪物到入口 *********************************
REM   (添加到入口的生物名字,数量)
ADD_CREATURE_TO_POOL(SPIDER,20)
ADD_CREATURE_TO_POOL(GHOST,20)
ADD_CREATURE_TO_POOL(VAMPIRE,1)

REM *******************************指定阵营（玩家）允许/禁止从入口（怪物池）中招指定生物*********************************
REM  (玩家,生物名称,1（只能是1）,[1允许招募/0禁止招募])
CREATURE_AVAILABLE(ALL_PLAYERS,GHOST,1,1)
CREATURE_AVAILABLE(PLAYER3,VAMPIRE,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,SPIDER,1,1)

REM ********************指定阵营（玩家）允许/禁止研究和建造指定房子（建筑）**********************************
REM  (玩家,房子名称,是否可研究或建造[1是允许；0是禁止],是否已经可建造[1已经可建/0待研究])
ROOM_AVAILABLE(ALL_PLAYERS,LAIR,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,GARDEN,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,TRAINING,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,RESEARCH,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,WORKSHOP,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,GUARD_POST,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,TORTURE,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,TREASURE,1,1)
ROOM_AVAILABLE(PLAYER3,TEMPLE,1,0)
ROOM_AVAILABLE(PLAYER3,GRAVEYARD,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,BRIDGE,1,0)

REM ********************指定阵营（玩家）允许/禁止研究和使用指定魔法**********************************
REM  (玩家,魔法名称,是否可研究或使用[1是允许；0是禁止],是否已经可使用[1已经可建/0待研究])
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HAND,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_IMP,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SLAP,1,1)
MAGIC_AVAILABLE(PLAYER3,POWER_SIGHT,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CALL_TO_ARMS,1,0)
MAGIC_AVAILABLE(PLAYER1,POWER_HEAL_CREATURE,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_PROTECT,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CONCEAL,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CHICKEN,1,0)
MAGIC_AVAILABLE(PLAYER3,POWER_LIGHTNING,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CAVE_IN,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SPEED,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_DISEASE,1,0)

REM *********************指定阵营（玩家）允许/禁止制造指定的门*********************************
REM  (玩家,门的名称,[1是允许；0是禁止],1[0是允许；数字是已经制造出来/赠送的数量]
DOOR_AVAILABLE(ALL_PLAYERS,STEEL,1,0)
DOOR_AVAILABLE(ALL_PLAYERS,MAGIC,1,0)

REM *********************指定阵营（玩家）允许/禁止制造指定陷阱*********************************
REM  (玩家,陷阱名称,[1是允许；0是禁止],1[0是允许；数字是已经制造出来/赠送的数量]
TRAP_AVAILABLE(PLAYER0,ALARM,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,POISON_GAS,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,LAVA,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,LIGHTNING,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,WORD_OF_POWER,1,0)

REM ****************************增援部队**********************************
REM CREATE_PARTY =>组建队伍                (队伍名称)   
REM ADD_TO_PARTY=>添加生物到队伍中   (队伍名称,生物名称,生物等级,携带金钱数量,任务,延时)
CREATE_PARTY(ONE)
    ADD_TO_PARTY(ONE,THIEF,5,200,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(ONE,THIEF,4,150,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(ONE,THIEF,4,300,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(ONE,ARCHER,4,250,DEFEND_PARTY,0)
    ADD_TO_PARTY(ONE,DWARFA,3,200,STEAL_GOLD,0)
CREATE_PARTY(TWO)
    ADD_TO_PARTY(TWO,BARBARIAN,4,300,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(TWO,BARBARIAN,5,350,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(TWO,ARCHER,4,250,DEFEND_PARTY,0)
    ADD_TO_PARTY(TWO,ARCHER,5,300,DEFEND_PARTY,0)
CREATE_PARTY(THREE)
    ADD_TO_PARTY(THREE,MONK,5,400,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(THREE,MONK,4,500,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(THREE,ARCHER,3,300,DEFEND_PARTY,0)
    ADD_TO_PARTY(THREE,ARCHER,3,300,DEFEND_PARTY,0)
CREATE_PARTY(FOUR)
    ADD_TO_PARTY(FOUR,SAMURAI,7,500,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(FOUR,GIANT,6,750,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(FOUR,WIZARD,7,400,DEFEND_PARTY,0)
    ADD_TO_PARTY(FOUR,ARCHER,6,400,DEFEND_PARTY,0)
CREATE_PARTY(BRUTES)
    ADD_TO_PARTY(BRUTES,GIANT,6,400,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(BRUTES,GIANT,6,400,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(BRUTES,GIANT,6,400,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(BRUTES,GIANT,6,400,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(BRUTES,GIANT,6,400,DEFEND_PARTY,0)
    ADD_TO_PARTY(BRUTES,GIANT,6,400,DEFEND_PARTY,0)
CREATE_PARTY(SCOUTS)
    ADD_TO_PARTY(SCOUTS,DWARFA,8,500,STEAL_GOLD,0)
    ADD_TO_PARTY(SCOUTS,THIEF,8,600,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(SCOUTS,THIEF,8,500,DEFEND_PARTY,0)
    ADD_TO_PARTY(SCOUTS,THIEF,8,400,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(SCOUTS,ARCHER,9,300,DEFEND_PARTY,0)
    ADD_TO_PARTY(SCOUTS,ARCHER,8,200,DEFEND_PARTY,0)
    ADD_TO_PARTY(SCOUTS,ARCHER,8,200,DEFEND_PARTY,0)
CREATE_PARTY(MYSTIC)
    ADD_TO_PARTY(MYSTIC,MONK,8,500,DEFEND_PARTY,0)
    ADD_TO_PARTY(MYSTIC,WIZARD,8,750,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(MYSTIC,MONK,9,1000,DEFEND_PARTY,0)
    ADD_TO_PARTY(MYSTIC,WITCH,8,500,STEAL_SPELLS,0)
    ADD_TO_PARTY(MYSTIC,SAMURAI,8,500,DEFEND_PARTY,0)
CREATE_PARTY(ELITES)
    ADD_TO_PARTY(ELITES,KNIGHT,10,1000,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(ELITES,WIZARD,8,1250,STEAL_SPELLS,0)
    ADD_TO_PARTY(ELITES,BARBARIAN,8,800,DEFEND_PARTY,0)
    ADD_TO_PARTY(ELITES,GIANT,8,800,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(ELITES,SAMURAI,9,750,DEFEND_PARTY,0)
    ADD_TO_PARTY(ELITES,BARBARIAN,8,800,DEFEND_PARTY,0)
    ADD_TO_PARTY(ELITES,GIANT,8,800,ATTACK_ENEMIES,0)


REM ****************************脚本主体(主要内容、核心内容）**********************************
SET_COMPUTER_PROCESS(PLAYER3,"BUILD A GRAVEYARD ROOM",0,5,6,11,0)
SET_TIMER(PLAYER0,TIMER0)
SET_TIMER(PLAYER3,TIMER0)

REM *************循环事件***********************
IF(PLAYER0,TIMER0 >= 700)
    IF(PLAYER0,TOTAL_CREATURES < 17)
        NEXT_COMMAND_REUSABLE
        ADD_CREATURE_TO_LEVEL(PLAYER0,SKELETON,1,1,1,0)
        NEXT_COMMAND_REUSABLE
        SET_TIMER(PLAYER0,TIMER0)
    ENDIF
ENDIF
IF(PLAYER0,TIMER2 >= 700)
    IF(PLAYER3,TOTAL_CREATURES < 32)
        NEXT_COMMAND_REUSABLE
        ADD_CREATURE_TO_LEVEL(PLAYER3,SKELETON,2,1,1,0)
        NEXT_COMMAND_REUSABLE
        SET_TIMER(PLAYER3,TIMER0)
    ENDIF
ENDIF

IF(PLAYER3,WORKSHOP >= 72)
    NEXT_COMMAND_REUSABLE
    ROOM_AVAILABLE(PLAYER3,WORKSHOP,0,0)
ENDIF
IF(PLAYER3,WORKSHOP < 72)
    NEXT_COMMAND_REUSABLE
    ROOM_AVAILABLE(PLAYER3,WORKSHOP,1,1)
ENDIF

REM *************触发点事件***********************
IF_ACTION_POINT(3,PLAYER0)
    SET_TIMER(PLAYER_GOOD,TIMER0)
ENDIF
IF(PLAYER_GOOD,TIMER0 >= 3750)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,TWO,-2,1)
ENDIF
IF(PLAYER_GOOD,TIMER0 >= 5000)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,THREE,-1,1)
ENDIF
IF(PLAYER_GOOD,TIMER0 >= 1750)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,ONE,-1,1)
ENDIF
IF_ACTION_POINT(4,PLAYER0)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,ONE,-1,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,THREE,-1,1)
ENDIF
IF_ACTION_POINT(5,PLAYER0)
    SET_TIMER(PLAYER_GOOD,TIMER1)
ENDIF
IF(PLAYER_GOOD,TIMER1 >= 1875)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,BRUTES,-3,1)
ENDIF
IF(PLAYER_GOOD,TIMER1 >= 3750)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,FOUR,-4,1)
ENDIF
IF_ACTION_POINT(6,PLAYER0)
    SET_TIMER(PLAYER_GOOD,TIMER2)
ENDIF
IF(PLAYER_GOOD,TIMER2 >= 500)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,BRUTES,-5,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SCOUTS,-6,1)
ENDIF
IF(PLAYER_GOOD,TIMER2 >= 1500)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,MYSTIC,-8,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,ELITES,-7,1)
    SET_FLAG(PLAYER_GOOD,FLAG1,1)
ENDIF

REM ****************************胜利/失败条件**********************************
IF(PLAYER0,ALL_DUNGEONS_DESTROYED == 1)
    IF(PLAYER_GOOD,TOTAL_CREATURES == 0)
        IF(PLAYER_GOOD,FLAG1 == 1)
            REM QUICK_OBJECTIVE(1,"Nex wanted to make this realm a paradise of darkness. It will be one but, rightfully, under your banner!",PLAYER0)
            QUICK_OBJECTIVE(8,"NEX想将这王国变成一个黑暗天堂，这将是理所当然的，但是，插上的是你的旗帜！",PLAYER0)

            REM 以下修改游戏，增加胜利后显示全图、送钱开放建造限制，让你回味一下胜利的喜悦！
            LEVEL_VERSION(1)
            REVEAL_MAP_RECT(PLAYER0,127,127,254,254)
            ADD_GOLD_TO_PLAYER (PLAYER0,9999999)

            ROOM_AVAILABLE(PLAYER0,TREASURE,1,1)
            ROOM_AVAILABLE(PLAYER0,GARDEN,1,1)
            ROOM_AVAILABLE(PLAYER0,LAIR,1,1)
            ROOM_AVAILABLE(PLAYER0,RESEARCH,1,1)
            ROOM_AVAILABLE(PLAYER0,TRAINING,1,1)
            ROOM_AVAILABLE(PLAYER0,BRIDGE,1,1)
            ROOM_AVAILABLE(PLAYER0,GUARD_POST,1,1)
            ROOM_AVAILABLE(PLAYER0,WORKSHOP,1,1)
            ROOM_AVAILABLE(PLAYER0,BARRACKS,1,1)
            ROOM_AVAILABLE(PLAYER0,TEMPLE,1,1)
            ROOM_AVAILABLE(PLAYER0,PRISON,1,1)
            ROOM_AVAILABLE(PLAYER0,TORTURE,1,1)
            ROOM_AVAILABLE(PLAYER0,SCAVENGER,1,1)
            ROOM_AVAILABLE(PLAYER0,GRAVEYARD,1,1)
            
            MAGIC_AVAILABLE(PLAYER0,POWER_IMP,1,1)
            MAGIC_AVAILABLE(PLAYER0,POWER_CALL_TO_ARMS,1,1
            MAGIC_AVAILABLE(PLAYER0,POWER_SPEED,1,1)
            MAGIC_AVAILABLE(PLAYER0,POWER_CONCEAL,1,1)
            MAGIC_AVAILABLE(PLAYER0,POWER_HEAL_CREATURE,1,1)
            MAGIC_AVAILABLE(PLAYER0,POWER_DESTROY_WALLS,1,1)

            DOOR_AVAILABLE(PLAYER0,MAGIC,1,200)
            TRAP_AVAILABLE(PLAYER0,LIGHTNING,1,0)
            TRAP_AVAILABLE(PLAYER0,BOULDER,1,200)
            QUICK_INFORMATION(15,"亮图送钱，让你回味一下胜利的喜悦！",PLAYER0) 

            WIN_GAME
        ENDIF
    ENDIF
ENDIF

REM ****************************提示信息**********************************
QUICK_OBJECTIVE(1,"参考提示：1、早期可抢到一个只能挖一面的富矿；2、敌人不久就会从外围的湖中传送过来；3、东南方有个显示地图的宝物；4、不要打紫金矿的主意，那个胜利后有破墙魔法才能挖。",PLAYER0) 

REM QUICK_OBJECTIVE(0,"The Heroes have restored a fortress here that belonged to some legendary Samurai warrior. Alas, he was slain along with his men and his castle fell into ruin. I must say this was not a smart move on the Heroes' part because the piles of corpses will provide you with a steady amount of warriors. However, you are not the only one with the ability of necromancy, Nex seeks to make this realm a paradise of darkness. Destroy her, and raze this fortress to the ground. It seems like deja vu all over again!",PLAYER0)
QUICK_INFORMATION(2,"英雄们修复了属于某个传奇忍者武士的堡垒，唉，他和他的部下一起被杀，他的城堡也毁了。我必须说这不是英雄们的明智之举，因为成堆的尸体将为你提供稳定的战士数量。然而，你并不是唯一一个巫术的能力，NEX试图使这王国变成黑暗天堂，毁灭她，夷平这座堡垒，好像似曾相识！",PLAYER0)

IF(PLAYER0,GAME_TURN > 200)
    QUICK_INFORMATION(3,"附近有只能挖一面的富矿，经济上马虎能解决，建议训练8个左右骷髅到10级就去干掉守护者。",PLAYER0) 
ENDIF
IF(PLAYER0,GAME_TURN > 500)
    QUICK_INFORMATION(4,"守护者附近虽然有富矿，由于地形限制，它不会发展得很好，干掉它就可以完全解决经济问题！",PLAYER0) 
ENDIF
IF(PLAYER0,GAME_TURN > 1000)
    QUICK_INFORMATION(5,"经济问题解决后可以养些高薪兵用于攻打人类。",PLAYER0) 
ENDIF
IF(PLAYER0,GAME_TURN > 1500)
    QUICK_INFORMATION(6,"人类兵力虽然很强大，但各自为政，你可以逐个击破。",PLAYER0) 
ENDIF
IF(PLAYER0,GAME_TURN > 2000)
    QUICK_INFORMATION(7,"祝你好运，守护者！",PLAYER0) 
ENDIF











