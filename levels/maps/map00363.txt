REM ***********************************************************************
REM   Script for Level  363(063). Unsettl Fortress 攻不下的要塞
REM ***********************************************************************
REM 
REM 
REM ***********************************************************************
REM 地图的大概情况与参考建议：
REM 1、 。
REM 2、 。
REM 3、 。
REM 4、 。
REM 5、 。
REM ***********************************************************************
REM ***************************各种基本设置 *****************************
REM 速度400，最大人口35，初始金钱5000
SET_GENERATE_SPEED(400)
MAX_CREATURES(PLAYER0,35)
START_MONEY(PLAYER0,5000)

REM *******************************添加怪物到入口 *********************************
REM   (添加到入口的生物名字,数量)
REM 蟑螂、苍蝇、蜘蛛、精灵
ADD_CREATURE_TO_POOL(BUG,50)
ADD_CREATURE_TO_POOL(FLY,35)
ADD_CREATURE_TO_POOL(SPIDER,75)
ADD_CREATURE_TO_POOL(TROLL,50)

REM *******************************指定阵营（玩家）允许/禁止从入口（怪物池）中招指定生物*********************************
REM  (玩家,生物名称,1（只能是1）,[1允许招募/0禁止招募])

CREATURE_AVAILABLE(PLAYER0,FLY,1,1)
CREATURE_AVAILABLE(PLAYER0,BUG,1,1)
CREATURE_AVAILABLE(PLAYER0,SPIDER,1,1)
CREATURE_AVAILABLE(PLAYER0,TROLL,1,1)

REM ********************指定阵营（玩家）允许/禁止研究和建造指定房子（建筑）**********************************
REM  (玩家,房子名称,是否可研究或建造[1是允许；0是禁止],是否已经可建造[1已经可建/0待研究])
REM 金库、巢穴、孵化室、图书馆、训练室、工厂、哨所、审讯室、兵营
ROOM_AVAILABLE(PLAYER0,TREASURE,1,1)
ROOM_AVAILABLE(PLAYER0,LAIR,1,1)
ROOM_AVAILABLE(PLAYER0,GARDEN,1,1)
ROOM_AVAILABLE(PLAYER0,TRAINING,1,1)
ROOM_AVAILABLE(PLAYER0,RESEARCH,1,1)
ROOM_AVAILABLE(PLAYER0,WORKSHOP,1,0)
ROOM_AVAILABLE(PLAYER0,GUARD_POST,1,0)
ROOM_AVAILABLE(PLAYER0,TORTURE,1,0)
ROOM_AVAILABLE(PLAYER0,BARRACKS,1,0)

REM ********************指定阵营（玩家）允许/禁止研究和使用指定魔法**********************************
REM  (玩家,魔法名称,是否可研究或使用[1是允许；0是禁止],是否已经可使用[1已经可建/0待研究])
MAGIC_AVAILABLE(PLAYER0,POWER_HAND,1,1)
MAGIC_AVAILABLE(PLAYER0,POWER_SLAP,1,1)
MAGIC_AVAILABLE(PLAYER0,POWER_CALL_TO_ARMS,1,0)
MAGIC_AVAILABLE(PLAYER0,POWER_HOLD_AUDIENCE,1,0)
MAGIC_AVAILABLE(PLAYER0,POWER_HEAL_CREATURE,1,0)
MAGIC_AVAILABLE(PLAYER0,POWER_PROTECT,1,0)
MAGIC_AVAILABLE(PLAYER0,POWER_CONCEAL,1,0)
MAGIC_AVAILABLE(PLAYER0,POWER_DISEASE,1,0)

REM *********************指定阵营（玩家）允许/禁止制造指定的门*********************************
REM  (玩家,门的名称,[1是允许；0是禁止],1[0是允许；数字是已经制造出来/赠送的数量]
DOOR_AVAILABLE(PLAYER0,STEEL,1,0)
DOOR_AVAILABLE(PLAYER0,MAGIC,1,0)

REM *********************指定阵营（玩家）允许/禁止制造指定陷阱*********************************
REM  (玩家,陷阱名称,[1是允许；0是禁止],1[0是允许；数字是已经制造出来/赠送的数量]
TRAP_AVAILABLE(PLAYER0,ALARM,1,0)
TRAP_AVAILABLE(PLAYER0,POISON_GAS,1,0)
TRAP_AVAILABLE(PLAYER0,WORD_OF_POWER,1,0)
TRAP_AVAILABLE(PLAYER0,LAVA,1,0)

REM ****************************增援部队**********************************
REM CREATE_PARTY =>组建队伍                (队伍名称)   
REM ADD_TO_PARTY=>添加生物到队伍中   (队伍名称,生物名称,生物等级,携带金钱数量,任务,延时)
REM 男巫、仙女、巨人、僧侣、忍者
CREATE_PARTY(GUARDIANS)
    ADD_TO_PARTY(GUARDIANS,WIZARD,5,200,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(GUARDIANS,FAIRY,5,100,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(GUARDIANS,GIANT,5,100,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(GUARDIANS,MONK,7,200,DEFEND_PARTY,0)
    ADD_TO_PARTY(GUARDIANS,SAMURAI,5,300,ATTACK_ENEMIES,0)
REM 第一队敌兵：弓箭手、野蛮人、巨人、地狱犬
CREATE_PARTY(ONE)
    ADD_TO_PARTY(ONE,ARCHER,5,500,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(ONE,BARBARIAN,5,500,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(ONE,GIANT,5,500,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(ONE,HELL_HOUND,6,600,ATTACK_ROOMS,0)
REM 第二队敌兵：男巫、忍者、贼、僧侣、龙
CREATE_PARTY(TWO)
    ADD_TO_PARTY(TWO,WIZARD,5,500,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(TWO,SAMURAI,5,500,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(TWO,THIEF,5,500,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(TWO,MONK,6,500,DEFEND_PARTY,0)
    ADD_TO_PARTY(TWO,DRAGON,6,600,ATTACK_ENEMIES,0)
REM 第三队敌兵：男巫、仙女、僧侣、女巫、骨骼兵
CREATE_PARTY(THREE)
    ADD_TO_PARTY(THREE,WIZARD,5,500,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(THREE,FAIRY,6,500,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(THREE,FAIRY,6,500,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(THREE,MONK,6,500,DEFEND_PARTY,0)
    ADD_TO_PARTY(THREE,WITCH,7,500,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(THREE,SKELETON,8,1000,ATTACK_ENEMIES,0)
REM 第四队敌兵：野蛮人、弓箭手、贼
CREATE_PARTY(FOUR)
    ADD_TO_PARTY(FOUR,BARBARIAN,3,250,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(FOUR,ARCHER,3,250,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(FOUR,THIEF,3,250,ATTACK_ENEMIES,0)
REM 第五队敌兵：巨人、忍者、僧侣
CREATE_PARTY(FIVE)
    ADD_TO_PARTY(FIVE,GIANT,3,250,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(FIVE,SAMURAI,3,250,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(FIVE,MONK,3,250,ATTACK_ENEMIES,0)
REM 第六队敌兵：女巫、仙女、矮人
CREATE_PARTY(SIX)
    ADD_TO_PARTY(SIX,WITCH,3,250,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(SIX,FAIRY,3,250,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(SIX,DWARFA,3,250,ATTACK_ENEMIES,0)

REM ****************************脚本主体(主要内容、核心内容）**********************************
REM 当挖了100000金子后，触发时钟，敌人出现。
IF(PLAYER0,TOTAL_GOLD_MINED >= 150000)
    SET_TIMER(PLAYER0,TIMER1)
ENDIF
REM 2000 game_turn后敌人进犯
IF(PLAYER0,TIMER1 >= 2000)
    ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,FOUR,-1,DUNGEON,0,9,50)
    ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,FIVE,-2,DUNGEON,0,9,50)
    ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,SIX,-3,DUNGEON,0,9,50)
ENDIF

REM 到达触发点1，可以研究桥
IF_ACTION_POINT(1,PLAYER0)
    ROOM_AVAILABLE(PLAYER0,BRIDGE,1,0)
ENDIF

REM 世界末日的守护神召集部队(到达触发点2，一批敌兵进犯)
IF_ACTION_POINT(2,PLAYER0)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,GUARDIANS,2,2)
ENDIF

REM IF_ACTION_POINT(3,PLAYER0)
REM DISPLAY_INFORMATION(1,PLAYER0)
REM ENDIF

REM 水怪死光后，可以建寺庙
IF(PLAYER_GOOD,TENTACLE == 0)
    ROOM_AVAILABLE(PLAYER0,TEMPLE,1,0)
ENDIF

REM 敌人的救援部队和开始计时器
IF_ACTION_POINT(4,PLAYER0)
    ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,ONE,-1,DUNGEON,0,8,50)
    SET_TIMER(PLAYER0,TIMER0)
ENDIF
REM 敌人增援部队
IF(PLAYER0,TIMER0 >= 500)
    ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,TWO,-2,DUNGEON,0,9,50)
ENDIF
REM  敌人增援部队2
IF(PLAYER0,TIMER0 >= 1000)
    ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,THREE,-3,DUNGEON,0,9,50)
    SET_FLAG(PLAYER0,FLAG0,1)
ENDIF

REM 骷髅兵死光，可研究监狱
IF(PLAYER0,FLAG0 == 1)
    IF(PLAYER_GOOD,SKELETON == 0)
        ROOM_AVAILABLE(PLAYER0,PRISON,1,0)
    ENDIF
ENDIF

REM ****************************胜利/失败条件**********************************
REM 歼灭敌人，就胜利
IF(PLAYER_GOOD,TOTAL_CREATURES == 0)

    REM 以下修改游戏，增加胜利后可以建造其它房子，让你回味一下胜利的喜悦！
    LEVEL_VERSION(1)
    REVEAL_MAP_RECT(PLAYER0,127,127,254,254)
    ADD_GOLD_TO_PLAYER (PLAYER0,9999999)

    ROOM_AVAILABLE(PLAYER0,TREASURE,1,1)
    ROOM_AVAILABLE(PLAYER0,GARDEN,1,1)
    ROOM_AVAILABLE(PLAYER0,LAIR,1,1)
    ROOM_AVAILABLE(PLAYER0,RESEARCH,1,1)
    ROOM_AVAILABLE(PLAYER0,TRAINING,1,1)
    ROOM_AVAILABLE(PLAYER0,BRIDGE,1,1)
    ROOM_AVAILABLE(PLAYER0,GUARD_POST,1,1)
    ROOM_AVAILABLE(PLAYER0,WORKSHOP,1,1)
    ROOM_AVAILABLE(PLAYER0,BARRACKS,1,1)
    ROOM_AVAILABLE(PLAYER0,TEMPLE,1,1)
    ROOM_AVAILABLE(PLAYER0,PRISON,1,1)
    ROOM_AVAILABLE(PLAYER0,TORTURE,1,1)
    ROOM_AVAILABLE(PLAYER0,SCAVENGER,1,1)
    ROOM_AVAILABLE(PLAYER0,GRAVEYARD,1,1)
    
    MAGIC_AVAILABLE(PLAYER0,POWER_IMP,1,1)
    MAGIC_AVAILABLE(PLAYER0,POWER_CALL_TO_ARMS,1,1
    MAGIC_AVAILABLE(PLAYER0,POWER_SPEED,1,1)
    MAGIC_AVAILABLE(PLAYER0,POWER_HEAL_CREATURE,1,1)
    MAGIC_AVAILABLE(PLAYER0,POWER_DESTROY_WALLS,1,1)

    DOOR_AVAILABLE(PLAYER0,MAGIC,1,200)
    TRAP_AVAILABLE(PLAYER0,LIGHTNING,1,0)
    TRAP_AVAILABLE(PLAYER0,BOULDER,1,200)
    QUICK_INFORMATION(15,"送钱亮图，让你回味一下胜利的喜悦！",PLAYER0)

    WIN_GAME
ENDIF
REM Defeat on annihilation on all players Imps 小鬼死光就失败
IF(PLAYER0,TOTAL_IMPS == 0)
    LOSE_GAME
ENDIF

REM ****************************提示信息**********************************
QUICK_OBJECTIVE(1,"参考提示：1、制造小鬼的魔法书遗落在你家附近的野外；2、传说某金矿内有个地方，对研究造桥术很有帮助；3、水怪控制了建寺庙的方法，骸骨兵控制了建监狱的方法，干掉它们，你就可以建相应的房子了！4、听说有个黑暗女王被困在东北角某地方，如果你救了她，她会加入到你的队伍中。",PLAYER0)  
IF(PLAYER0,GAME_TURN > 500)
    QUICK_INFORMATION(2,"你的大陆上，有三个人类的传送门，建议你用墙把它们封起来，你现在的兵实在太弱了！",PLAYER0) 
ENDIF
IF(PLAYER0,GAME_TURN > 1500)
    QUICK_INFORMATION(3,"本人居民说，西南面有个紫金矿没有敌人把守的，看来你的经济问题可以解决了！",PLAYER0) 
ENDIF
IF(PLAYER0,GAME_TURN > 2500)
    QUICK_INFORMATION(4,"本人居民说，西北方有个黑暗女王被困住了，如果谁救她，她就为谁卖命！",PLAYER0) 
ENDIF
IF(PLAYER0,GAME_TURN > 3500)
    QUICK_INFORMATION(5,"在这里，水怪垄断了寺庙的建造方法，骷髅兵垄断了监狱的建造方法，把它们干掉，你就可以研究出建寺庙和监狱的方法，因为你实在太需要寺庙和监狱了！！",PLAYER0) 
ENDIF
IF(PLAYER0,GAME_TURN > 4000)
    QUICK_INFORMATION(6,"这里没有人会破墙魔法，如果你想和你的敌人做个了断，你就得找到末日审判的魔法，传说它在东南面的另一个大陆上，祝你好运，守护者！",PLAYER0) 
ENDIF





















