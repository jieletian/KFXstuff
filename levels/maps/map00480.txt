REM ***********************************************************************
REM   Script for Level  480.Elysium - Post Undead Keeper
REM ***********************************************************************
REM Made by Duke Ragereaver
REM 已经收录到FX的Post Undead Keeper战役中。（310）
REM ***********************************************************************
REM 地图的大概情况与参考建议：
REM 1、 。
REM 2、 。
REM 3、 。
REM 4、 。
REM 5、 。
REM ***********************************************************************
REM ***************************各种基本设置 *****************************
START_MONEY(PLAYER0,39500)

COMPUTER_PLAYER(PLAYER1,8)
COMPUTER_PLAYER(PLAYER2,8)
COMPUTER_PLAYER(PLAYER3,8)

ALLY_PLAYERS(PLAYER0,PLAYER1)
ALLY_PLAYERS(PLAYER0,PLAYER2)
ALLY_PLAYERS(PLAYER0,PLAYER3)
ALLY_PLAYERS(PLAYER1,PLAYER3)
ALLY_PLAYERS(PLAYER2,PLAYER3)
ALLY_PLAYERS(PLAYER1,PLAYER2)

REM ****************************增援部队**********************************
REM CREATE_PARTY =>组建队伍                (队伍名称)   
REM ADD_TO_PARTY=>添加生物到队伍中   (队伍名称,生物名称,生物等级,携带金钱数量,任务,延时)
CREATE_PARTY(BOULDER_FAIL)
    ADD_TO_PARTY(BOULDER_FAIL,GIANT,6,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(BOULDER_FAIL,GIANT,6,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(BOULDER_FAIL,ARCHER,4,0,DEFEND_PARTY,0)
    ADD_TO_PARTY(BOULDER_FAIL,ARCHER,4,0,DEFEND_PARTY,0)
CREATE_PARTY(DEFENDERS_ONE)
    ADD_TO_PARTY(DEFENDERS_ONE,GIANT,7,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(DEFENDERS_ONE,GIANT,7,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(DEFENDERS_ONE,BARBARIAN,7,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(DEFENDERS_ONE,BARBARIAN,7,0,ATTACK_ENEMIES,0)
CREATE_PARTY(DEFENDERS_TWO)
    ADD_TO_PARTY(DEFENDERS_TWO,THIEF,7,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(DEFENDERS_TWO,THIEF,7,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(DEFENDERS_TWO,ARCHER,7,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(DEFENDERS_TWO,ARCHER,7,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(DEFENDERS_TWO,DWARFA,7,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(DEFENDERS_TWO,THIEF,7,0,ATTACK_ENEMIES,0)
CREATE_PARTY(SKELETONS)
    ADD_TO_PARTY(SKELETONS,SKELETON,8,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(SKELETONS,SKELETON,10,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(SKELETONS,SKELETON,6,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(SKELETONS,SKELETON,6,0,ATTACK_ENEMIES,0)
CREATE_PARTY(GHOSTS)
    ADD_TO_PARTY(GHOSTS,GHOST,8,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(GHOSTS,GHOST,10,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(GHOSTS,GHOST,6,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(GHOSTS,GHOST,6,0,ATTACK_ENEMIES,0)
CREATE_PARTY(SPIDERS)
    ADD_TO_PARTY(SPIDERS,SPIDER,8,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(SPIDERS,SPIDER,10,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(SPIDERS,SPIDER,6,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(SPIDERS,SPIDER,6,0,ATTACK_ENEMIES,0)
CREATE_PARTY(LAST_STAND)
    ADD_TO_PARTY(LAST_STAND,SORCEROR,7,0,DEFEND_PARTY,0)
    ADD_TO_PARTY(LAST_STAND,WIZARD,8,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(LAST_STAND,ARCHER,6,0,DEFEND_PARTY,0)
    ADD_TO_PARTY(LAST_STAND,GIANT,6,0,DEFEND_PARTY,0)
    ADD_TO_PARTY(LAST_STAND,SAMURAI,9,0,DEFEND_PARTY,0)



REM ****************************脚本主体(主要内容、核心内容）**********************************
REM 强化吸血鬼、圣者，一定程度上加强了蜘蛛、鬼魂、骷髅
SET_CREATURE_HEALTH(VAMPIRE,3250)
SET_CREATURE_STRENGTH(VAMPIRE,175)
SET_CREATURE_ARMOUR(VAMPIRE,150)

SET_CREATURE_HEALTH(AVATAR,3250)
SET_CREATURE_ARMOUR(AVATAR,150)

SET_CREATURE_HEALTH(SPIDER,550)
SET_CREATURE_HEALTH(GHOST,400)
SET_CREATURE_HEALTH(SKELETON,650)


REM *************触发点事件***********************
IF_ACTION_POINT(2,PLAYER0)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,THIEF,-1,1,4,0)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,ARCHER,-2,1,3,0)
ENDIF
IF_ACTION_POINT(3,PLAYER0)
    SET_FLAG(PLAYER1,FLAG0,1)
    REM QUICK_OBJECTIVE(3,"You have reached and secured one of the power structures. Remember, the goal is to secure them, NOT destroy them!",PLAYER0)
    QUICK_INFORMATION(8,"你已经达到并保护了一个电源结构（蓝色守护者），记住，我们的目标是保卫他们，而不是摧毁他们！",PLAYER0)
ENDIF
IF_ACTION_POINT(4,PLAYER0)
    SET_FLAG(PLAYER2,FLAG0,1)
    REM QUICK_OBJECTIVE(4,"You have reached and secured one of the power structures. Remember, the goal is to secure them, NOT destroy them!",PLAYER0)
    QUICK_INFORMATION(9,"你已经达到并保护了一个电源结构（黄色守护者），记住，我们的目标是保卫他们，而不是摧毁他们！",PLAYER0)
ENDIF
IF_ACTION_POINT(5,PLAYER0)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,BOULDER_FAIL,6,1)
ENDIF
IF_ACTION_POINT(7,PLAYER0)
    SET_FLAG(PLAYER3,FLAG0,1)
    REM QUICK_OBJECTIVE(3,"You have reached and secured one of the power structures. Remember, the goal is to secure them, NOT destroy them!",PLAYER0)
    QUICK_INFORMATION(10,"你已经达到并保护了一个电源结构（绿色色守护者），记住，我们的目标是保卫他们，而不是摧毁他们！",PLAYER0)
ENDIF
REM Endgame
IF_ACTION_POINT(8,PLAYER0)
    SET_TIMER(PLAYER_GOOD,TIMER0)
    ADD_PARTY_TO_LEVEL(PLAYER2,GHOSTS,8,1)
ENDIF
IF(PLAYER_GOOD,TIMER0 >= 550)
    ADD_PARTY_TO_LEVEL(PLAYER3,SPIDERS,8,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,DEFENDERS_ONE,-3,1)
ENDIF
IF(PLAYER_GOOD,TIMER0 >= 1000)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,DEFENDERS_TWO,-3,1)
    ADD_PARTY_TO_LEVEL(PLAYER1,SKELETONS,8,1)
ENDIF
IF_ACTION_POINT(12,PLAYER0)
    ADD_PARTY_TO_LEVEL(PLAYER2,GHOSTS,-4,1)
    ADD_PARTY_TO_LEVEL(PLAYER3,SPIDERS,-5,1)
    ADD_PARTY_TO_LEVEL(PLAYER1,SKELETONS,12,2)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,SAMURAI,14,2,6,0)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,SAMURAI,13,1,7,0)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,THIEF,14,2,6,0)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,THIEF,13,1,7,0)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,MONK,13,3,6,0)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,BARBARIAN,13,2,6,0)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,ARCHER,14,6,4,0)
ENDIF
IF_ACTION_POINT(15,PLAYER0)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,THIEF,-8,3,5,0)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,ARCHER,-9,1,8,0)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,BARBARIAN,-6,2,8,0)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,GIANT,-7,2,6,0)
ENDIF
IF_ACTION_POINT(16,PLAYER0)
    ADD_PARTY_TO_LEVEL(PLAYER2,GHOSTS,16,1)
ENDIF
IF_ACTION_POINT(17,PLAYER0)
    ADD_PARTY_TO_LEVEL(PLAYER3,SPIDERS,17,1)
ENDIF
IF_ACTION_POINT(18,PLAYER0)
    ADD_PARTY_TO_LEVEL(PLAYER1,SKELETONS,18,1)
ENDIF


REM 到齐三个盟友心脏处可得一10级吸血鬼
IF(PLAYER1,FLAG0 == 1)
    IF(PLAYER2,FLAG0 == 1)
        IF(PLAYER3,FLAG0 == 1)
            ADD_CREATURE_TO_LEVEL(PLAYER0,VAMPIRE,1,1,10,9001)
            REM QUICK_OBJECTIVE(1,"Jeddiah has awakened and is very eager on vengeance on those had imprisoned him, lead on. Your forces will gradually appear as the battle progresses... as do some special allies...",PLAYER0)
            QUICK_INFORMATION(11,"jeddiah觉醒了，他非常渴望报复那些曾囚禁他的人，随着战斗的进展，你的部队将逐渐显现…像一些特殊的盟友…",PLAYER0)
        ENDIF
    ENDIF
ENDIF
IF(PLAYER_GOOD,GHOST == 0)
    TUTORIAL_FLASH_BUTTON(13,PLAYER0)
    MAGIC_AVAILABLE(PLAYER0,POWER_DESTROY_WALLS,1,1)
    REM QUICK_OBJECTIVE(2,"Use this spell to break down the walls and destroy the remaining opposition!",PLAYER0)
    QUICK_INFORMATION(12,"使用这个法术打破墙壁，摧毁剩余的反对派！",PLAYER0)
ENDIF

REM 到齐三处寺庙（得到盟友支持）到后面人类心脏的触发点19才会刷友军
IF_ACTION_POINT(9,PLAYER0)
    SET_FLAG(PLAYER1,FLAG1,1)
    REM QUICK_OBJECTIVE(4,"More reinforcements are on the way to assist you.",PLAYER0)
    QUICK_INFORMATION(13,"更多的援军（蓝色守护者）正在协助你。",PLAYER0)
ENDIF
IF_ACTION_POINT(10,PLAYER0)
    SET_FLAG(PLAYER2,FLAG1,1)
    REM QUICK_OBJECTIVE(4,"More reinforcements are on the way to assist you.",PLAYER0)
    QUICK_INFORMATION(14,"更多的援军（黄色守护者）正在协助你。",PLAYER0)
ENDIF
IF_ACTION_POINT(11,PLAYER0)
    SET_FLAG(PLAYER3,FLAG1,1)
    REM QUICK_OBJECTIVE(5,"More reinforcements are on the way to assist you.",PLAYER0)
    QUICK_INFORMATION(16,"更多的援军（绿色色守护者）正在协助你。",PLAYER0)
ENDIF


REM The gang, part 1 使用破墙魔法破坏了人类心脏南边那格泥才能到达触发点19，到达触发点19后就刷人类圣者等兵保护它的心脏
IF_ACTION_POINT(19,PLAYER0)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,AVATAR,PLAYER_GOOD,1,10,0)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,GIANT,-10,6,6,0)
    ADD_PARTY_TO_LEVEL(PLAYER1,SKELETONS,21,1)
    ADD_CREATURE_TO_LEVEL(PLAYER1,KNIGHT,20,1,10,0)
    ADD_CREATURE_TO_LEVEL(PLAYER2,KNIGHT,21,1,10,0)
    ADD_CREATURE_TO_LEVEL(PLAYER3,WIZARD,22,1,10,0)
    SET_FLAG(PLAYER_GOOD,FLAG0,1)
ENDIF

REM 到达触发点19后就有条件地启动人类和各盟友的时钟，准备刷相方于的兵
IF(PLAYER_GOOD,FLAG0 == 1)
    SET_TIMER(PLAYER_GOOD,TIMER1)
ENDIF
IF_ACTION_POINT(19,PLAYER0)
    IF(PLAYER1,FLAG1 == 1)
        SET_TIMER(PLAYER1,TIMER1)
    ENDIF
ENDIF
IF_ACTION_POINT(19,PLAYER0)
    IF(PLAYER2,FLAG1 == 1)
        SET_TIMER(PLAYER2,TIMER1)
    ENDIF
ENDIF
IF_ACTION_POINT(19,PLAYER0)
    IF(PLAYER3,FLAG1 == 1)
        SET_TIMER(PLAYER3,TIMER1)
    ENDIF
ENDIF

REM 有条件地刷人类兵(条件是人类心脏未被打爆和兵的数量小于等于20)
IF(PLAYER_GOOD,DUNGEON_DESTROYED == 0)
    IF(PLAYER_GOOD,TOTAL_CREATURES <= 20)
        IF(PLAYER_GOOD,TIMER1 > 1350)
            NEXT_COMMAND_REUSABLE
            ADD_PARTY_TO_LEVEL(PLAYER_GOOD,LAST_STAND,-10,1)
            NEXT_COMMAND_REUSABLE
            SET_TIMER(PLAYER_GOOD,TIMER1)
        ENDIF
    ENDIF
ENDIF

REM 有条件地刷各盟友的兵(条件是到达了相应的寺庙和兵的数量小于等于20)
REM The gang, part 2
IF(PLAYER1,TOTAL_CREATURES <= 20)
    IF(PLAYER1,TIMER1 > 450)
        ADD_CREATURE_TO_LEVEL(PLAYER2,DRAGON,22,1,10,0)
        NEXT_COMMAND_REUSABLE
        ADD_CREATURE_TO_LEVEL(PLAYER1,SKELETON,20,1,8,0)
        NEXT_COMMAND_REUSABLE
        SET_TIMER(PLAYER1,TIMER1)
    ENDIF
ENDIF
IF(PLAYER2,TOTAL_CREATURES <= 20)
    IF(PLAYER2,TIMER1 > 650)
        ADD_CREATURE_TO_LEVEL(PLAYER1,SORCEROR,20,1,10,0)
        NEXT_COMMAND_REUSABLE
        ADD_CREATURE_TO_LEVEL(PLAYER2,GHOST,21,1,8,0)
        NEXT_COMMAND_REUSABLE
        SET_TIMER(PLAYER2,TIMER1)
    ENDIF
ENDIF
IF(PLAYER3,TOTAL_CREATURES <= 20)
    IF(PLAYER3,TIMER1 > 950)
        ADD_CREATURE_TO_LEVEL(PLAYER3,THIEF,22,1,10,0)
        NEXT_COMMAND_REUSABLE
        ADD_CREATURE_TO_LEVEL(PLAYER3,SPIDER,22,1,8,0)
        NEXT_COMMAND_REUSABLE
        SET_TIMER(PLAYER3,TIMER1)
    ENDIF
ENDIF

REM ****************************胜利/失败条件**********************************
REM 胜利条件是FLAG0为1（即你到达触发点19）且人类圣者数量为0；
REM 失败条件是其中一盟友心脏被打爆或你的怪死光。
REM ------------------------------------------------
IF(PLAYER_GOOD,AVATAR == 0)
    IF(PLAYER_GOOD,FLAG0 == 1)

            REM 以下修改游戏，增加胜利后显示全图、送钱开放建造限制，让你回味一下胜利的喜悦！
            LEVEL_VERSION(1)
            REVEAL_MAP_RECT(PLAYER0,127,127,254,254)
            ADD_GOLD_TO_PLAYER (PLAYER0,9999999)
            ROOM_AVAILABLE(PLAYER0,TREASURE,1,1)
            ROOM_AVAILABLE(PLAYER0,GARDEN,1,1)
            ROOM_AVAILABLE(PLAYER0,LAIR,1,1)
            ROOM_AVAILABLE(PLAYER0,RESEARCH,1,1)
            ROOM_AVAILABLE(PLAYER0,TRAINING,1,1)
            ROOM_AVAILABLE(PLAYER0,BRIDGE,1,1)
            ROOM_AVAILABLE(PLAYER0,GUARD_POST,1,1)
            ROOM_AVAILABLE(PLAYER0,WORKSHOP,1,1)
            ROOM_AVAILABLE(PLAYER0,BARRACKS,1,1)
            ROOM_AVAILABLE(PLAYER0,TEMPLE,1,1)
            ROOM_AVAILABLE(PLAYER0,PRISON,1,1)
            ROOM_AVAILABLE(PLAYER0,TORTURE,1,1)
            ROOM_AVAILABLE(PLAYER0,SCAVENGER,1,1)
            ROOM_AVAILABLE(PLAYER0,GRAVEYARD,1,1)
    
            MAGIC_AVAILABLE(PLAYER0,POWER_IMP,1,1)
            MAGIC_AVAILABLE(PLAYER0,POWER_CALL_TO_ARMS,1,1)
            MAGIC_AVAILABLE(PLAYER0,POWER_SPEED,1,1)
            MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CONCEAL,1,1)
            MAGIC_AVAILABLE(PLAYER0,POWER_HEAL_CREATURE,1,1)
            MAGIC_AVAILABLE(PLAYER0,POWER_DESTROY_WALLS,1,1)

            DOOR_AVAILABLE(PLAYER0,MAGIC,1,999)
            TRAP_AVAILABLE(PLAYER0,LIGHTNING,1,0)
            TRAP_AVAILABLE(PLAYER0,BOULDER,1,500)
            QUICK_INFORMATION(15,"亮图送钱，让你回味一下胜利的喜悦！",PLAYER0) 

        REM QUICK_OBJECTIVE(5,"The enemies of darkness and death have been shown no mercy... as it should be.... well done...",PLAYER0)
        QUICK_OBJECTIVE(17,"黑暗与死亡的敌人已毫不留情…因为它理应如此…做得好...",PLAYER0)
        WIN_GAME
    ENDIF
ENDIF
IF(PLAYER1,DUNGEON_DESTROYED == 1)
    LOSE_GAME
ENDIF
IF(PLAYER2,DUNGEON_DESTROYED == 1)
    LOSE_GAME
ENDIF
IF(PLAYER3,DUNGEON_DESTROYED == 1)
    LOSE_GAME
ENDIF
IF(PLAYER0,TOTAL_CREATURES == 0)
    LOSE_GAME
ENDIF

REM ****************************提示信息**********************************
QUICK_OBJECTIVE(1,"参考提示：1、圣者、吸血鬼、蜘蛛、鬼魂、骷髅都被强化过，你和盟友心脏被打爆就失败；2、“踩”到三个盟友的心脏后就得一个吸血鬼，消灭敌人的鬼魂就得到破墙魔法；3、你“踩”到三个寺庙和人类心脏前某个点后，三个盟友就会源源不断地派兵攻人类，打爆敌人心脏敌人就没增援了；4、这个吸血鬼的一号技能杀伤力巨大。",PLAYER0) 

REM QUICK_OBJECTIVE(0,"The Ancient Spirits of Intregity, Valor and Justice keep this realm protected with their ancient magic. They just missed one tiny detail, the elderly Vampire Lord Jedidiah was burried here long ago by those same spirits and is in slumber, awake him! For that, you will need some power structures with the Heroes protecting them, find and secure them. The Heroes will not hesitate to destroy them if nessesary so keep your eyes on the rascals.",PLAYER0)
QUICK_INFORMATION(2,"intregity的远古精灵、勇气、正义与他们古老的魔法保护保护这王国，他们只是错过了一个微小的细节，很久以前吸血鬼老主人Jedidiah被同样沉睡的精灵埋在这里，唤醒他！为此，你需要一些权力结构与英雄保护他们，找到并确保他们。如果必要的话，英雄毫不犹豫地摧毁他们，盯紧那些无赖。",PLAYER0)

IF(PLAYER0,GAME_TURN > 200)
    QUICK_INFORMATION(3,"圣者、吸血鬼、蜘蛛、鬼魂、骷髅都被强化过，你和盟友心脏被打爆就失败，骷髅不怕毒气，鬼魂可穿过上锁的门。",PLAYER0) 
ENDIF
IF(PLAYER0,GAME_TURN > 500)
    QUICK_INFORMATION(4,"“踩”到三个盟友的心脏后就得一个超级吸血鬼，消灭敌人的鬼魂就得到破墙魔法，这样才可以打开人类心脏外的围墙。",PLAYER0) 
ENDIF
IF(PLAYER0,GAME_TURN > 1000)
    QUICK_INFORMATION(5,"你“踩”到三个寺庙和人类心脏前某个点后，三个盟友就会源源不断地派兵攻人类，打爆敌人心脏敌人就没增援了。",PLAYER0) 
ENDIF
IF(PLAYER0,GAME_TURN > 1500)
    QUICK_INFORMATION(6,"这个吸血鬼的一号技能杀伤力巨大，关住你家的门让吸血鬼瞬移可能更快找到敌人。",PLAYER0) 
ENDIF
IF(PLAYER0,GAME_TURN > 2000)
    QUICK_INFORMATION(7,"祝你好运，守护者！",PLAYER0) 
ENDIF













