REM ***********************************************************************
REM   Script for Level   476.Havoth Dir - Post Undead Keeper
REM ***********************************************************************
REM Made by Duke Ragereaver
REM 已经收录到FX的Post Undead Keeper战役中。
REM ***********************************************************************
REM 地图的大概情况与参考建议：
REM 1、 。
REM 2、 。
REM 3、 。
REM 4、 。
REM 5、 。
REM ***********************************************************************
REM ***************************各种基本设置 *****************************
REM ***************************各种基本设置 *****************************
START_MONEY(PLAYER0,50000)
SET_GENERATE_SPEED(450)

REM *******************************添加怪物到入口 *********************************
REM   (添加到入口的生物名字,数量)
ADD_CREATURE_TO_POOL(SKELETON,60)
ADD_CREATURE_TO_POOL(SPIDER,20)
ADD_CREATURE_TO_POOL(GHOST,10)
MAX_CREATURES(PLAYER0,60)

REM *******************************指定阵营（玩家）允许/禁止从入口（怪物池）中招指定生物*********************************
REM  (玩家,生物名称,1（只能是1）,[1允许招募/0禁止招募])
CREATURE_AVAILABLE(PLAYER0,SPIDER,1,1)
CREATURE_AVAILABLE(PLAYER0,SKELETON,1,1)
CREATURE_AVAILABLE(PLAYER0,GHOST,1,1)

REM ********************指定阵营（玩家）允许/禁止研究和建造指定房子（建筑）**********************************
REM  (玩家,房子名称,是否可研究或建造[1是允许；0是禁止],是否已经可建造[1已经可建/0待研究])
ROOM_AVAILABLE(ALL_PLAYERS,RESEARCH,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,WORKSHOP,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,GARDEN,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,LAIR,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,TORTURE,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,GRAVEYARD,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,TRAINING,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,TREASURE,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,TEMPLE,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,PRISON,1,1)

REM ********************指定阵营（玩家）允许/禁止研究和使用指定魔法**********************************
REM  (玩家,魔法名称,是否可研究或使用[1是允许；0是禁止],是否已经可使用[1已经可建/0待研究])
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HAND,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SLAP,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_IMP,1,1)

MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SIGHT,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CALL_TO_ARMS,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HOLD_AUDIENCE,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SPEED,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CONCEAL,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HEAL_CREATURE,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_DISEASE,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CHICKEN,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_LIGHTNING,1,0)

REM *********************指定阵营（玩家）允许/禁止制造指定的门*********************************
REM  (玩家,门的名称,[1是允许；0是禁止],1[0是允许；数字是已经制造出来/赠送的数量]
DOOR_AVAILABLE(ALL_PLAYERS,WOOD,1,0)
DOOR_AVAILABLE(ALL_PLAYERS,BRACED,1,0)
DOOR_AVAILABLE(ALL_PLAYERS,STEEL,1,0)
DOOR_AVAILABLE(ALL_PLAYERS,MAGIC,1,0)

REM *********************指定阵营（玩家）允许/禁止制造指定陷阱*********************************
REM  (玩家,陷阱名称,[1是允许；0是禁止],1[0是允许；数字是已经制造出来/赠送的数量]
TRAP_AVAILABLE(ALL_PLAYERS,ALARM,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,POISON_GAS,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,LIGHTNING,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,WORD_OF_POWER,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,BOULDER,1,0)

REM ****************************增援部队**********************************
REM CREATE_PARTY =>组建队伍                (队伍名称)   
REM ADD_TO_PARTY=>添加生物到队伍中   (队伍名称,生物名称,生物等级,携带金钱数量,任务,延时)
CREATE_PARTY(FIRST)
    ADD_TO_PARTY(FIRST,ARCHER,4,750,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(FIRST,BARBARIAN,5,1000,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(FIRST,ARCHER,4,750,DEFEND_PARTY,0)
    ADD_TO_PARTY(FIRST,THIEF,4,600,STEAL_GOLD,0)
    ADD_TO_PARTY(FIRST,ARCHER,4,750,DEFEND_PARTY,0)
CREATE_PARTY(SECOND)
    ADD_TO_PARTY(SECOND,THIEF,4,750,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(SECOND,THIEF,5,1000,STEAL_GOLD,0)
    ADD_TO_PARTY(SECOND,DWARFA,4,800,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(SECOND,DWARFA,4,800,DEFEND_PARTY,0)
    ADD_TO_PARTY(SECOND,THIEF,4,750,ATTACK_ENEMIES,0)
CREATE_PARTY(THIRD)
    ADD_TO_PARTY(THIRD,MONK,4,750,STEAL_SPELLS,0)
    ADD_TO_PARTY(THIRD,WIZARD,5,1000,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(THIRD,MONK,4,750,STEAL_SPELLS,0)
    ADD_TO_PARTY(THIRD,SAMURAI,4,800,DEFEND_PARTY,0)
    ADD_TO_PARTY(THIRD,ARCHER,4,700,DEFEND_PARTY,0)
CREATE_PARTY(FOUR)
    ADD_TO_PARTY(FOUR,GIANT,3,800,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(FOUR,GIANT,3,1000,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(FOUR,WITCH,5,800,STEAL_SPELLS,0)
    ADD_TO_PARTY(FOUR,WITCH,4,750,DEFEND_PARTY,0)
CREATE_PARTY(BRUTES_LIGHT)
    ADD_TO_PARTY(BRUTES_LIGHT,GIANT,6,1200,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(BRUTES_LIGHT,GIANT,6,1200,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(BRUTES_LIGHT,BARBARIAN,6,1200,DEFEND_PARTY,0)
    ADD_TO_PARTY(BRUTES_LIGHT,BARBARIAN,6,1200,STEAL_SPELLS,0)
CREATE_PARTY(SAMURAI_LIGHT)
    ADD_TO_PARTY(SAMURAI_LIGHT,SAMURAI,5,850,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(SAMURAI_LIGHT,SAMURAI,6,1000,STEAL_SPELLS,0)
    ADD_TO_PARTY(SAMURAI_LIGHT,SAMURAI,4,850,DEFEND_PARTY,0)
    ADD_TO_PARTY(SAMURAI_LIGHT,SAMURAI,4,850,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(SAMURAI_LIGHT,SAMURAI,5,950,DEFEND_PARTY,0)
CREATE_PARTY(MYSTIC_LIGHT)
    ADD_TO_PARTY(MYSTIC_LIGHT,SAMURAI,6,1000,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(MYSTIC_LIGHT,WIZARD,6,1200,STEAL_SPELLS,0)
    ADD_TO_PARTY(MYSTIC_LIGHT,MONK,6,900,DEFEND_PARTY,0)
    ADD_TO_PARTY(MYSTIC_LIGHT,ARCHER,6,800,DEFEND_PARTY,0)
    ADD_TO_PARTY(MYSTIC_LIGHT,GIANT,6,900,ATTACK_ROOMS,0)
CREATE_PARTY(BRUTES_HEAVY)
    ADD_TO_PARTY(BRUTES_HEAVY,GIANT,8,2500,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(BRUTES_HEAVY,GIANT,8,2500,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(BRUTES_HEAVY,BARBARIAN,8,2000,DEFEND_PARTY,0)
    ADD_TO_PARTY(BRUTES_HEAVY,BARBARIAN,8,2000,STEAL_SPELLS,0)
CREATE_PARTY(SAMURAI_HEAVY)
    ADD_TO_PARTY(SAMURAI_HEAVY,SAMURAI,7,1250,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(SAMURAI_HEAVY,SAMURAI,8,1500,STEAL_SPELLS,0)
    ADD_TO_PARTY(SAMURAI_HEAVY,SAMURAI,6,1000,DEFEND_PARTY,0)
    ADD_TO_PARTY(SAMURAI_HEAVY,SAMURAI,6,1000,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(SAMURAI_HEAVY,SAMURAI,7,1250,DEFEND_PARTY,0)
CREATE_PARTY(MYSTIC_HEAVY)
    ADD_TO_PARTY(MYSTIC_HEAVY,SAMURAI,8,1500,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(MYSTIC_HEAVY,WIZARD,9,1500,STEAL_SPELLS,0)
    ADD_TO_PARTY(MYSTIC_HEAVY,MONK,8,1000,DEFEND_PARTY,0)
    ADD_TO_PARTY(MYSTIC_HEAVY,ARCHER,8,1000,DEFEND_PARTY,0)
    ADD_TO_PARTY(MYSTIC_HEAVY,GIANT,7,1000,ATTACK_ROOMS,0)
CREATE_PARTY(SUPPORT)
    ADD_TO_PARTY(SUPPORT,ARCHER,8,1000,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(SUPPORT,BARBARIAN,8,1000,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(SUPPORT,ARCHER,8,1000,DEFEND_PARTY,0)
    ADD_TO_PARTY(SUPPORT,ARCHER,8,1000,DEFEND_PARTY,0)
    ADD_TO_PARTY(SUPPORT,BARBARIAN,8,1000,ATTACK_ENEMIES,0)
CREATE_PARTY(BOSS)
    ADD_TO_PARTY(BOSS,WIZARD,9,1500,STEAL_SPELLS,0)
    ADD_TO_PARTY(BOSS,KNIGHT,10,5000,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(BOSS,SAMURAI,9,1500,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(BOSS,GIANT,9,1500,DEFEND_PARTY,0)
    ADD_TO_PARTY(BOSS,ARCHER,9,1000,DEFEND_PARTY,0)
    ADD_TO_PARTY(BOSS,FAIRY,10,1000,STEAL_SPELLS,0)



REM ****************************脚本主体(主要内容、核心内容）**********************************

REM *************时钟事件***********************
SET_TIMER(PLAYER0,TIMER0)
IF(PLAYER0,TIMER0 >= 10750)
    ADD_TUNNELLER_TO_LEVEL(PLAYER_GOOD,-1,DUNGEON_HEART,0,5,1000)
    ADD_TUNNELLER_TO_LEVEL(PLAYER_GOOD,2,DUNGEON_HEART,0,4,1000)
ENDIF
IF(PLAYER0,TIMER0 >= 11750)
    ADD_TUNNELLER_TO_LEVEL(PLAYER_GOOD,-2,DUNGEON_HEART,0,5,1000)
    ADD_TUNNELLER_TO_LEVEL(PLAYER_GOOD,1,DUNGEON_HEART,0,4,1000)
ENDIF
IF(PLAYER0,TIMER0 >= 14000)
    ADD_TUNNELLER_TO_LEVEL(PLAYER_GOOD,-3,DUNGEON_HEART,0,5,1000)
    ADD_TUNNELLER_TO_LEVEL(PLAYER_GOOD,4,DUNGEON_HEART,0,4,1000)
ENDIF
IF(PLAYER0,TIMER0 >= 15250)
    ADD_TUNNELLER_TO_LEVEL(PLAYER_GOOD,-4,DUNGEON_HEART,0,5,1000)
    ADD_TUNNELLER_TO_LEVEL(PLAYER_GOOD,3,DUNGEON_HEART,0,4,1000)
    SET_FLAG(PLAYER0,FLAG0,1)
ENDIF

IF(PLAYER0,FLAG0 == 1)
    SET_TIMER(PLAYER_GOOD,TIMER0)
    SET_TIMER(PLAYER_GOOD,TIMER1)
    SET_TIMER(PLAYER_GOOD,TIMER2)
    SET_TIMER(PLAYER_GOOD,TIMER3)
    SET_TIMER(PLAYER_GOOD,TIMER4)
ENDIF
REM *************循环事件***********************
IF(PLAYER_GOOD,FLAG0 == 0)
    IF(PLAYER_GOOD,TIMER0 >= 1000)
        IF(PLAYER_GOOD,TIMER1 > 1750)
            NEXT_COMMAND_REUSABLE
            ADD_PARTY_TO_LEVEL(PLAYER_GOOD,FIRST,-1,1)
            NEXT_COMMAND_REUSABLE
            SET_TIMER(PLAYER_GOOD,TIMER1)
        ENDIF
    ENDIF
ENDIF
IF(PLAYER_GOOD,FLAG0 == 0)
    IF(PLAYER_GOOD,TIMER0 >= 1750)
        IF(PLAYER_GOOD,TIMER2 > 2350)
            NEXT_COMMAND_REUSABLE
            ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SECOND,-2,1)
            NEXT_COMMAND_REUSABLE
            SET_TIMER(PLAYER_GOOD,TIMER2)
        ENDIF
    ENDIF
ENDIF
IF(PLAYER_GOOD,FLAG0 == 0)
    IF(PLAYER_GOOD,TIMER0 >= 2500)
        IF(PLAYER_GOOD,TIMER3 > 2950)
            NEXT_COMMAND_REUSABLE
            ADD_PARTY_TO_LEVEL(PLAYER_GOOD,THIRD,-3,1)
            NEXT_COMMAND_REUSABLE
            SET_TIMER(PLAYER_GOOD,TIMER3)
        ENDIF
    ENDIF
ENDIF
IF(PLAYER_GOOD,FLAG0 == 0)
    IF(PLAYER_GOOD,TIMER0 >= 3250)
        IF(PLAYER_GOOD,TIMER4 > 3800)
            NEXT_COMMAND_REUSABLE
            ADD_PARTY_TO_LEVEL(PLAYER_GOOD,FOUR,-4,1)
            NEXT_COMMAND_REUSABLE
            SET_TIMER(PLAYER_GOOD,TIMER4)
        ENDIF
    ENDIF
ENDIF

REM *************时钟事件***********************
IF(PLAYER_GOOD,TIMER0 >= 4200)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,BRUTES_LIGHT,3,1)
ENDIF
IF(PLAYER_GOOD,TIMER0 >= 7250)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,MYSTIC_LIGHT,4,1)
ENDIF
IF(PLAYER_GOOD,TIMER0 >= 12400)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SAMURAI_LIGHT,1,1)
    SET_FLAG(PLAYER_GOOD,FLAG0,1)
ENDIF
IF(PLAYER_GOOD,TIMER0 >= 12500)
    IF(PLAYER_GOOD,TOTAL_CREATURES == 0)
        IF(PLAYER_GOOD,FLAG0 == 1)
            SET_FLAG(PLAYER_GOOD,FLAG0,2)
            SET_TIMER(PLAYER_GOOD,TIMER5)
            REM QUICK_OBJECTIVE(1,"You have slaughtered the vast majority of the Heroes, now prepare yourself for their elite warriors. Maybe they can provide some some actual fight.",PLAYER0)
            QUICK_OBJECTIVE(1,"你们屠杀了绝大多数的英雄，现在为他们的精英战士做好准备，也许他们可以提供一些实际的战斗。",PLAYER0)
        ENDIF
    ENDIF
ENDIF

IF(PLAYER_GOOD,FLAG0 == 2)
    IF(PLAYER_GOOD,TIMER5 >= 8000)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,BRUTES_HEAVY,2,1)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,BRUTES_HEAVY,4,1)
        REM QUICK_OBJECTIVE(2,"Here they come again.",PLAYER0)
        QUICK_OBJECTIVE(8,"他们又来了",PLAYER0)
    ENDIF
ENDIF
IF(PLAYER_GOOD,FLAG0 == 2)
    IF(PLAYER_GOOD,TIMER5 >= 10000)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SAMURAI_HEAVY,3,1)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,MYSTIC_HEAVY,4,1)
        SET_FLAG(PLAYER_GOOD,FLAG0,3)
    ENDIF
ENDIF
IF(PLAYER_GOOD,FLAG0 == 3)
    IF(PLAYER_GOOD,TIMER5 >= 13250)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,BOSS,1,1)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SUPPORT,1,1)
        SET_FLAG(PLAYER_GOOD,FLAG0,4)
    ENDIF
ENDIF

REM ****************************胜利/失败条件**********************************
IF(PLAYER_GOOD,TIMER5 >= 13250)
    IF(PLAYER_GOOD,FLAG0 == 4)
        IF(PLAYER_GOOD,TOTAL_CREATURES == 0)
            REM QUICK_OBJECTIVE(3,"With so many deaths here on the field, I have good reason to believe this place will forever be haunted with wailing, vengeful revenants untill the end of time. Outstanding!",PLAYER0)
            QUICK_OBJECTIVE(9,"有这么多人在场，我有理由相信这个地方将永远被复仇的亡灵哀号，直到时间的尽头。优秀的！",PLAYER0)

            REM 以下修改游戏，增加胜利后显示全图、送钱开放建造限制，让你回味一下胜利的喜悦！
            LEVEL_VERSION(1)
            REVEAL_MAP_RECT(PLAYER0,127,127,254,254)
            ADD_GOLD_TO_PLAYER (PLAYER0,9999999)

            ROOM_AVAILABLE(PLAYER0,TREASURE,1,1)
            ROOM_AVAILABLE(PLAYER0,GARDEN,1,1)
            ROOM_AVAILABLE(PLAYER0,LAIR,1,1)
            ROOM_AVAILABLE(PLAYER0,RESEARCH,1,1)
            ROOM_AVAILABLE(PLAYER0,TRAINING,1,1)
            ROOM_AVAILABLE(PLAYER0,BRIDGE,1,1)
            ROOM_AVAILABLE(PLAYER0,GUARD_POST,1,1)
            ROOM_AVAILABLE(PLAYER0,WORKSHOP,1,1)
            ROOM_AVAILABLE(PLAYER0,BARRACKS,1,1)
            ROOM_AVAILABLE(PLAYER0,TEMPLE,1,1)
            ROOM_AVAILABLE(PLAYER0,PRISON,1,1)
            ROOM_AVAILABLE(PLAYER0,TORTURE,1,1)
            ROOM_AVAILABLE(PLAYER0,SCAVENGER,1,1)
            ROOM_AVAILABLE(PLAYER0,GRAVEYARD,1,1)
            
            MAGIC_AVAILABLE(PLAYER0,POWER_IMP,1,1)
            MAGIC_AVAILABLE(PLAYER0,POWER_CALL_TO_ARMS,1,1
            MAGIC_AVAILABLE(PLAYER0,POWER_SPEED,1,1)
            MAGIC_AVAILABLE(PLAYER0,POWER_CONCEAL,1,1)
            MAGIC_AVAILABLE(PLAYER0,POWER_HEAL_CREATURE,1,1)
            MAGIC_AVAILABLE(PLAYER0,POWER_DESTROY_WALLS,1,1)

            DOOR_AVAILABLE(PLAYER0,MAGIC,1,200)
            TRAP_AVAILABLE(PLAYER0,LIGHTNING,1,0)
            TRAP_AVAILABLE(PLAYER0,BOULDER,1,200)
            QUICK_INFORMATION(15,"亮图送钱，让你回味一下胜利的喜悦！",PLAYER0) 

           WIN_GAME
        ENDIF
    ENDIF
ENDIF

REM ****************************提示信息**********************************
QUICK_OBJECTIVE(1,"参考提示：1、没有富矿的土地上只能靠工厂解决经济问题；2、敌人不久就会从外围的湖中传送过来；3、东南方有个显示地图的宝物；4、不要打紫金矿的主意，那个胜利后有破墙魔法才能挖。",PLAYER0) 

REM QUICK_OBJECTIVE(0,"The Heroes are getting somewhat nervous and have send an army to attack you. Use the resources you have available to build an even bigger force and crush that so-called army.",PLAYER0)
QUICK_INFORMATION(2,"英雄们有些紧张，派军队去攻击你，利用你现有的资源建立更大的力量，粉碎所谓的军队。",PLAYER0)

IF(PLAYER0,GAME_TURN > 200)
    QUICK_INFORMATION(3,"敌人不久就会从外围的湖中传送过来，尽可以快地圈几大块地来建议基地，特别是工厂用地，记得修墙。",PLAYER0) 
ENDIF
IF(PLAYER0,GAME_TURN > 500)
    QUICK_INFORMATION(4,"没有富矿的土地上只能靠工厂解决经济问题，还可以利用陷阱来杀敌，如果你不是特别厉害的话建议精兵简政打持久战！",PLAYER0) 
ENDIF
IF(PLAYER0,GAME_TURN > 1000)
    QUICK_INFORMATION(5,"东南方有个显示地图的宝物，如果你操作速度够快的话可以去取。",PLAYER0) 
ENDIF
IF(PLAYER0,GAME_TURN > 1500)
    QUICK_INFORMATION(6,"不要打紫金矿的主意，那个胜利后有破墙魔法才能挖。",PLAYER0) 
ENDIF
IF(PLAYER0,GAME_TURN > 2000)
    QUICK_INFORMATION(7,"祝你好运，守护者！",PLAYER0) 
ENDIF








