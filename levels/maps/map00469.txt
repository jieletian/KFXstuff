REM ***********************************************************************
REM   Script for Level  469.Blistering Furnace - MoonShadow
REM ***********************************************************************
REM Blistering Furnace
REM 已经收录到FX的Burdened Imps' Level Pack战役中。（222）
REM ***********************************************************************
REM 地图的大概情况与参考建议：
REM 1、 。
REM 2、 。
REM 3、 。
REM 4、 。
REM 5、 。
REM ***********************************************************************
REM ***************************各种基本设置 *****************************
SET_GENERATE_SPEED(500)
MAX_CREATURES(PLAYER0,25)
MAX_CREATURES(PLAYER1,30)
START_MONEY(PLAYER0,20000)
START_MONEY(PLAYER1,50000)
COMPUTER_PLAYER(PLAYER1,10)

REM *******************************添加怪物到入口 *********************************
REM   (添加到入口的生物名字,数量)
ADD_CREATURE_TO_POOL(FLY,20)
ADD_CREATURE_TO_POOL(BUG,20)
ADD_CREATURE_TO_POOL(DEMONSPAWN,20)
ADD_CREATURE_TO_POOL(SPIDER,20)
ADD_CREATURE_TO_POOL(SORCEROR,20)
ADD_CREATURE_TO_POOL(TROLL,20)
ADD_CREATURE_TO_POOL(BILE_DEMON,20)
ADD_CREATURE_TO_POOL(ORC,20)
ADD_CREATURE_TO_POOL(DARK_MISTRESS,20)
ADD_CREATURE_TO_POOL(DRAGON,20)
ADD_CREATURE_TO_POOL(HELL_HOUND,20)
ADD_CREATURE_TO_POOL(TENTACLE,20)
ADD_CREATURE_TO_POOL(SKELETON,20)
ADD_CREATURE_TO_POOL(GHOST,20)
ADD_CREATURE_TO_POOL(VAMPIRE,20)

REM *******************************指定阵营（玩家）允许/禁止从入口（怪物池）中招指定生物*********************************
REM  (玩家,生物名称,1（只能是1）,[1允许招募/0禁止招募])
CREATURE_AVAILABLE(ALL_PLAYERS,FLY,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,BUG,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,DEMONSPAWN,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,SPIDER,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,SORCEROR,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,TROLL,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,BILE_DEMON,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,ORC,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,DARK_MISTRESS,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,DRAGON,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,HELL_HOUND,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,TENTACLE,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,SKELETON,0,0)
CREATURE_AVAILABLE(ALL_PLAYERS,GHOST,0,0)
CREATURE_AVAILABLE(ALL_PLAYERS,VAMPIRE,0,0)

REM ********************指定阵营（玩家）允许/禁止研究和建造指定房子（建筑）**********************************
REM  (玩家,房子名称,是否可研究或建造[1是允许；0是禁止],是否已经可建造[1已经可建/0待研究])
ROOM_AVAILABLE(PLAYER0,TREASURE,1,1)
ROOM_AVAILABLE(PLAYER0,LAIR,1,1)
ROOM_AVAILABLE(PLAYER0,GARDEN,1,1)
ROOM_AVAILABLE(PLAYER0,TRAINING,1,1)
ROOM_AVAILABLE(PLAYER0,RESEARCH,1,1)
ROOM_AVAILABLE(PLAYER0,WORKSHOP,1,0)
ROOM_AVAILABLE(PLAYER0,BARRACKS,1,0)
ROOM_AVAILABLE(PLAYER0,BRIDGE,1,0)
ROOM_AVAILABLE(PLAYER0,GUARD_POST,1,0)
ROOM_AVAILABLE(PLAYER0,PRISON,1,0)
ROOM_AVAILABLE(PLAYER0,TORTURE,1,0)
ROOM_AVAILABLE(PLAYER0,TEMPLE,1,0)
ROOM_AVAILABLE(PLAYER0,GRAVEYARD,1,0)
ROOM_AVAILABLE(PLAYER0,SCAVENGER,0,0)

REM ********************指定阵营（玩家）允许/禁止研究和使用指定魔法**********************************
REM  (玩家,魔法名称,是否可研究或使用[1是允许；0是禁止],是否已经可使用[1已经可建/0待研究])

MAGIC_AVAILABLE(ALL_PLAYERS,POWER_IMP,1,1)
MAGIC_AVAILABLE(PLAYER0,POWER_OBEY,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SIGHT,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CALL_TO_ARMS,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CAVE_IN,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HEAL_CREATURE,1,0)
MAGIC_AVAILABLE(PLAYER0,POWER_HOLD_AUDIENCE,1,0)
MAGIC_AVAILABLE(PLAYER1,POWER_LIGHTNING,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SPEED,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_PROTECT,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CONCEAL,1,0)
MAGIC_AVAILABLE(PLAYER0,POWER_DISEASE,0,0)
MAGIC_AVAILABLE(PLAYER0,POWER_CHICKEN,0,0)
MAGIC_AVAILABLE(PLAYER0,POWER_DESTROY_WALLS,0,0)
MAGIC_AVAILABLE(PLAYER0,POWER_ARMAGEDDON,0,0)

REM *********************指定阵营（玩家）允许/禁止制造指定的门*********************************
REM  (玩家,门的名称,[1是允许；0是禁止],1[0是允许；数字是已经制造出来/赠送的数量]
DOOR_AVAILABLE(ALL_PLAYERS,WOOD,0,0)
DOOR_AVAILABLE(ALL_PLAYERS,BRACED,1,0)
DOOR_AVAILABLE(ALL_PLAYERS,STEEL,1,0)
DOOR_AVAILABLE(ALL_PLAYERS,MAGIC,0,0)

REM *********************指定阵营（玩家）允许/禁止制造指定陷阱*********************************
REM  (玩家,陷阱名称,[1是允许；0是禁止],1[0是允许；数字是已经制造出来/赠送的数量]

TRAP_AVAILABLE(ALL_PLAYERS,ALARM,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,POISON_GAS,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,LIGHTNING,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,LAVA,0,0)
TRAP_AVAILABLE(ALL_PLAYERS,BOULDER,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,WORD_OF_POWER,1,0)

REM ****************************增援部队**********************************
REM CREATE_PARTY =>组建队伍                (队伍名称)   
REM ADD_TO_PARTY=>添加生物到队伍中   (队伍名称,生物名称,生物等级,携带金钱数量,任务,延时)
CREATE_PARTY(REVENGE)
    ADD_TO_PARTY(REVENGE,KNIGHT,10,1600,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(REVENGE,SAMURAI,8,800,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(REVENGE,MONK,8,800,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(REVENGE,GIANT,8,800,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(REVENGE,GIANT,8,800,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(REVENGE,WIZARD,8,800,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(REVENGE,BARBARIAN,8,800,ATTACK_DUNGEON_HEART,0)
CREATE_PARTY(CHECKUP)
    ADD_TO_PARTY(CHECKUP,BARBARIAN,8,800,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(CHECKUP,BARBARIAN,8,800,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(CHECKUP,ARCHER,8,800,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(CHECKUP,ARCHER,8,800,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(CHECKUP,WITCH,8,800,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(CHECKUP,SAMURAI,8,800,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(CHECKUP,SAMURAI,8,800,ATTACK_DUNGEON_HEART,0)


REM ****************************脚本主体(主要内容、核心内容）**********************************
SET_CREATURE_MAX_LEVEL(PLAYER0,THIEF,10)
SET_CREATURE_MAX_LEVEL(PLAYER0,DEMONSPAWN,10)

REM *************占领房子事件***********************
IF(PLAYER0,BRIDGE >= 1)
    ROOM_AVAILABLE(PLAYER0,BRIDGE,1,1)
ENDIF
IF(PLAYER0,GUARD_POST >= 1)
    ROOM_AVAILABLE(PLAYER0,GUARD_POST,1,1)
ENDIF
IF(PLAYER0,WORKSHOP >= 1)
    ROOM_AVAILABLE(PLAYER0,WORKSHOP,1,1)
ENDIF
IF(PLAYER0,BARRACKS >= 1)
    ROOM_AVAILABLE(PLAYER0,BARRACKS,1,1)
ENDIF
IF(PLAYER0,PRISON >= 1)
    ROOM_AVAILABLE(PLAYER0,PRISON,1,1)
ENDIF
IF(PLAYER0,TORTURE >= 1)
    ROOM_AVAILABLE(PLAYER0,TORTURE,1,1)
ENDIF
IF(PLAYER0,TEMPLE >= 1)
    ROOM_AVAILABLE(PLAYER0,TEMPLE,1,1)
ENDIF
IF(PLAYER0,GRAVEYARD >= 1)
    ROOM_AVAILABLE(PLAYER0,GRAVEYARD,1,1)
ENDIF
IF(PLAYER0,SCAVENGER >= 1)
    ROOM_AVAILABLE(PLAYER0,SCAVENGER,1,1)
ENDIF

IF(PLAYER0,TOTAL_CREATURES >= 30)
    REM DISPLAY_OBJECTIVE(41,ALL_PLAYERS)
    REM QUICK_OBJECTIVE(3,"You are well on your way, Keeper. Thirty or more minions might do to overthrow your adversary. But it will be wise to gather more. Remember, you cannot replace fallen minions easily without a portal.",PLAYER0)
    QUICK_INFORMATION(7,"你做得很好，守护者。三十个或更多的手下可能会推翻你的对手，但收集更多奴仆是明智的。记住，没有入口你不能轻松地更换堕落的爪牙。",PLAYER0)
ENDIF

REM *************时钟事件***********************
IF(PLAYER_GOOD,TOTAL_CREATURES == 0)
    SET_TIMER(PLAYER_GOOD,TIMER0)
ENDIF
IF(PLAYER_GOOD,TIMER0 >= 5000)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,CHECKUP,-1,1)
    REM DISPLAY_INFORMATION(42,ALL_PLAYERS)
    REM QUICK_INFORMATION(4,"An assortment of heroes have arrived. They will undoubtly attack your dungeon after seeing the grief you have caused to their friends. Potential future servants?")
    REM QUICK_INFORMATION(84,"各类英雄已经到来，见到你给他们朋友造成的悲伤后他们会义无反顾地攻击你的地下城，他们会是潜在的未来的仆人？")
ENDIF
IF(PLAYER_GOOD,TIMER0 >= 15000)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,REVENGE,-1,1)
    REM DISPLAY_INFORMATION(44,ALL_PLAYERS)
    REM QUICK_INFORMATION(5,"The lord of this land has come to revenge his comrades. What he doesn't know is that he'll soon be fighting them. One has got to love the irony in that.")
    REM QUICK_INFORMATION(9,"这片土地的主人来为他的战友们报仇。他不知道的是，他很快就会与他们开战，多讽刺呀。")
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,REVENGE,-1,1)
ENDIF

REM *************触发点事件***********************
IF_ACTION_POINT(1,PLAYER0)
    SET_FLAG(PLAYER0,FLAG1,1)
ENDIF
IF_ACTION_POINT(2,PLAYER0)
    SET_FLAG(PLAYER0,FLAG2,1)
ENDIF
IF_ACTION_POINT(3,PLAYER0)
    SET_FLAG(PLAYER0,FLAG3,1)
ENDIF
IF_ACTION_POINT(4,PLAYER0)
    SET_FLAG(PLAYER0,FLAG4,1)
ENDIF
IF_ACTION_POINT(5,PLAYER0)
    SET_FLAG(PLAYER0,FLAG5,1)
ENDIF
IF(PLAYER0,FLAG1 == 1)
    IF(PLAYER0,FLAG2 == 1)
        IF(PLAYER0,FLAG3 == 1)
            IF(PLAYER0,FLAG4 == 1)
                IF(PLAYER0,FLAG5 == 1)
                    REM DISPLAY_INFORMATION(45,ALL_PLAYERS)
                    REM QUICK_INFORMATION(6,"Magnificent thinking, Keeper. The contamination of these holy wells by your minions' dirty feet has led to an interesting gift from the Gods. A Horned Reaper has come to aid you.")
                    REM QUICK_INFORMATION(10,"宏伟的构想，守护者，被你手下脏脚污染这些神圣之泉导致了来自神的一个有趣的礼物，一收割者来帮助你。")
                    ADD_CREATURE_TO_LEVEL(PLAYER0,HORNY,6,1,10,0)
                ENDIF
            ENDIF
        ENDIF
    ENDIF
ENDIF

IF(PLAYER0,KNIGHT == 1)
    REM DISPLAY_INFORMATION(46,ALL_PLAYERS)
    REM QUICK_INFORMATION(7,"Most unpleasantly done, Keeper. This knight will make an excellent addition to your force.")
    REM QUICK_INFORMATION(11,"最令人讨厌的了，守护者，这个骑士将成为一个优秀的增援对你的军队。")
ENDIF

IF(PLAYER0,DRAGON == 1)
    REM DISPLAY_INFORMATION(47,ALL_PLAYERS)
    REM QUICK_INFORMATION(8,"A dragon has joined your cause. There may be others around. Be advised, Keeper, if you seek to imprisonate and torture your enemy, a dragon's flames can burn any victim beyond unconsciousness. It would be best to find other minions to subdue your enemy.")
    REM QUICK_INFORMATION(12,"一条龙投奔你，周围可能还有其它。注意，守护者，如果你寻求囚犯折磨你的敌人，龙的火焰可以燃烧失去知觉的受害者，最好能找到其他的爪牙征服你的敌人。")
ENDIF

REM ****************************胜利/失败条件**********************************
IF(PLAYER1,DUNGEON_DESTROYED == 1)
    REM DISPLAY_OBJECTIVE(39,ALL_PLAYERS)
    REM QUICK_OBJECTIVE(1,"A fine display of wickedness. Congratulations, Keeper. Now, what to do with your remaining heroes...",PLAYER0)
    QUICK_OBJECTIVE(13,"一个精彩的邪恶表演，祝贺你，守护者！现在，怎么处置你剩下的英雄…",PLAYER0)

    REM 以下修改游戏，增加胜利后可以建造其它房子，让你回味一下胜利的喜悦！
    LEVEL_VERSION(1)
    REVEAL_MAP_RECT(PLAYER0,127,127,254,254)
    ADD_GOLD_TO_PLAYER (PLAYER0,9999999)
    ROOM_AVAILABLE(PLAYER0,TREASURE,1,1)
    ROOM_AVAILABLE(PLAYER0,GARDEN,1,1)
    ROOM_AVAILABLE(PLAYER0,LAIR,1,1)
    ROOM_AVAILABLE(PLAYER0,RESEARCH,1,1)
    ROOM_AVAILABLE(PLAYER0,TRAINING,1,1)
    ROOM_AVAILABLE(PLAYER0,BRIDGE,1,1)
    ROOM_AVAILABLE(PLAYER0,GUARD_POST,1,1)
    ROOM_AVAILABLE(PLAYER0,WORKSHOP,1,1)
    ROOM_AVAILABLE(PLAYER0,BARRACKS,1,1)
    ROOM_AVAILABLE(PLAYER0,TEMPLE,1,1)
    ROOM_AVAILABLE(PLAYER0,PRISON,1,1)
    ROOM_AVAILABLE(PLAYER0,TORTURE,1,1)
    ROOM_AVAILABLE(PLAYER0,SCAVENGER,1,1)
    ROOM_AVAILABLE(PLAYER0,GRAVEYARD,1,1)
    
    MAGIC_AVAILABLE(PLAYER0,POWER_IMP,1,1)
    MAGIC_AVAILABLE(PLAYER0,POWER_CALL_TO_ARMS,1,1)
    MAGIC_AVAILABLE(PLAYER0,POWER_SPEED,1,1)
    MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CONCEAL,1,1)
    MAGIC_AVAILABLE(PLAYER0,POWER_HEAL_CREATURE,1,1)
    MAGIC_AVAILABLE(PLAYER0,POWER_DESTROY_WALLS,1,1)

    DOOR_AVAILABLE(PLAYER0,BRACED,1,0)
    DOOR_AVAILABLE(PLAYER0,MAGIC,1,999)
    TRAP_AVAILABLE(PLAYER0,POISON_GAS,1,0)
    TRAP_AVAILABLE(PLAYER0,LIGHTNING,1,0)
    TRAP_AVAILABLE(PLAYER0,BOULDER,1,500)
    QUICK_INFORMATION(15,"修改了游戏，增加胜利后可以建设一下，让你回味一下胜利的喜悦！",PLAYER0) 

    WIN_GAME
ENDIF
IF(PLAYER0,DUNGEON_DESTROYED == 1)
    LOSE_GAME
ENDIF
IF(PLAYER0,TOTAL_CREATURES == 0)
    IF(PLAYER0,ENTRANCE == 0)
        REM DISPLAY_OBJECTIVE(40,ALL_PLAYERS)
        REM QUICK_OBJECTIVE(2,"You cannot hope to win without minions, Keeper. You have failed.",PLAYER0)
        QUICK_OBJECTIVE(14,"没有奴才你没有赢的希望，守护者，你失败了。",PLAYER0)
        LOSE_GAME
    ENDIF
ENDIF

REM ****************************提示信息**********************************
REM DISPLAY_OBJECTIVE(38,ALL_PLAYERS)
REM QUICK_OBJECTIVE(0,"A portal is absent on this island. Without minions you have no hope of defeating the enemy Keeper here. However, visiting the nearby hero fortress might open some recruitment options. Starving heroes make fine skeletons and you might even be able to attract a vampire or two. Or, if you feel particularly evil today, why not convert them all to your cause and create a legion of heroes?",PLAYER0)
QUICK_INFORMATION(0,"在这个岛上没有一个入口，没有爪牙你就没有希望打败守护这里敌人。然而，访问附近的英雄堡垒可能会打开一些招聘选项。饥饿的英雄制作很好的骷髅，你甚至可能会吸引一两个吸血鬼。或者，如果今天你感到特别邪恶，为何不招降他们，创造一个英雄军团？",PLAYER0)

QUICK_OBJECTIVE(1,"参考提示：1、胜利条件是干掉守护者心脏；2、没有入口，必须要利用好人类的兵力和金矿；3、反复建桥卖桥可以把敌人分批解决;4、不要在守护者的陷阱阵中发动攻击，可以引它们到桥上或你的地盘上解决它们。",PLAYER0)  
IF(PLAYER0,GAME_TURN > 200)
    QUICK_INFORMATION(2,"没有入口，必须把人类兵力俘虏过来，不能打的兵饿死成骷髅，能打的招降过来，尽快占领人类的紫金！",PLAYER0) 
ENDIF
IF(PLAYER0,GAME_TURN > 500)
    QUICK_INFORMATION(3,"你兵力不强的时候，可以通过反复建桥卖桥的方式来分批解决人类，这样好快地占领人类的金矿。",PLAYER0) 
ENDIF
IF(PLAYER0,GAME_TURN > 1000)
    QUICK_INFORMATION(4,"东边有个吸血鬼可以领回来增加你的战斗力。",PLAYER0) 
ENDIF
IF(PLAYER0,GAME_TURN > 1500)
    QUICK_INFORMATION(5,"在敌人的陷阱阵中战斗，你会很吃亏。",PLAYER0) 
ENDIF
IF(PLAYER0,GAME_TURN > 2000)
    QUICK_INFORMATION(6,"祝你好运，守护者！",PLAYER0) 
ENDIF





