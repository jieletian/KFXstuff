REM ***********************************************************************
REM   Script for Level 252. Tempus fugit 光阴似箭
REM ***********************************************************************
REM vandal 9 a/p
REM 
REM ***********************************************************************
REM 地图的大概情况与参考建议：
REM 1、 。
REM 2、 。
REM 3、 。
REM 4、 。
REM 5、 。
REM ***********************************************************************
REM ***************************各种基本设置 *****************************
SET_GENERATE_SPEED(400)
START_MONEY(ALL_PLAYERS,30000)
MAX_CREATURES(ALL_PLAYERS,20)

REM *******************************添加怪物到入口 *********************************
REM   (添加到入口的生物名字,数量)
ADD_CREATURE_TO_POOL(ORC,8)
ADD_CREATURE_TO_POOL(BILE_DEMON,8)
ADD_CREATURE_TO_POOL(DARK_MISTRESS,6)
ADD_CREATURE_TO_POOL(SORCEROR,4)

REM *******************************指定阵营（玩家）允许/禁止从入口（怪物池）中招指定生物*********************************
REM  (玩家,生物名称,1（只能是1）,[1允许招募/0禁止招募])

CREATURE_AVAILABLE(ALL_PLAYERS,ORC,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,BILE_DEMON,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,DARK_MISTRESS,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,SORCEROR,1,1)

REM ********************指定阵营（玩家）允许/禁止研究和建造指定房子（建筑）**********************************
REM  (玩家,房子名称,是否可研究或建造[1是允许；0是禁止],是否已经可建造[1已经可建/0待研究])
ROOM_AVAILABLE(ALL_PLAYERS,GARDEN,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,LAIR,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,TREASURE,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,RESEARCH,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,TRAINING,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,BARRACKS,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,WORKSHOP,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,PRISON,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,TEMPLE,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,TORTURE,1,1)

REM ********************指定阵营（玩家）允许/禁止研究和使用指定魔法**********************************
REM  (玩家,魔法名称,是否可研究或使用[1是允许；0是禁止],是否已经可使用[1已经可建/0待研究])
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HAND,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SLAP,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_IMP,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SPEED,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CALL_TO_ARMS,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CHICKEN,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CAVE_IN,1,1)

REM *********************指定阵营（玩家）允许/禁止制造指定的门*********************************
REM  (玩家,门的名称,[1是允许；0是禁止],1[0是允许；数字是已经制造出来/赠送的数量]
TRAP_AVAILABLE(ALL_PLAYERS,LIGHTNING,1,0)

REM *********************指定阵营（玩家）允许/禁止制造指定陷阱*********************************
REM  (玩家,陷阱名称,[1是允许；0是禁止],1[0是允许；数字是已经制造出来/赠送的数量]
DOOR_AVAILABLE(ALL_PLAYERS,MAGIC,1,0)


REM ****************************增援部队**********************************
REM CREATE_PARTY =>组建队伍                (队伍名称)   
REM ADD_TO_PARTY=>添加生物到队伍中   (队伍名称,生物名称,生物等级,携带金钱数量,任务,延时)
REM 和时钟相关的刷怪
CREATE_PARTY(VANDAL)
    ADD_TO_PARTY(VANDAL,DWARFA,5,150,ATTACK_ROOMS,0)
    ADD_TO_PARTY(VANDAL,DWARFA,5,150,ATTACK_ROOMS,0)
    ADD_TO_PARTY(VANDAL,DWARFA,5,150,ATTACK_ROOMS,0)
    ADD_TO_PARTY(VANDAL,DWARFA,5,150,ATTACK_ROOMS,0)
REM 和时钟相关的刷怪
CREATE_PARTY(MAGIC)
    ADD_TO_PARTY(MAGIC,WIZARD,6,350,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(MAGIC,ARCHER,5,150,DEFEND_PARTY,0)
    ADD_TO_PARTY(MAGIC,SAMURAI,5,150,DEFEND_PARTY,0)
    ADD_TO_PARTY(MAGIC,MONK,5,150,DEFEND_PARTY,0)
    ADD_TO_PARTY(MAGIC,BARBARIAN,5,150,DEFEND_PARTY,0)
    ADD_TO_PARTY(MAGIC,GIANT,5,150,DEFEND_PARTY,0)
REM 人类战败50人后
CREATE_PARTY(THUGS)
    ADD_TO_PARTY(THUGS,GIANT,8,950,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(THUGS,BARBARIAN,7,0,DEFEND_PARTY,0)
    ADD_TO_PARTY(THUGS,BARBARIAN,7,0,DEFEND_PARTY,0)
    ADD_TO_PARTY(THUGS,BARBARIAN,7,0,DEFEND_PARTY,0)
    ADD_TO_PARTY(THUGS,MONK,7,0,DEFEND_PARTY,0)
REM 所有心脏(只有人类)被打爆后且光明力量生物少于6；和时钟4相关的刷怪。刷BITCHES，部分攻击敌人，部队就地防御不进攻
CREATE_PARTY(BITCHES)
    ADD_TO_PARTY(BITCHES,WITCH,5,450,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(BITCHES,FAIRY,4,0,DEFEND_PARTY,0)
    ADD_TO_PARTY(BITCHES,FAIRY,3,0,DEFEND_PARTY,0)
    ADD_TO_PARTY(BITCHES,FAIRY,3,0,DEFEND_PARTY,0)
REM 所有心脏(只有人类)被打爆后且光明力量生物少于6，就刷BOSS，就地防御不进攻
CREATE_PARTY(BOSS)
    ADD_TO_PARTY(BOSS,KNIGHT,10,10000,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(BOSS,BARBARIAN,10,0,DEFEND_PARTY,0)
    ADD_TO_PARTY(BOSS,GIANT,10,0,DEFEND_PARTY,0)
    ADD_TO_PARTY(BOSS,SAMURAI,10,0,DEFEND_PARTY,0)
    ADD_TO_PARTY(BOSS,MONK,10,0,DEFEND_PARTY,0)
    ADD_TO_PARTY(BOSS,WIZARD,10,0,DEFEND_PARTY,0)
    ADD_TO_PARTY(BOSS,FAIRY,10,0,DEFEND_PARTY,0)


REM ****************************脚本主体(主要内容、核心内容）**********************************
SET_CREATURE_MAX_LEVEL(PLAYER0,ORC,9)
SET_CREATURE_MAX_LEVEL(PLAYER0,DARK_MISTRESS,9)

REM 你招降水怪和龙以后就可以从入口招水怪和龙
ADD_CREATURE_TO_POOL(TENTACLE,6)
IF(PLAYER0,TENTACLE > 0)
    CREATURE_AVAILABLE(ALL_PLAYERS,TENTACLE,1,1)
ENDIF
ADD_CREATURE_TO_POOL(DRAGON,4)
IF(PLAYER0,DRAGON > 0)
    CREATURE_AVAILABLE(ALL_PLAYERS,DRAGON,1,1)
ENDIF

REM 一开始就刷第一批怪
ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,ARCHER,1,5,5,0)
ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,ARCHER,4,5,5,0)
REM ADD_CREATURE_TO_LEVEL(阵营,怪物名称,地标,怪物数量,怪物等级,怪物身上的现金) 
REM 1个3级矮人触发点5、1个3级女巫触发点6、1个3级水怪触发点8、1个3级仙女触发点9、1个3级巨人触发点10、1个3级龙触发点11
ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,DWARFA,5,1,3,0)
ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,WITCH,6,1,3,0)
ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,TENTACLE,8,1,3,0)
ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,FAIRY,9,1,3,0)
ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,GIANT,10,1,3,0)
ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,DRAGON,11,1,3,0)

REM 启动时钟 符合条件就刷怪
IF(PLAYER_GOOD,DWARFA == 0)
    SET_TIMER(PLAYER_GOOD,TIMER1)
ENDIF
IF(PLAYER_GOOD,WITCH == 0)
    SET_TIMER(PLAYER_GOOD,TIMER2)
ENDIF
IF(PLAYER_GOOD,TENTACLE == 0)
    SET_TIMER(PLAYER_GOOD,TIMER3)
ENDIF
IF(PLAYER_GOOD,FAIRY == 0)
    SET_TIMER(PLAYER_GOOD,TIMER4)
ENDIF
IF(PLAYER_GOOD,DRAGON == 0)
    SET_TIMER(PLAYER_GOOD,TIMER5)
ENDIF
IF(PLAYER_GOOD,GIANT == 0)
    SET_TIMER(PLAYER_GOOD,TIMER6)
ENDIF
REM ATTACKS
REM 人类心脏没有被打爆而时钟1达5300 Game trun后，每5300 Game trun刷一批敌人出来
IF(PLAYER0,ALL_DUNGEONS_DESTROYED == 0)
    IF(PLAYER_GOOD,TIMER1 >= 5300)
        NEXT_COMMAND_REUSABLE
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,VANDAL,5,1)
        NEXT_COMMAND_REUSABLE
        SET_TIMER(PLAYER_GOOD,TIMER1)
    ENDIF
ENDIF
REM 人类心脏没有被打爆而时钟2达5700 Game trun后，每5700 Game trun刷一批敌人出来
IF(PLAYER0,ALL_DUNGEONS_DESTROYED == 0)
    IF(PLAYER_GOOD,TIMER2 >= 5700)
        NEXT_COMMAND_REUSABLE
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,VANDAL,6,1)
        NEXT_COMMAND_REUSABLE
        SET_TIMER(PLAYER_GOOD,TIMER2)
    ENDIF
ENDIF
REM 人类心脏没有被打爆而时钟3达8300 Game trun后，每8300 Game trun刷一批敌人出来
IF(PLAYER0,ALL_DUNGEONS_DESTROYED == 0)
    IF(PLAYER_GOOD,TIMER3 >= 8300)
        NEXT_COMMAND_REUSABLE
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,MAGIC,8,1)
        NEXT_COMMAND_REUSABLE
        SET_TIMER(PLAYER_GOOD,TIMER3)
    ENDIF
ENDIF
REM 人类心脏没有被打爆而时钟4达5500 Game trun后，每5500 Game trun刷一批敌人出来
IF(PLAYER0,ALL_DUNGEONS_DESTROYED == 0)
    IF(PLAYER_GOOD,TIMER4 >= 5500)
        NEXT_COMMAND_REUSABLE
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,BITCHES,9,1)
        NEXT_COMMAND_REUSABLE
        SET_TIMER(PLAYER_GOOD,TIMER4)
    ENDIF
ENDIF
REM 人类心脏没有被打爆而时钟5达7200 Game trun后，每7200 Game trun刷一批敌人出来
IF(PLAYER0,ALL_DUNGEONS_DESTROYED == 0)
    IF(PLAYER_GOOD,TIMER5 >= 7200)
        NEXT_COMMAND_REUSABLE
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,VANDAL,10,1)
        NEXT_COMMAND_REUSABLE
        SET_TIMER(PLAYER_GOOD,TIMER5)
    ENDIF
ENDIF
REM 人类心脏没有被打爆而时钟6达7600 Game trun后，每7600 Game trun刷一批敌人出来
IF(PLAYER0,ALL_DUNGEONS_DESTROYED == 0)
    IF(PLAYER_GOOD,TIMER6 >= 7600)
        NEXT_COMMAND_REUSABLE
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,VANDAL,9,1)
        NEXT_COMMAND_REUSABLE
        SET_TIMER(PLAYER_GOOD,TIMER6)
    ENDIF
ENDIF

REM 人类50人战败后，就刷一批敌人出来。
IF(PLAYER_GOOD,BATTLES_LOST >= 50)
    ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,THUGS,7,DUNGEON,0,5,500)
ENDIF

REM 所有心脏被打爆后且光明力量生物少于6，就刷一批敌人出来。
IF(PLAYER_GOOD,DUNGEON_DESTROYED == 1)
    IF(PLAYER_GOOD,TOTAL_CREATURES < 6)
        QUICK_INFORMATION(2,"人类已经派出复仇的部队，准备迎战吧！",PLAYER0) 
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,BOSS,2,1)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,THUGS,3,1)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,BITCHES,5,2)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,BITCHES,6,2)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,MAGIC,8,1)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,MAGIC,9,1)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,VANDAL,9,2)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,VANDAL,10,2)
    ENDIF
ENDIF

REM ****************************胜利/失败条件**********************************
REM 所有心脏被打爆后且光明力量没生物，就胜利并可以建桥。
IF(PLAYER0,ALL_DUNGEONS_DESTROYED == 1)
    IF(PLAYER_GOOD,TOTAL_CREATURES == 0)
        ROOM_AVAILABLE(ALL_PLAYERS,BRIDGE,1,1)
        
        REM 以下修改游戏，增加胜利后可以建造其它房子，让你回味一下胜利的喜悦！
        LEVEL_VERSION(1)
        REVEAL_MAP_RECT(PLAYER0,127,127,254,254)
        ADD_GOLD_TO_PLAYER (PLAYER0,9999999)

        ROOM_AVAILABLE(PLAYER0,TREASURE,1,1)
        ROOM_AVAILABLE(PLAYER0,GARDEN,1,1)
        ROOM_AVAILABLE(PLAYER0,LAIR,1,1)
        ROOM_AVAILABLE(PLAYER0,RESEARCH,1,1)
        ROOM_AVAILABLE(PLAYER0,TRAINING,1,1)
        ROOM_AVAILABLE(PLAYER0,BRIDGE,1,1)
        ROOM_AVAILABLE(PLAYER0,GUARD_POST,1,1)
        ROOM_AVAILABLE(PLAYER0,WORKSHOP,1,1)
        ROOM_AVAILABLE(PLAYER0,BARRACKS,1,1)
        ROOM_AVAILABLE(PLAYER0,TEMPLE,1,1)
        ROOM_AVAILABLE(PLAYER0,PRISON,1,1)
        ROOM_AVAILABLE(PLAYER0,TORTURE,1,1)
        ROOM_AVAILABLE(PLAYER0,SCAVENGER,1,1)
        ROOM_AVAILABLE(PLAYER0,GRAVEYARD,1,1)
        
        MAGIC_AVAILABLE(PLAYER0,POWER_IMP,1,1)
        MAGIC_AVAILABLE(PLAYER0,POWER_CALL_TO_ARMS,1,1
        MAGIC_AVAILABLE(PLAYER0,POWER_SPEED,1,1)
        MAGIC_AVAILABLE(PLAYER0,POWER_HEAL_CREATURE,1,1)
        MAGIC_AVAILABLE(PLAYER0,POWER_DESTROY_WALLS,1,1)

        DOOR_AVAILABLE(PLAYER0,MAGIC,1,200)
        TRAP_AVAILABLE(PLAYER0,LIGHTNING,1,0)
        TRAP_AVAILABLE(PLAYER0,BOULDER,1,200)
        QUICK_INFORMATION(15,"送钱亮图，让你回味一下胜利的喜悦！",PLAYER0)

        WIN_GAME
    ENDIF
ENDIF

REM ****************************提示信息**********************************
QUICK_OBJECTIVE(1,"参考提示：1、如果人类的巡逻兵(矮人、女巫、水怪、仙女、巨人、龙)被杀，他们就会派增援部队进攻你，如果想智取的话，建议先监禁他们并不让它们饿死，等到你实力强大的时候再说；2、这里的金矿不多，建议还是利用工厂赚钱，挖金矿里破坏地形；3、不能造桥，就得附身去打敌人心脏，路上机关重重，这是艰难的任务，要多存档;4、敌人心脏被打爆后，它的部队数量少于6时就会疯狂报复你，小心，如果你把握不大，在你地盘岸边建寺庙防止他们进入。",PLAYER0)



























