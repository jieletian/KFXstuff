REM ***********************************************************************
REM   Script for Level 229. Mt dragoon
REM ***********************************************************************
REM Published by DzjeeAr
REM 
REM ***********************************************************************
REM 地图的大概情况与参考建议：
REM 1、 。
REM 2、 。
REM 3、 。
REM 4、 。
REM 5、 。
REM ***********************************************************************
REM ***************************各种基本设置 *****************************
SET_GENERATE_SPEED(300)
MAX_CREATURES(PLAYER0,38)
START_MONEY(PLAYER0,10000)

REM *******************************添加怪物到入口 *********************************
REM   (添加到入口的生物名字,数量)
ADD_CREATURE_TO_POOL(FLY,30)
ADD_CREATURE_TO_POOL(BUG,30)
ADD_CREATURE_TO_POOL(SPIDER,30)
ADD_CREATURE_TO_POOL(DEMONSPAWN,30)
ADD_CREATURE_TO_POOL(SORCEROR,30)
ADD_CREATURE_TO_POOL(BILE_DEMON,30)
ADD_CREATURE_TO_POOL(DRAGON,30)
ADD_CREATURE_TO_POOL(TROLL,30)
ADD_CREATURE_TO_POOL(ORC,30)
ADD_CREATURE_TO_POOL(DARK_MISTRESS,30)
ADD_CREATURE_TO_POOL(TENTACLE,30)
ADD_CREATURE_TO_POOL(HORNY,1)

REM *******************************指定阵营（玩家）允许/禁止从入口（怪物池）中招指定生物*********************************
REM  (玩家,生物名称,1（只能是1）,[1允许招募/0禁止招募])

CREATURE_AVAILABLE(ALL_PLAYERS,FLY,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,BUG,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,SPIDER,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,DEMONSPAWN,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,SORCEROR,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,BILE_DEMON,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,DRAGON,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,TROLL,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,ORC,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,DARK_MISTRESS,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,SKELETON,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,GHOST,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,HELL_HOUND,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,VAMPIRE,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,TENTACLE,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,HORNY,1,1)

REM ********************指定阵营（玩家）允许/禁止研究和建造指定房子（建筑）**********************************
REM  (玩家,房子名称,是否可研究或建造[1是允许；0是禁止],是否已经可建造[1已经可建/0待研究])
ROOM_AVAILABLE(ALL_PLAYERS,TREASURE,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,LAIR,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,GARDEN,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,TRAINING,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,RESEARCH,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,GUARD_POST,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,WORKSHOP,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,BARRACKS,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,TORTURE,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,TEMPLE,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,GRAVEYARD,1,0)

REM ********************指定阵营（玩家）允许/禁止研究和使用指定魔法**********************************
REM  (玩家,魔法名称,是否可研究或使用[1是允许；0是禁止],是否已经可使用[1已经可建/0待研究])
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HAND,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SLAP,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_IMP,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SIGHT,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SPEED,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_OBEY,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CALL_TO_ARMS,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CONCEAL,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HOLD_AUDIENCE,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CAVE_IN,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HEAL_CREATURE,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_PROTECT,1,0)

REM *********************指定阵营（玩家）允许/禁止制造指定的门*********************************
REM  (玩家,门的名称,[1是允许；0是禁止],1[0是允许；数字是已经制造出来/赠送的数量]
DOOR_AVAILABLE(ALL_PLAYERS,WOOD,1,0)
DOOR_AVAILABLE(ALL_PLAYERS,BRACED,1,0)
DOOR_AVAILABLE(ALL_PLAYERS,STEEL,1,0)
DOOR_AVAILABLE(ALL_PLAYERS,MAGIC,1,0)

REM *********************指定阵营（玩家）允许/禁止制造指定陷阱*********************************
REM  (玩家,陷阱名称,[1是允许；0是禁止],1[0是允许；数字是已经制造出来/赠送的数量]
TRAP_AVAILABLE(ALL_PLAYERS,ALARM,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,POISON_GAS,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,LIGHTNING,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,WORD_OF_POWER,1,0)

REM ***********************************************************************
REM 下述设置令没有设置的建筑和魔法不能研究，猜测作者不是要这种效果，所以注析屏蔽掉它
REM RESEARCH(PLAYER0,MAGIC,POWER_SIGHT,10000)
REM RESEARCH(PLAYER0,MAGIC,POWER_SPEED,10000)
REM RESEARCH(PLAYER0,MAGIC,POWER_OBEY,10000)
REM RESEARCH(PLAYER0,MAGIC,POWER_CONCEAL,10000)
REM RESEARCH(PLAYER0,MAGIC,POWER_CALL_TO_ARMS,10000)

REM ****************************增援部队**********************************
REM CREATE_PARTY =>组建队伍                (队伍名称)   
REM ADD_TO_PARTY=>添加生物到队伍中   (队伍名称,生物名称,生物等级,携带金钱数量,任务,延时)
CREATE_PARTY(PATROLX)
ADD_TO_PARTY(PATROLX,MONK,10,300,ATTACK_ENEMIES,0)
ADD_TO_PARTY(PATROLX,WIZARD,10,200,ATTACK_ENEMIES,0)
ADD_TO_PARTY(PATROLX,BARBARIAN,10,400,ATTACK_ENEMIES,0)
ADD_TO_PARTY(PATROLX,GIANT,10,300,ATTACK_ENEMIES,0)
ADD_TO_PARTY(PATROLX,GIANT,10,300,ATTACK_ENEMIES,0)
ADD_TO_PARTY(PATROLX,ARCHER,10,200,ATTACK_ENEMIES,0)
CREATE_PARTY(ONE)
ADD_TO_PARTY(ONE,ARCHER,10,100,ATTACK_ENEMIES,0)
ADD_TO_PARTY(ONE,ARCHER,10,100,ATTACK_ENEMIES,0)
ADD_TO_PARTY(ONE,GIANT,10,100,ATTACK_ENEMIES,0)
ADD_TO_PARTY(ONE,GIANT,10,100,ATTACK_ENEMIES,0)
ADD_TO_PARTY(ONE,BARBARIAN,10,200,ATTACK_ENEMIES,0)
ADD_TO_PARTY(ONE,BARBARIAN,10,200,ATTACK_ENEMIES,0)
ADD_TO_PARTY(ONE,WIZARD,10,300,ATTACK_ENEMIES,0)
CREATE_PARTY(TWO)
ADD_TO_PARTY(TWO,SAMURAI,10,500,ATTACK_ENEMIES,0)
ADD_TO_PARTY(TWO,SAMURAI,10,500,ATTACK_ENEMIES,0)
ADD_TO_PARTY(TWO,SAMURAI,10,500,ATTACK_ENEMIES,0)
ADD_TO_PARTY(TWO,SAMURAI,10,500,ATTACK_ENEMIES,0)
ADD_TO_PARTY(TWO,SAMURAI,10,500,ATTACK_ENEMIES,0)
ADD_TO_PARTY(TWO,KNIGHT,10,1000,ATTACK_ENEMIES,0)
ADD_TO_PARTY(TWO,AVATAR,10,2000,ATTACK_ENEMIES,0)
CREATE_PARTY(X)
ADD_TO_PARTY(X,DEMONSPAWN,10,100,ATTACK_ENEMIES,0)
ADD_TO_PARTY(X,DEMONSPAWN,10,100,ATTACK_ENEMIES,0)
ADD_TO_PARTY(X,DEMONSPAWN,10,100,ATTACK_ENEMIES,0)
ADD_TO_PARTY(X,DRAGON,10,600,ATTACK_ENEMIES,0)
ADD_TO_PARTY(X,DRAGON,10,600,ATTACK_ENEMIES,0)
ADD_TO_PARTY(X,ORC,10,300,ATTACK_ENEMIES,0)


REM ****************************脚本主体(主要内容、核心内容）**********************************
REM A25人类基地训练室东边滚石陷阱后面
IF_ACTION_POINT(25,PLAYER0)
    ROOM_AVAILABLE(ALL_PLAYERS,BRIDGE,1,0)
ENDIF

REM A1在西北熔岩中，H1在西北角
IF_ACTION_POINT(1,PLAYER0)
  ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,FAIRY,-1,10,10,0)
ENDIF
REM A2在中央岩石区域北面那个有门的房子中A3、A8在中央岩石区域中西面那房子中
IF_ACTION_POINT(2,PLAYER0)
    ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,X,3,ACTION_POINT,2,8,250)
ENDIF
REM A4东边中部，人类南面那金矿内，A5在中央岩石区域中南面墙内，A6在中央岩石区域中间巢穴中
IF_ACTION_POINT(4,PLAYER0)
    ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,X,5,ACTION_POINT,6,8,250)
ENDIF
REM A9在东南人类监狱附近，图书馆前面，A10在图书馆内。
IF_ACTION_POINT(9,PLAYER0)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,WIZARD,10,5,10,100)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,MONK,10,2,10,100)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,WITCH,10,2,10,100)
ENDIF
REM A11、A12在岩石区的东北方，入口附近
IF_ACTION_POINT(11,PLAYER0)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,DWARFA,12,10,6,100)
ENDIF

REM ***********************************************************************
REM 要把岩石周围的大部分触发点触发，才会从人类基地刷挖掘者出来，否则进不了人类基地
REM A7在中央岩石区域中南面墙内，A13在人类基地入口处，A20在心脏正北面人类那格金库中，A21北面岩石旁边监狱附近，A22在东南人类图书馆内
IF_ACTION_POINT(1,PLAYER0)
    IF_ACTION_POINT(7,PLAYER0)
        IF_ACTION_POINT(8,PLAYER0)
            IF_ACTION_POINT(11,PLAYER0)
                IF_ACTION_POINT(20,PLAYER0)
                    IF_ACTION_POINT(21,PLAYER0)
                        IF_ACTION_POINT(22,PLAYER0)
                            ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,PATROLX,13,ACTION_POINT,11,8,250)
                        ENDIF
                    ENDIF
                ENDIF
            ENDIF
        ENDIF
    ENDIF
ENDIF

REM ***********************************************************************
REM A14在人类基地入口的房子前面，A15、1617、18、19、25、H2都在人类基地内。
IF_ACTION_POINT(14,PLAYER0)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,WIZARD,15,4,10,100)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,MONK,16,4,10,100)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,ARCHER,17,4,10,100)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,ARCHER,18,4,10,100)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,SAMURAI,18,4,10,100)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,SAMURAI,19,4,10,100)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,DEMONSPAWN,19,5,10,100)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,DRAGON,19,2,10,100)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,SAMURAI,-2,5,10,100)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,BILE_DEMON,-2,4,10,100)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,BARBARIAN,25,8,10,100)
ENDIF
REM A24人类心脏上
IF_ACTION_POINT(24,PLAYER0)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,ONE,-2,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,TWO,-2,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PATROLX,-2,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PATROLX,-2,1)
    IF(PLAYER_GOOD,KNIGHT==0)
        SET_CREATURE_STRENGTH(KNIGHT,255)
        SET_CREATURE_HEALTH(KNIGHT,7000)
        SET_CREATURE_ARMOUR(KNIGHT,255)
    ENDIF
ENDIF
REM A26、A27、A28都在中央岩石区域中南面墙内
IF_ACTION_POINT(26,PLAYER0)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,DEMONSPAWN,27,3,10,100)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,DEMONSPAWN,28,5,10,200)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,ARCHER,27,4,8,100)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,ARCHER,28,5,10,200)
ENDIF

REM ****************************胜利/失败条件**********************************
REM 打爆人类心脏，干掉骑士就胜利
IF(PLAYER_GOOD,DUNGEON_DESTROYED == 1)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,KNIGHT,-2,1,10,30000)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,AVATAR,-2,2,10,5000)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,SAMURAI,-2,4,10,500)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,WIZARD,-2,4,10,300)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,MONK,-2,4,10,200)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,WITCH,-2,2,10,100)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,FAIRY,-2,5,10,100)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,GIANT,-2,2,10,300)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,ARCHER,-2,6,10,200)
    IF(PLAYER_GOOD,KNIGHT==0)

        REM 以下修改游戏，增加胜利后可以建造其它房子，让你回味一下胜利的喜悦！
        LEVEL_VERSION(1)
        REVEAL_MAP_RECT(PLAYER0,127,127,254,254)
        ADD_GOLD_TO_PLAYER (PLAYER0,9999999)

        ROOM_AVAILABLE(PLAYER0,TREASURE,1,1)
        ROOM_AVAILABLE(PLAYER0,GARDEN,1,1)
        ROOM_AVAILABLE(PLAYER0,LAIR,1,1)
        ROOM_AVAILABLE(PLAYER0,RESEARCH,1,1)
        ROOM_AVAILABLE(PLAYER0,TRAINING,1,1)
        ROOM_AVAILABLE(PLAYER0,BRIDGE,1,1)
        ROOM_AVAILABLE(PLAYER0,GUARD_POST,1,1)
        ROOM_AVAILABLE(PLAYER0,WORKSHOP,1,1)
        ROOM_AVAILABLE(PLAYER0,BARRACKS,1,1)
        ROOM_AVAILABLE(PLAYER0,TEMPLE,1,1)
        ROOM_AVAILABLE(PLAYER0,PRISON,1,1)
        ROOM_AVAILABLE(PLAYER0,TORTURE,1,1)
        ROOM_AVAILABLE(PLAYER0,SCAVENGER,1,1)
        ROOM_AVAILABLE(PLAYER0,GRAVEYARD,1,1)
        
        MAGIC_AVAILABLE(PLAYER0,POWER_IMP,1,1)
        MAGIC_AVAILABLE(PLAYER0,POWER_CALL_TO_ARMS,1,1
        MAGIC_AVAILABLE(PLAYER0,POWER_SPEED,1,1)
        MAGIC_AVAILABLE(PLAYER0,POWER_HEAL_CREATURE,1,1)
        MAGIC_AVAILABLE(PLAYER0,POWER_DESTROY_WALLS,1,1)

        DOOR_AVAILABLE(PLAYER0,MAGIC,1,200)
        TRAP_AVAILABLE(PLAYER0,LIGHTNING,1,0)
        TRAP_AVAILABLE(PLAYER0,BOULDER,1,200)
        QUICK_INFORMATION(15,"送钱亮图，让你回味一下胜利的喜悦！",PLAYER0)

        WIN_GAME
  ENDIF
ENDIF
IF(PLAYER0,DUNGEON_DESTROYED == 1)
    LOSE_GAME
ENDIF

REM ****************************提示信息**********************************
QUICK_OBJECTIVE(1,"参考提示：1、吹风技能有时候很有用;2、利用好监狱；3、不消灭外围的敌人，它们不会出来；4、不能造桥，到人类地盘找找原因。",PLAYER0)
IF(PLAYER0,GAME_TURN > 500)
    QUICK_INFORMATION(2,"人类基地的建筑都被熔岩包围，把它们吹出来更容易消灭！就要求你有吹风技术的兵。",PLAYER0) 
ENDIF
IF(PLAYER0,GAME_TURN > 1500)
    QUICK_INFORMATION(3,"虽然你不能建监狱，但外面有几个监狱，用好它会让你有意外收获。",PLAYER0) 
ENDIF
IF(PLAYER0,GAME_TURN > 2500)
    QUICK_INFORMATION(4,"消灭外围的人类部队，人类才会派挖掘者从墙出挖出来。",PLAYER0) 
ENDIF
IF(PLAYER0,GAME_TURN > 3500)
    QUICK_INFORMATION(5,"踩了人类基地中间滚石处触发点(25)就能研究桥！",PLAYER0) 
ENDIF
IF(PLAYER0,GAME_TURN > 4000)
    QUICK_INFORMATION(6,"祝你好运，守护者！",PLAYER0) 
ENDIF













