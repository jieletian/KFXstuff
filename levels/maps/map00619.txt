REM ***********************************************************************
REM   Script for Level    619(119).Blocked 此路不通
REM ***********************************************************************
REM Published by DzjeeAr
REM ***********************************************************************
REM 地图的大概情况与参考建议：
REM 1、 。
REM 2、 。
REM 3、 。
REM 4、 。
REM 5、 。
REM ***********************************************************************
REM ***************************各种基本设置 *****************************
SET_GENERATE_SPEED(400)
MAX_CREATURES(PLAYER0,12)
START_MONEY(PLAYER0,10000)

REM *******************************添加怪物到入口 *********************************
REM   (添加到入口的生物名字,数量)
ADD_CREATURE_TO_POOL(FLY,10)
ADD_CREATURE_TO_POOL(BUG,10)
ADD_CREATURE_TO_POOL(SPIDER,4)
ADD_CREATURE_TO_POOL(DEMONSPAWN,5)
ADD_CREATURE_TO_POOL(SORCEROR,8)
ADD_CREATURE_TO_POOL(BILE_DEMON,3)
ADD_CREATURE_TO_POOL(DRAGON,5)
ADD_CREATURE_TO_POOL(TROLL,10)
ADD_CREATURE_TO_POOL(ORC,2)

REM ***********************可招的生物 *********************************
CREATURE_AVAILABLE(ALL_PLAYERS,FLY,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,BUG,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,SPIDER,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,DEMONSPAWN,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,SORCEROR,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,BILE_DEMON,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,DRAGON,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,TROLL,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,ORC,1,1)

REM ********************可研究和建筑的房子**********************************
ROOM_AVAILABLE(ALL_PLAYERS,TREASURE,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,LAIR,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,GARDEN,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,TRAINING,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,RESEARCH,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,BRIDGE,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,GUARD_POST,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,WORKSHOP,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,BARRACKS,1,0)

REM ********************指定阵营（玩家）允许/禁止研究和使用指定魔法**********************************
REM  (玩家,魔法名称,是否可研究或使用[1是允许；0是禁止],是否已经可使用[1已经可建/0待研究])
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HAND,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SLAP,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_IMP,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SIGHT,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SPEED,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_OBEY,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CALL_TO_ARMS,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CONCEAL,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HOLD_AUDIENCE,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HEAL_CREATURE,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_PROTECT,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CHICKEN,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_DISEASE,1,0)

REM *********************指定阵营（玩家）允许/禁止制造指定的门*********************************
REM  (玩家,门的名称,[1是允许；0是禁止],1[0是允许；数字是已经制造出来/赠送的数量]
DOOR_AVAILABLE(ALL_PLAYERS,WOOD,1,0)
DOOR_AVAILABLE(ALL_PLAYERS,BRACED,1,0)
DOOR_AVAILABLE(ALL_PLAYERS,STEEL,1,0)

REM *********************指定阵营（玩家）允许/禁止制造指定陷阱*********************************
REM  (玩家,陷阱名称,[1是允许；0是禁止],1[0是允许；数字是已经制造出来/赠送的数量]
TRAP_AVAILABLE(ALL_PLAYERS,ALARM,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,POISON_GAS,1,0)

REM ****************************增援部队**********************************
REM CREATE_PARTY =>组建队伍                (队伍名称)   
REM ADD_TO_PARTY=>添加生物到队伍中   (队伍名称,生物名称,生物等级,携带金钱数量,任务,延时)
CREATE_PARTY(ONE)
    ADD_TO_PARTY(ONE,THIEF,3,200,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(ONE,THIEF,3,200,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(ONE,ARCHER,2,100,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(ONE,DWARFA,1,100,ATTACK_ENEMIES,0)
CREATE_PARTY(TWO)
    ADD_TO_PARTY(TWO,BARBARIAN,4,100,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(TWO,THIEF,2,100,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(TWO,BARBARIAN,3,200,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(TWO,ARCHER,2,100,ATTACK_ENEMIES,0)
CREATE_PARTY(THREE)
    ADD_TO_PARTY(THREE,ARCHER,1,100,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(THREE,ARCHER,2,100,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(THREE,FAIRY,3,100,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(THREE,WIZARD,3,100,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(THREE,BARBARIAN,3,200,ATTACK_ENEMIES,0)
CREATE_PARTY(FOUR)
    ADD_TO_PARTY(FOUR,THIEF,5,100,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(FOUR,THIEF,5,100,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(FOUR,ARCHER,4,200,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(FOUR,GIANT,2,300,ATTACK_ENEMIES,0)
CREATE_PARTY(FIVE)
    ADD_TO_PARTY(FIVE,ARCHER,3,100,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(FIVE,DWARFA,2,100,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(FIVE,FAIRY,4,200,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(FIVE,FAIRY,4,200,ATTACK_ENEMIES,0)
CREATE_PARTY(SIX)
    ADD_TO_PARTY(SIX,BARBARIAN,4,500,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(SIX,BARBARIAN,4,500,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(SIX,ARCHER,4,500,ATTACK_ENEMIES,0)
CREATE_PARTY(SEVEN)
    ADD_TO_PARTY(SEVEN,KNIGHT,7,1000,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(SEVEN,ARCHER,5,400,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(SEVEN,ARCHER,4,400,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(SEVEN,BARBARIAN,3,400,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(SEVEN,THIEF,4,400,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(SEVEN,DWARFA,4,400,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(SEVEN,WIZARD,3,500,ATTACK_ENEMIES,0)


REM ****************************脚本主体(主要内容、核心内容）**********************************
IF(PLAYER0,TOTAL_CREATURES >= 12)
    SET_TIMER(PLAYER0,TIMER0)
    ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,ONE,5,DUNGEON_HEART,0,1,100)
    IF(PLAYER0,TIMER0 >= 600)
        ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,TWO,6,DUNGEON_HEART,0,1,100)
        IF(PLAYER0,TIMER0 >= 1200)
            ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,THREE,7,DUNGEON_HEART,0,1,100)
        ENDIF
    ENDIF
ENDIF

IF_ACTION_POINT(1,PLAYER0)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,THIEF,2,2,3,100)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,THIEF,3,3,3,100)
    SET_TIMER(PLAYER0,TIMER1)
    IF(PLAYER0,TIMER1 >= 200)
        ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,ARCHER,2,1,2,100)
        ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,THIEF,2,2,2,100)
        ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,BARBARIAN,2,2,1,100)
        ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,ARCHER,3,2,1,100)
        ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,THIEF,3,1,2,500)
        ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,WIZARD,3,1,3,200)
    ENDIF
ENDIF

IF_ACTION_POINT(4,PLAYER0)
    IF(PLAYER_GOOD,TOTAL_CREATURES == 0)
        SET_TIMER(PLAYER0,TIMER2)
        ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,THIEF,-1,2,1,100)
        ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,ARCHER,-1,2,1,100)
        ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,DWARFA,-1,2,1,100)
        ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,DWARFA,-2,1,1,100)
        ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,DWARFA,-2,2,2,100)
        ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,WIZARD,-2,1,3,100)
        IF(PLAYER0,TIMER2 >= 800)
            ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,ARCHER,-1,2,2,100)
            ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,THIEF,-1,1,3,100)
            ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,DWARFA,-1,2,2,100)
            ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,ARCHER,-2,3,1,100)
            ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,BARBARIAN,-2,1,2,100)
            ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,WIZARD,-2,2,2,100)
            IF(PLAYER0,TIMER2 >= 1600)
                ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,GIANT,-1,2,1,100)
                ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,WIZARD,-1,1,3,100)
                ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,ARCHER,-1,1,2,100)
                ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,GIANT,-2,2,1,100)
                ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,WIZARD,-2,1,2,100)
                ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,ARCHER,-2,1,3,100)
            ENDIF
        ENDIF
    ENDIF
ENDIF

SET_FLAG(PLAYER0,FLAG0,0)
IF(PLAYER_GOOD,DUNGEON_DESTROYED == 1)
    ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,FOUR,8,DUNGEON_HEART,0,1,100)
    ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,FIVE,9,DUNGEON_HEART,0,1,100)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SIX,-3,1)
    IF(PLAYER_GOOD,TOTAL_CREATURES == 0)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SEVEN,-3,1)
        SET_FLAG(PLAYER0,FLAG0,1)
    ENDIF
ENDIF

REM ****************************胜利/失败条件**********************************
IF(PLAYER0,FLAG0 == 1)
    IF(PLAYER_GOOD,KNIGHT == 0)
        
        REM 以下修改游戏，增加胜利后可以建造其它房子，让你回味一下胜利的喜悦！
        LEVEL_VERSION(1)
        REVEAL_MAP_RECT(PLAYER0,127,127,254,254)
        ADD_GOLD_TO_PLAYER (PLAYER0,9999999)

        ROOM_AVAILABLE(PLAYER0,TREASURE,1,1)
        ROOM_AVAILABLE(PLAYER0,GARDEN,1,1)
        ROOM_AVAILABLE(PLAYER0,LAIR,1,1)
        ROOM_AVAILABLE(PLAYER0,RESEARCH,1,1)
        ROOM_AVAILABLE(PLAYER0,TRAINING,1,1)
        ROOM_AVAILABLE(PLAYER0,BRIDGE,1,1)
        ROOM_AVAILABLE(PLAYER0,GUARD_POST,1,1)
        ROOM_AVAILABLE(PLAYER0,WORKSHOP,1,1)
        ROOM_AVAILABLE(PLAYER0,BARRACKS,1,1)
        ROOM_AVAILABLE(PLAYER0,TEMPLE,1,1)
        ROOM_AVAILABLE(PLAYER0,PRISON,1,1)
        ROOM_AVAILABLE(PLAYER0,TORTURE,1,1)
        ROOM_AVAILABLE(PLAYER0,SCAVENGER,1,1)
        ROOM_AVAILABLE(PLAYER0,GRAVEYARD,1,1)
        
        MAGIC_AVAILABLE(PLAYER0,POWER_IMP,1,1)
        MAGIC_AVAILABLE(PLAYER0,POWER_CALL_TO_ARMS,1,1
        MAGIC_AVAILABLE(PLAYER0,POWER_SPEED,1,1)
        MAGIC_AVAILABLE(PLAYER0,POWER_HEAL_CREATURE,1,1)
        MAGIC_AVAILABLE(PLAYER0,POWER_DESTROY_WALLS,1,1)

        DOOR_AVAILABLE(PLAYER0,MAGIC,1,200)
        TRAP_AVAILABLE(PLAYER0,LIGHTNING,1,0)
        TRAP_AVAILABLE(PLAYER0,BOULDER,1,200)
        QUICK_INFORMATION(15,"送钱亮图，让你回味一下胜利的喜悦！",PLAYER0)

        WIN_GAME
    ENDIF
ENDIF
IF(PLAYER0,DUNGEON_DESTROYED == 1)
    LOSE_GAME
ENDIF

REM ****************************提示信息**********************************
QUICK_OBJECTIVE(1,"参考提示：1、胜利的方法有很多种，建议智取；2、精灵在工厂的工作效率很高；3、北面的熔岩是可以利用的;4、你人口满12个人类进攻你。",PLAYER0)  
IF(PLAYER0,GAME_TURN > 500)
    QUICK_INFORMATION(2,"如果你的人口满12人，人类就会从四面挖过来攻击你，最好先不要占领入口，挖了所有金矿、建好基地并修好墙才占领入口！",PLAYER0) 
ENDIF
IF(PLAYER0,GAME_TURN > 1500)
    QUICK_INFORMATION(3,"如果想用熔岩烧死敌人，就先挖一条通道连通熔岩，修好通道两边的墙，在靠熔岩的地上建哨所",PLAYER0) 
ENDIF
IF(PLAYER0,GAME_TURN > 2500)
    QUICK_INFORMATION(4,"没有紫金矿，就要靠工厂赚钱，先些能打仗又能在工厂干活的兵！",PLAYER0) 
ENDIF
IF(PLAYER0,GAME_TURN > 3500)
    QUICK_INFORMATION(5,"你的兵只能分批训练，未轮到训练的兵就到工厂干活赚钱去！",PLAYER0) 
ENDIF
IF(PLAYER0,GAME_TURN > 4000)
    QUICK_INFORMATION(6,"祝你好运，守护者！",PLAYER0) 
ENDIF













