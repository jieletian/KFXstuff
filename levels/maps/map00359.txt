REM ***********************************************************************
REM   Script for Level 359(059). Meatpack Skeleton 肉包骨架？
REM ***********************************************************************
REM 
REM 
REM ***********************************************************************
REM 地图的大概情况与参考建议：
REM 1、 。
REM 2、 。
REM 3、 。
REM 4、 。
REM 5、 。
REM ***********************************************************************
REM ***************************各种基本设置 *****************************
SET_GENERATE_SPEED(600)
REM 速度、三个正常智商电脑、电脑1和电脑2分别与电脑3结盟，但电脑1和电脑2没结盟，最大人口，初始金钱数目
COMPUTER_PLAYER(PLAYER1,10)
COMPUTER_PLAYER(PLAYER2,10)
COMPUTER_PLAYER(PLAYER3,10)

ALLY_PLAYERS(PLAYER1,PLAYER3)
ALLY_PLAYERS(PLAYER2,PLAYER3)

MAX_CREATURES(PLAYER0,20)
MAX_CREATURES(PLAYER0,20)
MAX_CREATURES(PLAYER0,15)
MAX_CREATURES(PLAYER0,20)

START_MONEY(PLAYER0,5000)
START_MONEY(PLAYER1,2500)
START_MONEY(PLAYER2,33500)
START_MONEY(PLAYER3,33500)

REM *******************************添加怪物到入口 *********************************
REM   (添加到入口的生物名字,数量)
ADD_CREATURE_TO_POOL(TROLL,20)
ADD_CREATURE_TO_POOL(ORC,20)
ADD_CREATURE_TO_POOL(BILE_DEMON,20)
ADD_CREATURE_TO_POOL(TENTACLE,30)
ADD_CREATURE_TO_POOL(DARK_MISTRESS,30)
ADD_CREATURE_TO_POOL(SORCEROR,21)
ADD_CREATURE_TO_POOL(DRAGON,35)
ADD_CREATURE_TO_POOL(HELL_HOUND,40)

REM *******************************指定阵营（玩家）允许/禁止从入口（怪物池）中招指定生物*********************************
REM  (玩家,生物名称,1（只能是1）,[1允许招募/0禁止招募])

CREATURE_AVAILABLE(ALL_PLAYERS,TROLL,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,ORC,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,BILE_DEMON,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,TENTACLE,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,DARK_MISTRESS,1,1)
CREATURE_AVAILABLE(PLAYER1,SORCEROR,1,1)
CREATURE_AVAILABLE(PLAYER2,DRAGON,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,HELL_HOUND,1,1)

REM ********************指定阵营（玩家）允许/禁止研究和建造指定房子（建筑）**********************************
REM  (玩家,房子名称,是否可研究或建造[1是允许；0是禁止],是否已经可建造[1已经可建/0待研究])
ROOM_AVAILABLE(ALL_PLAYERS,TREASURE,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,LAIR,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,GARDEN,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,TRAINING,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,RESEARCH,,1)
ROOM_AVAILABLE(PLAYER0,BRIDGE,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,WORKSHOP,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,GUARD_POST,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,BARRACKS,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,PRISON,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,TORTURE,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,TEMPLE,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,GRAVEYARD,1,0)

REM ********************指定阵营（玩家）允许/禁止研究和使用指定魔法**********************************
REM  (玩家,魔法名称,是否可研究或使用[1是允许；0是禁止],是否已经可使用[1已经可建/0待研究])
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HAND,1,1)
MAGIC_AVAILABLE(PLAYER1,POWER_IMP,1,1)
MAGIC_AVAILABLE(PLAYER2,POWER_IMP,1,1)
MAGIC_AVAILABLE(PLAYER3,POWER_IMP,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SLAP,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SIGHT,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CALL_TO_ARMS,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HEAL_CREATURE,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HEAL_CREATURE,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SPEED,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_PROTECT,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CONCEAL,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CAVE_IN,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CHICKEN,1,0)

REM *********************指定阵营（玩家）允许/禁止制造指定的门*********************************
REM  (玩家,门的名称,[1是允许；0是禁止],1[0是允许；数字是已经制造出来/赠送的数量]
DOOR_AVAILABLE(ALL_PLAYERS,STEEL,1,0)
DOOR_AVAILABLE(ALL_PLAYERS,MAGIC,1,0)
DOOR_AVAILABLE(PLAYER0,MAGIC,1,2)

REM *********************指定阵营（玩家）允许/禁止制造指定陷阱*********************************
REM  (玩家,陷阱名称,[1是允许；0是禁止],1[0是允许；数字是已经制造出来/赠送的数量]
TRAP_AVAILABLE(ALL_PLAYERS,BOULDER,1,0)


REM ****************************增援部队**********************************
REM CREATE_PARTY =>组建队伍                (队伍名称)   
REM ADD_TO_PARTY=>添加生物到队伍中   (队伍名称,生物名称,生物等级,携带金钱数量,任务,延时)
CREATE_PARTY(L1)
    ADD_TO_PARTY(L1,WIZARD,9,200,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(L1,BARBARIAN,8,200,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(L1,MONK,7,200,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(L1,WITCH,7,200,ATTACK_ENEMIES,0)
CREATE_PARTY(L2)
    ADD_TO_PARTY(L2,SAMURAI,8,200,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(L2,THIEF,8,200,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(L2,WITCH,8,200,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(L2,ARCHER,8,200,ATTACK_ENEMIES,0)
CREATE_PARTY(L3)
    ADD_TO_PARTY(L3,GIANT,9,200,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(L3,WIZARD,8,200,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(L3,BARBARIAN,8,200,ATTACK_ENEMIES,0)

REM ****************************脚本主体(主要内容、核心内容）**********************************
REM 触发点1(监狱东一格):刷兵并计时
IF_ACTION_POINT(1,PLAYER0)
    SET_FLAG(PLAYER0,FLAG0,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,L1,-1,2)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,L2,-1,1)
    SET_TIMER(PLAYER0,TIMER0)
ENDIF
REM 重设时钟并刷怪(不打胜利之前无限刷怪)
IF(PLAYER0,TIMER0 >= 6000)
    NEXT_COMMAND_REUSABLE
    SET_TIMER(PLAYER0,TIMER0)
    IF(PLAYER0,FLAG0 > 0)
        NEXT_COMMAND_REUSABLE
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,L1,-1,2)
        NEXT_COMMAND_REUSABLE
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,L2,-1,1)
    ENDIF
    IF(PLAYER0,FLAG0 > 1)
        NEXT_COMMAND_REUSABLE
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,L2,-2,2)
        NEXT_COMMAND_REUSABLE
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,L3,-2,1)
    ENDIF
    IF(PLAYER0,FLAG0 > 2)
        NEXT_COMMAND_REUSABLE
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,L1,-6,2)
        NEXT_COMMAND_REUSABLE
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,L2,-6,2)
        NEXT_COMMAND_REUSABLE
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,L3,-6,2)
    ENDIF
ENDIF

REM 触发点2(东北龙所在熔岩上的金矿中)
IF_ACTION_POINT(2,PLAYER0)
    SET_FLAG(PLAYER0,FLAG0,2)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,L2,-2,2)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,L3,-2,1)
    ADD_TUNNELLER_TO_LEVEL(PLAYER_GOOD,-2,DUNGEON_HEART,0,10,1000)
ENDIF

REM 守护者1被打爆心脏
IF(PLAYER1,DUNGEON_DESTROYED == 1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,L2,-3,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,L3,-3,2)
ENDIF
REM 守护者2被打爆心脏
IF(PLAYER2,DUNGEON_DESTROYED == 1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,L2,-4,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,L3,-4,2)
ENDIF
REM 守护者3被打爆心脏
IF(PLAYER3,DUNGEON_DESTROYED == 1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,L2,-5,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,L3,-5,2)
ENDIF

REM 三个守护者都被打爆心脏
IF(PLAYER1,DUNGEON_DESTROYED == 1)
    IF(PLAYER2,DUNGEON_DESTROYED == 1)
        IF(PLAYER3,DUNGEON_DESTROYED == 1)
            SET_FLAG(PLAYER0,FLAG0,3)
            ADD_TUNNELLER_TO_LEVEL(PLAYER_GOOD,-6,DUNGEON_HEART,0,10,1000)
            ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,KNIGHT,-6,1,10,10000)
            ADD_PARTY_TO_LEVEL(PLAYER_GOOD,L2,-3,1)
            ADD_PARTY_TO_LEVEL(PLAYER_GOOD,L3,-3,2)
            ADD_PARTY_TO_LEVEL(PLAYER_GOOD,L2,-4,1)
            ADD_PARTY_TO_LEVEL(PLAYER_GOOD,L3,-4,2)
            ADD_PARTY_TO_LEVEL(PLAYER_GOOD,L2,-5,1)
            ADD_PARTY_TO_LEVEL(PLAYER_GOOD,L3,-5,2)
            ADD_PARTY_TO_LEVEL(PLAYER_GOOD,L1,-6,2)
            ADD_PARTY_TO_LEVEL(PLAYER_GOOD,L2,-6,2)
            ADD_PARTY_TO_LEVEL(PLAYER_GOOD,L3,-6,2)
        ENDIF
    ENDIF
ENDIF

REM ****************************胜利/失败条件**********************************
REM 打爆人类心脏胜利
IF(PLAYER_GOOD,DUNGEON_DESTROYED == 1)

        REM 以下修改游戏，增加胜利后可以建造其它房子，让你回味一下胜利的喜悦！
        LEVEL_VERSION(1)
        REVEAL_MAP_RECT(PLAYER0,127,127,254,254)
        ADD_GOLD_TO_PLAYER (PLAYER0,9999999)

        ROOM_AVAILABLE(PLAYER0,TREASURE,1,1)
        ROOM_AVAILABLE(PLAYER0,GARDEN,1,1)
        ROOM_AVAILABLE(PLAYER0,LAIR,1,1)
        ROOM_AVAILABLE(PLAYER0,RESEARCH,1,1)
        ROOM_AVAILABLE(PLAYER0,TRAINING,1,1)
        ROOM_AVAILABLE(PLAYER0,BRIDGE,1,1)
        ROOM_AVAILABLE(PLAYER0,GUARD_POST,1,1)
        ROOM_AVAILABLE(PLAYER0,WORKSHOP,1,1)
        ROOM_AVAILABLE(PLAYER0,BARRACKS,1,1)
        ROOM_AVAILABLE(PLAYER0,TEMPLE,1,1)
        ROOM_AVAILABLE(PLAYER0,PRISON,1,1)
        ROOM_AVAILABLE(PLAYER0,TORTURE,1,1)
        ROOM_AVAILABLE(PLAYER0,SCAVENGER,1,1)
        ROOM_AVAILABLE(PLAYER0,GRAVEYARD,1,1)
        
        MAGIC_AVAILABLE(PLAYER0,POWER_IMP,1,1)
        MAGIC_AVAILABLE(PLAYER0,POWER_CALL_TO_ARMS,1,1
        MAGIC_AVAILABLE(PLAYER0,POWER_SPEED,1,1)
        MAGIC_AVAILABLE(PLAYER0,POWER_HEAL_CREATURE,1,1)
        MAGIC_AVAILABLE(PLAYER0,POWER_DESTROY_WALLS,1,1)

        DOOR_AVAILABLE(PLAYER0,MAGIC,1,200)
        TRAP_AVAILABLE(PLAYER0,LIGHTNING,1,0)
        TRAP_AVAILABLE(PLAYER0,BOULDER,1,200)
        QUICK_INFORMATION(15,"送钱亮图，让你回味一下胜利的喜悦！",PLAYER0)

    WIN_GAME
ENDIF
IF(PLAYER0,DUNGEON_DESTROYED == 1)
    LOSE_GAME
ENDIF

REM ****************************提示信息**********************************
QUICK_OBJECTIVE(1,"参考提示：1、胜利的方法有很多种，建议智取；2、精灵在工厂的工作效率很高；3、东边某地方有监狱，过了监狱人类就会不断增援;4、好好利用监狱。",PLAYER0)  
IF(PLAYER0,GAME_TURN > 500)
    QUICK_INFORMATION(2,"善用滚石陷阱，它威力很大的，东边有个工厂，能制造出这种机关，东边那群人类后面，有个监狱，如果你过了监狱往东，人类就会跟你没完！",PLAYER0) 
ENDIF
IF(PLAYER0,GAME_TURN > 1500)
    QUICK_INFORMATION(3,"精灵在工厂的工作效率很高，同样，骷髅骨骼兵不吃不喝没伤的时候还不用休息，10级以后还有闪电技能，对你现有的条件来说，它们都是好兵",PLAYER0) 
ENDIF
IF(PLAYER0,GAME_TURN > 2500)
    QUICK_INFORMATION(4,"你说如果你把敌人的小鬼都活捉了，那些守护者会怎样？",PLAYER0) 
ENDIF
IF(PLAYER0,GAME_TURN > 3500)
    QUICK_INFORMATION(5,"以弱胜强，就得用以弱胜强的方法，巧取豪夺，比硬拼杀人越货更高明！",PLAYER0) 
ENDIF
IF(PLAYER0,GAME_TURN > 4000)
    QUICK_INFORMATION(6,"祝你好运，守护者！",PLAYER0) 
ENDIF


