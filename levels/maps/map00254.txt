REM ***********************************************************************
REM   Script for Level 254. Titan 泰坦
REM ***********************************************************************
REM titan2.txt - 4 apt, 1 hg(se), 2 enemy keepers
REM 
REM ***********************************************************************
REM 地图的大概情况与参考建议：
REM 1、 。
REM 2、 。
REM 3、 。
REM 4、 。
REM 5、 。
REM ***********************************************************************
REM ***************************各种基本设置 *****************************
SET_GENERATE_SPEED(400)
START_MONEY(ALL_PLAYERS,8000)
COMPUTER_PLAYER(PLAYER1,1)
COMPUTER_PLAYER(PLAYER2,1)
MAX_CREATURES(ALL_PLAYERS,25)

REM *******************************添加怪物到入口 *********************************
REM   (添加到入口的生物名字,数量)
ADD_CREATURE_TO_POOL(ORC,20)
ADD_CREATURE_TO_POOL(SORCEROR,20)
ADD_CREATURE_TO_POOL(DRAGON,20)
ADD_CREATURE_TO_POOL(BILE_DEMON,30)

REM *******************************指定阵营（玩家）允许/禁止从入口（怪物池）中招指定生物*********************************
REM  (玩家,生物名称,1（只能是1）,[1允许招募/0禁止招募])

CREATURE_AVAILABLE(PLAYER1,ORC,1,1)
CREATURE_AVAILABLE(PLAYER2,ORC,1,1)
CREATURE_AVAILABLE(PLAYER1,SORCEROR,1,1)
CREATURE_AVAILABLE(PLAYER2,SORCEROR,1,1)
CREATURE_AVAILABLE(PLAYER1,DRAGON,1,1)
CREATURE_AVAILABLE(PLAYER2,DRAGON,1,1)
CREATURE_AVAILABLE(PLAYER1,BILE_DEMON,1,1)
CREATURE_AVAILABLE(PLAYER2,BILE_DEMON,1,1)

REM ********************指定阵营（玩家）允许/禁止研究和使用指定魔法**********************************
REM  (玩家,魔法名称,是否可研究或使用[1是允许；0是禁止],是否已经可使用[1已经可建/0待研究])
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HAND,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SLAP,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_IMP,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CALL_TO_ARMS,1,1)
MAGIC_AVAILABLE(PLAYER0,POWER_SIGHT,1,1)
MAGIC_AVAILABLE(PLAYER0,POWER_SPEED,1,1)

REM ****************************增援部队**********************************
REM CREATE_PARTY =>组建队伍                (队伍名称)   
REM ADD_TO_PARTY=>添加生物到队伍中   (队伍名称,生物名称,生物等级,携带金钱数量,任务,延时)
CREATE_PARTY(MIX)
    ADD_TO_PARTY(MIX,THIEF,5,500,STEAL_GOLD,0)
    ADD_TO_PARTY(MIX,WITCH,5,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(MIX,MONK,5,0,DEFEND_PARTY,0)
    ADD_TO_PARTY(MIX,SAMURAI,5,0,DEFEND_PARTY,0)
    ADD_TO_PARTY(MIX,BARBARIAN,5,0,DEFEND_PARTY,0)
CREATE_PARTY(WRECK)
    ADD_TO_PARTY(WRECK,DWARFA,2,0,ATTACK_ROOMS,0)
    ADD_TO_PARTY(WRECK,DWARFA,3,0,ATTACK_ROOMS,0)
    ADD_TO_PARTY(WRECK,DWARFA,3,0,ATTACK_ROOMS,0)
CREATE_PARTY(WING)
    ADD_TO_PARTY(WING,FAIRY,3,0,DEFEND_PARTY,0)
    ADD_TO_PARTY(WING,WITCH,5,0,ATTACK_ENEMIES,0)


REM ****************************脚本主体(主要内容、核心内容）**********************************
REM 怪物等级限制：魔术师7级，龙8级，黑暗女王9级，仙女3级。
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,SORCEROR,7)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,DRAGON,8)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,DARK_MISTRESS,9)
SET_CREATURE_MAX_LEVEL(PLAYER_GOOD,FAIRY,3)

REM 占领金库之后可建金库
IF(PLAYER0,TREASURE > 0)
    ROOM_AVAILABLE(PLAYER0,TREASURE,1,1)
ENDIF
REM 占领巢穴之后可建巢穴
IF(PLAYER0,LAIR > 0)
    ROOM_AVAILABLE(PLAYER0,LAIR,1,1)
ENDIF
REM 占领孵化室之后可建孵化室
IF(PLAYER0,GARDEN > 0)
    ROOM_AVAILABLE(PLAYER0,GARDEN,1,1)
ENDIF
REM 占领图书馆之后可建图书馆和兵营
IF(PLAYER0,RESEARCH > 0)
    ROOM_AVAILABLE(PLAYER0,RESEARCH,1,1)
    ROOM_AVAILABLE(PLAYER0,BARRACKS,1,0)
ENDIF
REM 占领工厂之后可制造魔法门、闪电陷阱、滚石陷阱并刷精灵给玩家
IF(PLAYER0,WORKSHOP > 0)
    ADD_CREATURE_TO_LEVEL(PLAYER0,TROLL,PLAYER0,1,5,0)
    DOOR_AVAILABLE(PLAYER0,MAGIC,1,1)
    TRAP_AVAILABLE(PLAYER0,LIGHTNING,1,0)
    TRAP_AVAILABLE(PLAYER0,BOULDER,1,0)
ENDIF
REM 占领审讯室之后可建审讯室、哨所、桥，并刷两队挖掘者带领的敌人出来
触发点1在人类心脏附近，触发点2在东南角和传送门1一起，触发点3在西南部空地上，触发点4在东南部水域中水怪起始点，触发点5在绿色守护巢穴中
IF(PLAYER0,TORTURE > 0)
    ROOM_AVAILABLE(PLAYER0,BRIDGE,1,0)
    ROOM_AVAILABLE(PLAYER0,GUARD_POST,1,0)
    ROOM_AVAILABLE(PLAYER0,TORTURE,1,0)
REM (阵营,小队名称,起点,目的地类型,目的地名称,挖掘者等级,挖掘者现金) 
    ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,MIX,3,DUNGEON,1,3,500)
    ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,MIX,4,DUNGEON,2,3,500)
ENDIF

REM *****************************************************************************************************************
SET_TIMER(PLAYER1,TIMER0)
SET_TIMER(PLAYER2,TIMER0)
REM 守护者1小鬼小于3只就不停刷小鬼给它
IF(PLAYER1,TOTAL_IMPS < 3)
    IF(PLAYER1,TIMER0 >= 1500)
        NEXT_COMMAND_REUSABLE
        ADD_CREATURE_TO_LEVEL(PLAYER1,IMP,PLAYER1,5,1,0)
        NEXT_COMMAND_REUSABLE
        SET_TIMER(PLAYER1,TIMER0)
    ENDIF
ENDIF
REM 守护者2小鬼小于3只就不停刷小鬼给它
IF(PLAYER2,TOTAL_IMPS < 3)
    IF(PLAYER2,TIMER0 >= 1500)
        NEXT_COMMAND_REUSABLE
        ADD_CREATURE_TO_LEVEL(PLAYER2,IMP,PLAYER2,5,1,0)
        NEXT_COMMAND_REUSABLE
        SET_TIMER(PLAYER2,TIMER0)
    ENDIF
ENDIF

REM **************************************************************************************************************
REM 人类心脏被打爆后
REM 光明力量心脏被打爆后，启动它的时钟0，并刷几队人类出来，所有守护者可建工厂和陵墓，另两守护者可造钢门。
IF(PLAYER_GOOD,DUNGEON_DESTROYED == 1)
    SET_TIMER(PLAYER_GOOD,TIMER0)
    QUICK_INFORMATION(7,"人类心脏被干掉了，复仇的火种点燃了！",PLAYER0) 
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,MIX,1,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,MIX,2,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,WING,3,2)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,WING,4,2)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,AVATAR,1,1,10,5000)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,MONK,1,1,10,1000)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,MONK,1,1,10,1000)
    ROOM_AVAILABLE(ALL_PLAYERS,GRAVEYARD,1,1)
    ROOM_AVAILABLE(ALL_PLAYERS,WORKSHOP,1,1)
    DOOR_AVAILABLE(PLAYER1,STEEL,1,0)
    DOOR_AVAILABLE(PLAYER2,STEEL,1,0)
ENDIF
REM 光明力量心脏被打爆后，其它守护者心脏没被打爆，人类数量小于5，时钟0大于7300 game turn，就每7300 game turn不停地刷人类出来
IF(PLAYER_GOOD,DUNGEON_DESTROYED == 1)
    IF(PLAYER0,ALL_DUNGEONS_DESTROYED == 0)
        IF(PLAYER_GOOD,TOTAL_CREATURES < 5)
            IF(PLAYER_GOOD,TIMER0 >= 7300)
                NEXT_COMMAND_REUSABLE
                ADD_PARTY_TO_LEVEL(PLAYER_GOOD,MIX,1,1)
                NEXT_COMMAND_REUSABLE
                ADD_PARTY_TO_LEVEL(PLAYER_GOOD,MIX,3,1)
                NEXT_COMMAND_REUSABLE
                ADD_PARTY_TO_LEVEL(PLAYER_GOOD,MIX,4,1)                
                NEXT_COMMAND_REUSABLE
                SET_TIMER(PLAYER_GOOD,TIMER0)
            ENDIF
        ENDIF
    ENDIF
ENDIF
REM 光明力量心脏被打爆后，玩家有兵营且光明力量仙女小于3个，就不停地刷仙女等人类出来并启动时钟1。
IF(PLAYER_GOOD,DUNGEON_DESTROYED == 1)
    IF(PLAYER0,BARRACKS > 0)
        IF(PLAYER_GOOD,FAIRY < 3)
            NEXT_COMMAND_REUSABLE
            ADD_PARTY_TO_LEVEL(PLAYER_GOOD,WING,1,8)
            NEXT_COMMAND_REUSABLE
            ADD_PARTY_TO_LEVEL(PLAYER_GOOD,MIX,3,1)
            NEXT_COMMAND_REUSABLE
            ADD_PARTY_TO_LEVEL(PLAYER_GOOD,MIX,4,1)
            NEXT_COMMAND_REUSABLE
            SET_TIMER(PLAYER_GOOD,TIMER1)
        ENDIF
    ENDIF
ENDIF
REM *****************************************************************************************************************
REM 光明力量不怪数量等于0，就启动时间1
IF(PLAYER_GOOD,TENTACLE == 0)
    SET_TIMER(PLAYER_GOOD,TIMER1)
ENDIF
REM 光明力量心脏未被打爆后且时钟1大于4300 game turn，就每4300 game turn不停刷怪出来。
IF(PLAYER_GOOD,DUNGEON_DESTROYED == 0)
    IF(PLAYER_GOOD,TIMER1 >= 4300)
        NEXT_COMMAND_REUSABLE
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,MIX,4,1)
        NEXT_COMMAND_REUSABLE
        SET_TIMER(PLAYER_GOOD,TIMER1)
    ENDIF
ENDIF

REM ****************************胜利/失败条件**********************************
IF(PLAYER0,ALL_DUNGEONS_DESTROYED == 1)
    IF(PLAYER_GOOD,TOTAL_CREATURES == 0)
        
        REM 以下修改游戏，增加胜利后可以建造其它房子，让你回味一下胜利的喜悦！
        LEVEL_VERSION(1)
        REVEAL_MAP_RECT(PLAYER0,127,127,254,254)
        ADD_GOLD_TO_PLAYER (PLAYER0,9999999)

        ROOM_AVAILABLE(PLAYER0,TREASURE,1,1)
        ROOM_AVAILABLE(PLAYER0,GARDEN,1,1)
        ROOM_AVAILABLE(PLAYER0,LAIR,1,1)
        ROOM_AVAILABLE(PLAYER0,RESEARCH,1,1)
        ROOM_AVAILABLE(PLAYER0,TRAINING,1,1)
        ROOM_AVAILABLE(PLAYER0,BRIDGE,1,1)
        ROOM_AVAILABLE(PLAYER0,GUARD_POST,1,1)
        ROOM_AVAILABLE(PLAYER0,WORKSHOP,1,1)
        ROOM_AVAILABLE(PLAYER0,BARRACKS,1,1)
        ROOM_AVAILABLE(PLAYER0,TEMPLE,1,1)
        ROOM_AVAILABLE(PLAYER0,PRISON,1,1)
        ROOM_AVAILABLE(PLAYER0,TORTURE,1,1)
        ROOM_AVAILABLE(PLAYER0,SCAVENGER,1,1)
        ROOM_AVAILABLE(PLAYER0,GRAVEYARD,1,1)
        
        MAGIC_AVAILABLE(PLAYER0,POWER_IMP,1,1)
        MAGIC_AVAILABLE(PLAYER0,POWER_CALL_TO_ARMS,1,1
        MAGIC_AVAILABLE(PLAYER0,POWER_SPEED,1,1)
        MAGIC_AVAILABLE(PLAYER0,POWER_HEAL_CREATURE,1,1)
        MAGIC_AVAILABLE(PLAYER0,POWER_DESTROY_WALLS,1,1)

        DOOR_AVAILABLE(PLAYER0,MAGIC,1,200)
        TRAP_AVAILABLE(PLAYER0,LIGHTNING,1,0)
        TRAP_AVAILABLE(PLAYER0,BOULDER,1,200)
        QUICK_INFORMATION(15,"送钱亮图，让你回味一下胜利的喜悦！",PLAYER0)

        WIN_GAME
    ENDIF
ENDIF


REM ****************************提示信息**********************************
REM 提示信息
QUICK_OBJECTIVE(1,"参考提示：1、这里的人类有很强的复仇情绪，做好保护措施不要让他们过来；2、在暗处借刀杀人是最聪明的战略，四两拔千斤，干掉人类的心脏，点燃人类的复仇火种，把那两个愚蠢野蛮的家伙烧为灰烬，你只需要找到显示地图的宝物，坐山观虎斗；3、人类的援兵不断，直到干掉你那两个对手为止;4、那两家伙被消灭，人类增援被消灭，你就胜利了。",PLAYER0)  
IF(PLAYER0,GAME_TURN > 500)
    QUICK_INFORMATION(2,"人类心脏未被打爆前，它的水怪被干掉后，就每4300 gameturn派一队部队出来复仇！",PLAYER0) 
ENDIF
IF(PLAYER0,GAME_TURN > 1500)
    QUICK_INFORMATION(3,"人类心脏被打爆后，就会派几队厉害的部队出来复仇，所有守护者可建工厂和陵墓，你的两对手可造钢门！",PLAYER0) 
ENDIF
IF(PLAYER0,GAME_TURN > 2500)
    QUICK_INFORMATION(4,"人类心脏被打爆后，那两个家伙心脏没被打爆且人类部队小于5，就每7300 gameturn派一队增援部队过来！",PLAYER0) 
ENDIF
IF(PLAYER0,GAME_TURN > 3500)
    QUICK_INFORMATION(5,"人类心脏被打爆后，你家有兵营且人类仙女小于3个，就不停地派仙女等部队过来！",PLAYER0) 
ENDIF
IF(PLAYER0,GAME_TURN > 4000)
    QUICK_INFORMATION(6,"以弱胜强，就得用以弱胜强的方法，巧取豪夺，比硬拼杀人越货更高明，祝你好运，守护者！",PLAYER0) 
ENDIF










