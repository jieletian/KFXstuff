REM ***********************************************************************
REM   Script for Level 401.Piranis2
REM ***********************************************************************
REM 
REM 
REM ***********************************************************************
REM 地图的大概情况与参考建议：
REM 1、地图和215基本上一样，只是把心脏北边的白方金库改成了监狱，增加了2个白方女王，白蜘蛛旁边增加了一只白蟑螂。
REM 2、脚本和215基本上一致，没有一行一行比较，大概上看了看，没看出有大区别。
REM 3、 。
REM 4、 。
REM 5、 。
REM ***********************************************************************
REM ***************************各种基本设置 *****************************
SET_GENERATE_SPEED(400)
MAX_CREATURES(PLAYER0,15)
START_MONEY(PLAYER0,5000)

REM *******************************添加怪物到入口 *********************************
REM   (添加到入口的生物名字,数量)
ADD_CREATURE_TO_POOL(FLY,5)
ADD_CREATURE_TO_POOL(TROLL,10)
ADD_CREATURE_TO_POOL(TENTACLE,5)
ADD_CREATURE_TO_POOL(DARK_MISTRESS,2)
ADD_CREATURE_TO_POOL(SORCEROR,10)
ADD_CREATURE_TO_POOL(BILE_DEMON,10)
ADD_CREATURE_TO_POOL(BUG,10)
ADD_CREATURE_TO_POOL(ORC,10)
ADD_CREATURE_TO_POOL(DRAGON,10)

REM *******************************指定阵营（玩家）允许/禁止从入口（怪物池）中招指定生物*********************************
REM  (玩家,生物名称,1（只能是1）,[1允许招募/0禁止招募])
CREATURE_AVAILABLE(PLAYER0,FLY,1,1)
CREATURE_AVAILABLE(PLAYER0,DRAGON,1,1)
CREATURE_AVAILABLE(PLAYER0,TROLL,1,1)
CREATURE_AVAILABLE(PLAYER0,TENTACLE,1,1)
CREATURE_AVAILABLE(PLAYER0,DARK_MISTRESS,1,1)
CREATURE_AVAILABLE(PLAYER0,SORCEROR,1,1)
CREATURE_AVAILABLE(PLAYER0,BILE_DEMON,1,1)
CREATURE_AVAILABLE(PLAYER0,BUG,1,1)
CREATURE_AVAILABLE(PLAYER0,ORC,1,1)

REM ********************指定阵营（玩家）允许/禁止研究和建造指定房子（建筑）**********************************
REM  (玩家,房子名称,是否可研究或建造[1是允许；0是禁止],是否已经可建造[1已经可建/0待研究])
ROOM_AVAILABLE(PLAYER0,TREASURE,1,1)
ROOM_AVAILABLE(PLAYER0,RESEARCH,1,1)
ROOM_AVAILABLE(PLAYER0,WORKSHOP,1,0)
ROOM_AVAILABLE(PLAYER0,GARDEN,1,1)
ROOM_AVAILABLE(PLAYER0,LAIR,1,1)
ROOM_AVAILABLE(PLAYER0,GUARD_POST,1,0)
ROOM_AVAILABLE(PLAYER0,TRAINING,1,0)
ROOM_AVAILABLE(PLAYER0,BARRACKS,1,0)
ROOM_AVAILABLE(PLAYER0,TEMPLE,1,0)
ROOM_AVAILABLE(PLAYER0,BRIDGE,1,0)

REM ********************指定阵营（玩家）允许/禁止研究和使用指定魔法**********************************
REM  (玩家,魔法名称,是否可研究或使用[1是允许；0是禁止],是否已经可使用[1已经可建/0待研究])
MAGIC_AVAILABLE(PLAYER0,POWER_OBEY,1,1)
MAGIC_AVAILABLE(PLAYER0,POWER_IMP,1,1)
MAGIC_AVAILABLE(PLAYER0,POWER_SLAP,1,1)
MAGIC_AVAILABLE(PLAYER0,POWER_HOLD_AUDIENCE,1,0)
MAGIC_AVAILABLE(PLAYER0,POWER_SPEED,1,0)
MAGIC_AVAILABLE(PLAYER0,POWER_PROTECT,1,0)

REM *********************指定阵营（玩家）允许/禁止制造指定的门*********************************
REM  (玩家,门的名称,[1是允许；0是禁止],1[0是允许；数字是已经制造出来/赠送的数量]
DOOR_AVAILABLE(PLAYER0,WOOD,1,0)
DOOR_AVAILABLE(PLAYER0,BRACED,1,0)
DOOR_AVAILABLE(PLAYER0,STEEL,1,0)
DOOR_AVAILABLE(PLAYER0,MAGIC,1,0)
REM *********************指定阵营（玩家）允许/禁止制造指定陷阱*********************************
REM  (玩家,陷阱名称,[1是允许；0是禁止],1[0是允许；数字是已经制造出来/赠送的数量]
TRAP_AVAILABLE(PLAYER0,WORD_OF_POWER,1,0)
TRAP_AVAILABLE(PLAYER0,ALARM,1,0)
TRAP_AVAILABLE(PLAYER0,POISON_GAS,1,0)

REM CREATE_PARTY =>组建队伍                (队伍名称)   
REM ADD_TO_PARTY=>添加生物到队伍中   (队伍名称,生物名称,生物等级,携带金钱数量,任务,延时)
CREATE_PARTY(MINE)
    ADD_TO_PARTY(MINE,BARBARIAN,4,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(MINE,BARBARIAN,3,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(MINE,BARBARIAN,3,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(MINE,BARBARIAN,3,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(MINE,BARBARIAN,3,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(MINE,ARCHER,2,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(MINE,ARCHER,2,0,ATTACK_DUNGEON_HEART,0)
CREATE_PARTY(MINE2)
    ADD_TO_PARTY(MINE2,WIZARD,6,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(MINE2,WIZARD,6,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(MINE2,WIZARD,5,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(MINE2,MONK,6,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(MINE2,MONK,6,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(MINE2,MONK,6,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(MINE2,MONK,5,0,ATTACK_DUNGEON_HEART,0)
CREATE_PARTY(MINE3)
    ADD_TO_PARTY(MINE3,GIANT,7,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(MINE3,GIANT,7,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(MINE3,GIANT,7,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(MINE3,THIEF,5,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(MINE3,THIEF,5,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(MINE3,THIEF,4,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(MINE3,THIEF,4,0,ATTACK_DUNGEON_HEART,0)
CREATE_PARTY(MINE4)
    ADD_TO_PARTY(MINE4,SAMURAI,7,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(MINE4,SAMURAI,7,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(MINE4,SAMURAI,7,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(MINE4,SAMURAI,7,0,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(MINE4,FAIRY,9,0,ATTACK_DUNGEON_HEART,0)

REM ****************************脚本主体(主要内容、核心内容）**********************************
REM QUICK_OBJECTIVE(1,"Welcome to the syndicate mine complex. Gold is more than present in this land. However,the smugglers built many galleries to protect their treasures. The only gem of the country is the personnal treasure of the Syndicate's Constable,Keeper.",PLAYER0)
QUICK_INFORMATION(7,"欢迎您到本集团矿山综合区。在这片土地上，黄金比现在更多。然而，走私者建造了许多走廊来保护他们的财富。该国仅有宝石是康斯特布尔财团的个人财富，守护者。",PLAYER0)

IF(PLAYER_GOOD,SAMURAI == 0)
    IF(PLAYER_GOOD,SKELETON == 0)
        REM QUICK_OBJECTIVE(3,"You have now conquered a large part of the territory and won the battle,but not the war,be alert,Keeper.",PLAYER0)
        QUICK_INFORMATION(8,"你现在已经征服了一大片领土并赢得了战斗，但没有战争，小心，守护者。",PLAYER0)
    ENDIF
ENDIF

IF(PLAYER0,TORTURE >= 1)
    ROOM_AVAILABLE(PLAYER0,TORTURE,1,1)
    TUTORIAL_FLASH_BUTTON(2,-1)
    TUTORIAL_FLASH_BUTTON(17,-1)
ENDIF

IF_ACTION_POINT(5,PLAYER0)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,MINE2,6,1)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,THIEF,7,5,5,0)
ENDIF

IF(PLAYER_GOOD,BARBARIAN == 0)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,MINE,-2,1)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,DWARFA,-2,5,4,0)
    SET_TIMER(PLAYER_GOOD,TIMER0)
ENDIF
IF(PLAYER_GOOD,TIMER0 >= 3200)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,WITCH,-1,5,4,0)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,GIANT,-1,6,4,0)
ENDIF

IF(PLAYER_GOOD,SAMURAI == 0)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,FAIRY,1,5,7,0)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,BARBARIAN,9,3,4,0)
    SET_TIMER(PLAYER_GOOD,TIMER1)
ENDIF
IF(PLAYER_GOOD,TIMER1 >= 3200)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,MONK,9,4,7,0)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,DWARFA,9,5,6,0)
ENDIF

IF(PLAYER_GOOD,GIANT == 0)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,MINE3,3,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,MINE2,-3,1)
    SET_TIMER(PLAYER_GOOD,TIMER2)
ENDIF
IF(PLAYER_GOOD,TIMER2 >= 4000)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,FAIRY,3,3,8,0)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,MONK,-4,3,9,0)
    SET_TIMER(PLAYER_GOOD,TIMER3)
ENDIF
IF(PLAYER_GOOD,TIMER3 >= 5500)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,MINE3,-3,2)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,ARCHER,3,5,5,0)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,MINE,-4,2)
    SET_TIMER(PLAYER_GOOD,TIMER4)
ENDIF
IF(PLAYER_GOOD,TIMER4 >= 5500)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,MINE4,3,2)
ENDIF

IF(PLAYER_GOOD,THIEF == 0)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,MINE2,6,1)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,GIANT,6,5,5,0)
    SET_TIMER(PLAYER_GOOD,TIMER5)
ENDIF
IF(PLAYER_GOOD,TIMER5 >= 6500)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,WIZARD,6,5,6,0)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,ARCHER,6,7,7,0)
ENDIF

IF_ACTION_POINT(8,PLAYER0)
     REM QUICK_OBJECTIVE(2," The Constable sends his more fervent troops to annihilate you. Failure is not an option,Keeper.",PLAYER0)
     QUICK_INFORMATION(9," 康斯特布尔把他更狂热的部队消灭你。失败不是一种选择，守护者。",PLAYER0)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,BARBARIAN,-2,4,10,0)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,ARCHER,-4,4,8,0)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,MONK,9,4,8,0)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,GIANT,4,3,10,0)
    ADD_TUNNELLER_TO_LEVEL(PLAYER_GOOD,10,ACTION_POINT,11,9,800)
    SET_FLAG(PLAYER_GOOD,FLAG0,1)
ENDIF
IF(PLAYER_GOOD,FLAG0 == 1)
    IF(PLAYER_GOOD,TOTAL_CREATURES == 0)
        ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,KNIGHT,2,1,10,0)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,MINE2,2,2)
        ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,MONK,2,4,9,0)
        ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,FAIRY,2,4,9,0)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,MINE,4,2)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,MINE,9,2)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,MINE,-4,2)
        SET_FLAG(PLAYER_GOOD,FLAG1,1)
    ENDIF
ENDIF
REM ****************************胜利/失败条件**********************************
IF(PLAYER_GOOD,FLAG1 == 1)
    IF(PLAYER_GOOD,TOTAL_CREATURES == 0)
        REM 以下修改游戏，增加胜利后可以建造其它房子，让你回味一下胜利的喜悦！
        LEVEL_VERSION(1)
        REVEAL_MAP_RECT(PLAYER0,127,127,254,254)
        ADD_GOLD_TO_PLAYER (PLAYER0,9999999)

        ROOM_AVAILABLE(PLAYER0,TREASURE,1,1)
        ROOM_AVAILABLE(PLAYER0,GARDEN,1,1)
        ROOM_AVAILABLE(PLAYER0,LAIR,1,1)
        ROOM_AVAILABLE(PLAYER0,RESEARCH,1,1)
        ROOM_AVAILABLE(PLAYER0,TRAINING,1,1)
        ROOM_AVAILABLE(PLAYER0,BRIDGE,1,1)
        ROOM_AVAILABLE(PLAYER0,GUARD_POST,1,1)
        ROOM_AVAILABLE(PLAYER0,WORKSHOP,1,1)
        ROOM_AVAILABLE(PLAYER0,BARRACKS,1,1)
        ROOM_AVAILABLE(PLAYER0,TEMPLE,1,1)
        ROOM_AVAILABLE(PLAYER0,PRISON,1,1)
        ROOM_AVAILABLE(PLAYER0,TORTURE,1,1)
        ROOM_AVAILABLE(PLAYER0,SCAVENGER,1,1)
        ROOM_AVAILABLE(PLAYER0,GRAVEYARD,1,1)
        
        MAGIC_AVAILABLE(PLAYER0,POWER_IMP,1,1)
        MAGIC_AVAILABLE(PLAYER0,POWER_CALL_TO_ARMS,1,1
        MAGIC_AVAILABLE(PLAYER0,POWER_SPEED,1,1)
        MAGIC_AVAILABLE(PLAYER0,POWER_HEAL_CREATURE,1,1)
        MAGIC_AVAILABLE(PLAYER0,POWER_DESTROY_WALLS,1,1)

        DOOR_AVAILABLE(PLAYER0,MAGIC,1,200)
        TRAP_AVAILABLE(PLAYER0,LIGHTNING,1,0)
        TRAP_AVAILABLE(PLAYER0,BOULDER,1,200)
        QUICK_INFORMATION(15,"送钱亮图，让你回味一下胜利的喜悦！",PLAYER0)

        WIN_GAME
    ENDIF
ENDIF


REM ****************************提示信息**********************************
QUICK_OBJECTIVE(0,"提示：1、你的兵不多，避免大量敌人一起涌过来；2、建议以工厂赚钱为主，除非你是打仗高手；3、好好利用好那些不起眼的熔岩，还有监狱和审讯室;4、忍者、野蛮人、骷髅、骑士是敌人的官二代。",PLAYER0)  
IF(PLAYER0,GAME_TURN > 200)
    QUICK_INFORMATION(2,"在这里，到处是敌人的哨点，离你家很近，你要绕圈挖过去，让敌人在长长的路上慢慢走过来，以免敌人太集中你少量兵应付不过来！",PLAYER0) 
ENDIF
IF(PLAYER0,GAME_TURN > 500)
    QUICK_INFORMATION(3,"外面有大量敌人，挖金矿的时候不要破坏地形，以免敌人蜂涌过来，建议少招不到工厂干活的兵，以工厂赚钱为主，除非你是打仗高手。",PLAYER0) 
ENDIF
IF(PLAYER0,GAME_TURN > 1000)
    QUICK_INFORMATION(4,"好好利用好那些不起眼的熔岩，从上面绕过去会轻松很多，必要时也可以把敌人吹到熔岩上去。",PLAYER0) 
ENDIF
IF(PLAYER0,GAME_TURN > 1500)
    QUICK_INFORMATION(5,"忍者、野蛮人、骷髅、骑士是敌人的官二代，干掉它们后会有敌人为他们报仇，如果你去挖紫金矿，敌人也会不高兴的！",PLAYER0) 
ENDIF
IF(PLAYER0,GAME_TURN > 2000)
    QUICK_INFORMATION(6,"祝你好运，守护者！",PLAYER0) 
ENDIF









