REM ***********************************************************************
REM   Script for Level 249. Ashfordania
REM ***********************************************************************
REM  Tim Daish 
REM 
REM ***********************************************************************
REM 地图的大概情况与参考建议：
REM 1、游戏总体设计还是比较好，但一些细节出了问题。
REM 2、设计中生物数量比较大，可能会引起内存出错弹出游戏，这里修改了守护者生物上限，但刷出来的人在数量没有修改，打游戏的时候注意不要大规模战争。
REM 4、盟友心脏被打爆心脏后，它的建筑不能被占领，偏离作者设计初衷。
REM 5、人类能拆建筑，但连心脏也被拆了一部分，而不是被打爆。
REM 6、提示信息序号不正确，导致后面的信息不能正常显示。
REM 7、慎用RESEARCH语句，容易出错。
REM ***********************************************************************
SET_GENERATE_SPEED(670)
COMPUTER_PLAYER(PLAYER1, 10)
ALLY_PLAYERS(PLAYER0, PLAYER1)

REM MAX_CREATURES(PLAYER0,34)
REM MAX_CREATURES(PLAYER1,38)
MAX_CREATURES(PLAYER0,24)
MAX_CREATURES(PLAYER1,28)

START_MONEY(PLAYER0,17000)
START_MONEY(PLAYER1,28000)

REM *******************************添加怪物到入口 *********************************
REM   (添加到入口的生物名字,数量)
ADD_CREATURE_TO_POOL(BUG,4)
ADD_CREATURE_TO_POOL(FLY,5)
ADD_CREATURE_TO_POOL(SPIDER,6)
ADD_CREATURE_TO_POOL(SORCEROR,11)
ADD_CREATURE_TO_POOL(DEMONSPAWN,9)
ADD_CREATURE_TO_POOL(DRAGON,9)
ADD_CREATURE_TO_POOL(TROLL,7)
ADD_CREATURE_TO_POOL(ORC,8)
ADD_CREATURE_TO_POOL(HELL_HOUND,6)
ADD_CREATURE_TO_POOL(DARK_MISTRESS,7)
ADD_CREATURE_TO_POOL(BILE_DEMON,7)

REM *******************************指定阵营（玩家）允许/禁止从入口（怪物池）中招指定生物*********************************
REM  (玩家,生物名称,1（只能是1）,[1允许招募/0禁止招募])

CREATURE_AVAILABLE(ALL_PLAYERS,BUG,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,FLY,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,SPIDER,1,1)
CREATURE_AVAILABLE(PLAYER1,SORCEROR,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,DEMONSPAWN,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,DRAGON,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,TROLL,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,ORC,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,HELL_HOUND,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,DARK_MISTRESS,1,1)
CREATURE_AVAILABLE(PLAYER1,BILE_DEMON,1,1)

REM ********************指定阵营（玩家）允许/禁止研究和建造指定房子（建筑）**********************************
REM  (玩家,房子名称,是否可研究或建造[1是允许；0是禁止],是否已经可建造[1已经可建/0待研究])
ROOM_AVAILABLE(ALL_PLAYERS,TREASURE,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,LAIR,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,GARDEN,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,TRAINING,1,1)
ROOM_AVAILABLE(PLAYER1,RESEARCH,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,BRIDGE,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,GUARD_POST,1,0)
ROOM_AVAILABLE(PLAYER1,WORKSHOP,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,BARRACKS,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,PRISON,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,TORTURE,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,TEMPLE,1,0)
ROOM_AVAILABLE(PLAYER1, GRAVEYARD,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,SCAVENGER,1,0)

REM ********************指定阵营（玩家）允许/禁止研究和使用指定魔法**********************************
REM  (玩家,魔法名称,是否可研究或使用[1是允许；0是禁止],是否已经可使用[1已经可建/0待研究])
MAGIC_AVAILABLE(PLAYER1,POWER_IMP,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_OBEY,1,0)
MAGIC_AVAILABLE(PLAYER1,POWER_SIGHT,0,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CALL_TO_ARMS,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CAVE_IN,1,0)
MAGIC_AVAILABLE(PLAYER1,POWER_HEAL_CREATURE,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HOLD_AUDIENCE,1,0)
MAGIC_AVAILABLE(PLAYER1,POWER_LIGHTNING,1,0)
MAGIC_AVAILABLE(PLAYER1,POWER_SPEED,1,0)
MAGIC_AVAILABLE(PLAYER1,POWER_PROTECT,1,0)
MAGIC_AVAILABLE(PLAYER1,POWER_CONCEAL,1,0)
MAGIC_AVAILABLE(PLAYER1,POWER_CHICKEN,1,0)
MAGIC_AVAILABLE(PLAYER1,POWER_DESTROY_WALLS,1,0)

REM ********************魔法的研究时间**********************************
RESEARCH(ALL_PLAYERS,ROOM,BRIDGE,9600)
RESEARCH(ALL_PLAYERS,MAGIC,POWER_OBEY,6000)
RESEARCH(ALL_PLAYERS,ROOM,GUARD_POST,11700)
RESEARCH(ALL_PLAYERS,MAGIC,POWER_HEAL_CREATURE,14000)
RESEARCH(ALL_PLAYERS,ROOM,WORKSHOP,77000)
RESEARCH(ALL_PLAYERS,MAGIC,POWER_PROTECT,15000)
RESEARCH(ALL_PLAYERS,MAGIC,POWER_CONCEAL,9400)
RESEARCH(ALL_PLAYERS,ROOM,BARRACKS,32000)
RESEARCH(ALL_PLAYERS,MAGIC,POWER_CALL_TO_ARMS,7400)
RESEARCH(ALL_PLAYERS,ROOM,PRISON,45000)
RESEARCH(ALL_PLAYERS,MAGIC,POWER_SPEED,5700)
RESEARCH(ALL_PLAYERS,ROOM,TORTURE,49000)
RESEARCH(ALL_PLAYERS,MAGIC,POWER_HOLD_AUDIENCE,11000)
RESEARCH(ALL_PLAYERS,ROOM,TEMPLE,55000)
RESEARCH(ALL_PLAYERS,MAGIC,POWER_CAVE_IN,25000)
RESEARCH(ALL_PLAYERS,ROOM,GRAVEYARD,65000)
RESEARCH(ALL_PLAYERS,MAGIC,POWER_LIGHTNING,35000)
RESEARCH(ALL_PLAYERS,ROOM,SCAVENGER,57500)
RESEARCH(ALL_PLAYERS,MAGIC,POWER_CHICKEN,20000)
RESEARCH(PLAYER1,MAGIC,POWER_DESTROY_WALLS,730000)

REM *********************指定阵营（玩家）允许/禁止制造指定的门*********************************
REM  (玩家,门的名称,[1是允许；0是禁止],1[0是允许；数字是已经制造出来/赠送的数量]
DOOR_AVAILABLE(ALL_PLAYERS,WOOD,0,0)
DOOR_AVAILABLE(ALL_PLAYERS,BRACED,1,0)
DOOR_AVAILABLE(ALL_PLAYERS,STEEL,1,0)
DOOR_AVAILABLE(ALL_PLAYERS,MAGIC,1,0)

REM *********************指定阵营（玩家）允许/禁止制造指定陷阱*********************************
REM  (玩家,陷阱名称,[1是允许；0是禁止],1[0是允许；数字是已经制造出来/赠送的数量]
TRAP_AVAILABLE(ALL_PLAYERS,BOULDER,0,0)
TRAP_AVAILABLE(ALL_PLAYERS,ALARM,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,POISON_GAS,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,LIGHTNING,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,WORD_OF_POWER,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,LAVA,1,0)


REM ****************************增援部队**********************************
REM CREATE_PARTY =>组建队伍                (队伍名称)   
REM ADD_TO_PARTY=>添加生物到队伍中   (队伍名称,生物名称,生物等级,携带金钱数量,任务,延时)
CREATE_PARTY(ATTACK1)
ADD_TO_PARTY(ATTACK1, THIEF, 9, 250, ATTACK_ROOMS, 0)
ADD_TO_PARTY(ATTACK1, ARCHER, 10, 150, DEFEND_PARTY, 0)
ADD_TO_PARTY(ATTACK1, THIEF, 9, 250, ATTACK_ROOMS, 0)
ADD_TO_PARTY(ATTACK1, ARCHER, 10, 150, DEFEND_PARTY, 0)
ADD_TO_PARTY(ATTACK1, THIEF, 9, 250, ATTACK_ROOMS, 0)
ADD_TO_PARTY(ATTACK1, THIEF, 9, 250, ATTACK_ROOMS, 0)
CREATE_PARTY(THEBOYS1)
ADD_TO_PARTY(THEBOYS1, BARBARIAN, 9, 400, ATTACK_ROOMS,0)
ADD_TO_PARTY(THEBOYS1, BARBARIAN, 9, 400, ATTACK_ROOMS,0)
ADD_TO_PARTY(THEBOYS1, SAMURAI, 9, 700, DEFEND_PARTY,0)
ADD_TO_PARTY(THEBOYS1, THIEF, 9, 200, STEAL_SPELLS,0)
ADD_TO_PARTY(THEBOYS1, THIEF, 7, 900, ATTACK_ENEMIES,0)
ADD_TO_PARTY(THEBOYS1, THIEF, 6, 500, ATTACK_ENEMIES,0)
CREATE_PARTY(THEBOYS2)
ADD_TO_PARTY(THEBOYS2, ARCHER, 9, 700, ATTACK_ENEMIES,0)
CREATE_PARTY(HEROES1)
ADD_TO_PARTY(HEROES1, BARBARIAN, 10, 400, DEFEND_PARTY, 0)
ADD_TO_PARTY(HEROES1, BARBARIAN, 10, 400, ATTACK_ROOMS, 0)
ADD_TO_PARTY(HEROES1, SAMURAI, 10, 700, ATTACK_ENEMIES, 0)
ADD_TO_PARTY(HEROES1, WIZARD, 10, 900, ATTACK_ROOMS, 0)
ADD_TO_PARTY(HEROES1, THIEF, 10, 200, STEAL_GOLD,0)
ADD_TO_PARTY(HEROES1, MONK, 10, 900, ATTACK_ROOMS, 0)
CREATE_PARTY(HEROES2)
ADD_TO_PARTY(HEROES2, WIZARD, 10, 500, ATTACK_ROOMS, 0)
CREATE_PARTY(HEROES3)
ADD_TO_PARTY(HEROES3, GIANT, 10, 400, ATTACK_ENEMIES, 0)
ADD_TO_PARTY(HEROES3, WITCH, 10, 300, ATTACK_ENEMIES, 0)
ADD_TO_PARTY(HEROES3, DWARFA, 10, 100, DEFEND_PARTY,  0)
ADD_TO_PARTY(HEROES3, DWARFA, 10, 100, DEFEND_PARTY,  0)
ADD_TO_PARTY(HEROES3, SAMURAI, 10, 650, ATTACK_ROOMS,  0)
ADD_TO_PARTY(HEROES3, SAMURAI, 10, 650, ATTACK_ROOMS,  0)
ADD_TO_PARTY(HEROES3, SAMURAI, 10, 650, ATTACK_ROOMS,  0)
CREATE_PARTY(HEROES4)
ADD_TO_PARTY(HEROES4, WIZARD, 10, 530, ATTACK_ROOMS,  0)
ADD_TO_PARTY(HEROES4, WIZARD, 10, 660, ATTACK_ROOMS,  0)
ADD_TO_PARTY(HEROES4, WIZARD, 10, 660, ATTACK_ENEMIES,  0)
ADD_TO_PARTY(HEROES4, MONK, 10, 350, DEFEND_PARTY,  0)
ADD_TO_PARTY(HEROES4, MONK, 10, 350, ATTACK_ROOMS,  0)
ADD_TO_PARTY(HEROES4, MONK, 10, 350, ATTACK_ROOMS,  0)
CREATE_PARTY(HEROES5)
ADD_TO_PARTY(HEROES5, SAMURAI, 10, 650, ATTACK_ROOMS,  0)
ADD_TO_PARTY(HEROES5, GIANT, 10, 250, ATTACK_ROOMS,  0)
ADD_TO_PARTY(HEROES5, THIEF, 10, 350, STEAL_SPELLS,  0)
ADD_TO_PARTY(HEROES5, THIEF, 10, 100, STEAL_SPELLS,  0)
ADD_TO_PARTY(HEROES5, THIEF, 10, 100, STEAL_SPELLS,  0)
ADD_TO_PARTY(HEROES5, BARBARIAN, 10, 250, DEFEND_PARTY,  0)
CREATE_PARTY(HEROES6)
ADD_TO_PARTY(HEROES6, SAMURAI, 10, 650, ATTACK_ENEMIES,  0)
ADD_TO_PARTY(HEROES6, SAMURAI, 10, 650, ATTACK_ENEMIES,  0)
ADD_TO_PARTY(HEROES6, SAMURAI, 10, 750, ATTACK_ENEMIES,  0)
ADD_TO_PARTY(HEROES6, WITCH, 10, 450, ATTACK_ENEMIES,  0)
ADD_TO_PARTY(HEROES6, WITCH, 10, 450, ATTACK_ENEMIES,  0)
ADD_TO_PARTY(HEROES6, WITCH, 10, 450, ATTACK_ENEMIES,  0)
ADD_TO_PARTY(HEROES6, KNIGHT, 10, 900, ATTACK_ENEMIES,  0)
CREATE_PARTY(HEROES7)
ADD_TO_PARTY(HEROES7, GIANT, 10, 350, ATTACK_ENEMIES,  0)
ADD_TO_PARTY(HEROES7, BARBARIAN, 10, 350, ATTACK_ENEMIES,  0)
ADD_TO_PARTY(HEROES7, WITCH, 10, 350, ATTACK_DUNGEON_HEART,  0)
ADD_TO_PARTY(HEROES7, MONK, 10, 350, ATTACK_DUNGEON_HEART,  0)
ADD_TO_PARTY(HEROES7, DWARFA, 10, 350, ATTACK_DUNGEON_HEART,  0)
ADD_TO_PARTY(HEROES7, DWARFA, 10, 550, ATTACK_DUNGEON_HEART,  0)
ADD_TO_PARTY(HEROES7, KNIGHT, 10, 950, ATTACK_DUNGEON_HEART,  0)
CREATE_PARTY(HEROES8)
ADD_TO_PARTY(HEROES8, ARCHER, 10, 350, ATTACK_ENEMIES,  0)
ADD_TO_PARTY(HEROES8, WIZARD, 10, 350, ATTACK_ENEMIES,  0)
ADD_TO_PARTY(HEROES8, WIZARD, 10, 350, ATTACK_DUNGEON_HEART,  0)
ADD_TO_PARTY(HEROES8, WITCH, 10, 550, ATTACK_DUNGEON_HEART,  0)
ADD_TO_PARTY(HEROES8, SAMURAI, 10, 560, ATTACK_DUNGEON_HEART,  0)
ADD_TO_PARTY(HEROES8, SAMURAI, 10, 750, ATTACK_DUNGEON_HEART,  0)
ADD_TO_PARTY(HEROES8, KNIGHT, 10, 950, ATTACK_DUNGEON_HEART,  0)

REM ****************************脚本主体(主要内容、核心内容）**********************************
SET_CREATURE_MAX_LEVEL(PLAYER0, TROLL, 5)
SET_CREATURE_MAX_LEVEL(PLAYER0, DEMONSPAWN, 9)
SET_CREATURE_MAX_LEVEL(PLAYER0, SORCEROR, 7)
SET_CREATURE_MAX_LEVEL(PLAYER0, DARK_MISTRESS, 6)
SET_CREATURE_MAX_LEVEL(PLAYER0, HELL_HOUND, 7)
SET_CREATURE_MAX_LEVEL(PLAYER0, IMP, 5)
SET_CREATURE_FEAR(IMP, 220)

SET_FLAG(PLAYER0,FLAG1,0)
SET_FLAG(PLAYER_GOOD,FLAG1,0)
REM 下面这个变量在后面没有使用
SET_FLAG(PLAYER1,FLAG0,0)
SET_FLAG(PLAYER0,FLAG0,0)
SET_FLAG(PLAYER1, FLAG1,0)

REM PlayerTimer Name 时钟0用于提示信息
SET_TIMER(PLAYER0,TIMER0)
REM 时钟0游戏开始后就启动
IF(PLAYER0,TIMER0 >= 15000)
  REM QUICK_INFORMATION(3, "The Heroes are forming quite an army. Prepare yourself well, Keeper.")
  QUICK_INFORMATION(16, "英雄们正在形成一支军队，准备好自己，守护者。")
ENDIF

REM 当你的人口达到50时，你就可以研究闪电魔法
REM  IF(PLAYER0, TOTAL_CREATURES >= 50
IF(PLAYER0, TOTAL_CREATURES >= 34)
  MAGIC_AVAILABLE( PLAYER0, POWER_LIGHTNING, 1, 0)
ENDIF

REM 当你战胜50个敌人(BATTLES_WON )后，就会重设你的生物最高级别，小鬼10级、黑女10级、精灵8级、地狱犬10级，恶鬼10级
IF(PLAYER0, BATTLES_WON > 50)
  SET_CREATURE_MAX_LEVEL(PLAYER0, IMP, 10)
  SET_CREATURE_MAX_LEVEL(PLAYER0, DARK_MISTRESS, 10)
  SET_CREATURE_MAX_LEVEL(PLAYER0, TROLL, 8)
  SET_CREATURE_MAX_LEVEL(PLAYER0, HELL_HOUND, 10)
  SET_CREATURE_MAX_LEVEL(PLAYER0, DEMONSPAWN ,10)
ENDIF

REM 当你工厂大于1时，你就可以建造工厂，弓箭手恐惧值重设为20，力量值为50
IF(PLAYER0, WORKSHOP >  1)
  ROOM_AVAILABLE( PLAYER0, WORKSHOP, 1, 1)
  SET_CREATURE_FEAR(ARCHER ,20)
  SET_CREATURE_STRENGTH(ARCHER, 50)
ENDIF
REM 当你的战死15人(BATTLES_LOST )时，就会重设你的生物最高级别，地狱犬10级、魔术师10级、巨魔10级、龙10级
IF(PLAYER0, BATTLES_LOST >= 15)
  SET_CREATURE_MAX_LEVEL(PLAYER0, HELL_HOUND, 10)
  SET_CREATURE_MAX_LEVEL(PLAYER0, SORCEROR, 10)
  SET_CREATURE_MAX_LEVEL(PLAYER0, BILE_DEMON, 10)
  SET_CREATURE_MAX_LEVEL(PLAYER0, DRAGON, 10)
ENDIF
REM 当你的图书馆数量大于4时，你就可以建造图书馆
IF(PLAYER0, RESEARCH > 4)
     ROOM_AVAILABLE(PLAYER0, RESEARCH, 1, 1)
ENDIF



IF_ACTION_POINT(3, PLAYER0)
  REM QUICK_INFORMATION(1, "You might regret this little venture Westwards! Curb you explorations or else!")
  QUICK_INFORMATION(2, "你可能会后悔这小冒险向西！控制你的探索或其他！")
  ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, SAMURAI, -1, 3, 7, 250)
  ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, THIEF, -1, 2, 8, 100)
  ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, THIEF, -1, 2, 8, 100)
  ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, THIEF, -1, 2, 8, 100)
  SET_TIMER(PLAYER0, TIMER3)
ENDIF
IF(PLAYER0,TIMER3 >= 7200)
  ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD, HEROES5, -1, DUNGEON, 0, 7, 500)
ENDIF
IF_ACTION_POINT(4, PLAYER0)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD, HEROES1, -4, 1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD, HEROES1, -3, 1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD, HEROES2, -2, 7)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD, HEROES3, -1, 1)
ENDIF
REM 触发点5后启动时钟1
IF_ACTION_POINT(5, PLAYER0)
  SET_TIMER( PLAYER0, TIMER1)
  ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, SAMURAI, -3, 3, 9, 850)
ENDIF
IF( PLAYER0, TIMER1 >= 19500)
  ADD_PARTY_TO_LEVEL(PLAYER_GOOD, THEBOYS1, -2, 1)
  ADD_PARTY_TO_LEVEL(PLAYER_GOOD, THEBOYS2, -2, 6)
  ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD, THEBOYS1, -4, DUNGEON, 0, 8, 500)
ENDIF
IF_ACTION_POINT(6, PLAYER0)
  REM QUICK_INFORMATION(9, "You have found several imps willing to join your cause!")
  QUICK_INFORMATION(8, "你已经发现了几个小鬼愿意加入你的事业！")
ENDIF
IF_ACTION_POINT(7, PLAYER0)
   SET_FLAG( PLAYER0, FLAG1, 1)
ENDIF
IF_ACTION_POINT(7, PLAYER_GOOD)
   SET_FLAG( PLAYER_GOOD, FLAG1, 1)
ENDIF
IF_ACTION_POINT(10, PLAYER0)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD, HEROES6, 10, 1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD, HEROES2, 14, 7)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD, HEROES3, -5, 2)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD, HEROES4, -4, 2)
    SET_TIMER(PLAYER0, TIMER2)
ENDIF
IF(PLAYER0,TIMER2>=1500)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD, HEROES7, -3, 1)
ENDIF
IF(PLAYER0,TIMER2>=2750)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD, HEROES8, -2, 1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD, HEROES1, -1, 1)
ENDIF
IF_ACTION_POINT(11, PLAYER0)
  SET_CREATURE_MAX_LEVEL(PLAYER0, IMP, 9)
ENDIF
REM 西南部，桥的东边
IF_ACTION_POINT(12, PLAYER0)
  REM QUICK_OBJECTIVE(8, "One of your creatures got bored and ate all of the instructions for making the traps! Pray you have laid enough traps already.", PLAYER0)
  QUICK_INFORMATION(7, "你的一个生物厌倦了，吃了所有制造陷阱的指南！祈祷你已经有足够的陷阱。", PLAYER0)
  TRAP_AVAILABLE(PLAYER0, BOULDER, 0, 0)
  TRAP_AVAILABLE(PLAYER0, LAVA, 0, 0)
  TRAP_AVAILABLE(PLAYER0, ALARM, 0, 0)
  TRAP_AVAILABLE(PLAYER0, POISON_GAS, 0, 0)
  TRAP_AVAILABLE(PLAYER0, LIGHTNING, 0, 0)
  TRAP_AVAILABLE(PLAYER0, WORD_OF_POWER, 0, 0)
ENDIF
IF_ACTION_POINT(13, PLAYER0)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD, THEBOYS1, -2, 1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD, THEBOYS2, -2, 6)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD, THEBOYS1, -1, 1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD, THEBOYS2, -1, 3)
ENDIF
IF_ACTION_POINT(14, PLAYER0)
    SET_CREATURE_FEAR(WITCH, 5)
    SET_CREATURE_FEAR(WIZARD, 5)
    SET_CREATURE_ARMOUR(DWARFA, 70)
    SET_CREATURE_ARMOUR(WITCH, 50)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD, THEBOYS1, -4, 1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD, THEBOYS2, -1, 6)
    ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD, ATTACK1, -5, DUNGEON, 0, 9, 300)
    ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD, HEROES4, -3, DUNGEON, 0, 7, 230)
    ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD, HEROES5, -1, DUNGEON, 0, 8, 330)
ENDIF
IF_ACTION_POINT(15, PLAYER0)
  REM QUICK_OBJECTIVE(7, "You appear to have stumbled upon an old scroll containing the instructions for making a boulder trap. Set your creatures to work immediately,and use them well!", PLAYER0)
  QUICK_INFORMATION(6, "你出现在一个旧的卷轴上，有一个古老的卷轴，包含了制作一个巨石陷阱的指令。让你的生物立即工作，并使用它们！", PLAYER0)
  TRAP_AVAILABLE(PLAYER0, BOULDER, 1, 0)
ENDIF
IF_ACTION_POINT(16, PLAYER0)
    ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD, ATTACK1, -1, DUNGEON, 0, 9, 300)
ENDIF

IF(PLAYER1, WORKSHOP >= 1)
     REM  QUICK_INFORMATION(2, "Blue dungeon has workshop")
     QUICK_INFORMATION(3, "蓝守护者有了工厂")
     SET_TIMER( PLAYER1, TIMER1)
ENDIF
IF(PLAYER1,TIMER1 >= 2000)
  REM QUICK_INFORMATION(4, "Information has come to light that your allies have begun laying traps.")
  QUICK_INFORMATION(4, "信息已经到来，你的盟友已经开始铺设陷阱。")
ENDIF
REM *****PLAYER0, FLAG1到触发点7后值变为1
IF( PLAYER1, TIMER1 >= 23500)
  IF( PLAYER0, FLAG1 == 0)
    REM QUICK_INFORMATION(6, "The Heroes are attacking your ally. Go over there and and give some support before it is too late!")
    QUICK_INFORMATION(5, "英雄们正在攻击你的盟友，去那里，并给予一些支持之前，为时已晚！")
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD, THEBOYS1, -2, 2)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD, THEBOYS2, -4, 6)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD, HEROES2, -2, 1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD, ATTACK1, -2, 1)
    ADD_TUNNELLER_TO_LEVEL(PLAYER_GOOD, -4, DUNGEON_HEART, 1, 9, 400)
  ENDIF
ENDIF
IF(PLAYER0, FLAG1 == 1)
  IF( PLAYER1, TIMER1 < 33500)
    IF(PLAYER1, DUNGEON_DESTROYED == 1)
      IF_AVAILABLE(PLAYER0, POWER_IMP, ==, 0)
        MAGIC_AVAILABLE( PLAYER0, POWER_IMP, 1, 1)
        REM QUICK_INFORMATION(14, "Well done! As a little reward for turning upon your so-called friends, you have been given the power to create Imps. Keep them under control, or suffer the consequences.")
        QUICK_INFORMATION(12, "做得好！至于转弯后，你所谓的朋友一点奖励，你得到的力量创造的小鬼。让他们在控制之下，或遭受的后果。")
      ENDIF
    ENDIF
  ENDIF
ENDIF

REM ***** Send in the boys to defeat the blue dungeon if not conquered by red keeper *****
IF(PLAYER_GOOD, FLAG1 == 1)
  IF(PLAYER0, FLAG1 == 0)
    IF(PLAYER1, DUNGEON_DESTROYED == 1)
      REM QUICK_OBJECTIVE(15, "The Heroes have vanquished the other Keeper. Get over there now and try to salvage what you can before it is all destroyed.", PLAYER0)
      QUICK_INFORMATION(13, "英雄已经征服了其他人。现在就去那里，试着去挽救你能在它被摧毁之前。", PLAYER0)
    ENDIF
  ENDIF
ENDIF
REM this is an example of a correct IF_AVAILABLE statement
IF_AVAILABLE(PLAYER0, PRISON, ==, 1)
    REM QUICK_INFORMATION(10, "There's talk of many Heroes massing nearby. Make sure your prison is big enough to handle the influx.")
    QUICK_INFORMATION(9, "有许多英雄集结在附近说话。确保你的监狱足够大，以处理大量涌入。")
    ROOM_AVAILABLE(PLAYER0,GRAVEYARD,1,0)
ENDIF

IF_AVAILABLE(PLAYER0, POWER_IMP, ==, 0)
  IF(PLAYER0, TOTAL_IMPS == 0)
    REM QUICK_INFORMATION(12, "Your imps have not survived the onslaught! Be more careful with these 3 who have heard of your plight and come to join you.")
    QUICK_INFORMATION(10, "你的小鬼都没有存活的冲击！要更小心，这3个谁听说你的困境，并加入你。")
    NEXT_COMMAND_REUSABLE
    ADD_CREATURE_TO_LEVEL(PLAYER0, IMP, 7, 3, 4, 0)
  ENDIF
ENDIF


REM ***** Make power to create imp available, as a reward for defeating the enemy keeper *****
REM ***** Also power to build workshop, but not if heroes destroyed blue dungeon *****



REM ****************************胜利/失败条件**********************************
IF(PLAYER0, ALL_DUNGEONS_DESTROYED == 1)
   REM QUICK_OBJECTIVE(13, "Your minions have served you well, and you have led them to a glorious victory in Ashfordania. They surely deserve a bonus, and a rest before your next conquest!", PLAYER0)
   QUICK_INFORMATION(11, "您的宠物有很好的服务于你，而你让他们在ashfordania光荣的胜利。他们当然应该得到一个额外的奖金，在你下一个征服前休息一下！", PLAYER0)
   
        REM 以下修改游戏，增加胜利后可以建造其它房子，让你回味一下胜利的喜悦！
        LEVEL_VERSION(1)
        REVEAL_MAP_RECT(PLAYER0,127,127,254,254)
        ADD_GOLD_TO_PLAYER (PLAYER0,9999999)

        ROOM_AVAILABLE(PLAYER0,TREASURE,1,1)
        ROOM_AVAILABLE(PLAYER0,GARDEN,1,1)
        ROOM_AVAILABLE(PLAYER0,LAIR,1,1)
        ROOM_AVAILABLE(PLAYER0,RESEARCH,1,1)
        ROOM_AVAILABLE(PLAYER0,TRAINING,1,1)
        ROOM_AVAILABLE(PLAYER0,BRIDGE,1,1)
        ROOM_AVAILABLE(PLAYER0,GUARD_POST,1,1)
        ROOM_AVAILABLE(PLAYER0,WORKSHOP,1,1)
        ROOM_AVAILABLE(PLAYER0,BARRACKS,1,1)
        ROOM_AVAILABLE(PLAYER0,TEMPLE,1,1)
        ROOM_AVAILABLE(PLAYER0,PRISON,1,1)
        ROOM_AVAILABLE(PLAYER0,TORTURE,1,1)
        ROOM_AVAILABLE(PLAYER0,SCAVENGER,1,1)
        ROOM_AVAILABLE(PLAYER0,GRAVEYARD,1,1)
        
        MAGIC_AVAILABLE(PLAYER0,POWER_IMP,1,1)
        MAGIC_AVAILABLE(PLAYER0,POWER_CALL_TO_ARMS,1,1
        MAGIC_AVAILABLE(PLAYER0,POWER_SPEED,1,1)
        MAGIC_AVAILABLE(PLAYER0,POWER_HEAL_CREATURE,1,1)
        MAGIC_AVAILABLE(PLAYER0,POWER_DESTROY_WALLS,1,1)

        DOOR_AVAILABLE(PLAYER0,MAGIC,1,200)
        TRAP_AVAILABLE(PLAYER0,LIGHTNING,1,0)
        TRAP_AVAILABLE(PLAYER0,BOULDER,1,200)
        QUICK_INFORMATION(15,"送钱亮图，让你回味一下胜利的喜悦！",PLAYER0)

        WIN_GAME
ENDIF

REM ****************************提示信息**********************************
QUICK_OBJECTIVE(1,"参考提示：1、盟友被干掉也不影响你胜利的进程；2、西北的紫金矿周围没有敌人把守的；3、经过熔岩去攻打人类是保险的方法，过去了就把桥卖掉，敌人过不来;4、人类地盘机关重重，小心，多存档。",PLAYER0)  

REM QUICK_OBJECTIVE(0, "The Heroes in Ashfordania want to crush you to death. Find some willing creatures to join you in your quest. Beat a path to the hero's door, and make him beg for mercy, Oh Dark One!", PLAYER0)
QUICK_INFORMATION(0, "在ashfordania英雄想要挤死你。找到一些愿意的生物加入你的探索。打一条通往英雄之门的路，让他乞求怜悯，一个黑暗的人！", PLAYER0)

IF(PLAYER0, GAME_TURN > 100)
  REM QUICK_INFORMATION(16, "You might find a little 'help' from the fellows to the East, but beware!")
  QUICK_INFORMATION(14, "你可能会发现一个小小的“帮助”从东方的家伙，但要提防！")
ENDIF















