REM ***********************************************************************
REM   Script for Level  475.Ludim - Post Undead Keeper
REM ***********************************************************************
REM Made by Duke Ragereaver
REM   已经收录到FX的Post Undead Keeper战役中。（305）
REM ***********************************************************************
REM 地图的大概情况与参考建议：
REM 1、 。
REM 2、 。
REM 3、 。
REM 4、 。
REM 5、 。
REM ***********************************************************************
REM ***************************各种基本设置 *****************************
COMPUTER_PLAYER(PLAYER1,0)
START_MONEY(PLAYER0,50000)
START_MONEY(PLAYER1,250000)
SET_GENERATE_SPEED(650)
MAX_CREATURES(PLAYER0,21)
MAX_CREATURES(PLAYER1,35)

REM *******************************添加怪物到入口 *********************************
REM   (添加到入口的生物名字,数量)
ADD_CREATURE_TO_POOL(TROLL,20)
ADD_CREATURE_TO_POOL(DARK_MISTRESS,30)
ADD_CREATURE_TO_POOL(BILE_DEMON,30)
ADD_CREATURE_TO_POOL(TENTACLE,20)
ADD_CREATURE_TO_POOL(ORC,20)
ADD_CREATURE_TO_POOL(VAMPIRE,2)
ADD_CREATURE_TO_POOL(DRAGON,20)
ADD_CREATURE_TO_POOL(SPIDER,20)
ADD_CREATURE_TO_POOL(HELL_HOUND,20)
ADD_CREATURE_TO_POOL(SKELETON,20)

REM *******************************指定阵营（玩家）允许/禁止从入口（怪物池）中招指定生物*********************************
REM  (玩家,生物名称,1（只能是1）,[1允许招募/0禁止招募])
CREATURE_AVAILABLE(PLAYER0,SKELETON,1,1)
CREATURE_AVAILABLE(PLAYER0,GHOST,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,VAMPIRE,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,SPIDER,1,1)
CREATURE_AVAILABLE(PLAYER1,TROLL,1,1)
CREATURE_AVAILABLE(PLAYER1,DARK_MISTRESS,1,1)
CREATURE_AVAILABLE(PLAYER1,BILE_DEMON,1,1)
CREATURE_AVAILABLE(PLAYER1,ORC,1,1)
CREATURE_AVAILABLE(PLAYER1,DRAGON,1,1)
CREATURE_AVAILABLE(PLAYER1,SPIDER,1,1)
CREATURE_AVAILABLE(PLAYER1,HELL_HOUND,1,1)

REM ********************指定阵营（玩家）允许/禁止研究和建造指定房子（建筑）**********************************
REM  (玩家,房子名称,是否可研究或建造[1是允许；0是禁止],是否已经可建造[1已经可建/0待研究])
ROOM_AVAILABLE(ALL_PLAYERS,GUARD_POST,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,RESEARCH,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,WORKSHOP,1,0)
ROOM_AVAILABLE(PLAYER0,GARDEN,1,0)
ROOM_AVAILABLE(PLAYER1,GARDEN,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,LAIR,1,1)
ROOM_AVAILABLE(PLAYER1,TORTURE,1,0)
ROOM_AVAILABLE(PLAYER1,BARRACKS,1,0)
ROOM_AVAILABLE(PLAYER1,TEMPLE,1,0)
ROOM_AVAILABLE(PLAYER1,SCAVENGER,1,0)
ROOM_AVAILABLE(PLAYER1,GRAVEYARD,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,TRAINING,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,TREASURE,1,1)

REM ********************指定阵营（玩家）允许/禁止研究和使用指定魔法**********************************
REM  (玩家,魔法名称,是否可研究或使用[1是允许；0是禁止],是否已经可使用[1已经可建/0待研究])
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HAND,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SLAP,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_IMP,1,1)

MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SIGHT,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CALL_TO_ARMS,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HOLD_AUDIENCE,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SPEED,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CONCEAL,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HEAL_CREATURE,1,0)

REM *********************指定阵营（玩家）允许/禁止制造指定的门*********************************
REM  (玩家,门的名称,[1是允许；0是禁止],1[0是允许；数字是已经制造出来/赠送的数量]
DOOR_AVAILABLE(PLAYER0,BRACED,1,0)
DOOR_AVAILABLE(ALL_PLAYERS,STEEL,1,0)
DOOR_AVAILABLE(ALL_PLAYERS,MAGIC,1,0)

REM *********************指定阵营（玩家）允许/禁止制造指定陷阱*********************************
REM  (玩家,陷阱名称,[1是允许；0是禁止],1[0是允许；数字是已经制造出来/赠送的数量]
TRAP_AVAILABLE(PLAYER0,ALARM,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,POISON_GAS,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,LIGHTNING,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,WORD_OF_POWER,1,0)

REM ****************************增援部队**********************************
REM CREATE_PARTY =>组建队伍                (队伍名称)   
REM ADD_TO_PARTY=>添加生物到队伍中   (队伍名称,生物名称,生物等级,携带金钱数量,任务,延时)
CREATE_PARTY(EASY_ONE)
    ADD_TO_PARTY(EASY_ONE,THIEF,3,300,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(EASY_ONE,THIEF,3,300,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(EASY_ONE,ARCHER,3,350,DEFEND_PARTY,0)
CREATE_PARTY(EASY_TWO)
    ADD_TO_PARTY(EASY_TWO,DWARFA,3,300,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(EASY_TWO,THIEF,3,300,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(EASY_TWO,ARCHER,3,350,DEFEND_PARTY,0)
CREATE_PARTY(MEDIUM_ONE)
    ADD_TO_PARTY(MEDIUM_ONE,ARCHER,5,600,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(MEDIUM_ONE,BARBARIAN,5,600,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(MEDIUM_ONE,MONK,5,700,DEFEND_PARTY,0)
CREATE_PARTY(MEDIUM_TWO)
    ADD_TO_PARTY(MEDIUM_TWO,GIANT,4,600,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(MEDIUM_TWO,BARBARIAN,5,600,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(MEDIUM_TWO,ARCHER,5,600,ATTACK_DUNGEON_HEART,0)
CREATE_PARTY(BRUTES)
    ADD_TO_PARTY(BRUTES,GIANT,6,500,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(BRUTES,GIANT,6,500,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(BRUTES,GIANT,6,500,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(BRUTES,GIANT,6,500,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(BRUTES,GIANT,6,500,DEFEND_PARTY,0)
    ADD_TO_PARTY(BRUTES,GIANT,6,500,DEFEND_PARTY,0)
CREATE_PARTY(DEFENDERS)
    ADD_TO_PARTY(DEFENDERS,BARBARIAN,7,750,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(DEFENDERS,GIANT,6,500,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(DEFENDERS,THIEF,7,400,STEAL_GOLD,0)
    ADD_TO_PARTY(DEFENDERS,ARCHER,8,500,DEFEND_PARTY,0)
    ADD_TO_PARTY(DEFENDERS,ARCHER,8,500,DEFEND_PARTY,0)
    ADD_TO_PARTY(DEFENDERS,ARCHER,8,500,DEFEND_PARTY,0)

REM ****************************脚本主体(主要内容、核心内容）**********************************

SET_TIMER(PLAYER_GOOD,TIMER0)
SET_TIMER(PLAYER_GOOD,TIMER1)
SET_FLAG(PLAYER_GOOD,FLAG0,1)

REM *************占领房子事件***********************
IF(PLAYER0,BRIDGE >= 1)
    ROOM_AVAILABLE(PLAYER0,BRIDGE,1,1)
ENDIF
IF(PLAYER1,BRIDGE >= 1)
    ROOM_AVAILABLE(PLAYER1,BRIDGE,1,1)
ENDIF

REM *************循环事件***********************
REM 弓箭手、小偷
IF(PLAYER_GOOD,TIMER0 >= 9375)
    IF(PLAYER_GOOD,FLAG0 == 1)
        NEXT_COMMAND_REUSABLE
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,EASY_ONE,-1,1)
        NEXT_COMMAND_REUSABLE
        SET_TIMER(PLAYER_GOOD,TIMER0)
    ENDIF
ENDIF
REM 弓箭手、小偷、矮人
IF(PLAYER_GOOD,TIMER1 >= 17550)
    IF(PLAYER_GOOD,FLAG0 == 1)
        NEXT_COMMAND_REUSABLE
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,EASY_TWO,-2,1)
        NEXT_COMMAND_REUSABLE
        SET_TIMER(PLAYER_GOOD,TIMER1)
    ENDIF
ENDIF

REM *************时钟事件***********************
IF(PLAYER0,TOTAL_CREATURES >= 17)
    SET_FLAG(PLAYER_GOOD,FLAG0,2)
    SET_TIMER(PLAYER_GOOD,TIMER2)
    SET_TIMER(PLAYER_GOOD,TIMER3)
ENDIF
REM 巨人、野蛮人、僧侣
IF(PLAYER_GOOD,TIMER2 >= 11250)
    IF(PLAYER_GOOD,FLAG0 == 2)
        NEXT_COMMAND_REUSABLE
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,MEDIUM_ONE,-1,1)
        NEXT_COMMAND_REUSABLE
        SET_TIMER(PLAYER_GOOD,TIMER2)
    ENDIF
ENDIF
REM 巨人、野蛮人、弓箭手
IF(PLAYER_GOOD,TIMER3 >= 21250)
    IF(PLAYER_GOOD,FLAG0 == 2)
        NEXT_COMMAND_REUSABLE
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,MEDIUM_TWO,-2,1)
        NEXT_COMMAND_REUSABLE
        SET_TIMER(PLAYER_GOOD,TIMER3)
    ENDIF
ENDIF

IF(PLAYER0,TOTAL_CREATURES >= 30)
    SET_FLAG(PLAYER_GOOD,FLAG0,3)
ENDIF

REM *************触发点事件***********************
IF_ACTION_POINT(1,PLAYER0)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,DEFENDERS,2,1)
ENDIF
IF_ACTION_POINT(3,PLAYER0)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,BRUTES,-4,1)
ENDIF
IF_ACTION_POINT(4,PLAYER0)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,DEFENDERS,-3,1)
ENDIF

REM ****************************胜利/失败条件**********************************
IF(PLAYER0,ALL_DUNGEONS_DESTROYED == 1)
    IF(PLAYER_GOOD,TOTAL_CREATURES == 0)
        REM QUICK_OBJECTIVE(1,"That is one less, well done.",PLAYER0)
        QUICK_OBJECTIVE(8,"做得好，守护者.",PLAYER0)

        REM 以下修改游戏，增加胜利后显示全图、送钱开放建造限制，让你回味一下胜利的喜悦！
        LEVEL_VERSION(1)
        REVEAL_MAP_RECT(PLAYER0,127,127,254,254)
        ADD_GOLD_TO_PLAYER (PLAYER0,9999999)

        ROOM_AVAILABLE(PLAYER0,TREASURE,1,1)
        ROOM_AVAILABLE(PLAYER0,GARDEN,1,1)
        ROOM_AVAILABLE(PLAYER0,LAIR,1,1)
        ROOM_AVAILABLE(PLAYER0,RESEARCH,1,1)
        ROOM_AVAILABLE(PLAYER0,TRAINING,1,1)
        ROOM_AVAILABLE(PLAYER0,BRIDGE,1,1)
        ROOM_AVAILABLE(PLAYER0,GUARD_POST,1,1)
        ROOM_AVAILABLE(PLAYER0,WORKSHOP,1,1)
        ROOM_AVAILABLE(PLAYER0,BARRACKS,1,1)
        ROOM_AVAILABLE(PLAYER0,TEMPLE,1,1)
        ROOM_AVAILABLE(PLAYER0,PRISON,1,1)
        ROOM_AVAILABLE(PLAYER0,TORTURE,1,1)
        ROOM_AVAILABLE(PLAYER0,SCAVENGER,1,1)
        ROOM_AVAILABLE(PLAYER0,GRAVEYARD,1,1)
        
        MAGIC_AVAILABLE(PLAYER0,POWER_IMP,1,1)
        MAGIC_AVAILABLE(PLAYER0,POWER_CALL_TO_ARMS,1,1
        MAGIC_AVAILABLE(PLAYER0,POWER_SPEED,1,1)
        MAGIC_AVAILABLE(PLAYER0,POWER_CONCEAL,1,1)
        MAGIC_AVAILABLE(PLAYER0,POWER_HEAL_CREATURE,1,1)
        MAGIC_AVAILABLE(PLAYER0,POWER_DESTROY_WALLS,1,1)

        DOOR_AVAILABLE(PLAYER0,MAGIC,1,200)
        TRAP_AVAILABLE(PLAYER0,LIGHTNING,1,0)
        TRAP_AVAILABLE(PLAYER0,BOULDER,1,200)
        QUICK_INFORMATION(15,"亮图送钱，让你回味一下胜利的喜悦！",PLAYER0) 

        WIN_GAME
    ENDIF
ENDIF


REM ****************************提示信息**********************************
QUICK_OBJECTIVE(1,"参考提示：1、没有富矿的土地上只能靠工厂解决经济问题；2、敌人不久就会从湖中传送过来；3、东北人类基地后面有紫金矿；4、西北的守护者是个难缠的家伙。",PLAYER0) 

REM QUICK_OBJECTIVE(0,"An evil Keeper by the name of Dracovich has his designs for this realm. Let him know how you feel about those plans... in the appriopiate way of course.",PLAYER0)
QUICK_INFORMATION(2,"一个名叫Dracovich的邪恶守护者对个王国进行了规划，让他知道你对这些计划的感受…当然适当的方式。",PLAYER0)

IF(PLAYER0,GAME_TURN > 200)
    QUICK_INFORMATION(3,"没有富矿的土地上只能靠工厂解决经济问题，如果你不是特别厉害的话建议精兵简政打持久战！",PLAYER0) 
ENDIF
IF(PLAYER0,GAME_TURN > 500)
    QUICK_INFORMATION(4,"敌人不久就会从湖中传送过来，久早修好围墙，在不能修墙的地方建哨所防止敌人进入。",PLAYER0) 
ENDIF
IF(PLAYER0,GAME_TURN > 1000)
    QUICK_INFORMATION(5,"东北人类基地后面有紫金矿，你的兵训练得差不多就去拿下它缓解经济对工厂的完全依赖性。",PLAYER0) 
ENDIF
IF(PLAYER0,GAME_TURN > 1500)
    QUICK_INFORMATION(6,"西北的守护者是个难缠的家伙，最简单的方法是建个陷阱阵杀光它的小鬼再占它地盘！",PLAYER0) 
ENDIF
IF(PLAYER0,GAME_TURN > 2000)
    QUICK_INFORMATION(7,"祝你好运，守护者！",PLAYER0) 
ENDIF











