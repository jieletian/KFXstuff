REM ***********************************************************************
REM   Script for Level    618(118).Raider 丽影
REM ***********************************************************************
REM 
REM ***********************************************************************
REM 地图的大概情况与参考建议：
REM 1、 。
REM 2、 。
REM 3、 。
REM 4、 。
REM 5、 。
REM ***********************************************************************
REM ***************************各种基本设置 *****************************
REM 速度、最大人口，游戏者34，守护者二16，守护者三14，两守护者正常智商、开始金钱数、
SET_GENERATE_SPEED(100)
MAX_CREATURES(PLAYER0,34)
MAX_CREATURES(PLAYER2,16)
MAX_CREATURES(PLAYER3,14)
COMPUTER_PLAYER(PLAYER2,0)
COMPUTER_PLAYER(PLAYER3,0)
START_MONEY(PLAYER0,450)
START_MONEY(PLAYER2,9000000)
START_MONEY(PLAYER3,9000000)

REM *******************************指定阵营（玩家）允许/禁止从入口（怪物池）中招指定生物*********************************
REM  (玩家,生物名称,1（只能是1）,[1允许招募/0禁止招募])

ADD_CREATURE_TO_POOL(FLY,40)
ADD_CREATURE_TO_POOL(BUG,40)
ADD_CREATURE_TO_POOL(SPIDER,40)
ADD_CREATURE_TO_POOL(DEMONSPAWN,6)
ADD_CREATURE_TO_POOL(SORCEROR,5)
ADD_CREATURE_TO_POOL(BILE_DEMON,5)
ADD_CREATURE_TO_POOL(DRAGON,5)
ADD_CREATURE_TO_POOL(TROLL,5)
ADD_CREATURE_TO_POOL(ORC,5)
ADD_CREATURE_TO_POOL(DARK_MISTRESS,6)
ADD_CREATURE_TO_POOL(HELL_HOUND,5)

REM ***********************可招的生物 *********************************
CREATURE_AVAILABLE(PLAYER0,FLY,1,1)
CREATURE_AVAILABLE(PLAYER0,BUG,1,1)
CREATURE_AVAILABLE(PLAYER0,SPIDER,1,1)
CREATURE_AVAILABLE(PLAYER0,DEMONSPAWN,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,SORCEROR,1,1)
CREATURE_AVAILABLE(PLAYER0,BILE_DEMON,1,1)
CREATURE_AVAILABLE(PLAYER0,DRAGON,1,1)
CREATURE_AVAILABLE(PLAYER0,TROLL,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,ORC,1,1)
CREATURE_AVAILABLE(PLAYER0,DARK_MISTRESS,1,1)
CREATURE_AVAILABLE(PLAYER0,SKELETON,1,1)
CREATURE_AVAILABLE(PLAYER0,GHOST,1,1)
CREATURE_AVAILABLE(PLAYER0,HELL_HOUND,1,1)
CREATURE_AVAILABLE(PLAYER0,VAMPIRE,1,1)
CREATURE_AVAILABLE(PLAYER0,HORNY,1,1)

REM ********************可研究和建筑的房子**********************************
ROOM_AVAILABLE(ALL_PLAYERS,TREASURE,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,LAIR,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,GARDEN,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,TRAINING,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,RESEARCH,1,1)
ROOM_AVAILABLE(PLAYER0,WORKSHOP,1,0)
ROOM_AVAILABLE(PLAYER0,BARRACKS,1,0)
ROOM_AVAILABLE(PLAYER0,TORTURE,1,0)
ROOM_AVAILABLE(PLAYER0,GRAVEYARD,1,0)

REM ********************指定阵营（玩家）允许/禁止研究和使用指定魔法**********************************
REM  (玩家,魔法名称,是否可研究或使用[1是允许；0是禁止],是否已经可使用[1已经可建/0待研究])
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HAND,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SLAP,1,1)
MAGIC_AVAILABLE(PLAYER0,POWER_IMP,1,0)
MAGIC_AVAILABLE(PLAYER2,POWER_SIGHT,1,0)
MAGIC_AVAILABLE(PLAYER2,POWER_SPEED,1,0)
MAGIC_AVAILABLE(PLAYER2,POWER_OBEY,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CALL_TO_ARMS,1,0)
MAGIC_AVAILABLE(PLAYER2,POWER_CONCEAL,1,0)
MAGIC_AVAILABLE(PLAYER2,POWER_HOLD_AUDIENCE,1,0)
MAGIC_AVAILABLE(PLAYER2,POWER_PROTECT,1,0)
MAGIC_AVAILABLE(PLAYER2,POWER_CHICKEN,1,0)

REM *********************指定阵营（玩家）允许/禁止制造指定的门*********************************
REM  (玩家,门的名称,[1是允许；0是禁止],1[0是允许；数字是已经制造出来/赠送的数量]
DOOR_AVAILABLE(ALL_PLAYERS,WOOD,1,0)
DOOR_AVAILABLE(ALL_PLAYERS,BRACED,1,0)

REM *********************指定阵营（玩家）允许/禁止制造指定陷阱*********************************
REM  (玩家,陷阱名称,[1是允许；0是禁止],1[0是允许；数字是已经制造出来/赠送的数量]
TRAP_AVAILABLE(ALL_PLAYERS,ALARM,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,POISON_GAS,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,LIGHTNING,1,0)

REM ****************************增援部队**********************************
REM CREATE_PARTY =>组建队伍                (队伍名称)   
REM ADD_TO_PARTY=>添加生物到队伍中   (队伍名称,生物名称,生物等级,携带金钱数量,任务,延时)
REM 1级小偷4个、1级弓箭手1个、1级男巫1个
CREATE_PARTY(ONE)
    ADD_TO_PARTY(ONE,THIEF,1,5,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(ONE,THIEF,1,5,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(ONE,THIEF,1,5,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(ONE,THIEF,1,5,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(ONE,ARCHER,1,5,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(ONE,WIZARD,1,5,ATTACK_ENEMIES,0)
REM 2级矮人3个、3级弓箭手3个
CREATE_PARTY(TWO)
    ADD_TO_PARTY(TWO,DWARFA,2,10,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(TWO,DWARFA,2,10,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(TWO,DWARFA,2,10,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(TWO,BARBARIAN,3,20,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(TWO,BARBARIAN,3,20,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(TWO,BARBARIAN,3,20,ATTACK_ENEMIES,0)
REM 5级男巫4个、4级巨人3个
CREATE_PARTY(THREE)
    ADD_TO_PARTY(THREE,WIZARD,5,20,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(THREE,WIZARD,5,20,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(THREE,WIZARD,5,20,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(THREE,WIZARD,5,20,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(THREE,GIANT,4,10,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(THREE,GIANT,4,10,ATTACK_ENEMIES,0)
REM 6级僧侣2个、5级忍者1个、7级女巫2个
CREATE_PARTY(FOUR)
    ADD_TO_PARTY(FOUR,MONK,6,30,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(FOUR,MONK,6,30,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(FOUR,SAMURAI,5,40,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(FOUR,WITCH,7,20,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(FOUR,WITCH,7,20,ATTACK_ENEMIES,0)
REM 5级仙女2个、7级小偷2个、6级忍者1个
CREATE_PARTY(FIVE)
    ADD_TO_PARTY(FIVE,FAIRY,5,50,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(FIVE,FAIRY,5,50,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(FIVE,THIEF,7,0,STEAL_GOLD,0)
    ADD_TO_PARTY(FIVE,THIEF,7,0,STEAL_GOLD,0)
    ADD_TO_PARTY(FIVE,SAMURAI,6,50,ATTACK_ENEMIES,0)
REM 6级野蛮人1个、6级巨人1个、6级忍者1个、7级男巫1个、7级僧侣1个、6级弓箭手1个、7级女巫1个
CREATE_PARTY(SIX)
    ADD_TO_PARTY(SIX,BARBARIAN,6,25,DEFEND_PARTY,0)
    ADD_TO_PARTY(SIX,GIANT,6,25,DEFEND_PARTY,0)
    ADD_TO_PARTY(SIX,SAMURAI,6,50,DEFEND_PARTY,0)
    ADD_TO_PARTY(SIX,WIZARD,7,20,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(SIX,MONK,7,20,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(SIX,ARCHER,6,20,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(SIX,WITCH,7,20,ATTACK_ENEMIES,0)
REM 10级骑士1个、9级僧侣2个、8级男巫2个、8级忍者1个
CREATE_PARTY(SEVEN)
    ADD_TO_PARTY(SEVEN,KNIGHT,10,5000,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(SEVEN,MONK,9,200,DEFEND_PARTY,0)
    ADD_TO_PARTY(SEVEN,MONK,9,200,DEFEND_PARTY,0)
    ADD_TO_PARTY(SEVEN,WIZARD,8,100,DEFEND_PARTY,0)
    ADD_TO_PARTY(SEVEN,WIZARD,8,100,DEFEND_PARTY,0)
    ADD_TO_PARTY(SEVEN,SAMURAI,8,100,DEFEND_PARTY,0)


REM ****************************脚本主体(主要内容、核心内容）**********************************
REM 强化骑士
SET_CREATURE_STRENGTH(KNIGHT,255)
SET_CREATURE_ARMOUR(KNIGHT,255)
SET_CREATURE_HEALTH(KNIGHT,7000)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,THIEF,10)

REM 如果游戏者不满18只小鬼，1000game turn 送一只小鬼
SET_TIMER(PLAYER0,TIMER0)
IF(PLAYER0,TIMER0 >= 1000)
    IF(PLAYER0,TOTAL_IMPS < 18)
        IF(PLAYER0,DUNGEON_DESTROYED == 0)
            NEXT_COMMAND_REUSABLE
            ADD_CREATURE_TO_LEVEL(PLAYER0,IMP,-1,1,1,0)
            NEXT_COMMAND_REUSABLE
            SET_TIMER(PLAYER0,TIMER0)
        ENDIF
    ENDIF
ENDIF

SET_TIMER(PLAYER_GOOD,TIMER2)
IF(PLAYER_GOOD,TIMER2 >= 7500)
    QUICK_INFORMATION(7,"你撕破了和平协议，人类准备与你不共戴天，不是你死就是我亡，战死方休，部分敌兵正在进攻你的路上！",PLAYER0) 
   IF(PLAYER0,TRAINING > 0)
        IF(PLAYER0,RESEARCH > 0)
            SET_TIMER(PLAYER_GOOD,TIMER0)
        ENDIF
    ENDIF
ENDIF
IF(PLAYER_GOOD,TIMER0 >= 1000)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,WIZARD,-1,9,1,25)
ENDIF
IF(PLAYER_GOOD,TIMER0 >= 2000)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,WIZARD,-1,7,1,25)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,DWARFA,-1,5,1,50)
ENDIF
IF(PLAYER_GOOD,TIMER0 >= 3000)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,BARBARIAN,-1,7,1,10)
ENDIF
IF(PLAYER_GOOD,TIMER0 >= 4000)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,ONE,-1,3)
ENDIF
IF(PLAYER_GOOD,TIMER0 >= 5000)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,GIANT,-1,5,2,50)
    ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,ONE,6,DUNGEON_HEART,0,1,20)
ENDIF
IF(PLAYER_GOOD,TIMER0 >= 7000)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,ONE,-1,1)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,GIANT,-1,5,3,10)
ENDIF
IF(PLAYER_GOOD,TIMER0 >= 8000)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,MONK,-1,6,4,50)
    ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,TWO,7,DUNGEON_HEART,0,1,20)
ENDIF
IF(PLAYER_GOOD,TIMER0 >= 10000)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,TWO,-1,2)
ENDIF
IF(PLAYER_GOOD,TIMER0 >= 11000)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,THIEF,-1,20,5,25)
ENDIF
IF(PLAYER_GOOD,TIMER0 >= 12000)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,DWARFA,-1,10,3,50)
    ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,TWO,8,DUNGEON_HEART,0,3,60)
ENDIF
IF(PLAYER_GOOD,TIMER0 >= 14000)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,WIZARD,-1,8,5,10)
ENDIF
IF(PLAYER_GOOD,TIMER0 >= 15000)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,ARCHER,-1,9,4,10)
    ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,THREE,9,DUNGEON_HEART,0,4,10)
ENDIF
IF(PLAYER_GOOD,TIMER0 >= 17000)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,THIEF,-1,8,5,40)
ENDIF
IF(PLAYER_GOOD,TIMER0 >= 18000)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,DWARFA,-1,6,4,10)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,BARBARIAN,-1,6,3,10)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,GIANT,-1,6,2,10)
    ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,THREE,10,DUNGEON_HEART,0,4,10)
ENDIF
IF(PLAYER_GOOD,TIMER0 >= 21000)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,SAMURAI,-1,2,6,50)
ENDIF
IF(PLAYER_GOOD,TIMER0 >= 22000)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,FAIRY,-1,4,4,40)
    ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,TWO,11,DUNGEON_HEART,0,3,75)
ENDIF
IF(PLAYER_GOOD,TIMER0 >= 25000)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,ARCHER,-1,5,5,40)
ENDIF
IF(PLAYER_GOOD,TIMER0 >= 26000)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,SAMURAI,-1,2,7,50)
    ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,FOUR,12,DUNGEON_HEART,0,4,10)
ENDIF
IF(PLAYER_GOOD,TIMER0 >= 27000)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,WITCH,-1,2,7,30)
ENDIF
IF(PLAYER_GOOD,TIMER0 >= 28000)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,THIEF,-1,20,7,80)
ENDIF
IF(PLAYER_GOOD,TIMER0 >= 29000)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,GIANT,-1,4,6,50)
ENDIF
IF(PLAYER_GOOD,TIMER0 >= 30000)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,WIZARD,-1,6,7,40)
    ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,FOUR,13,DUNGEON_HEART,0,4,10)
ENDIF
IF(PLAYER_GOOD,TIMER0 >= 32000)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,MONK,-1,4,7,50)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,WIZARD,-1,4,7,50)
ENDIF
IF(PLAYER_GOOD,TIMER0 >= 33000)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,SAMURAI,-1,2,8,30)
ENDIF
IF(PLAYER_GOOD,TIMER0 >= 34000)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,ARCHER,-1,4,6,40)
    ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,FIVE,14,DUNGEON_HEART,0,6,10)
ENDIF
IF(PLAYER_GOOD,TIMER0 >= 36000)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,BARBARIAN,-1,8,5,30)
ENDIF
IF(PLAYER_GOOD,TIMER0 >= 37000)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,MONK,-1,6,6,40)
ENDIF
IF(PLAYER_GOOD,TIMER0 >= 38000)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,SAMURAI,-1,2,8,500)
    ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,FOUR,15,DUNGEON_HEART,0,4,100)
    SET_TIMER(PLAYER_GOOD,TIMER1)
ENDIF
REM 无限刷怪
IF(PLAYER_GOOD,TIMER1 >= 1800)
    IF(PLAYER_GOOD,DUNGEON_DESTROYED == 0)
        NEXT_COMMAND_REUSABLE
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SIX,-1,1)
        NEXT_COMMAND_REUSABLE
        SET_TIMER(PLAYER_GOOD,TIMER1)
    ENDIF
ENDIF

REM 打爆两个守护者心脏，就会有挖掘者带队从人类心脏挖墙进攻你
IF(PLAYER2,DUNGEON_DESTROYED == 1)
    IF(PLAYER3,DUNGEON_DESTROYED == 1)
        ADD_TUNNELLER_TO_LEVEL(PLAYER_GOOD,1,DUNGEON_HEART,0,3,10)
        ADD_TUNNELLER_TO_LEVEL(PLAYER_GOOD,2,DUNGEON_HEART,0,3,10)
        ADD_TUNNELLER_TO_LEVEL(PLAYER_GOOD,3,DUNGEON_HEART,0,3,10)
        ADD_TUNNELLER_TO_LEVEL(PLAYER_GOOD,4,DUNGEON_HEART,0,3,10)
    ENDIF
ENDIF

REM ****************************胜利/失败条件**********************************
REM 打爆人类心脏就刷几队兵进攻你，杀掉强化骑士(国王)就胜利
IF(PLAYER_GOOD,DUNGEON_DESTROYED == 1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,ONE,5,3)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,FOUR,5,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SEVEN,5,1)
    IF(PLAYER_GOOD,KNIGHT == 0)
        
        REM 以下修改游戏，增加胜利后可以建造其它房子，让你回味一下胜利的喜悦！
        LEVEL_VERSION(1)
        REVEAL_MAP_RECT(PLAYER0,127,127,254,254)
        ADD_GOLD_TO_PLAYER (PLAYER0,9999999)

        ROOM_AVAILABLE(PLAYER0,TREASURE,1,1)
        ROOM_AVAILABLE(PLAYER0,GARDEN,1,1)
        ROOM_AVAILABLE(PLAYER0,LAIR,1,1)
        ROOM_AVAILABLE(PLAYER0,RESEARCH,1,1)
        ROOM_AVAILABLE(PLAYER0,TRAINING,1,1)
        ROOM_AVAILABLE(PLAYER0,BRIDGE,1,1)
        ROOM_AVAILABLE(PLAYER0,GUARD_POST,1,1)
        ROOM_AVAILABLE(PLAYER0,WORKSHOP,1,1)
        ROOM_AVAILABLE(PLAYER0,BARRACKS,1,1)
        ROOM_AVAILABLE(PLAYER0,TEMPLE,1,1)
        ROOM_AVAILABLE(PLAYER0,PRISON,1,1)
        ROOM_AVAILABLE(PLAYER0,TORTURE,1,1)
        ROOM_AVAILABLE(PLAYER0,SCAVENGER,1,1)
        ROOM_AVAILABLE(PLAYER0,GRAVEYARD,1,1)
        
        MAGIC_AVAILABLE(PLAYER0,POWER_IMP,1,1)
        MAGIC_AVAILABLE(PLAYER0,POWER_CALL_TO_ARMS,1,1
        MAGIC_AVAILABLE(PLAYER0,POWER_SPEED,1,1)
        MAGIC_AVAILABLE(PLAYER0,POWER_HEAL_CREATURE,1,1)
        MAGIC_AVAILABLE(PLAYER0,POWER_DESTROY_WALLS,1,1)

        DOOR_AVAILABLE(PLAYER0,MAGIC,1,200)
        TRAP_AVAILABLE(PLAYER0,LIGHTNING,1,0)
        TRAP_AVAILABLE(PLAYER0,BOULDER,1,200)
        QUICK_INFORMATION(15,"送钱亮图，让你回味一下胜利的喜悦！",PLAYER0)

        WIN_GAME
    ENDIF
ENDIF
IF(PLAYER0,DUNGEON_DESTROYED == 1)
    LOSE_GAME
ENDIF

REM ****************************提示信息**********************************
QUICK_OBJECTIVE(1,"参考提示：1、如果你同时建图书馆和训练室，人类就会攻击你；2、你周围的金矿都安全可以挖的；3、干掉两个守护者二，人类就会从它心脏附近派一队兵进攻你;4、杀掉骑士就胜利。",PLAYER0)  
IF(PLAYER0,GAME_TURN > 500)
    QUICK_INFORMATION(2,"你和人类有默认的约定，就能同时建图书馆和训练室，它们认为这是你想撕破和平协定的标志，如果你做了，它们就不停地攻击你，不是你死就是我亡！",PLAYER0) 
ENDIF
IF(PLAYER0,GAME_TURN > 1500)
    QUICK_INFORMATION(3,"这个地方是和平的，周围的金矿都可以安全地挖。",PLAYER0) 
ENDIF
IF(PLAYER0,GAME_TURN > 2500)
    QUICK_INFORMATION(4,"如果你消灭两个守护者，人类就会从它的基地派一队兵进攻你。",PLAYER0) 
ENDIF
IF(PLAYER0,GAME_TURN > 3500)
    QUICK_INFORMATION(5,"打爆人类心脏，它的国王就会来攻击你，杀掉国王，你就胜利了！",PLAYER0) 
ENDIF
IF(PLAYER0,GAME_TURN > 4000)
    QUICK_INFORMATION(6,"祝你好运，守护者！",PLAYER0) 
ENDIF











