REM ***********************************************************************
REM   Script for Level    612(112)Anoumordis
REM ***********************************************************************
REM by DzjeeAr
REM Play Level  Anoumordis     
REM ***********************************************************************
REM 地图的大概情况与参考建议：
REM 1、 。
REM 2、 。
REM 3、 。
REM 4、 。
REM 5、 。
REM ***********************************************************************
REM ***************************各种基本设置 *****************************
REM  速度、最大人口、两守护者为正常智商的电脑、初始金钱数
SET_GENERATE_SPEED(350)

MAX_CREATURES(PLAYER0,40)
MAX_CREATURES(PLAYER1,40)
MAX_CREATURES(PLAYER2,40)

COMPUTER_PLAYER(PLAYER1,0)
COMPUTER_PLAYER(PLAYER2,0)

START_MONEY(PLAYER0,15000)
START_MONEY(PLAYER1,16000)
START_MONEY(PLAYER2,16000)

REM *******************************添加怪物到入口 *********************************
REM   (添加到入口的生物名字,数量)
ADD_CREATURE_TO_POOL(FLY,40)
ADD_CREATURE_TO_POOL(BUG,40)
ADD_CREATURE_TO_POOL(SPIDER,30)
ADD_CREATURE_TO_POOL(DEMONSPAWN,5)
ADD_CREATURE_TO_POOL(SORCEROR,20)
ADD_CREATURE_TO_POOL(BILE_DEMON,40)
ADD_CREATURE_TO_POOL(DRAGON,40)
ADD_CREATURE_TO_POOL(TROLL,50)
ADD_CREATURE_TO_POOL(ORC,60)
ADD_CREATURE_TO_POOL(DARK_MISTRESS,30)
ADD_CREATURE_TO_POOL(TENTACLE,1)
ADD_CREATURE_TO_POOL(HORNY,1)

REM *******************************指定阵营（玩家）允许/禁止从入口（怪物池）中招指定生物*********************************
REM  (玩家,生物名称,1（只能是1）,[1允许招募/0禁止招募])

CREATURE_AVAILABLE(ALL_PLAYERS,FLY,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,BUG,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,SPIDER,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,DEMONSPAWN,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,SORCEROR,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,BILE_DEMON,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,DRAGON,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,TROLL,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,ORC,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,DARK_MISTRESS,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,SKELETON,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,GHOST,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,VAMPIRE,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,TENTACLE,1,1)
CREATURE_AVAILABLE(ALL_PLAYERS,HORNY,1,1)

REM ********************可研究和建筑的房子**********************************
ROOM_AVAILABLE(ALL_PLAYERS,TREASURE,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,LAIR,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,GARDEN,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,TRAINING,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,RESEARCH,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,GUARD_POST,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,WORKSHOP,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,BARRACKS,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,TORTURE,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,TEMPLE,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,GRAVEYARD,1,0)

REM ********************指定阵营（玩家）允许/禁止研究和使用指定魔法**********************************
REM  (玩家,魔法名称,是否可研究或使用[1是允许；0是禁止],是否已经可使用[1已经可建/0待研究])
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HAND,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SLAP,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_IMP,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SIGHT,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SPEED,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_OBEY,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CALL_TO_ARMS,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CONCEAL,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HOLD_AUDIENCE,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CAVE_IN,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HEAL_CREATURE,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_PROTECT,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CHICKEN,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_DISEASE,1,0)

REM *********************指定阵营（玩家）允许/禁止制造指定的门*********************************
REM  (玩家,门的名称,[1是允许；0是禁止],1[0是允许；数字是已经制造出来/赠送的数量]
DOOR_AVAILABLE(ALL_PLAYERS,WOOD,1,0)
DOOR_AVAILABLE(ALL_PLAYERS,BRACED,1,0)
DOOR_AVAILABLE(ALL_PLAYERS,STEEL,1,0)

REM *********************指定阵营（玩家）允许/禁止制造指定陷阱*********************************
REM  (玩家,陷阱名称,[1是允许；0是禁止],1[0是允许；数字是已经制造出来/赠送的数量]
TRAP_AVAILABLE(ALL_PLAYERS,ALARM,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,POISON_GAS,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,LIGHTNING,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,WORD_OF_POWER,1,0)

REM ****************************增援部队**********************************
REM CREATE_PARTY =>组建队伍                (队伍名称)   
REM ADD_TO_PARTY=>添加生物到队伍中   (队伍名称,生物名称,生物等级,携带金钱数量,任务,延时)REM  增援部队
CREATE_PARTY(ONE)
    ADD_TO_PARTY(ONE,SAMURAI,8,400,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(ONE,THIEF,8,400,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(ONE,SAMURAI,8,400,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(ONE,THIEF,9,500,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(ONE,SAMURAI,9,500,ATTACK_ENEMIES,0)
CREATE_PARTY(TWO)
    ADD_TO_PARTY(TWO,BARBARIAN,8,400,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(TWO,BARBARIAN,8,400,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(TWO,BARBARIAN,9,500,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(TWO,GIANT,8,400,ATTACK_ENEMIES,0)
        ADD_TO_PARTY(TWO,GIANT,9,500,ATTACK_ENEMIES,0)
CREATE_PARTY(THREE)
    ADD_TO_PARTY(THREE,FAIRY,8,400,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(THREE,FAIRY,8,400,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(THREE,FAIRY,9,500,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(THREE,ARCHER,9,500,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(THREE,WITCH,9,500,ATTACK_ENEMIES,0)
CREATE_PARTY(FOUR)
    ADD_TO_PARTY(FOUR,DWARFA,8,400,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(FOUR,DWARFA,9,500,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(FOUR,FAIRY,9,500,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(FOUR,SAMURAI,8,400,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(FOUR,SAMURAI,9,500,ATTACK_ENEMIES,0)
CREATE_PARTY(FOURB)
    ADD_TO_PARTY(FOURB,WITCH,9,500,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(FOURB,SAMURAI,8,400,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(FOURB,FAIRY,9,500,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(FOURB,SAMURAI,8,400,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(FOURB,SAMURAI,9,500,ATTACK_ENEMIES,0)
CREATE_PARTY(FIVE)
    ADD_TO_PARTY(FIVE,WIZARD,8,400,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(FIVE,WIZARD,9,500,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(FIVE,MONK,8,400,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(FIVE,MONK,9,500,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(FIVE,SAMURAI,9,500,DEFEND_PARTY,0)
CREATE_PARTY(SIX)
    ADD_TO_PARTY(SIX,SAMURAI,8,400,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(SIX,SAMURAI,9,500,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(SIX,DWARFA,8,400,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(SIX,BARBARIAN,9,500,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(SIX,DWARFA,9,500,ATTACK_ENEMIES,0)



REM ****************************脚本主体(主要内容、核心内容）**********************************
REM  强化骑士
REM ********* settings **********
SET_CREATURE_STRENGTH(KNIGHT,255)
SET_CREATURE_HEALTH(KNIGHT,6800)
SET_CREATURE_ARMOUR(KNIGHT,255)

REM  守护者被消灭后，倒数时钟刷兵
IF(PLAYER1,DUNGEON_DESTROYED==1)
    IF(PLAYER2,DUNGEON_DESTROYED==1)
        SET_TIMER(PLAYER0,TIMER0)
        IF(PLAYER0,TIMER0>=200)
            ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,ONE,1,DUNGEON_HEART,0,8,300)
            ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,ONE,2,DUNGEON_HEART,0,8,300)
            ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,FOUR,3,DUNGEON_HEART,0,8,300)
            ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,THREE,4,DUNGEON_HEART,0,8,300)
            ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,ONE,5,DUNGEON_HEART,0,8,300)
            ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,TWO,5,DUNGEON_HEART,0,8,300)
            ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,FOURB,6,DUNGEON_HEART,0,8,300)
        ENDIF
    ENDIF
ENDIF

REM ********** Action points **********触发点
IF_ACTION_POINT(7,PLAYER0)
    ADD_TUNNELLER_TO_LEVEL(PLAYER_GOOD,10,ACTION_POINT,13,8,300)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,ONE,10,1)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,ARCHER,16,1,10,0)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,ARCHER,17,1,10,0)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,ARCHER,18,2,10,0)
ENDIF
IF_ACTION_POINT(8,PLAYER0)
    ADD_TUNNELLER_TO_LEVEL(PLAYER_GOOD,11,ACTION_POINT,14,8,300)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,ONE,11,1)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,ARCHER,16,1,10,0)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,ARCHER,17,1,10,0)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,ARCHER,18,2,10,0)
ENDIF
IF_ACTION_POINT(9,PLAYER0)
    ADD_TUNNELLER_TO_LEVEL(PLAYER_GOOD,12,ACTION_POINT,15,8,300)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,ONE,12,1)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,ARCHER,16,1,10,0)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,ARCHER,17,1,10,0)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,ARCHER,18,2,10,0)
ENDIF
IF_ACTION_POINT(19,PLAYER0)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,THIEF,-1,5,10,0)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,ARCHER,-1,5,10,0)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,SAMURAI,-1,6,10,0)
ENDIF
IF_ACTION_POINT(20,PLAYER0)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,THIEF,-2,5,10,0)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,ARCHER,-2,5,10,0)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,SAMURAI,-2,6,10,0)
ENDIF
IF_ACTION_POINT(21,PLAYER0)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,THIEF,-4,5,10,0)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,ARCHER,-4,5,10,0)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,ARCHER,-1,5,10,0)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,SAMURAI,-1,6,10,0)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,ONE,-3,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,ONE,-2,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,THREE,-3,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,THREE,-2,1)
ENDIF
IF_ACTION_POINT(22,PLAYER0)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,THIEF,23,12,10,0)
ENDIF

REM ****************************胜利/失败条件**********************************
REM  打爆人类心脏后刷兵，刷出来的兵被消灭后胜利
IF(PLAYER_GOOD,DUNGEON_DESTROYED==1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,FIVE,1,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,ONE,2,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,FIVE,3,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SIX,4,1)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,AVATAR,4,2,10,0)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,KNIGHT,4,2,10,0)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,ONE,5,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,ONE,-3,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,TWO,-2,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,ONE,-3,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,FIVE,-2,1)
    QUICK_INFORMATION(7,"人类大批部队疯狂地增援，是清算的时刻了，守护者！",PLAYER0) 
    IF(PLAYER_GOOD,KNIGHT==0)

        REM 以下修改游戏，增加胜利后可以建造其它房子，让你回味一下胜利的喜悦！
        LEVEL_VERSION(1)
        REVEAL_MAP_RECT(PLAYER0,127,127,254,254)
        ADD_GOLD_TO_PLAYER (PLAYER0,9999999)

        ROOM_AVAILABLE(PLAYER0,TREASURE,1,1)
        ROOM_AVAILABLE(PLAYER0,GARDEN,1,1)
        ROOM_AVAILABLE(PLAYER0,LAIR,1,1)
        ROOM_AVAILABLE(PLAYER0,RESEARCH,1,1)
        ROOM_AVAILABLE(PLAYER0,TRAINING,1,1)
        ROOM_AVAILABLE(PLAYER0,BRIDGE,1,1)
        ROOM_AVAILABLE(PLAYER0,GUARD_POST,1,1)
        ROOM_AVAILABLE(PLAYER0,WORKSHOP,1,1)
        ROOM_AVAILABLE(PLAYER0,BARRACKS,1,1)
        ROOM_AVAILABLE(PLAYER0,TEMPLE,1,1)
        ROOM_AVAILABLE(PLAYER0,PRISON,1,1)
        ROOM_AVAILABLE(PLAYER0,TORTURE,1,1)
        ROOM_AVAILABLE(PLAYER0,SCAVENGER,1,1)
        ROOM_AVAILABLE(PLAYER0,GRAVEYARD,1,1)
        
        MAGIC_AVAILABLE(PLAYER0,POWER_IMP,1,1)
        MAGIC_AVAILABLE(PLAYER0,POWER_CALL_TO_ARMS,1,1
        MAGIC_AVAILABLE(PLAYER0,POWER_SPEED,1,1)
        MAGIC_AVAILABLE(PLAYER0,POWER_HEAL_CREATURE,1,1)
        MAGIC_AVAILABLE(PLAYER0,POWER_DESTROY_WALLS,1,1)

        DOOR_AVAILABLE(PLAYER0,MAGIC,1,200)
        TRAP_AVAILABLE(PLAYER0,LIGHTNING,1,0)
        TRAP_AVAILABLE(PLAYER0,BOULDER,1,200)
        QUICK_INFORMATION(15,"送钱亮图，让你回味一下胜利的喜悦！",PLAYER0)

        WIN_GAME
    ENDIF
ENDIF
IF(PLAYER0,DUNGEON_DESTROYED == 1)
    LOSE_GAME
ENDIF

REM ****************************提示信息**********************************
QUICK_OBJECTIVE(1,"参考提示：1、东北面的紫金矿是最有可能抢到的；2、你可以用机关消灭一部分人类；3、那两个守护者不是你的主要敌人，你的主要敌人是人类；4、消灭人类并打爆它心脏，人类就有很多增援，疯狂进攻你；5、黑暗女王是打仗的主力东面有个中间的圣者也可以利用的。",PLAYER0)  
IF(PLAYER0,GAME_TURN > 500)
    QUICK_INFORMATION(2,"尽快抢占东北面的紫金矿并修好墙，它是最有可能抢到的，其它金矿就最好不要想了！",PLAYER0) 
ENDIF
IF(PLAYER0,GAME_TURN > 1500)
    QUICK_INFORMATION(3,"那北方的两个守护者不是你的主要敌人，他们很快就扛上，相互撕杀，还很可能很快就被人类消灭，你的主要敌人是南方的人类。",PLAYER0) 
ENDIF
IF(PLAYER0,GAME_TURN > 2500)
    QUICK_INFORMATION(4,"既然南方的人类这么厉害，你可以修一条长长的通道，放置陷阱，用机关消灭它们中的一部分.",PLAYER0) 
ENDIF
IF(PLAYER0,GAME_TURN > 3500)
    QUICK_INFORMATION(5,"对付人类，可能找黑暗女王最合适不过了，东面有个中立的圣者，让它加盟你，这样更保险！",PLAYER0) 
ENDIF
IF(PLAYER0,GAME_TURN > 4000)
    QUICK_INFORMATION(6,"打爆人类心脏之前要准备好大决战，祝你好运，守护者！",PLAYER0) 
ENDIF
















