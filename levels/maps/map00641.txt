REM ***********************************************************************
REM   Script for Level   641(141).Cathedrl Thieves
REM ***********************************************************************
REM by DzjeeAr
REM ***********************************************************************
REM 地图的大概情况与参考建议：
REM 1、 。
REM 2、 。
REM 3、 。
REM 4、 。
REM 5、 。
REM ***********************************************************************
REM ***************************各种基本设置 *****************************
SET_GENERATE_SPEED(400)
MAX_CREATURES(PLAYER0,20)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,THIEF,10)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,DEMONSPAWN,10)
START_MONEY(PLAYER0,10000)

REM *******************************添加怪物到入口 *********************************
REM   (添加到入口的生物名字,数量)
ADD_CREATURE_TO_POOL(FLY,20)
ADD_CREATURE_TO_POOL(BUG,20)
ADD_CREATURE_TO_POOL(SPIDER,20)
ADD_CREATURE_TO_POOL(DEMONSPAWN,10)
ADD_CREATURE_TO_POOL(SORCEROR,10)
ADD_CREATURE_TO_POOL(BILE_DEMON,15)
ADD_CREATURE_TO_POOL(DRAGON,15)
ADD_CREATURE_TO_POOL(SKELETON,10)
ADD_CREATURE_TO_POOL(TROLL,10)
ADD_CREATURE_TO_POOL(ORC,20)
ADD_CREATURE_TO_POOL(DARK_MISTRESS,10)
ADD_CREATURE_TO_POOL(HELL_HOUND,12)
ADD_CREATURE_TO_POOL(TENTACLE,5)
ADD_CREATURE_TO_POOL(VAMPIRE,5)
ADD_CREATURE_TO_POOL(HORNY,1)

REM ***********************可招的生物 *********************************
CREATURE_AVAILABLE(PLAYER0,FLY,1,1)
CREATURE_AVAILABLE(PLAYER0,BUG,1,1)
CREATURE_AVAILABLE(PLAYER0,SPIDER,1,1)
CREATURE_AVAILABLE(PLAYER0,DEMONSPAWN,1,1)
CREATURE_AVAILABLE(PLAYER0,TROLL,1,1)

REM ********************可研究和建筑的房子**********************************
ROOM_AVAILABLE(ALL_PLAYERS,TREASURE,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,LAIR,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,GARDEN,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,TRAINING,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,RESEARCH,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,WORKSHOP,1,0)

REM ********************指定阵营（玩家）允许/禁止研究和使用指定魔法**********************************
REM  (玩家,魔法名称,是否可研究或使用[1是允许；0是禁止],是否已经可使用[1已经可建/0待研究])
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HAND,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SLAP,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SPEED,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_OBEY,1,0)

REM *********************指定阵营（玩家）允许/禁止制造指定的门*********************************
REM  (玩家,门的名称,[1是允许；0是禁止],1[0是允许；数字是已经制造出来/赠送的数量]
DOOR_AVAILABLE(ALL_PLAYERS,WOOD,1,0)
DOOR_AVAILABLE(ALL_PLAYERS,BRACED,1,0)

REM *********************指定阵营（玩家）允许/禁止制造指定陷阱*********************************
REM  (玩家,陷阱名称,[1是允许；0是禁止],1[0是允许；数字是已经制造出来/赠送的数量]
TRAP_AVAILABLE(ALL_PLAYERS,ALARM,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,POISON_GAS,1,0)

REM ****************************增援部队**********************************
REM CREATE_PARTY =>组建队伍                (队伍名称)   
REM ADD_TO_PARTY=>添加生物到队伍中   (队伍名称,生物名称,生物等级,携带金钱数量,任务,延时)
CREATE_PARTY(ONE)
    ADD_TO_PARTY(ONE,ARCHER,3,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(ONE,ARCHER,4,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(ONE,THIEF,2,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(ONE,DWARFA,3,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(ONE,BARBARIAN,2,0,ATTACK_ENEMIES,0)
CREATE_PARTY(TWO)
    ADD_TO_PARTY(TWO,ARCHER,4,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(TWO,THIEF,3,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(TWO,WIZARD,3,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(TWO,MONK,3,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(TWO,ARCHER,3,0,ATTACK_ENEMIES,0)
CREATE_PARTY(THREE)
    ADD_TO_PARTY(THREE,WITCH,3,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(THREE,WITCH,3,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(THREE,WIZARD,4,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(THREE,MONK,3,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(THREE,FAIRY,3,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(THREE,GIANT,2,0,ATTACK_ENEMIES,0)
CREATE_PARTY(FOUR)
    ADD_TO_PARTY(FOUR,ARCHER,8,200,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(FOUR,ARCHER,7,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(FOUR,THIEF,6,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(FOUR,DWARFA,6,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(FOUR,BARBARIAN,5,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(FOUR,THIEF,6,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(FOUR,ARCHER,7,0,ATTACK_ENEMIES,0)
CREATE_PARTY(FIVE)
    ADD_TO_PARTY(FIVE,ARCHER,5,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(FIVE,ARCHER,4,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(FIVE,ARCHER,4,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(FIVE,ARCHER,4,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(FIVE,BARBARIAN,3,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(FIVE,FAIRY,4,0,ATTACK_ENEMIES,0)
CREATE_PARTY(SIX)
    ADD_TO_PARTY(SIX,BARBARIAN,5,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(SIX,BARBARIAN,5,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(SIX,MONK,6,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(SIX,SAMURAI,4,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(SIX,SAMURAI,4,0,ATTACK_ENEMIES,0)
CREATE_PARTY(SEVEN)
    ADD_TO_PARTY(SEVEN,BARBARIAN,6,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(SEVEN,GIANT,6,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(SEVEN,SAMURAI,5,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(SEVEN,SAMURAI,5,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(SEVEN,SAMURAI,4,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(SEVEN,WITCH,5,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(SEVEN,FAIRY,6,0,ATTACK_ENEMIES,0)
CREATE_PARTY(X)
    ADD_TO_PARTY(X,SAMURAI,6,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(X,SAMURAI,5,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(X,SAMURAI,5,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(X,SAMURAI,4,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(X,SAMURAI,4,0,ATTACK_ENEMIES,0)
CREATE_PARTY(Y)
    ADD_TO_PARTY(Y,MONK,5,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(Y,WIZARD,6,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(Y,WIZARD,6,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(Y,WIZARD,6,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(Y,GIANT,6,0,ATTACK_ENEMIES,0)
CREATE_PARTY(MAGIC)
    ADD_TO_PARTY(MAGIC,MONK,6,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(MAGIC,WIZARD,6,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(MAGIC,WITCH,6,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(MAGIC,FAIRY,6,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(MAGIC,WIZARD,5,0,ATTACK_ENEMIES,0)
CREATE_PARTY(Z)
    ADD_TO_PARTY(Z,KNIGHT,10,5000,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(Z,THIEF,9,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(Z,THIEF,9,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(Z,THIEF,9,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(Z,THIEF,9,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(Z,THIEF,9,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(Z,THIEF,9,0,ATTACK_ENEMIES,0)

REM ****************************脚本主体(主要内容、核心内容）**********************************
SET_CREATURE_HEALTH(AVATAR,2000)
SET_CREATURE_HEALTH(KNIGHT,5000)
SET_CREATURE_STRENGTH(KNIGHT,255)
SET_CREATURE_ARMOUR(KNIGHT,250)
SET_CREATURE_HEALTH(BILE_DEMON,1300)

REM 魔术师、兽人、骷髅、鬼魂、水怪、哨所、兵营
IF(PLAYER0,BARRACKS >= 1)
    CREATURE_AVAILABLE(PLAYER0,SORCEROR,1,1)
    CREATURE_AVAILABLE(PLAYER0,ORC,1,1)
    CREATURE_AVAILABLE(PLAYER0,SKELETON,1,1)
    CREATURE_AVAILABLE(PLAYER0,GHOST,1,1)
    CREATURE_AVAILABLE(PLAYER0,TENTACLE,1,1)
    ROOM_AVAILABLE(ALL_PLAYERS,GUARD_POST,1,0)
    ROOM_AVAILABLE(ALL_PLAYERS,BARRACKS,1,0)
ENDIF
REM 巨魔、龙、黑暗女王、地狱犬、吸血鬼、审讯室、寺庙、陵墓
IF(PLAYER0,TORTURE >= 1)
    CREATURE_AVAILABLE(PLAYER0,BILE_DEMON,1,1)
    CREATURE_AVAILABLE(PLAYER0,DRAGON,1,1)
    CREATURE_AVAILABLE(PLAYER0,DARK_MISTRESS,1,1)
    CREATURE_AVAILABLE(PLAYER0,HELL_HOUND,1,1)
    CREATURE_AVAILABLE(PLAYER0,VAMPIRE,1,1)
    ROOM_AVAILABLE(ALL_PLAYERS,TORTURE,1,0)
    ROOM_AVAILABLE(ALL_PLAYERS,TEMPLE,1,0)
    ROOM_AVAILABLE(ALL_PLAYERS,GRAVEYARD,1,0)
ENDIF

REM 人类心脏两边的入口附近，招募室、监狱
IF_ACTION_POINT(28,PLAYER0)
    IF_ACTION_POINT(29,PLAYER0)
        CREATURE_AVAILABLE(PLAYER0,HORNY,1,1)
        ROOM_AVAILABLE(ALL_PLAYERS,SCAVENGER,1,0)
        ROOM_AVAILABLE(ALL_PLAYERS,PRISON,1,0)
    ENDIF
ENDIF

REM 小鬼数量小于24时，制造小鬼魔法有效
IF(PLAYER0,TOTAL_IMPS < 24)
    NEXT_COMMAND_REUSABLE
    MAGIC_AVAILABLE(ALL_PLAYERS,POWER_IMP,1,1)
ENDIF
REM 小鬼数量达24后，制造小鬼魔法失效
IF(PLAYER0,TOTAL_IMPS >= 24)
    NEXT_COMMAND_REUSABLE
    MAGIC_AVAILABLE(ALL_PLAYERS,POWER_IMP,0,0)
ENDIF

REM 当你的生物数量达15后，在你家附近刷几队挖掘者带队的敌人进攻你，并设置时钟0开始计时，然后再刷两次敌人
IF(PLAYER0,TOTAL_CREATURES >= 15)
    ADD_TUNNELLER_TO_LEVEL(PLAYER_GOOD,3,DUNGEON_HEART,0,1,500)
    ADD_TUNNELLER_TO_LEVEL(PLAYER_GOOD,4,DUNGEON_HEART,0,1,500)
    ADD_TUNNELLER_TO_LEVEL(PLAYER_GOOD,-9,DUNGEON_HEART,0,1,1000)
    SET_TIMER(PLAYER0,TIMER0)
    IF(PLAYER0,TIMER0 >= 2000)
        ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,ARCHER,3,1,2,300)
        ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,THIEF,3,3,2,200)
        ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,DWARFA,4,1,2,300)
        ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,THIEF,4,3,2,200)
        ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,ARCHER,-9,1,2,300)
        ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,DWARFA,-9,1,2,300)
        IF(PLAYER0,TIMER0 >= 4000)
            ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,ARCHER,3,1,2,300)
            ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,THIEF,3,3,2,200)
            ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,DWARFA,4,1,2,300)
            ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,THIEF,4,3,2,200)
            ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,ARCHER,-9,1,2,300)
            ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,DWARFA,-9,1,2,300)
        ENDIF
    ENDIF
ENDIF

REM 过熔岩后第一道门后
IF_ACTION_POINT(1,PLAYER0)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,ARCHER,2,2,2,0)
ENDIF

REM 北面金库下的空地,并设置时钟1开始计时，然后再刷9次敌人
IF_ACTION_POINT(5,PLAYER0)
    SET_TIMER(PLAYER0,TIMER1)
    IF(PLAYER0,TIMER1 >= 5000)
        ADD_TUNNELLER_TO_LEVEL(PLAYER_GOOD,6,ACTION_POINT,5,2,800)
        IF(PLAYER0,TIMER1 >= 7500)
            ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,ONE,7,ACTION_POINT,5,2,200)
            IF(PLAYER0,TIMER1 >= 8200)
                ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,TWO,8,ACTION_POINT,5,2,200)
                IF(PLAYER0,TIMER1 >= 8900)
                    ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,ONE,9,ACTION_POINT,5,2,200)
                    IF(PLAYER0,TIMER1 >= 9600)
                        ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,TWO,10,ACTION_POINT,5,2,200)
                        IF(PLAYER0,TIMER1 >= 10300)
                            ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,ONE,11,ACTION_POINT,5,2,200)
                            IF(PLAYER0,TIMER1 >= 11000)
                                ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,ONE,27,ACTION_POINT,6,2,200)
                                IF(PLAYER0,TIMER1 >= 11700)
                                    ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,THREE,12,ACTION_POINT,9,2,200)
                                    IF(PLAYER0,TIMER1 >= 12400)
                                        ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,ONE,13,ACTION_POINT,5,2,200)
                                    ENDIF
                                ENDIF
                            ENDIF
                        ENDIF
                    ENDIF
                ENDIF
            ENDIF
        ENDIF
    ENDIF
ENDIF
REM 东北部的训练室南面、金库西面，刷一队敌人后设置时钟2开始计时，然后再刷1次敌人
IF_ACTION_POINT(6,PLAYER0)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,FIVE,6,1)
    SET_TIMER(PLAYER0,TIMER2)
    IF(PLAYER0,TIMER2 >= 6000)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,FIVE,7,1)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,FIVE,11,1)
        ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,ARCHER,10,2,4,300)
        ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,THIEF,8,1,5,200)
        ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,DWARFA,9,2,3,300)
    ENDIF
ENDIF

REM 东南部人类地盘上十字架形水域附近
IF_ACTION_POINT(14,PLAYER0)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,THIEF,19,4,6,100)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,THIEF,20,4,6,100)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,FOUR,-4,1)
    SET_TIMER(PLAYER0,TIMER4)
    IF(PLAYER0,TIMER4 >= 6000)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SIX,-4,1)
    ENDIF
ENDIF

IF(PLAYER_GOOD,RESEARCH == 0)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,THIEF,15,2,5,100)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,THIEF,16,3,5,100)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,THIEF,15,3,6,100)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,THIEF,16,2,6,100)
ENDIF

IF(PLAYER_GOOD,TEMPLE == 0)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,THIEF,17,2,5,100)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,THIEF,18,3,5,100)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,THIEF,17,3,6,100)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,THIEF,18,2,6,100)
ENDIF
REM 东南部人类地盘寺庙南面
IF_ACTION_POINT(18,PLAYER0)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SIX,-5,1)
    SET_TIMER(PLAYER0,TIMER3)
    IF(PLAYER0,TIMER3 >= 1000)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,FOUR,7,1)
        IF(PLAYER0,TIMER3 >= 1500)
            ADD_PARTY_TO_LEVEL(PLAYER_GOOD,FIVE,8,1)
            IF(PLAYER0,TIMER3 >= 1750)
                ADD_PARTY_TO_LEVEL(PLAYER_GOOD,ONE,9,1)
                IF(PLAYER0,TIMER3 >= 2250)
                    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,FOUR,10,1)
                    IF(PLAYER0,TIMER3 >= 2500)
                        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,FIVE,11,1)
                        IF(PLAYER0,TIMER3 >= 3000)
                            ADD_PARTY_TO_LEVEL(PLAYER_GOOD,ONE,12,1)
                        ENDIF
                    ENDIF
                ENDIF
            ENDIF
        ENDIF
    ENDIF
ENDIF
REM 东南部人类地盘上敌人心脏西边入口处
IF_ACTION_POINT(28,PLAYER0)
    IF_ACTION_POINT(29,PLAYER0)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SEVEN,-1,1)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SEVEN,-2,1)
        ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,SIX,22,ACTION_POINT,23,5,0)
        ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,x,22,ACTION_POINT,24,5,0)
        ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,FOUR,22,ACTION_POINT,25,5,0)
        ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,Y,22,ACTION_POINT,26,5,0)
    ENDIF
ENDIF

REM ****************************胜利/失败条件**********************************
IF(PLAYER_GOOD,DUNGEON_DESTROYED == 1)
    ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,MAGIC,-3,ACTION_POINT,22,8,0)
    ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,X,-1,ACTION_POINT,22,8,0)
    ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,FOUR,-2,ACTION_POINT,22,8,0)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,ARCHER,-3,4,5,0)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,ARCHER,22,4,5,0)
    SET_TIMER(PLAYER_GOOD,TIMER0)
    IF(PLAYER_GOOD,TIMER0 >= 1000)
        ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,ARCHER,-4,8,7,0)
        ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,AVATAR,-4,2,8,0)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,Z,-3,1)
        IF(PLAYER_GOOD,KNIGHT == 0)

            REM 以下修改游戏，增加胜利后可以建造其它房子，让你回味一下胜利的喜悦！
            LEVEL_VERSION(1)
            REVEAL_MAP_RECT(PLAYER0,127,127,254,254)
            ADD_GOLD_TO_PLAYER (PLAYER0,9999999)

            ROOM_AVAILABLE(PLAYER0,TREASURE,1,1)
            ROOM_AVAILABLE(PLAYER0,GARDEN,1,1)
            ROOM_AVAILABLE(PLAYER0,LAIR,1,1)
            ROOM_AVAILABLE(PLAYER0,RESEARCH,1,1)
            ROOM_AVAILABLE(PLAYER0,TRAINING,1,1)
            ROOM_AVAILABLE(PLAYER0,BRIDGE,1,1)
            ROOM_AVAILABLE(PLAYER0,GUARD_POST,1,1)
            ROOM_AVAILABLE(PLAYER0,WORKSHOP,1,1)
            ROOM_AVAILABLE(PLAYER0,BARRACKS,1,1)
            ROOM_AVAILABLE(PLAYER0,TEMPLE,1,1)
            ROOM_AVAILABLE(PLAYER0,PRISON,1,1)
            ROOM_AVAILABLE(PLAYER0,TORTURE,1,1)
            ROOM_AVAILABLE(PLAYER0,SCAVENGER,1,1)
            ROOM_AVAILABLE(PLAYER0,GRAVEYARD,1,1)
            
            MAGIC_AVAILABLE(PLAYER0,POWER_IMP,1,1)
            MAGIC_AVAILABLE(PLAYER0,POWER_CALL_TO_ARMS,1,1
            MAGIC_AVAILABLE(PLAYER0,POWER_SPEED,1,1)
            MAGIC_AVAILABLE(PLAYER0,POWER_HEAL_CREATURE,1,1)
            MAGIC_AVAILABLE(PLAYER0,POWER_DESTROY_WALLS,1,1)

            DOOR_AVAILABLE(PLAYER0,MAGIC,1,200)
            TRAP_AVAILABLE(PLAYER0,LIGHTNING,1,0)
            TRAP_AVAILABLE(PLAYER0,BOULDER,1,200)
            QUICK_INFORMATION(15,"送钱亮图，让你回味一下胜利的喜悦！",PLAYER0)

            WIN_GAME
        ENDIF
    ENDIF
ENDIF
IF(PLAYER0,DUNGEON_DESTROYED == 1)
    LOSE_GAME
ENDIF

REM ****************************提示信息**********************************
QUICK_OBJECTIVE(1,"参考提示：1、你的人口达15就有敌人进攻你，最好找到它们的出口用墙封起来；2、开始你招的兵都不能打，建议建个大工厂，既可以制造陷阱杀敌又可以帮补经济；3、西北部有个兵营，占了它你就可以招魔术师、兽人、骷髅、鬼魂、水怪还可以建哨所、兵营;4、东北部有个审讯室，占了它你就可以招巨魔、龙、黑暗女王、地狱犬、吸血鬼还可以建审讯室、寺庙、陵墓。",PLAYER0)  
IF(PLAYER0,GAME_TURN > 500)
    QUICK_INFORMATION(2,"你的人口达15就有敌人进攻你，而且你早期的兵不能打仗，最好找到它们的出口用墙封起来！",PLAYER0) 
ENDIF
IF(PLAYER0,GAME_TURN > 1500)
    QUICK_INFORMATION(3,"建议建个大工厂，既可以制造陷阱杀敌又可以帮补经济，挖条长长的通道，布满陷阱，用陷阱消灭敌人是比较好的办法！",PLAYER0) 
ENDIF
IF(PLAYER0,GAME_TURN > 2500)
    QUICK_INFORMATION(4,"西北部有个兵营，占了它你就可以招魔术师、兽人、骷髅、鬼魂、水怪还可以建哨所、兵营；东北部有个审讯室，占了它你就可以招巨魔、龙、黑暗女王、地狱犬、吸血鬼还可以建审讯室、寺庙、陵墓。",PLAYER0) 
ENDIF
IF(PLAYER0,GAME_TURN > 3500)
    QUICK_INFORMATION(5,"用龙或巫师清敌人的陷阱是经济的方法！",PLAYER0) 
ENDIF
IF(PLAYER0,GAME_TURN > 4000)
    QUICK_INFORMATION(6,"祝你好运，守护者！",PLAYER0) 
ENDIF
REM ***********************************************************************















