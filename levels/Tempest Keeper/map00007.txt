LEVEL_VERSION(1)

SET_GENERATE_SPEED(550)

QUICK_OBJECTIVE(0,"你终于找到了守护者克罗纽斯。虽然他不是沉睡者中最强的，但他的攻击性和凶猛弥补了这一点。他听起来是个不错的伙伴。",PLAYER1)
QUICK_MESSAGE(1, "蓝色风暴，我要打败你！", PLAYER0)

START_MONEY(PLAYER1,15000)
START_MONEY(PLAYER0,25000)

ALLY_PLAYERS(PLAYER0,PLAYER_GOOD,1)
ALLY_PLAYERS(PLAYER1,PLAYER0,2)

CONCEAL_MAP_RECT(PLAYER1, 128, 128, 254, 254, 1)
SET_GAME_RULE(PayDayGap,12500)
SET_GAME_RULE(128,1)
SET_GAME_RULE(ALWAYS_TUNNEL_TO_RED,1)

REMOVE_SACRIFICE_RECIPE(TROLL,BILE_DEMON,DARK_MISTRESS)

COMPUTER_PLAYER(PLAYER0,0)
SET_PLAYER_COLOR(PLAYER0,ORANGE)

SET_COMPUTER_CHECKS(PLAYER0,"COMPUTER CHECK IMPRISONMENT",0,1,25,30,200)
REM SET_COMPUTER_PROCESS(PLAYER0,"ATTACK SAFE ATTACK",0,70,8,70,0)
REM SET_COMPUTER_PROCESS(PLAYER0,"ATTACK PLAN 1",0,70,8,70,0)

SET_CREATURE_TENDENCIES(PLAYER0,FLEE,1)
SET_COMPUTER_GLOBALS(PLAYER0,70,100,4,1,400,5,1)

MAX_CREATURES(ALL_PLAYERS,25)

SET_COMPUTER_PROCESS(PLAYER0,"BUILD A TREASURE ROOM",0,6,6,2,0)
SET_COMPUTER_PROCESS(PLAYER0,"BUILD A LAIR ROOM",0,8,8,14,0)
SET_COMPUTER_PROCESS(PLAYER0,"BUILD A BARRACK ROOM",0,6,6,12,0)
SET_COMPUTER_PROCESS(PLAYER0,"BUILD A TRAINING ROOM",0,6,5,6,0)
SET_COMPUTER_PROCESS(PLAYER0,"BUILD A TORTURE ROOM",0,4,5,5,0)
SET_COMPUTER_PROCESS(PLAYER0,"BUILD A RESEARCH ROOM",0,5,6,3,0)

SET_COMPUTER_PROCESS(PLAYER0,"DIG TO GOLD",0,7500,150,12,0)

IF(PLAYER0,TRAINING >= 55)
	ROOM_AVAILABLE(PLAYER0,TRAINING,0,0)
ENDIF

ADD_CREATURE_TO_POOL(DARK_MISTRESS,15)
ADD_CREATURE_TO_POOL(SORCEROR,20)
ADD_CREATURE_TO_POOL(BILE_DEMON,10)
ADD_CREATURE_TO_POOL(HELL_HOUND,20)
ADD_CREATURE_TO_POOL(DEMONSPAWN,20)
ADD_CREATURE_TO_POOL(DRAGON,40)
ADD_CREATURE_TO_POOL(ORC,30)
ADD_CREATURE_TO_POOL(TROLL,20)

REM	Creature availability
CREATURE_AVAILABLE(ALL_PLAYERS,DARK_MISTRESS,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,SORCEROR,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,BILE_DEMON,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,DRAGON,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,HELL_HOUND,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,ORC,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,TROLL,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,DEMONSPAWN,1,0)

REM	Room availability
ROOM_AVAILABLE(ALL_PLAYERS,TREASURE,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,LAIR,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,GARDEN,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,TRAINING,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,RESEARCH,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,WORKSHOP,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,BARRACKS,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,GUARD_POST,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,PRISON,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,TORTURE,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,TEMPLE,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,GRAVEYARD,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,SCAVENGER,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,BRIDGE,1,0)

RESEARCH(PLAYER1,ROOM,WORKSHOP,7200)
RESEARCH(PLAYER1,ROOM,BARRACKS,9600)
RESEARCH(PLAYER1,ROOM,PRISON,14000)
RESEARCH(PLAYER1,ROOM,TORTURE,16000)
RESEARCH(PLAYER1,ROOM,TEMPLE,20000)
RESEARCH(PLAYER1,ROOM,GRAVEYARD,22500)
RESEARCH(PLAYER1,ROOM,SCAVENGER,24750)

REM	Spells availability
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HAND,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_IMP,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SLAP,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SIGHT,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CALL_TO_ARMS,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HEAL_CREATURE,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SPEED,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_LIGHTNING,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CHICKEN,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HOLD_AUDIENCE,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_PROTECT,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CONCEAL,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_DISEASE,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CAVE_IN,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_OBEY,1,0)

REM	Doors and traps availability
DOOR_AVAILABLE(ALL_PLAYERS,WOOD,1,0)
DOOR_AVAILABLE(ALL_PLAYERS,BRACED,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,POISON_GAS,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,LIGHTNING,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,LAVA,1,0)
DOOR_AVAILABLE(ALL_PLAYERS,STEEL,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,BOULDER,1,0)

CREATE_PARTY(WANDERING1)
	ADD_TO_PARTY(WANDERING1,THIEF,7,800,DEFEND_LOCATION,2000)

CREATE_PARTY(WANDERING2)
	ADD_TO_PARTY(WANDERING2,ARCHER,5,750,DEFEND_LOCATION,2000)

CREATE_PARTY(WANDERING3)
	ADD_TO_PARTY(WANDERING3,BARBARIAN,6,800,DEFEND_LOCATION,2000)

CREATE_PARTY(PATROL_HEAVY)
	ADD_TO_PARTY(PATROL_HEAVY,BARBARIAN,6,1500,DEFEND_PARTY,500)
	ADD_TO_PARTY(PATROL_HEAVY,BARBARIAN,5,1500,DEFEND_LOCATION,500)
	ADD_TO_PARTY(PATROL_HEAVY,ARCHER,5,1000,DEFEND_PARTY,500)
	ADD_TO_PARTY(PATROL_HEAVY,ARCHER,5,1000,DEFEND_LOCATION,500)

CREATE_PARTY(FAIRY4)
	ADD_TO_PARTY(FAIRY4,FAIRY,4,1500,DEFEND_LOCATION,30000)

CREATE_PARTY(GIANT4)
	ADD_TO_PARTY(GIANT4,GIANT,4,1500,DEFEND_LOCATION,30000)

CREATE_PARTY(SCOUTS)
	ADD_TO_PARTY(SCOUTS,THIEF,5,250,STEAL_GOLD,1000)
	ADD_TO_PARTY(SCOUTS,THIEF,5,250,STEAL_GOLD,1000)
	ADD_TO_PARTY(SCOUTS,DWARFA,5,400,ATTACK_ENEMIES,1000)
	ADD_TO_PARTY(SCOUTS,ARCHER,5,300,DEFEND_PARTY,1000)

CREATE_PARTY(BARBARIAN_ATTACKERS)
	ADD_TO_PARTY(BARBARIAN_ATTACKERS,BARBARIAN,5,400,ATTACK_ENEMIES,1200)
	ADD_TO_PARTY(BARBARIAN_ATTACKERS,BARBARIAN,6,400,ATTACK_ENEMIES,1200)
	ADD_TO_PARTY(BARBARIAN_ATTACKERS,BARBARIAN,5,400,ATTACK_ENEMIES,1200)
	ADD_TO_PARTY(BARBARIAN_ATTACKERS,ARCHER,5,400,DEFEND_PARTY,1200)
	ADD_TO_PARTY(BARBARIAN_ATTACKERS,ARCHER,5,400,DEFEND_PARTY,1200)

CREATE_PARTY(HOLY)
	ADD_TO_PARTY(HOLY,MONK,6,450,STEAL_SPELLS,1500)
	ADD_TO_PARTY(HOLY,MONK,6,600,ATTACK_DUNGEON_HEART,1500)
	ADD_TO_PARTY(HOLY,WIZARD,7,500,STEAL_SPELLS,1500)
	ADD_TO_PARTY(HOLY,FAIRY,7,400,STEAL_SPELLS,1500)
	ADD_TO_PARTY(HOLY,WITCH,6,400,DEFEND_PARTY,1500)
	ADD_TO_PARTY(HOLY,MONK,6,450,STEAL_SPELLS,1500)

CREATE_PARTY(FAIRY_ATTACKERS)
	ADD_TO_PARTY(FAIRY_ATTACKERS,FAIRY,5,500,STEAL_SPELLS,200)
	ADD_TO_PARTY(FAIRY_ATTACKERS,FAIRY,4,500,STEAL_SPELLS,200)

CREATE_PARTY(GIANT_ATTACKERS)
	ADD_TO_PARTY(GIANT_ATTACKERS,GIANT,5,500,ATTACK_ROOMS,200)
	ADD_TO_PARTY(GIANT_ATTACKERS,GIANT,4,500,ATTACK_ROOMS,200)
	
CREATE_PARTY(LORD)
	ADD_TO_PARTY(LORD,WIZARD,8,600,STEAL_SPELLS,5000)
	ADD_TO_PARTY(LORD,KNIGHT,9,2500,DEFEND_LOCATION,5000)
	ADD_TO_PARTY(LORD,FAIRY,10,800,DEFEND_PARTY,5000)
	ADD_TO_PARTY(LORD,GIANT,8,500,ATTACK_ROOMS,5000)
	ADD_TO_PARTY(LORD,WITCH,10,800,DEFEND_PARTY,5000)
	ADD_TO_PARTY(LORD,GIANT,8,500,ATTACK_ROOMS,5000)	
	
ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PATROL_HEAVY,2,1)
ADD_PARTY_TO_LEVEL(PLAYER_GOOD,PATROL_HEAVY,5,1)
ADD_PARTY_TO_LEVEL(PLAYER_GOOD,WANDERING1,3,1)
ADD_PARTY_TO_LEVEL(PLAYER_GOOD,WANDERING1,1,1)
ADD_PARTY_TO_LEVEL(PLAYER_GOOD,WANDERING2,1,2)
ADD_PARTY_TO_LEVEL(PLAYER_GOOD,WANDERING2,4,1)
ADD_PARTY_TO_LEVEL(PLAYER_GOOD,WANDERING3,4,1)
ADD_PARTY_TO_LEVEL(PLAYER_GOOD,WANDERING2,6,2)
ADD_PARTY_TO_LEVEL(PLAYER_GOOD,WANDERING3,6,1)
ADD_PARTY_TO_LEVEL(PLAYER_GOOD,FAIRY4,2,1)
ADD_PARTY_TO_LEVEL(PLAYER_GOOD,FAIRY4,5,1)
ADD_PARTY_TO_LEVEL(PLAYER_GOOD,FAIRY4,7,2)
ADD_PARTY_TO_LEVEL(PLAYER_GOOD,FAIRY4,8,2)
ADD_PARTY_TO_LEVEL(PLAYER_GOOD,GIANT4,2,1)
ADD_PARTY_TO_LEVEL(PLAYER_GOOD,GIANT4,5,1)
ADD_PARTY_TO_LEVEL(PLAYER_GOOD,GIANT4,7,2)
ADD_PARTY_TO_LEVEL(PLAYER_GOOD,GIANT4,8,2)

RANDOMISE_FLAG(PLAYER1,FLAG1,2)

IF(PLAYER1,FLAG1 == 1)
	ADD_OBJECT_TO_LEVEL(SPECBOX_STEALHR,13,0,PLAYER_NEUTRAL)
	ADD_OBJECT_TO_LEVEL(SPECBOX_INCLEV,14,0,PLAYER_NEUTRAL)
ENDIF

IF(PLAYER1,FLAG1 == 2)
	ADD_OBJECT_TO_LEVEL(SPECBOX_STEALHR,14,0,PLAYER_NEUTRAL)
	ADD_OBJECT_TO_LEVEL(SPECBOX_INCLEV,13,0,PLAYER_NEUTRAL)
ENDIF

IF(PLAYER1,GAME_TURN >= 1000)
	QUICK_INFORMATION(2,"夏日之地的战争状态：随着每一天的过去，英雄的队伍变得越来越薄弱，他们的决心受到了考验。",PLAYER1)
ENDIF


IF(PLAYER1,GAME_TURN >= 1500)
	QUICK_INFORMATION(3,"众多英雄活跃起来，他们是战士之王的残兵败将。然而他们似乎不愿意参与即将到来的冲突。他们是被你最近的胜利吓倒了，还是只是在等待时机？",PLAYER1)
ENDIF

IF(PLAYER1,GAME_TURN >= 7500)
	QUICK_INFORMATION(4,"我感觉到一丝微弱的存在。感觉很遥远……现在它消失了。真好奇。",PLAYER1)
ENDIF

IF(PLAYER0,TOTAL_CREATURES >= 10)
	IF(PLAYER0,TIMES_LEVELUP_CREATURE >= 30)
		ALLY_PLAYERS(PLAYER0,PLAYER_GOOD,0)
	ENDIF
ENDIF

IF(PLAYER0,IMP == 0)
	IF(PLAYER0,MONEY <= 1000)
		ADD_GOLD_TO_PLAYER(PLAYER0,15000)
	ENDIF
ENDIF

REM Enemy Keeper is killed quickly

IF(PLAYER0,GAME_TURN <= 25000)
	IF(PLAYER0,DUNGEON_DESTROYED == 1)
		SET_FLAG(PLAYER_GOOD,FLAG0,1)
	ENDIF
ENDIF

REM Enemy Keeper is losing badly

IF(PLAYER0,GAME_TURN > 25000)
	IF(PLAYER0,TOTAL_CREATURES <= 7)
		IF(PLAYER0,BATTLES_LOST >= 60)
			SET_FLAG(PLAYER_GOOD,FLAG0,2)
			QUICK_MESSAGE(5, "这次邂逅的神秘感已经完全消失。", PLAYER0)
		ENDIF
	ENDIF
ENDIF

REM Most Heroes are killed

IF(PLAYER_GOOD,GAME_TURN <= 60000)
	IF(PLAYER_GOOD,TOTAL_CREATURES <= 3)
		SET_FLAG(PLAYER_GOOD,FLAG0,3)
	ENDIF
ENDIF

REM Fallback trigger

IF(PLAYER_GOOD,GAME_TURN > 60000)
	SET_FLAG(PLAYER_GOOD,FLAG0,4)
ENDIF

REM Heroes attack

IF(PLAYER_GOOD,FLAG0 != 0)
	SET_TIMER(PLAYER_GOOD,TIMER0)
	QUICK_OBJECTIVE(6,"请注意，英雄们不再满足于袖手旁观。",PLAYER1)
ENDIF

IF(PLAYER_GOOD,TIMER0 >= 1000)
	QUICK_INFORMATION(7,"英雄正在进攻！",PLAYER1)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SCOUTS,9,1)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SCOUTS,10,1)
	ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,FAIRY,2,1,5,400)
	ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,FAIRY,5,1,5,400)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SCOUTS,11,1)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SCOUTS,12,1)
ENDIF

IF(PLAYER_GOOD,TIMER0 >= 1500)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,BARBARIAN_ATTACKERS,11,1)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,HOLY,12,1)
ENDIF

IF(PLAYER_GOOD,TIMER0 >= 2000)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,LORD,12,1)
	SET_TIMER(PLAYER_GOOD,TIMER1)
ENDIF

IF(PLAYER_GOOD,TIMER1 >= 5500)
	IF(PLAYER_GOOD,KNIGHT == 1)
		IF(PLAYER_GOOD,TOTAL_CREATURES <= 50)
			NEXT_COMMAND_REUSABLE
			ADD_PARTY_TO_LEVEL(PLAYER_GOOD,FAIRY_ATTACKERS,11,1)
			NEXT_COMMAND_REUSABLE
			ADD_PARTY_TO_LEVEL(PLAYER_GOOD,GIANT_ATTACKERS,12,1)
			NEXT_COMMAND_REUSABLE
			SET_TIMER(PLAYER_GOOD,TIMER1)
		ENDIF
	ENDIF
ENDIF

IF(PLAYER_GOOD,TOTAL_CREATURES == 0)
	IF(PLAYER0,DUNGEON_DESTROYED == 1)
		QUICK_OBJECTIVE(8,"恭喜您，您击败了主要对手之一，并击溃了英雄军队的残余势力。",PLAYER1)
		WIN_GAME
	ENDIF
ENDIF
