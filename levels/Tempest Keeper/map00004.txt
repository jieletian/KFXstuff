LEVEL_VERSION(1)

SET_GENERATE_SPEED(650)

QUICK_OBJECTIVE(0,"这个王国中的英雄堡垒打算是监视邻近的寂静平原的活动，而寂静平原是白色部落的居住地。白色部落有能力威胁到夏日之地。当您在这里取得成功后，前往寂静平原并消灭这个对手。",PLAYER1)

CONCEAL_MAP_RECT(PLAYER1, 163, 13, 1, 1, 1)

COMPUTER_PLAYER(PLAYER3,0)

START_MONEY(PLAYER1,25000)
START_MONEY(PLAYER3,50000)

SET_CREATURE_TENDENCIES(PLAYER3,FLEE,1)

SET_COMPUTER_GLOBALS(PLAYER3,90,14,10,1,600,1)

SET_PLAYER_MODIFIER(ALL_PLAYERS,Loyalty,75)

ADD_HEART_HEALTH(PLAYER1,-10000,0)

ALLY_PLAYERS(PLAYER1,PLAYER3,2)

REMOVE_SACRIFICE_RECIPE(TROLL,BILE_DEMON,DARK_MISTRESS)

REM SET_COMPUTER_CHECKS(PLAYER3,"CHECK FOR NEUTRAL PLACES",15000,0,0,0,65000)
REM "CHECK FOR QUICK ATTACK" CheckEvery AttackPercent CTADuration MinForAttack LastCheck
REM SET_COMPUTER_CHECKS(PLAYER3,"CHECK FOR QUICK ATTACK",700,50,3000,8,12500)
REM SET_COMPUTER_PROCESS(PLAYER3,"ATTACK SAFE ATTACK",-1,100,25,100,0)
SET_COMPUTER_PROCESS(PLAYER3,"ATTACK PLAN 1",-1,100,335,100,0)

SET_COMPUTER_PROCESS(PLAYER3,"BUILD A TRAINING ROOM",0,5,5,6,0)
SET_COMPUTER_PROCESS(PLAYER3,"BUILD A RESEARCH ROOM",0,5,6,3,0)
SET_COMPUTER_PROCESS(PLAYER3,"BUILD A TORTURE ROOM",0,4,5,5,0)

IF(PLAYER3,TRAINING >= 25)
	ROOM_AVAILABLE(PLAYER3,TRAINING,0,0)
ENDIF

IF(PLAYER3,WORKSHOP >= 108)
	ROOM_AVAILABLE(PLAYER3,WORKSHOP,0,0)
ENDIF

MAX_CREATURES(PLAYER1,18)
MAX_CREATURES(PLAYER3,25)

ADD_CREATURE_TO_POOL(TROLL,30)
ADD_CREATURE_TO_POOL(DEMONSPAWN,30)
ADD_CREATURE_TO_POOL(FLY,15)
ADD_CREATURE_TO_POOL(DARK_MISTRESS,8)
ADD_CREATURE_TO_POOL(SORCEROR,30)
ADD_CREATURE_TO_POOL(BUG,15)
ADD_CREATURE_TO_POOL(SPIDER,5)
ADD_CREATURE_TO_POOL(TENTACLE,20)
ADD_CREATURE_TO_POOL(DRAGON,8)
ADD_CREATURE_TO_POOL(BILE_DEMON,4)
ADD_CREATURE_TO_POOL(ORC,30)
ADD_CREATURE_TO_POOL(HELL_HOUND,10)

CREATURE_AVAILABLE(ALL_PLAYERS,TROLL,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,DEMONSPAWN,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,FLY,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,BUG,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,DARK_MISTRESS,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,SORCEROR,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,SPIDER,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,HELL_HOUND,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,TENTACLE,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,ORC,1,0)
CREATURE_AVAILABLE(PLAYER3,DRAGON,1,0)
CREATURE_AVAILABLE(PLAYER3,BILE_DEMON,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,HELL_HOUND,1,0)

SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,IMP,7)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,TROLL,7)
SET_CREATURE_MAX_LEVEL(PLAYER1,DEMONSPAWN,7)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,FLY,7)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,BUG,7)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,DARK_MISTRESS,7)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,SORCEROR,7)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,SPIDER,7)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,TENTACLE,7)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,ORC,7)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,HELL_HOUND,7)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,DRAGON,7)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,BILE_DEMON,7)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,SKELETON,7)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,DRAGON,7)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,HORNY,7)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,GHOST,7)

SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,THIEF,7)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,ARCHER,7)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,DWARFA,7)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,FAIRY,7)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,WIZARD,7)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,MONK,7)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,GIANT,7)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,BARBARIAN,7)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,WITCH,7)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,SAMURAI,7)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,KNIGHT,7)

ROOM_AVAILABLE(ALL_PLAYERS,TREASURE,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,RESEARCH,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,TORTURE,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,TRAINING,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,WORKSHOP,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,GARDEN,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,LAIR,1,1)
ROOM_AVAILABLE(PLAYER3,BARRACKS,1,0)
ROOM_AVAILABLE(PLAYER3,TEMPLE,1,0)

DOOR_AVAILABLE(ALL_PLAYERS,WOOD,1,0)
DOOR_AVAILABLE(ALL_PLAYERS,BRACED,1,0)
DOOR_AVAILABLE(ALL_PLAYERS,STEEL,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,ALARM,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,POISON_GAS,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,LIGHTNING,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,WORD_OF_POWER,1,0)

MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SIGHT,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CALL_TO_ARMS,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HEAL_CREATURE,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_POSSESS,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SLAP,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HAND,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CAVE_IN,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_IMP,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SPEED,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CONCEAL,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SIGHT,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_PROTECT,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HOLD_AUDIENCE,1,0)

CREATE_PARTY(TENTACLE_GUARD)
    ADD_TO_PARTY(TENTACLE_GUARD,TENTACLE,5,0,DEFEND_LOCATION,72000)

CREATE_PARTY(FIRST)
    ADD_TO_PARTY(FIRST,THIEF,3,200,STEAL_GOLD,0)
    ADD_TO_PARTY(FIRST,THIEF,4,250,STEAL_GOLD,0)
    ADD_TO_PARTY(FIRST,DWARFA,3,300,ATTACK_ROOMS,0)
    ADD_TO_PARTY(FIRST,ARCHER,4,200,DEFEND_PARTY,0)

CREATE_PARTY(SECOND)
    ADD_TO_PARTY(SECOND,BARBARIAN,4,300,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(SECOND,BARBARIAN,5,300,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(SECOND,ARCHER,4,200,DEFEND_PARTY,0)
    ADD_TO_PARTY(SECOND,ARCHER,5,250,DEFEND_PARTY,0)

CREATE_PARTY(THIRD)
    ADD_TO_PARTY(THIRD,MONK,6,300,STEAL_SPELLS,0)
    ADD_TO_PARTY(THIRD,WIZARD,7,500,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(THIRD,SAMURAI,6,300,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(THIRD,DWARFA,5,300,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(THIRD,DWARFA,5,300,ATTACK_ROOMS,0)
    ADD_TO_PARTY(THIRD,THIEF,7,200,STEAL_GOLD,0)

CREATE_PARTY(FOURTH)
    ADD_TO_PARTY(FOURTH,GIANT,6,400,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(FOURTH,GIANT,7,500,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(FOURTH,WITCH,7,400,DEFEND_PARTY,0)
    ADD_TO_PARTY(FOURTH,WITCH,6,500,STEAL_SPELLS,0)

CREATE_PARTY(FIVE)
    ADD_TO_PARTY(FIVE,GIANT,6,400,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(FIVE,WIZARD,7,500,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(FIVE,SAMURAI,7,400,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(FIVE,BARBARIAN,6,300,ATTACK_ROOMS,0)
    ADD_TO_PARTY(FIVE,MONK,7,400,DEFEND_PARTY,0)

CREATE_PARTY(LORD)
    ADD_TO_PARTY(LORD,WIZARD,7,600,STEAL_SPELLS,0)
    ADD_TO_PARTY(LORD,KNIGHT,8,5000,DEFEND_HEART,0)
    ADD_TO_PARTY(LORD,MONK,7,500,STEAL_GOLD,0)
    ADD_TO_PARTY(LORD,ARCHER,6,400,DEFEND_PARTY,0)
    ADD_TO_PARTY(LORD,SAMURAI,6,500,ATTACK_ROOMS,0)
    ADD_TO_PARTY(LORD,ARCHER,7,400,DEFEND_PARTY,0)
    ADD_TO_PARTY(LORD,SAMURAI,7,500,ATTACK_ROOMS,0)

ADD_PARTY_TO_LEVEL(PLAYER_GOOD,TENTACLE_GUARD,1,6)

IF(PLAYER1,GAME_TURN > 1000)
	QUICK_INFORMATION(1,"夏日之地战争状态：随着压力的增大，英雄队伍中开始出现怀疑的声音。",PLAYER1)
ENDIF

IF(PLAYER3,TOTAL_CREATURES >= 8)
	QUICK_INFORMATION(2,"这是意料之外的。一个名叫尼非利姆的野心勃勃的守护者已经醒来并加入了战斗。她是一个中等力量和能力的次要守护者。不要让这个闯入者占领附近的英雄堡垒，从而扩大这个领域。从积极的一面来看：这个领域现在实际上会正确地测试你的能力。",PLAYER1)
ENDIF

IF(PLAYER3,TOTAL_CREATURES >= 12)
	QUICK_MESSAGE(3,"蓝色风暴，这个王国属于我。滚开。",PLAYER3)
ENDIF

IF(PLAYER1,TEMPLE >= 1)
	TUTORIAL_FLASH_BUTTON(12,PLAYER1)
	ROOM_AVAILABLE(PLAYER1,TEMPLE,1,1)
ENDIF

IF(PLAYER1,SCAVENGER >= 1)
	TUTORIAL_FLASH_BUTTON(14,PLAYER1)
	ROOM_AVAILABLE(PLAYER1,SCAVENGER,1,1)
ENDIF

IF(PLAYER1,GUARD_POST >= 1)
    ROOM_AVAILABLE(ALL_PLAYERS,GUARD_POST,1,1)
    TUTORIAL_FLASH_BUTTON(19,PLAYER1)
ENDIF

IF(PLAYER_GOOD,DUNGEON_DESTROYED == 1)
    ROOM_AVAILABLE(ALL_PLAYERS,BRIDGE,1,1)
    TUTORIAL_FLASH_BUTTON(18,PLAYER1)
ENDIF

IF(PLAYER1,GAME_TURN >= 12500)
	IF_ACTION_POINT(3,PLAYER1)
		ADD_CREATURE_TO_LEVEL(PLAYER1,ORC,3,1,2,0)
		DISPLAY_INFORMATION(72,ALL_PLAYERS)
		ROOM_AVAILABLE(PLAYER1,BARRACKS,1,1)
	ENDIF
ENDIF

IF(PLAYER1,GAME_TURN >= 10000)
    ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,FIRST,-2,DUNGEON_HEART,1,4,250)
	QUICK_INFORMATION(4,"当地的英雄现在已经发现了你的存在并正在赶来。",PLAYER1)
	COMPUTER_DIG_TO_LOCATION(PLAYER3,PLAYER3,4)
	COMPUTER_DIG_TO_LOCATION(PLAYER3,PLAYER3,-1)
ENDIF

IF(PLAYER1,GAME_TURN >= 22500)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SECOND,-2,1)
ENDIF

IF(PLAYER1,GAME_TURN >= 30000)
    QUICK_INFORMATION(5,"我带来了情报。战士之王与古代守护者詹达佐恩发生了冲突。守护者克罗纽斯欺骗英雄们离开罗斯维尔的地下城，并在空旷的地方彻底摧毁了他们。守护者阿斯塔罗斯正在围攻黎明之城。最后，全能的守护者克兰扎拉克在米德黑文醒来。",PLAYER1)
ENDIF

IF(PLAYER1,GAME_TURN >= 35000)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,THIRD,-2,1)
ENDIF

IF(PLAYER1,GAME_TURN >= 47500)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,FOURTH,-2,1)
ENDIF

IF(PLAYER1,GAME_TURN >= 52500)
    QUICK_INFORMATION(6,"来自这个王国之外的更多情报。如前所述，守护者克兰扎拉克已经醒来，拥有只有守护者詹达佐恩才能匹敌的威力。然而，众所周知，他们都是古老的对手。守护者詹达佐恩实际上从与战士之王的战斗中脱离出来，与克兰扎拉克会面。这可能会变得很糟糕。",PLAYER1)
ENDIF

IF(PLAYER1,GAME_TURN >= 60000)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,FIVE,-2,1)
	COMPUTER_DIG_TO_LOCATION(PLAYER3,4,5)
ENDIF

IF(PLAYER1,GAME_TURN >= 80000)
	COMPUTER_DIG_TO_LOCATION(PLAYER3,5,6)
ENDIF

IF(PLAYER1,ENTRANCE == 18)
    MAX_CREATURES(PLAYER1,24)
	QUICK_INFORMATION(7,"你已找到一个额外的入口。更多的怪物将会加入你的地下城。",PLAYER1)
ENDIF

IF(PLAYER3,ENTRANCE == 18)
    MAX_CREATURES(PLAYER3,30)
ENDIF

IF_ACTION_POINT(2,PLAYER1)
    SET_FLAG(PLAYER_GOOD,FLAG0,1)
ENDIF

IF_ACTION_POINT(2,PLAYER3)
    SET_FLAG(PLAYER_GOOD,FLAG0,1)
ENDIF

IF(PLAYER_GOOD,FLAG0 == 1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,LORD,-1,1)
	QUICK_INFORMATION(8,"国王和他的残余部队正在进行最后的抵抗。",PLAYER1)
ENDIF

IF_SLAB_OWNER(42,21,PLAYER3)
	IF(PLAYER3,KEEPERS_DESTROYED == 1)
		IF(PLAYER1,DUNGEON_DESTROYED == 0)
			QUICK_OBJECTIVE(9,"你让守护者尼非利姆控制住了这个王国的英雄堡垒。现在再也无法取代她了。",PLAYER1)
			LOSE_GAME
		ENDIF
	ENDIF
ENDIF

IF(PLAYER1,HEART_HEALTH <= 2500)
	ALLY_PLAYERS(PLAYER1,PLAYER_GOOD,3)
	ALLY_PLAYERS(PLAYER1,PLAYER3,3)
	LOSE_GAME
ENDIF

IF(PLAYER_GOOD,FLAG0 == 1)
    IF(PLAYER1,ALL_DUNGEONS_DESTROYED == 1)
        IF(PLAYER_GOOD,TOTAL_CREATURES == 0)
			QUICK_OBJECTIVE(10,"一场当之无愧的胜利。你已经慢慢找回感觉了。现在出发前往寂静平原吧。",PLAYER1)
            WIN_GAME
        ENDIF
    ENDIF
ENDIF
