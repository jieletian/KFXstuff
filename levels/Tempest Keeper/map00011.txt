LEVEL_VERSION(1)
SET_GENERATE_SPEED(400)
START_MONEY(ALL_PLAYERS,2500)
MAX_CREATURES(ALL_PLAYERS,15)

CONCEAL_MAP_RECT(PLAYER1, 213, 52, 10, 10, 1)

QUICK_OBJECTIVE(0,"您已到达黎明之地。这个王国矗立着至高的王座。他的城堡是迄今为止夏日之地最大的地下城。基于您目前的力量和资源，直接向城墙进攻无异于自寻死路，因此请谨慎考虑您的下一步行动。",PLAYER1) 

CREATURE_ENTRANCE_LEVEL(PLAYER0,3)

SET_CREATURE_HEALTH(AVATAR,850)
SET_CREATURE_ARMOUR(AVATAR,65)
SET_CREATURE_STRENGTH(AVATAR,75)
SET_CREATURE_PROPERTY(AVATAR,LORD,0)
SET_CREATURE_PROPERTY(AVATAR,NEVER_CHICKENS,0)
SET_CREATURE_PROPERTY(AVATAR,IMMUNE_TO_BOULDER,0)
SET_CREATURE_PROPERTY(AVATAR,ONE_OF_KIND,0)
SET_CREATURE_PROPERTY(AVATAR,NO_IMPRISONMENT,0)

SET_CREATURE_HEALTH(KNIGHT,4000)
SET_CREATURE_ARMOUR(KNIGHT,150)
SET_CREATURE_STRENGTH(KNIGHT,100)
SET_CREATURE_PROPERTY(KNIGHT,NO_IMPRISONMENT,0)
SET_CREATURE_PROPERTY(KNIGHT,IMMUNE_TO_DISEASE,1)
SET_CREATURE_PROPERTY(KNIGHT,NEVER_CHICKENS,1)
SET_CREATURE_PROPERTY(KNIGHT,ONE_OF_KIND,1)
SET_CREATURE_CONFIGURATION(KNIGHT,LUCK,35)
SET_CREATURE_PROPERTY(KNIGHT,ILLUMINATED,1)
SET_CREATURE_CONFIGURATION(KNIGHT,BaseSpeed,30)

SET_CREATURE_INSTANCE(KNIGHT, 9, ARMOUR, 9)
SET_CREATURE_INSTANCE(KNIGHT, 10, WORD_OF_POWER, 10)

ADD_CREATURE_TO_POOL(FLY,20)
ADD_CREATURE_TO_POOL(BUG,20)
ADD_CREATURE_TO_POOL(DEMONSPAWN,20)
ADD_CREATURE_TO_POOL(TROLL,20)
ADD_CREATURE_TO_POOL(SPIDER,20)
ADD_CREATURE_TO_POOL(HELL_HOUND,20)
ADD_CREATURE_TO_POOL(TENTACLE,20)
ADD_CREATURE_TO_POOL(SORCEROR,20)
ADD_CREATURE_TO_POOL(ORC,20)
ADD_CREATURE_TO_POOL(VAMPIRE,3)
ADD_CREATURE_TO_POOL(BILE_DEMON,20)
ADD_CREATURE_TO_POOL(DRAGON,20)
ADD_CREATURE_TO_POOL(DARK_MISTRESS,20)
ADD_CREATURE_TO_POOL(HORNY,1)

CREATURE_AVAILABLE(ALL_PLAYERS,FLY,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,BUG,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,DEMONSPAWN,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,TROLL,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,SPIDER,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,HELL_HOUND,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,TENTACLE,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,SORCEROR,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,ORC,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,BILE_DEMON,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,DRAGON,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,VAMPIRE,1,3)
CREATURE_AVAILABLE(ALL_PLAYERS,DARK_MISTRESS,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,HORNY,1,1)

ROOM_AVAILABLE(ALL_PLAYERS,TREASURE,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,LAIR,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,GARDEN,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,RESEARCH,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,BRIDGE,1,0)

MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HAND,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SLAP,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_POSSESS,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_IMP,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SIGHT,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SPEED,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_OBEY,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CALL_TO_ARMS,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HOLD_AUDIENCE,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CAVE_IN,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HEAL_CREATURE,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_PROTECT,1,0)

TRAP_AVAILABLE(ALL_PLAYERS,ALARM,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,POISON_GAS,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,LIGHTNING,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,WORD_OF_POWER,1,0)

DOOR_AVAILABLE(ALL_PLAYERS,WOOD,1,4)
DOOR_AVAILABLE(ALL_PLAYERS,BRACED,1,2)
DOOR_AVAILABLE(ALL_PLAYERS,STEEL,1,1)


CREATE_PARTY(SAMURAI_GUARD)
    ADD_TO_PARTY(SAMURAI_GUARD,SAMURAI,6,0,DEFEND_ROOMS,1000)

CREATE_PARTY(MONK_GUARD)
    ADD_TO_PARTY(MONK_GUARD,MONK,4,0,DEFEND_LOCATION,200000)

CREATE_PARTY(FAIRY_GUARD)
    ADD_TO_PARTY(FAIRY_GUARD,FAIRY,4,0,DEFEND_LOCATION,200000)

CREATE_PARTY(WITCH_GUARD)
    ADD_TO_PARTY(WITCH_GUARD,WITCH,4,0,DEFEND_LOCATION,200000)

CREATE_PARTY(WIZARD_GUARD)
    ADD_TO_PARTY(WIZARD_GUARD,WIZARD,4,0,DEFEND_LOCATION,200000)
	
CREATE_PARTY(BARBARIAN_GUARD)
    ADD_TO_PARTY(BARBARIAN_GUARD,BARBARIAN,4,0,DEFEND_LOCATION,200000)
	
CREATE_PARTY(GIANT_GUARD)
    ADD_TO_PARTY(GIANT_GUARD,GIANT,4,0,DEFEND_LOCATION,200000)
	
CREATE_PARTY(LIGHT_GUARDS)
    ADD_TO_PARTY(LIGHT_GUARDS,THIEF,3,100,DEFEND_LOCATION,200000)
	ADD_TO_PARTY(LIGHT_GUARDS,ARCHER,3,100,DEFEND_LOCATION,200000)

CREATE_PARTY(LIGHT_RESPONSE)
	ADD_TO_PARTY(LIGHT_RESPONSE,DWARFA,3,200,STEAL_GOLD,200)
	ADD_TO_PARTY(LIGHT_RESPONSE,AVATAR,3,200,ATTACK_ROOMS,200)
	ADD_TO_PARTY(LIGHT_RESPONSE,ARCHER,3,200,ATTACK_ROOMS,200)

CREATE_PARTY(LIGHT_THIEF)
	ADD_TO_PARTY(LIGHT_THIEF,THIEF,3,0,STEAL_GOLD,200)

CREATE_PARTY(MEDIUM_RESPONSE)
	ADD_TO_PARTY(MEDIUM_RESPONSE,BARBARIAN,4,300,STEAL_GOLD,300)
	ADD_TO_PARTY(MEDIUM_RESPONSE,AVATAR,5,300,ATTACK_ROOMS,300)
	ADD_TO_PARTY(MEDIUM_RESPONSE,FAIRY,4,300,STEAL_SPELLS,300)
	ADD_TO_PARTY(MEDIUM_RESPONSE,ARCHER,4,300,ATTACK_ROOMS,300)

CREATE_PARTY(MEDIUM_THIEF)
	ADD_TO_PARTY(MEDIUM_THIEF,THIEF,5,0,STEAL_GOLD,200)

CREATE_PARTY(HEAVY_RESPONSE)
	ADD_TO_PARTY(HEAVY_RESPONSE,FAIRY,6,400,ATTACK_ROOMS,500)
	ADD_TO_PARTY(HEAVY_RESPONSE,AVATAR,7,400,ATTACK_ROOMS,500)
	ADD_TO_PARTY(HEAVY_RESPONSE,WIZARD,6,400,STEAL_SPELLS,500)
	ADD_TO_PARTY(HEAVY_RESPONSE,ARCHER,5,400,ATTACK_ROOMS,300)
	ADD_TO_PARTY(HEAVY_RESPONSE,BARBARIAN,5,400,ATTACK_ROOMS,300)

CREATE_PARTY(HEAVY_THIEF)
	ADD_TO_PARTY(HEAVY_THIEF,THIEF,7,0,STEAL_SPELLS,200)

CREATE_PARTY(LORD)
	ADD_TO_PARTY(LORD,KNIGHT,10,5000,ATTACK_DUNGEON_HEART,10000)
	ADD_TO_PARTY(LORD,WIZARD,8,1500,STEAL_SPELLS,10000)
	ADD_TO_PARTY(LORD,BARBARIAN,6,1500,STEAL_SPELLS,10000)
	ADD_TO_PARTY(LORD,ARCHER,6,1500,DEFEND_PARTY,10000)
	ADD_TO_PARTY(LORD,FAIRY,8,1500,STEAL_SPELLS,10000)
	ADD_TO_PARTY(LORD,AVATAR,9,1500,DEFEND_PARTY,10000)

CREATE_PARTY(ASSASSIN)
	ADD_TO_PARTY(HEAVY_THIEF,THIEF,10,0,SNIPE_DUNGEON_HEART,200)

IF(PLAYER1,GUARD_POST > 0)
	ROOM_AVAILABLE(ALL_PLAYERS,GUARD_POST,1,1)
	TUTORIAL_FLASH_BUTTON(19,100)
ENDIF

IF(PLAYER1,BARRACKS > 0)
	ROOM_AVAILABLE(ALL_PLAYERS,BARRACKS,1,1)
	TUTORIAL_FLASH_BUTTON(16,100)
ENDIF

IF(PLAYER1,PRISON > 0)
	ROOM_AVAILABLE(ALL_PLAYERS,PRISON,1,1)
	TUTORIAL_FLASH_BUTTON(11,100)
ENDIF

IF(PLAYER1,TORTURE > 0)
	ROOM_AVAILABLE(ALL_PLAYERS,TORTURE,1,1)
	TUTORIAL_FLASH_BUTTON(17,100)
ENDIF

IF(PLAYER1,TRAINING > 0)
	ROOM_AVAILABLE(ALL_PLAYERS,TRAINING,1,1)
	TUTORIAL_FLASH_BUTTON(10,100)
ENDIF

IF(PLAYER1,WORKSHOP > 0)
	ROOM_AVAILABLE(ALL_PLAYERS,WORKSHOP,1,1)
	TUTORIAL_FLASH_BUTTON(13,100)
ENDIF

IF(PLAYER1,TEMPLE > 0)
	ROOM_AVAILABLE(ALL_PLAYERS,TEMPLE,1,1)
	TUTORIAL_FLASH_BUTTON(12,100)
ENDIF

IF(PLAYER1,GAME_TURN >= 1000)
	QUICK_INFORMATION(1,"夏日之地的战争状态：当英雄们意识到战线日益恶化的困境时，绝望情绪笼罩着核心领域。",PLAYER1)
ENDIF

IF(PLAYER1,GAME_TURN >= 500)
	QUICK_INFORMATION(2,"多年来，许多守护者都围攻过黎明堡，但没有人成功占领它，守护者阿斯塔罗斯就是最近的一次例子。然而，他们的攻击给堡垒留下了薄弱之处。利用这一点来发挥你的优势吧。",PLAYER1)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,LIGHT_GUARDS,-2,1)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,LIGHT_GUARDS,-5,1)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,LIGHT_GUARDS,-6,2)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,LIGHT_GUARDS,-7,1)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,LIGHT_GUARDS,-8,1)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,LIGHT_GUARDS,-9,1)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,LIGHT_GUARDS,-10,1)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,LIGHT_GUARDS,2,1)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,LIGHT_GUARDS,3,1)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,LIGHT_GUARDS,4,1)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SAMURAI_GUARD,5,2)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SAMURAI_GUARD,6,2)
	SET_TIMER(PLAYER_GOOD,TIMER0)
	SET_TIMER(PLAYER_GOOD,TIMER1)
	SET_TIMER(PLAYER_GOOD,TIMER2)
	SET_TIMER(PLAYER_GOOD,TIMER3)
	SET_TIMER(PLAYER_GOOD,TIMER4)
	SET_TIMER(PLAYER_GOOD,TIMER5)
	SET_TIMER(PLAYER_GOOD,TIMER6)
	SET_TIMER(PLAYER_GOOD,TIMER7)
	REVEAL_MAP_RECT(PLAYER1,72,232,7,7)
	REVEAL_MAP_RECT(PLAYER1,97,186,7,7)
	REVEAL_MAP_RECT(PLAYER1,127,187,7,7)
	REVEAL_MAP_RECT(PLAYER1,200,209,7,7)
	REVEAL_MAP_RECT(PLAYER1,37,118,7,7)
ENDIF

IF(PLAYER1,GAME_TURN >= 1250)
	QUICK_INFORMATION(3,"守护者阿斯塔罗斯的部分怪物可能已被囚禁或躲藏起来了。找到它们来增强您现有的力量吧。",PLAYER1)
ENDIF


IF(PLAYER1,GAME_TURN >= 11500)
	QUICK_INFORMATION(4,"在你再次与守护者克罗诺斯战斗时，我短暂地感觉到了同样的存在。我不喜欢这样。",PLAYER1)
ENDIF


IF(PLAYER1,BATTLES_WON >= 2)
	QUICK_OBJECTIVE(5,"虽然与守护者不在一个级别，但至高之王本人也是一位强大的战士。他多年来没有遇到过真正的对手了，使他和他的驻军变得自负和懒惰。所以向他们发起战斗，在他们眼皮底下占领他们的堡垒吧！",PLAYER1)
ENDIF

IF(PLAYER1,BATTLES_WON >= 12)
	IF(PLAYER1,TOTAL_CREATURES >= 6)
		QUICK_OBJECTIVE(6,"你的进展不错，但要小心神秘骑士。如前所述，他们是由战士之王亲自训练的骑士，负责保卫荣耀谷，而黎明堡是他们的大本营。他们不会轻易放弃这个地方。",PLAYER1)
		SET_TIMER(PLAYER1,TIMER0)
		SET_DOOR(UNLOCKED,30,15)
		SET_DOOR(UNLOCKED,30,19)
		SET_DOOR(UNLOCKED,30,19)
		SET_DOOR(UNLOCKED,54,17)
		SET_DOOR(UNLOCKED,42,30)
	ENDIF
ENDIF

IF(PLAYER1,TIMER0 >= 6000)
	IF(PLAYER1,FLAG0 == 0)
		IF(PLAYER_GOOD,DUNGEON_DESTROYED == 0)
			IF(PLAYER_GOOD,AVATAR <= 18)
				NEXT_COMMAND_REUSABLE
				ADD_PARTY_TO_LEVEL(PLAYER_GOOD,LIGHT_RESPONSE,-1,1)
				NEXT_COMMAND_REUSABLE
				ADD_PARTY_TO_LEVEL(PLAYER_GOOD,LIGHT_THIEF,-1,1)
				NEXT_COMMAND_REUSABLE
				SET_TIMER(PLAYER1,TIMER0)
			ENDIF
		ENDIF
	ENDIF
ENDIF

IF(PLAYER1,BATTLES_WON >= 50)
	IF(PLAYER1,TOTAL_CREATURES >= 15)
		SET_TIMER(PLAYER1,TIMER1)
		ADD_TO_FLAG(PLAYER1,FLAG0,1)
	ENDIF
ENDIF

IF(PLAYER1,TIMER1 >= 6250)
	IF(PLAYER1,FLAG0 == 1)
		IF(PLAYER_GOOD,DUNGEON_DESTROYED == 0)
			IF(PLAYER_GOOD,AVATAR <= 14)
				NEXT_COMMAND_REUSABLE
				ADD_PARTY_TO_LEVEL(PLAYER_GOOD,MEDIUM_RESPONSE,-1,1)
				NEXT_COMMAND_REUSABLE
				ADD_PARTY_TO_LEVEL(PLAYER_GOOD,MEDIUM_THIEF,-1,1)
				NEXT_COMMAND_REUSABLE
				SET_TIMER(PLAYER1,TIMER1)
			ENDIF
		ENDIF
	ENDIF
ENDIF

IF(PLAYER1,ENTRANCE >= 9)
	SET_TIMER(PLAYER1,TIMER2)
	ADD_TO_FLAG(PLAYER1,FLAG0,1)
ENDIF

IF(PLAYER1,TIMER2 >= 6500)
	IF(PLAYER1,FLAG0 == 2)
		IF(PLAYER_GOOD,DUNGEON_DESTROYED == 0)
			IF(PLAYER_GOOD,AVATAR <= 6)
				NEXT_COMMAND_REUSABLE
				ADD_PARTY_TO_LEVEL(PLAYER_GOOD,HEAVY_RESPONSE,-1,1)
				NEXT_COMMAND_REUSABLE
				ADD_PARTY_TO_LEVEL(PLAYER_GOOD,HEAVY_THIEF,-1,1)
				NEXT_COMMAND_REUSABLE
				SET_TIMER(PLAYER1,TIMER2)
			ENDIF
		ENDIF
	ENDIF
ENDIF



IF(PLAYER_GOOD,TIMER0 >= 2000)
	IF(PLAYER_GOOD,MONK < 8)
		IF_SLAB_OWNER(44,39,PLAYER_GOOD)
			IF_SLAB_OWNER(40,39,PLAYER_GOOD)
				NEXT_COMMAND_REUSABLE
				ADD_PARTY_TO_LEVEL(PLAYER_GOOD,MONK_GUARD,DRAWFROM(-3,-4),1)
				NEXT_COMMAND_REUSABLE
				SET_TIMER(PLAYER_GOOD,TIMER0)
			ENDIF
		ENDIF
	ENDIF
ENDIF

IF(PLAYER_GOOD,TIMER1 >= 2750)
	IF(PLAYER_GOOD,WIZARD < 5)
		IF_SLAB_OWNER(50,34,PLAYER_GOOD)
			NEXT_COMMAND_REUSABLE
			ADD_PARTY_TO_LEVEL(PLAYER_GOOD,WIZARD_GUARD,-2,1)
			NEXT_COMMAND_REUSABLE
			SET_TIMER(PLAYER_GOOD,TIMER1)
		ENDIF
	ENDIF
ENDIF

IF(PLAYER_GOOD,TIMER2 >= 2750)
	IF(PLAYER_GOOD,BARBARIAN < 4)
		IF_SLAB_OWNER(20,40,PLAYER_GOOD)
			NEXT_COMMAND_REUSABLE
			ADD_PARTY_TO_LEVEL(PLAYER_GOOD,BARBARIAN_GUARD,-8,1)
			NEXT_COMMAND_REUSABLE
			SET_TIMER(PLAYER_GOOD,TIMER2)
		ENDIF
	ENDIF
ENDIF

IF(PLAYER_GOOD,TIMER3 >= 5500)
	IF(PLAYER_GOOD,GIANT < 3)
		IF_SLAB_OWNER(57,12,PLAYER_GOOD)
			NEXT_COMMAND_REUSABLE
			ADD_PARTY_TO_LEVEL(PLAYER_GOOD,GIANT_GUARD,-7,1)
			NEXT_COMMAND_REUSABLE
			SET_TIMER(PLAYER_GOOD,TIMER3)
		ENDIF
	ENDIF
ENDIF

IF(PLAYER_GOOD,TIMER4 >= 3850)
	IF(PLAYER_GOOD,FAIRY < 4)
		IF_SLAB_OWNER(73,46,PLAYER_GOOD)
			NEXT_COMMAND_REUSABLE
			ADD_PARTY_TO_LEVEL(PLAYER_GOOD,FAIRY_GUARD,-5,1)
			NEXT_COMMAND_REUSABLE
			SET_TIMER(PLAYER_GOOD,TIMER4)
		ENDIF
	ENDIF
ENDIF

IF(PLAYER_GOOD,TIMER5 >= 3750)
	IF(PLAYER_GOOD,WITCH <= 6)
		IF_SLAB_OWNER(18,31,PLAYER_GOOD)
			NEXT_COMMAND_REUSABLE
			ADD_PARTY_TO_LEVEL(PLAYER_GOOD,WITCH_GUARD,-6,1)
			NEXT_COMMAND_REUSABLE
			SET_TIMER(PLAYER_GOOD,TIMER5)
		ENDIF
	ENDIF
ENDIF

IF(PLAYER_GOOD,TIMER6 >= 4250)
	IF(PLAYER_GOOD,WITCH <= 6)
		IF_SLAB_OWNER(72,30,PLAYER_GOOD)
			NEXT_COMMAND_REUSABLE
			ADD_PARTY_TO_LEVEL(PLAYER_GOOD,WITCH_GUARD,-11,1)
			NEXT_COMMAND_REUSABLE
			SET_TIMER(PLAYER_GOOD,TIMER6)
		ENDIF
	ENDIF
ENDIF

IF(PLAYER_GOOD,TIMER7 >= 2850)
	IF(PLAYER_GOOD,GIANT < 3)
		IF_SLAB_OWNER(73,46,PLAYER_GOOD)
			NEXT_COMMAND_REUSABLE
			ADD_PARTY_TO_LEVEL(PLAYER_GOOD,GIANT_GUARD,-5,1)
			NEXT_COMMAND_REUSABLE
			SET_TIMER(PLAYER_GOOD,TIMER7)
		ENDIF
	ENDIF
ENDIF

IF_ACTION_POINT(1,PLAYER1)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,LORD,-1,1)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,ASSASSIN,-1,1)
	ADD_TO_FLAG(PLAYER1,FLAG0,1)
	QUICK_OBJECTIVE(7,"至高之王亲自到来了！消灭这个愚蠢的王！",PLAYER1)
ENDIF

IF(PLAYER_GOOD,TOTAL_CREATURES == 0)
	IF(PLAYER_GOOD,DUNGEON_DESTROYED == 1)
		IF(PLAYER1,FLAG0 == 3)
			QUICK_OBJECTIVE(8,"你已经占领了黎明之谷。现在，当你加入守护者阿斯塔罗斯对荣耀之谷的持续围攻时，这里将不再有来自增援部队的烦人干扰了。",PLAYER1)
			WIN_GAME
		ENDIF
	ENDIF
ENDIF
