LEVEL_VERSION(1)

QUICK_MESSAGE(0, "主人，外面有什么动静吗？", PLAYER1)

CONCEAL_MAP_RECT(PLAYER1,128,128,252,252,1)

ADD_HEART_HEALTH(PLAYER1,-15000,0)

SET_GENERATE_SPEED(1000)

SET_DOOR_CONFIGURATION(WOOD,ManufactureRequired,6000)
SET_TRAP_CONFIGURATION(POISON_GAS,ManufactureRequired,8000)

MAX_CREATURES(PLAYER1,16)

START_MONEY(PLAYER1,5000)

ADD_CREATURE_TO_POOL(FLY,6)
ADD_CREATURE_TO_POOL(BUG,24)
ADD_CREATURE_TO_POOL(SPIDER,2)

CREATURE_AVAILABLE(PLAYER1,FLY,1,0)
CREATURE_AVAILABLE(PLAYER1,BUG,1,0)
CREATURE_AVAILABLE(PLAYER1,SPIDER,1,0)

SET_CREATURE_MAX_LEVEL(PLAYER1,FLY,2)
SET_CREATURE_MAX_LEVEL(PLAYER1,BUG,2)
SET_CREATURE_MAX_LEVEL(PLAYER1,SPIDER,2)
SET_CREATURE_MAX_LEVEL(PLAYER1,IMP,2)

SET_CREATURE_MAX_LEVEL(PLAYER_GOOD,THIEF,2)
SET_CREATURE_MAX_LEVEL(PLAYER_GOOD,ARCHER,2)
SET_CREATURE_MAX_LEVEL(PLAYER_GOOD,DWARFA,2)
SET_CREATURE_MAX_LEVEL(PLAYER_GOOD,FAIRY,2)
SET_CREATURE_MAX_LEVEL(PLAYER_GOOD,WIZARD,2)
SET_CREATURE_MAX_LEVEL(PLAYER_GOOD,KNIGHT,3)
SET_CREATURE_MAX_LEVEL(PLAYER_GOOD,MONK,2)
SET_CREATURE_MAX_LEVEL(PLAYER_GOOD,BARBARIAN,2)

SET_CREATURE_HEALTH(KNIGHT,1250)
SET_CREATURE_ARMOUR(KNIGHT,60)
SET_CREATURE_STRENGTH(KNIGHT,25)
SET_CREATURE_CONFIGURATION(KNIGHT,BaseSpeed,30)

SET_CREATURE_HEALTH(BARBARIAN,700)
SET_CREATURE_ARMOUR(BARBARIAN,25)
SET_CREATURE_STRENGTH(BARBARIAN,30)

SET_CREATURE_HEALTH(THIEF,225)
SET_CREATURE_ARMOUR(THIEF,15)
SET_CREATURE_STRENGTH(THIEF,25)
SET_CREATURE_CONFIGURATION(THIEF,BaseSpeed,48)

SET_CREATURE_HEALTH(ARCHER,275)
SET_CREATURE_ARMOUR(ARCHER,15)
SET_CREATURE_STRENGTH(ARCHER,15)

SET_CREATURE_HEALTH(DWARFA,450)
SET_CREATURE_ARMOUR(DWARFA,30)
SET_CREATURE_STRENGTH(DWARFA,40)

ROOM_AVAILABLE(PLAYER1,TREASURE,1,1)
ROOM_AVAILABLE(PLAYER1,LAIR,1,1)
ROOM_AVAILABLE(PLAYER1,GARDEN,1,1)
ROOM_AVAILABLE(PLAYER1,TRAINING,1,1)

MAGIC_AVAILABLE(PLAYER1,POWER_HAND,1,1)
MAGIC_AVAILABLE(PLAYER1,POWER_SLAP,1,1)

DOOR_AVAILABLE(PLAYER1,WOOD,1,4)
TRAP_AVAILABLE(PLAYER1,POISON_GAS,1,0)

CREATE_PARTY(THIEF_GUARD)
    ADD_TO_PARTY(THIEF_GUARD,THIEF,2,0,DEFEND_LOCATION,25000)

CREATE_PARTY(ARCHER_GUARD)
    ADD_TO_PARTY(ARCHER_GUARD,ARCHER,2,0,DEFEND_LOCATION,25000)
	
CREATE_PARTY(DWARF_GUARD)
    ADD_TO_PARTY(DWARF_GUARD,DWARFA,2,0,DEFEND_LOCATION,25000)

CREATE_PARTY(FAIRY_GUARD)
    ADD_TO_PARTY(FAIRY_GUARD,FAIRY,2,0,STEAL_SPELLS,500)

CREATE_PARTY(SPELL_THIEVES)
    ADD_TO_PARTY(SPELL_THIEVES,FAIRY,2,0,STEAL_SPELLS,500)
	ADD_TO_PARTY(SPELL_THIEVES,WIZARD,2,0,STEAL_SPELLS,500)
	ADD_TO_PARTY(SPELL_THIEVES,MONK,2,0,STEAL_SPELLS,500)

CREATE_PARTY(THIEF_SNIPER)
	ADD_TO_PARTY(THIEF_SNIPER,THIEF,2,0,SNIPE_DUNGEON_HEART ,750)

CREATE_PARTY(FIRST_STRIKE)
    ADD_TO_PARTY(FIRST_STRIKE,THIEF,2,0,STEAL_GOLD,0)
    ADD_TO_PARTY(FIRST_STRIKE,THIEF,2,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(FIRST_STRIKE,THIEF,2,0,ATTACK_ROOMS,0)

CREATE_PARTY(SECOND_STRIKE)
    ADD_TO_PARTY(SECOND_STRIKE,THIEF,2,0,STEAL_GOLD,0)
    ADD_TO_PARTY(SECOND_STRIKE,THIEF,2,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(SECOND_STRIKE,ARCHER,2,0,DEFEND_PARTY,0)
    ADD_TO_PARTY(SECOND_STRIKE,THIEF,2,0,STEAL_GOLD,0)
    ADD_TO_PARTY(SECOND_STRIKE,ARCHER,2,0,DEFEND_PARTY,0)

CREATE_PARTY(THIRD_STRIKE)
    ADD_TO_PARTY(THIRD_STRIKE,THIEF,2,0,ATTACK_ROOMS,0)
    ADD_TO_PARTY(THIRD_STRIKE,THIEF,2,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(THIRD_STRIKE,THIEF,2,0,STEAL_GOLD,0)
    ADD_TO_PARTY(THIRD_STRIKE,ARCHER,2,0,DEFEND_PARTY,0)
    ADD_TO_PARTY(THIRD_STRIKE,ARCHER,2,0,ATTACK_ROOMS,0)
    ADD_TO_PARTY(THIRD_STRIKE,DWARFA,2,0,DEFEND_PARTY,0)

CREATE_PARTY(LORD)
    ADD_TO_PARTY(LORD,WIZARD,2,500,STEAL_SPELLS,1200)
    ADD_TO_PARTY(LORD,KNIGHT,3,5000,ATTACK_DUNGEON_HEART,1200)
    ADD_TO_PARTY(LORD,MONK,2,400,STEAL_GOLD,1200)
    ADD_TO_PARTY(LORD,ARCHER,2,300,DEFEND_PARTY,1200)
    ADD_TO_PARTY(LORD,BARBARIAN,2,200,ATTACK_ROOMS,1200)

IF(PLAYER1,GAME_TURN >= 250)
	QUICK_OBJECTIVE(1,"守护者，夏日之地的地下世界正在骚动。沉睡的守护者们正在苏醒，现在该你上场了。关于当前的情况：一个名叫玛拉的弱小守护者正在攻击邻近的火焰堡，而白色部落在寂静平原上的重新活动已经将大多数英雄从这个王国带走了。利用这个机会迅速逃离这个监狱，并吸引这里为数不多的怪物。 ",PLAYER1)
ENDIF

IF(PLAYER1,GAME_TURN >= 3000)
	IF(PLAYER1,BATTLES_WON >= 3)
		QUICK_OBJECTIVE(2,"小心，守护者。英雄们已经认识到，削弱并控制住守护者比摧毁他们的地下城心脏更好，因为这会让他们重生。不要让他们进一步破坏你的地下城心脏。",PLAYER1)
	ENDIF
ENDIF

IF(PLAYER1,GAME_TURN >= 9000)
	IF(PLAYER1,BATTLES_WON >= 10)
		QUICK_OBJECTIVE(3,"守护者，虽然火焰堡和寂静平原的邪恶力量可以对很好的阻挠英雄的行动，但现在就暴露自身是不明智的，因为这无疑会激起守护者的回应。不要留下任何幸存者。 ",PLAYER1)
	ENDIF
ENDIF

IF_ACTION_POINT(1,PLAYER1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,THIEF_GUARD,2,1)
	QUICK_MESSAGE(4, "“警报！蓝色风暴爆发！”", THIEF)
ENDIF

IF_ACTION_POINT(3,PLAYER1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,THIEF_GUARD,4,1)
ENDIF

IF_ACTION_POINT(5,PLAYER1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,THIEF_GUARD,6,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,ARCHER_GUARD,7,1)
	QUICK_MESSAGE(5, "“警报！立即通知典狱长！”", ARCHER)
ENDIF

IF_ACTION_POINT(8,PLAYER1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,ARCHER_GUARD,9,1)
	QUICK_MESSAGE(6, "“停！快停下来！”", ARCHER)
ENDIF

IF_ACTION_POINT(10,PLAYER1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,THIEF_GUARD,11,1)
ENDIF

IF_ACTION_POINT(12,PLAYER1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,THIEF_GUARD,13,1)
ENDIF

IF_ACTION_POINT(14,PLAYER1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,FAIRY_GUARD,16,2)
ENDIF

IF_ACTION_POINT(15,PLAYER1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,FAIRY_GUARD,16,2)
ENDIF

IF(PLAYER1,BRIDGE > 1)
	ROOM_AVAILABLE(ALL_PLAYERS,BRIDGE,1,1)
	TUTORIAL_FLASH_BUTTON(18,100)
ENDIF

IF(PLAYER1,MONEY <= 2500)
	QUICK_INFORMATION(7,"你的金子不足了。在地下城附近的区域挖点金子吧。",PLAYER1)
	REVEAL_MAP_RECT(PLAYER1,33,42,7,7)
	REVEAL_MAP_RECT(PLAYER1,111,45,7,7)
	REVEAL_MAP_RECT(PLAYER1,81,66,7,7)
	REVEAL_MAP_RECT(PLAYER1,55,109,7,7)
	REVEAL_MAP_RECT(PLAYER1,219,46,7,7)
	REVEAL_MAP_RECT(PLAYER1,153,59,7,7)
	REVEAL_MAP_RECT(PLAYER1,178,88,7,7)
	REVEAL_MAP_RECT(PLAYER1,201,108,7,7)
	REVEAL_MAP_RECT(PLAYER1,40,222,7,7)
	REVEAL_MAP_RECT(PLAYER1,57,149,7,7)
	REVEAL_MAP_RECT(PLAYER1,78,166,7,7)
	REVEAL_MAP_RECT(PLAYER1,105,200,7,7)
	REVEAL_MAP_RECT(PLAYER1,198,150,7,7)
	REVEAL_MAP_RECT(PLAYER1,189,165,7,7)
	REVEAL_MAP_RECT(PLAYER1,147,204,7,7)
	REVEAL_MAP_RECT(PLAYER1,219,221,7,7)
ENDIF


IF(PLAYER1,TOTAL_CREATURES >= 10)
	IF(PLAYER1,TOTAL_GOLD_MINED >= 5000)
		IF(PLAYER1,TOTAL_AREA >= 180)
			ADD_PARTY_TO_LEVEL(PLAYER_GOOD,FIRST_STRIKE,-1,1)
			ADD_PARTY_TO_LEVEL(PLAYER_GOOD,FIRST_STRIKE,-2,1)
			ADD_PARTY_TO_LEVEL(PLAYER_GOOD,THIEF_GUARD,-2,1)
			ADD_PARTY_TO_LEVEL(PLAYER_GOOD,DWARF_GUARD,-2,1)
			ADD_PARTY_TO_LEVEL(PLAYER_GOOD,ARCHER_GUARD,-2,1)
			ADD_PARTY_TO_LEVEL(PLAYER_GOOD,THIEF_GUARD,-1,1)
			ADD_PARTY_TO_LEVEL(PLAYER_GOOD,ARCHER_GUARD,-1,1)
			ADD_PARTY_TO_LEVEL(PLAYER_GOOD,DWARF_GUARD,-1,1)
			SET_FLAG(PLAYER1,FLAG0,1)
			SET_TIMER(PLAYER1,TIMER0)
			QUICK_OBJECTIVE(8,"到目前为止，你做得不错。但要注意，当地的英雄在过了很长时间后终于已经注意到了你的行动。准备反击吧。",PLAYER1)
		ENDIF
	ENDIF
ENDIF

IF(PLAYER1,TIMER0 >= 2500)
    IF(PLAYER1,FLAG0 == 1)
        NEXT_COMMAND_REUSABLE
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,FIRST_STRIKE,-1,1)
		NEXT_COMMAND_REUSABLE
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,THIEF_SNIPER,DRAWFROM(16,17),1)
        NEXT_COMMAND_REUSABLE
        SET_TIMER(PLAYER1,TIMER0)
    ENDIF
ENDIF

IF(PLAYER1,TOTAL_CREATURES >= 16)
	IF(PLAYER1,BATTLES_WON >= 35)
		IF(PLAYER1,TOTAL_GOLD_MINED >= 10000)
			SET_FLAG(PLAYER1,FLAG0,2)
			SET_TIMER(PLAYER1,TIMER1)
			ADD_PARTY_TO_LEVEL(PLAYER_GOOD,FIRST_STRIKE_STRIKE,-2,1)
			ADD_PARTY_TO_LEVEL(PLAYER_GOOD,THIEF_SNIPER,DRAWFROM(16,17),1)
		ENDIF
	ENDIF
ENDIF

IF(PLAYER1,TIMER1 >= 3000)
    IF(PLAYER1,FLAG0 == 2)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SECOND_STRIKE,-1,1)
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,THIEF_SNIPER,DRAWFROM(16,17),1)
    ENDIF
ENDIF

IF(PLAYER1,TIMER1 >= 5750)
    IF(PLAYER1,FLAG0 == 2)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SECOND_STRIKE,-2,1)
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,THIEF_SNIPER,DRAWFROM(16,17),1)
    ENDIF
ENDIF

IF(PLAYER1,TIMER1 >= 8300)
    IF(PLAYER1,FLAG0 == 2)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,THIRD_STRIKE,-2,1)
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,THIEF_SNIPER,DRAWFROM(16,17),1)
    ENDIF
ENDIF

IF(PLAYER1,TIMER1 >= 10500)
    IF(PLAYER1,FLAG0 == 2)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,THIRD_STRIKE,-1,1)
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,THIEF_SNIPER,DRAWFROM(16,17),1)
		SET_FLAG(PLAYER1,FLAG0,3)
		SET_TIMER(PLAYER1,TIMER2)
    ENDIF
ENDIF

IF_AVAILABLE(PLAYER1,POWER_SPEED >= 1)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SPELL_THIEVES,-2,1)
ENDIF

IF_AVAILABLE(PLAYER1,POWER_HEAL_CREATURE >= 1)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SPELL_THIEVES,-1,1)
ENDIF

IF(PLAYER1,TIMER2 >= 1500)
    IF(PLAYER1,FLAG0 == 3)
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,THIRD_STRIKE,-2,1)
		QUICK_INFORMATION(9,"监狱长正在进入这个王国。",PLAYER1)
		SET_GENERATE_SPEED(500)
    ENDIF
ENDIF

IF(PLAYER1,TIMER2 >= 4500)
    IF(PLAYER1,FLAG0 == 3)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,LORD,-1,1)
		QUICK_MESSAGE(10, "守护者，这一切该结束了。", Knight)
        SET_FLAG(PLAYER1,FLAG0,4)
    ENDIF
ENDIF

IF(PLAYER1,FLAG0 == 4)
	IF(PLAYER_GOOD,TOTAL_CREATURES == 0)
		WIN_GAME
		SET_FLAG(PLAYER1,FLAG0,5)
		QUICK_OBJECTIVE(11,"你的坚韧意志是这里取得胜利的决定性因素。随着夏日之地的战况进行，你在未来一定需要保持这种品质。现在，为当地居民介绍一下你的臭虫仆从们吧。",PLAYER1)
    ENDIF
ENDIF

IF(PLAYER1,HEART_HEALTH <= 2500)
	ALLY_PLAYERS(PLAYER1,PLAYER_GOOD,3)
	LOSE_GAME
ENDIF


RUN_AFTER_VICTORY(1)

IF(PLAYER1,FLAG0 == 5)
	CHANGE_SLAB_TYPE(5,66,DIRT)
ENDIF
