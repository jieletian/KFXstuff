LEVEL_VERSION(1)
SET_GENERATE_SPEED(600)
START_MONEY(ALL_PLAYERS,5000)
MAX_CREATURES(PLAYER0,30)
MAX_CREATURES(PLAYER1,15)

QUICK_OBJECTIVE(0,"狡猾的守护者阿斯塔罗斯挡住了你前往荣耀谷和黎明堡的路。你和他是最后的沉睡者。因此，你征服夏日之地的任务在这里显然岌岌可危。",PLAYER1)

COMPUTER_PLAYER(PLAYER0,0)

SET_COMPUTER_PROCESS(PLAYER0,"BUILD A TREASURE ROOM",0,5,6,2,0)
SET_COMPUTER_PROCESS(PLAYER0,"BUILD A RESEARCH ROOM",0,5,6,3,0)
SET_COMPUTER_PROCESS(PLAYER0,"BUILD A GRAVEYARD ROOM",0,5,6,11,0)

SET_CREATURE_TENDENCIES(PLAYER0,FLEE,1)

REM Black Knights
SET_CREATURE_PROPERTY(KNIGHT,EVIL,1)
SET_CREATURE_CONFIGURATION(KNIGHT,PAY,405)

REM Mystic Knights
SET_CREATURE_HEALTH(AVATAR,850)
SET_CREATURE_ARMOUR(AVATAR,65)
SET_CREATURE_STRENGTH(AVATAR,75)
SET_CREATURE_PROPERTY(AVATAR,ONE_OF_KIND,0)
SET_CREATURE_PROPERTY(AVATAR,LORD,0)
SET_CREATURE_PROPERTY(AVATAR,NEVER_CHICKENS,0)
SET_CREATURE_PROPERTY(AVATAR,IMMUNE_TO_BOULDER,0)
SET_CREATURE_PROPERTY(AVATAR,ONE_OF_KIND,0)
SET_CREATURE_PROPERTY(AVATAR,NO_IMPRISONMENT,0)

ADD_CREATURE_TO_POOL(TROLL,20)
ADD_CREATURE_TO_POOL(DEMONSPAWN,20)
ADD_CREATURE_TO_POOL(SPIDER,20)
ADD_CREATURE_TO_POOL(HELL_HOUND,20)
ADD_CREATURE_TO_POOL(BILE_DEMON,20)
ADD_CREATURE_TO_POOL(SORCEROR,20)
ADD_CREATURE_TO_POOL(ORC,20)
ADD_CREATURE_TO_POOL(DRAGON,40)
ADD_CREATURE_TO_POOL(DARK_MISTRESS,10)
ADD_CREATURE_TO_POOL(VAMPIRE,1)
ADD_CREATURE_TO_POOL(HORNY,1)

CREATURE_AVAILABLE(ALL_PLAYERS,DEMONSPAWN,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,TROLL,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,SPIDER,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,HELL_HOUND,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,BILE_DEMON,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,SORCEROR,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,ORC,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,DRAGON,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,DARK_MISTRESS,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,VAMPIRE,1,0)

ROOM_AVAILABLE(ALL_PLAYERS,TREASURE,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,LAIR,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,GARDEN,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,TRAINING,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,RESEARCH,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,GUARD_POST,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,WORKSHOP,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,TORTURE,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,BARRACKS,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,TEMPLE,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,GRAVEYARD,1,0)

MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HAND,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SLAP,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_POSSESS,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_IMP,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SIGHT,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CALL_TO_ARMS,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CONCEAL,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HOLD_AUDIENCE,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CAVE_IN,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HEAL_CREATURE,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_PROTECT,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CHICKEN,1,0)

RESEARCH(PLAYER0,MAGIC,POWER_DESTROY_WALLs,100000)

TRAP_AVAILABLE(ALL_PLAYERS,ALARM,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,POISON_GAS,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,LIGHTNING,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,LAVA,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,BOULDER,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,WORD_OF_POWER,1,0)

DOOR_AVAILABLE(ALL_PLAYERS,BRACED,1,0)
DOOR_AVAILABLE(ALL_PLAYERS,STEEL,1,0)

CREATE_PARTY(GIANT5)
	ADD_TO_PARTY(GIANT5,GIANT,5,1500,ATTACK_ROOMS,60000)
	
CREATE_PARTY(FAIRY5)
	ADD_TO_PARTY(FAIRY5,FAIRY,5,1500,STEAL_SPELLS,60000)
	
CREATE_PARTY(SCOUTS)
	ADD_TO_PARTY(SCOUTS,THIEF,8,250,STEAL_GOLD,200)
	ADD_TO_PARTY(SCOUTS,THIEF,8,250,STEAL_GOLD,200)
	ADD_TO_PARTY(SCOUTS,DWARFA,8,400,ATTACK_ROOMS,200)
	ADD_TO_PARTY(SCOUTS,ARCHER,8,300,DEFEND_PARTY,200)
	
CREATE_PARTY(BARBARIAN_ATTACKERS)
	ADD_TO_PARTY(BARBARIAN_ATTACKERS,BARBARIAN,8,400,ATTACK_ROOMS,200)
	ADD_TO_PARTY(BARBARIAN_ATTACKERS,BARBARIAN,8,400,ATTACK_ROOMS,200)
	ADD_TO_PARTY(BARBARIAN_ATTACKERS,BARBARIAN,8,400,ATTACK_ROOMS,200)
	ADD_TO_PARTY(BARBARIAN_ATTACKERS,ARCHER,7,400,DEFEND_PARTY,200)
	ADD_TO_PARTY(BARBARIAN_ATTACKERS,ARCHER,7,400,DEFEND_PARTY,200)

CREATE_PARTY(HOLY)
	ADD_TO_PARTY(HOLY,MONK,8,450,STEAL_SPELLS,200)
	ADD_TO_PARTY(HOLY,MONK,8,600,ATTACK_DUNGEON_HEART,200)
	ADD_TO_PARTY(HOLY,WIZARD,9,500,STEAL_SPELLS,200)
	ADD_TO_PARTY(HOLY,FAIRY,8,400,STEAL_SPELLS,200)
	ADD_TO_PARTY(HOLY,WITCH,8,400,DEFEND_PARTY,200)
	ADD_TO_PARTY(HOLY,MONK,8,450,STEAL_SPELLS,200)
	
CREATE_PARTY(MYSTIC_KNIGHTS)
	ADD_TO_PARTY(MYSTIC_KNIGHTS,AVATAR,8,450,STEAL_SPELLS,200)
	ADD_TO_PARTY(MYSTIC_KNIGHTS,AVATAR,9,600,ATTACK_DUNGEON_HEART,200)
	ADD_TO_PARTY(MYSTIC_KNIGHTS,AVATAR,8,500,STEAL_SPELLS,200)
	ADD_TO_PARTY(MYSTIC_KNIGHTS,WIZARD,8,400,STEAL_SPELLS,200)
	ADD_TO_PARTY(MYSTIC_KNIGHTS,FAIRY,7,400,DEFEND_PARTY,200)
	ADD_TO_PARTY(MYSTIC_KNIGHTS,WITCH,7,450,STEAL_SPELLS,200)
	
CREATE_PARTY(GIANT_ATTACKERS)
	ADD_TO_PARTY(GIANT_ATTACKERS,GIANT,9,500,ATTACK_ROOMS,200)
	ADD_TO_PARTY(GIANT_ATTACKERS,GIANT,8,500,ATTACK_ROOMS,200)
	
CREATE_PARTY(FAIRY_ATTACKERS)
	ADD_TO_PARTY(FAIRY_ATTACKERS,FAIRY,9,500,STEAL_SPELLS,200)
	ADD_TO_PARTY(FAIRY_ATTACKERS,FAIRY,8,500,STEAL_SPELLS,200)

ADD_PARTY_TO_LEVEL(PLAYER_GOOD,FAIRY5,2,2)
ADD_PARTY_TO_LEVEL(PLAYER_GOOD,GIANT5,2,1)

ADD_PARTY_TO_LEVEL(PLAYER_GOOD,FAIRY5,5,2)
ADD_PARTY_TO_LEVEL(PLAYER_GOOD,GIANT5,5,1)
ADD_PARTY_TO_LEVEL(PLAYER_GOOD,FAIRY5,4,1)
ADD_PARTY_TO_LEVEL(PLAYER_GOOD,GIANT5,3,1)
ADD_PARTY_TO_LEVEL(PLAYER_GOOD,GIANT5,1,1)

IF(PLAYER1,GAME_TURN >= 1000)
	QUICK_INFORMATION(1,"夏日之地的战争状态：当英雄们意识到战线日益恶化的困境时，绝望情绪笼罩着核心领域。",PLAYER1)
ENDIF

IF(PLAYER1,GAME_TURN >= 500)
	QUICK_MESSAGE(2,"蓝色风暴，最终只能有一个。",PLAYER0)
ENDIF

IF(PLAYER1,TOTAL_CREATURES >= 5)
	QUICK_INFORMATION(3,"“屠夫”在春日之境事件发生后独自走上了自己的道路。他声称自己对上层世界有自己的计划。不过，他会将夏日之境正在发生的事情告知他的族人。也许他的一些族人会来到你的地下城。（冥界使者现在可以从入口进来和寺庙献祭获得）。",PLAYER1)
ENDIF

IF(PLAYER1,TOTAL_CREATURES >= 10)
	CREATURE_AVAILABLE(PLAYER1,HORNY,1,1)
	SET_CREATURE_CONFIGURATION(HORNY,PAY,750)
ENDIF

IF(PLAYER1,GAME_TURN >= 15000)
	QUICK_INFORMATION(4,"夏日之地的首都格洛里代尔近在咫尺。不过，如果你愿意，也可以考虑黎明霍尔德。黎明霍尔德是夏日之地高王的所在地，也是神秘骑士团的堡垒。",PLAYER1)
ENDIF

IF_AVAILABLE(PLAYER1,POWER_SIGHT >= 1)
	IF(PLAYER1,TOTAL_CREATURES >= 8)
		QUICK_OBJECTIVE(5,"这个王国中的大多数怪物已经投靠了守护者阿斯塔罗斯，因此您在人数上处于劣势。寻找方法来增强您的力量吧。但要小心；强大的英雄在该地区巡逻。",PLAYER1)
	ENDIF
ENDIF

IF(PLAYER1,TOTAL_CREATURES >= 15)
	QUICK_INFORMATION(6,"据说有一群黑骑士在这一地区游荡。也许你可以让他们加入你的阵营。",PLAYER1)
ENDIF

IF(PLAYER1,KNIGHT >= 1)
	QUICK_INFORMATION(7,"年轻的黑骑士卡莱布非常感激你的救命之恩。救出他的兄弟，你将获得强大的盟友。",PLAYER1)
	MAGIC_AVAILABLE(PLAYER0,POWER_DESTROY_WALLS,1,0)
ENDIF

IF(PLAYER0,TRAINING >= 60)
	ROOM_AVAILABLE(PLAYER0,TRAINING,0,0)
ENDIF

IF(PLAYER1,BRIDGE > 1)
	ROOM_AVAILABLE(PLAYER1,BRIDGE,1,1)
	ROOM_AVAILABLE(PLAYER0,BRIDGE,1,0)
	TUTORIAL_FLASH_BUTTON(18,100)
ENDIF

IF(PLAYER1,SCAVENGER > 1)
	ROOM_AVAILABLE(PLAYER1,SCAVENGER,1,1)
	ROOM_AVAILABLE(PLAYER0,SCAVENGER,1,0)
	TUTORIAL_FLASH_BUTTON(14,100)
ENDIF

IF_CONTROLS(PLAYER0,TOTAL_CREATURES < 14)
	IF(PLAYER0,GAME_TURN > 40000)
		ADD_TO_FLAG(PLAYER0,FLAG0,1)
	ENDIF
ENDIF

IF(PLAYER0,HEART_HEALTH <= 25000)
	IF_ALLIED(PLAYER1,PLAYER0 == 0)
		IF_ALLIED(PLAYER0,PLAYER1 == 0)
			ADD_TO_FLAG(PLAYER0,FLAG0,1)
		ENDIF
	ENDIF
ENDIF

IF(PLAYER0,FLAG0 >= 1)
	ADD_HEART_HEALTH(PLAYER0,5000,0)
	USE_POWER(PLAYER0,POWER_HOLD_AUDIENCE,1)
	USE_POWER(PLAYER1,POWER_HOLD_AUDIENCE,1)
	ADD_TO_FLAG(PLAYER0,FLAG1,1)
	SET_TIMER(PLAYER0,TIMER0)
	CREATURE_ENTRANCE_LEVEL(PLAYER0,4)
	ADD_TO_FLAG(PLAYER_GOOD,FLAG0,1)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SCOUTS,6,1)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SCOUTS,11,1)
	QUICK_MESSAGE(8,"蓝色风暴，召唤你的进攻。英雄正在逼近。",PLAYER0)
ENDIF

IF(PLAYER0,TIMER0 >= 250)
	IF(PLAYER0,FLAG1 >= 1)
		QUICK_MESSAGE(9,"我建议我们去处理掉这些英雄的入侵者。",PLAYER0)
		QUICK_INFORMATION(10,"（与红色守护者结盟以继续）",PLAYER1)
	ENDIF
ENDIF

IF_ALLIED(PLAYER1,PLAYER0 == 1)
	IF(PLAYER0,FLAG1 >= 1)
		ALLY_PLAYERS(PLAYER1,PLAYER0,3)
		SET_GAME_RULE(AlliesShareCta,1)
		SET_GAME_RULE(AlliesShareDrop,1)
		SET_GAME_RULE(AlliesShareVision,1)
		SET_GAME_RULE(PreserveClassicBugs,128)
		ADD_TO_FLAG(PLAYER0,FLAG0,1)
		ADD_TO_FLAG(PLAYER_GOOD,FLAG0,1)
		SET_GENERATE_SPEED(1000)
		QUICK_OBJECTIVE(11,"这是一个意想不到的发展，但也许可以转化为长期优势。一旦击退了英雄的攻击，结盟也许是一种选择。不要让守护者阿斯塔罗斯被英雄放逐。",PLAYER1)
	ENDIF
ENDIF

IF(PLAYER0,TIMER0 >= 500)
	SET_TIMER(PLAYER_GOOD,TIMER0)
	SET_TIMER(PLAYER_GOOD,TIMER1)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SCOUTS,10,1)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SCOUTS,11,1)
ENDIF

IF(PLAYER0,TIMER0 >= 750)
	IF(PLAYER_GOOD,FLAG0 == 2)
		SET_TIMER(PLAYER_GOOD,TIMER2)
		SET_TIMER(PLAYER_GOOD,TIMER3)
		SET_TIMER(PLAYER_GOOD,TIMER4)
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SCOUTS,10,1)
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SCOUTS,11,1)
	ENDIF
ENDIF

IF(PLAYER_GOOD,TIMER0 >= 1800)
	IF(PLAYER_GOOD,FLAG0 >= 1)
		NEXT_COMMAND_REUSABLE
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SCOUTS,11,1)
		NEXT_COMMAND_REUSABLE
		SET_TIMER(PLAYER_GOOD,TIMER0)
	ENDIF
ENDIF

IF(PLAYER_GOOD,TIMER1 >= 1750)
	IF(PLAYER_GOOD,FLAG0 >= 1)
		NEXT_COMMAND_REUSABLE
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SCOUTS,10,1)
		NEXT_COMMAND_REUSABLE
		SET_TIMER(PLAYER_GOOD,TIMER1)
		ENDIF
	ENDIF


IF(PLAYER_GOOD,TIMER2 >= 2500)
	IF(PLAYER_GOOD,FLAG0 >= 2)
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SCOUTS,6,1)
		NEXT_COMMAND_REUSABLE
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,BARBARIAN_ATTACKERS,7,1)
		NEXT_COMMAND_REUSABLE
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,FAIRY_ATTACKERS,5,1)
		NEXT_COMMAND_REUSABLE
		SET_TIMER(PLAYER_GOOD,TIMER2)
	ENDIF
ENDIF

IF(PLAYER_GOOD,TIMER3 >= 2750)
	IF(PLAYER_GOOD,FLAG0 >= 2)
		NEXT_COMMAND_REUSABLE
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,HOLY,9,1)
		NEXT_COMMAND_REUSABLE
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,GIANT_ATTACKERS,5,1)
		NEXT_COMMAND_REUSABLE
		SET_TIMER(PLAYER_GOOD,TIMER3)
	ENDIF
ENDIF

IF(PLAYER_GOOD,TIMER4 >= 12500)
	ADD_TO_FLAG(PLAYER_GOOD,FLAG0,-2)
	SET_TIMER(PLAYER_GOOD,TIMER5)
	QUICK_INFORMATION(12,"英雄们已被击退，但尚未被击败。一股新的力量正在逼近，这次是来自黎明霍德。",PLAYER1)
	SET_GENERATE_SPEED(1000)
	CREATURE_ENTRANCE_LEVEL(PLAYER0,5)
ENDIF

IF(PLAYER_GOOD,TIMER5 >= 4500)
	SET_TIMER(PLAYER_GOOD,TIMER6)
	SET_TIMER(PLAYER_GOOD,TIMER7)
	SET_TIMER(PLAYER1,TIMER0)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,MYSTIC_KNIGHTS,11,1)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,MYSTIC_KNIGHTS,10,1)
	QUICK_INFORMATION(13,"神秘的骑士正在逼近。小心他们，因为他们是战士之王亲自训练的骑士，负责保卫荣耀谷。他们不容小觑。",PLAYER1)
ENDIF

IF(PLAYER_GOOD,TIMER6 >= 2150)
	IF(PLAYER_GOOD,FLAG1 == 0)
		NEXT_COMMAND_REUSABLE
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,MYSTIC_KNIGHTS,6,1)
		NEXT_COMMAND_REUSABLE
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,MYSTIC_KNIGHTS,9,1)
		NEXT_COMMAND_REUSABLE
		SET_TIMER(PLAYER_GOOD,TIMER6)
	ENDIF
ENDIF

IF(PLAYER_GOOD,TIMER7 >= 2350)
	IF(PLAYER_GOOD,FLAG1 == 0)
		NEXT_COMMAND_REUSABLE
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,GIANT_ATTACKERS,10,1)
		NEXT_COMMAND_REUSABLE
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,HOLY,8,1)
		NEXT_COMMAND_REUSABLE
		SET_TIMER(PLAYER_GOOD,TIMER7)
	ENDIF
ENDIF

IF(PLAYER1,TIMER0 >= 12500)
	ADD_TO_FLAG(PLAYER_GOOD,FLAG1,1)
	SET_TIMER(PLAYER_GOOD,TIMER5)
	ADD_TO_FLAG(PLAYER1,FLAG1,1)
ENDIF

IF(PLAYER0,DUNGEON_DESTROYED == 1)
	QUICK_OBJECTIVE(14,"您让守护者阿斯塔罗斯被英雄放逐了。现在，对抗英雄们的联盟已经破碎了。",PLAYER1)
	LOSE_GAME
ENDIF


IF_ALLIED(PLAYER1,PLAYER0 == 1)
	IF(PLAYER1,FLAG1 == 1)
		IF_CONTROLS(PLAYER_GOOD,TOTAL_CREATURES == 0)
			QUICK_OBJECTIVE(15,"英雄们已被击败。现在是向他们发起攻击的时候了。鉴于神秘骑士的威胁，你的下一个目的地将是黎明之城。守护者阿斯塔罗斯表示他将开始围攻荣耀谷，以防止你的干扰。",PLAYER1)
			WIN_GAME
		ENDIF
	ENDIF
ENDIF