LEVEL_VERSION(1)

COMPUTER_PLAYER(PLAYER3,0)
SET_PLAYER_COLOR(PLAYER3,WHITE)

ALLY_PLAYERS(PLAYER1,PLAYER3,2)
ALLY_PLAYERS(PLAYER3,PLAYER_GOOD,1)

START_MONEY(PLAYER1,25000)
START_MONEY(PLAYER3,500000)
START_MONEY(PLAYER_GOOD,200000)

CONCEAL_MAP_RECT(PLAYER1,229, 136, 3, 3, 1)

SET_CREATURE_HEALTH(WITCH,5000)
SET_CREATURE_STRENGTH(WITCH,150)
SET_CREATURE_ARMOUR(WITCH,170)
SET_CREATURE_PROPERTY(WITCH,NEVER_CHICKENS,1)
SET_CREATURE_PROPERTY(WITCH,IMMUNE_TO_BOULDER,1)
SET_CREATURE_PROPERTY(WITCH,ONE_OF_KIND,1)
SET_CREATURE_PROPERTY(WITCH,UNAFFECTED_BY_WIND,1)
SET_CREATURE_PROPERTY(WITCH,ILLUMINATED,1)
SET_CREATURE_CONFIGURATION(WITCH,BaseSpeed,30)
SET_CREATURE_PROPERTY(WITCH,NO_IMPRISONMENT,0)
SET_CREATURE_CONFIGURATION(WITCH,LUCK,30)
SET_CREATURE_CONFIGURATION(WITCH,FearsomeFactor,10)

SET_CREATURE_INSTANCE(WITCH, 2, CAST_SPELL_CHICKEN, 2)
SET_CREATURE_INSTANCE(WITCH, 6, FREEZE, 6)
SET_CREATURE_INSTANCE(WITCH, 9, FIRE_BOMB, 9)
SET_CREATURE_INSTANCE(WITCH, 10, WORD_OF_POWER, 10)

SET_CREATURE_INSTANCE(SAMURAI, 10, INVISIBILITY, 10)

REM Mystic Knights
SET_CREATURE_HEALTH(AVATAR,850)
SET_CREATURE_ARMOUR(AVATAR,65)
SET_CREATURE_STRENGTH(AVATAR,75)
SET_CREATURE_PROPERTY(AVATAR,ONE_OF_KIND,0)
SET_CREATURE_PROPERTY(AVATAR,LORD,0)
SET_CREATURE_PROPERTY(AVATAR,NEVER_CHICKENS,0)
SET_CREATURE_PROPERTY(AVATAR,IMMUNE_TO_BOULDER,0)
SET_CREATURE_PROPERTY(AVATAR,ONE_OF_KIND,0)
SET_CREATURE_PROPERTY(AVATAR,NO_IMPRISONMENT,0)

SET_CREATURE_CONFIGURATION(TUNNELLER,BaseSpeed,80)
SET_CREATURE_CONFIGURATION(TUNNELLER,PAY,0)

SET_CREATURE_PROPERTY(KNIGHT,LORD,0)

REM Elite Vampire Kessler
SET_CREATURE_PROPERTY(VAMPIRE,ONE_OF_KIND,1)
SET_CREATURE_HEALTH(VAMPIRE,1000)
SET_CREATURE_STRENGTH(VAMPIRE,80)
SET_CREATURE_ARMOUR(VAMPIRE,40)
SET_CREATURE_CONFIGURATION(VAMPIRE,LUCK,30)

SET_COMPUTER_GLOBALS(PLAYER3,50,15,1,1,2000,5)
SET_COMPUTER_PROCESS(PLAYER3,"ATTACK SAFE ATTACK",-1,100,335,100,0)
SET_COMPUTER_PROCESS(PLAYER3,"ATTACK PLAN 1",-1,100,335,100,0)
SET_CREATURE_TENDENCIES(PLAYER3,FLEE,1)

SET_HAND_RULE(PLAYER3,ANY_CREATURE,RULE0,DENY,LEVEL_HIGHER,5)
SET_HAND_RULE(PLAYER3,ANY_CREATURE,RULE1,DENY,AGE_HIGHER,30)

SET_GENERATE_SPEED(600)

MAX_CREATURES(ALL_PLAYERS,20)

ADD_CREATURE_TO_POOL(TROLL,17)
ADD_CREATURE_TO_POOL(SORCEROR,17)
ADD_CREATURE_TO_POOL(BILE_DEMON,12)
ADD_CREATURE_TO_POOL(DARK_MISTRESS,5)
ADD_CREATURE_TO_POOL(DRAGON,30)
ADD_CREATURE_TO_POOL(TENTACLE,30)
ADD_CREATURE_TO_POOL(ORC,17)
ADD_CREATURE_TO_POOL(HELL_HOUND,20)
ADD_CREATURE_TO_POOL(DEMONSPAWN,20)
ADD_CREATURE_TO_POOL(BUG,10)
ADD_CREATURE_TO_POOL(FLY,10)
ADD_CREATURE_TO_POOL(SPIDER,10)

ADD_CREATURE_TO_POOL(DWARFA,4)
ADD_CREATURE_TO_POOL(ARCHER,10)
ADD_CREATURE_TO_POOL(THIEF,4)
ADD_CREATURE_TO_POOL(FAIRY,10)
ADD_CREATURE_TO_POOL(MONK,10)
ADD_CREATURE_TO_POOL(BARBARIAN,10)
ADD_CREATURE_TO_POOL(GIANT,10)
ADD_CREATURE_TO_POOL(KNIGHT,15)
ADD_CREATURE_TO_POOL(AVATAR,6)
ADD_CREATURE_TO_POOL(WIZARD,12)
ADD_CREATURE_TO_POOL(SAMURAI,10)

ROOM_AVAILABLE(PLAYER1,TREASURE,1,1)
ROOM_AVAILABLE(PLAYER1,LAIR,1,1)
ROOM_AVAILABLE(PLAYER1,GARDEN,1,1)
ROOM_AVAILABLE(PLAYER1,TRAINING,1,1)
ROOM_AVAILABLE(PLAYER1,RESEARCH,1,1)
ROOM_AVAILABLE(PLAYER1,WORKSHOP,1,0)
ROOM_AVAILABLE(PLAYER1,TORTURE,1,0)
ROOM_AVAILABLE(PLAYER1,BARRACKS,1,0)

CREATURE_AVAILABLE(PLAYER1,TROLL,1,0)
CREATURE_AVAILABLE(PLAYER1,SORCEROR,1,0)
CREATURE_AVAILABLE(PLAYER1,BILE_DEMON,1,0)
CREATURE_AVAILABLE(PLAYER1,DARK_MISTRESS,1,0)
CREATURE_AVAILABLE(PLAYER1,DRAGON,1,0)
CREATURE_AVAILABLE(PLAYER1,TENTACLE,1,0)
CREATURE_AVAILABLE(PLAYER1,ORC,1,0)
CREATURE_AVAILABLE(PLAYER1,HELL_HOUND,1,0)
CREATURE_AVAILABLE(PLAYER1,BUG,1,0)
CREATURE_AVAILABLE(PLAYER1,FLY,1,0)



MAGIC_AVAILABLE(PLAYER1,POWER_HAND,1,1)
MAGIC_AVAILABLE(PLAYER3,POWER_HAND,1,1)
MAGIC_AVAILABLE(PLAYER1,POWER_IMP,1,1)
MAGIC_AVAILABLE(PLAYER1,POWER_SLAP,1,1)
MAGIC_AVAILABLE(PLAYER3,POWER_SLAP,1,1)
MAGIC_AVAILABLE(PLAYER3,POWER_SIGHT,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_OBEY,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CONCEAL,1,0)
MAGIC_AVAILABLE(PLAYER1,POWER_CALL_TO_ARMS,1,0)
MAGIC_AVAILABLE(PLAYER3,POWER_CALL_TO_ARMS,1,1)
MAGIC_AVAILABLE(PLAYER1,POWER_HEAL_CREATURE,1,0)
MAGIC_AVAILABLE(PLAYER1,POWER_PROTECT,1,0)
MAGIC_AVAILABLE(PLAYER3,POWER_CONCEAL,1,0)
MAGIC_AVAILABLE(PLAYER3,POWER_CAVE_IN,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HOLD_AUDIENCE,1,0)
MAGIC_AVAILABLE(PLAYER_GOOD,POWER_CHICKEN,1,1)
MAGIC_AVAILABLE(PLAYER_GOOD,POWER_SPEED,1,1)

TRAP_AVAILABLE(PLAYER3,BOULDER,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,ALARM,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,POISON_GAS,1,0)
DOOR_AVAILABLE(ALL_PLAYERS,STEEL,1,0)
DOOR_AVAILABLE(ALL_PLAYERS,MAGIC,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,LIGHTNING,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,WORD_OF_POWER,1,0)

REM -------------------------------------------------------------------------------------------------------------------------------------------

CREATE_PARTY(BLUE_ONE)
    ADD_TO_PARTY(BLUE_ONE,DWARFA,4,200,ATTACK_ROOMS,0)
    ADD_TO_PARTY(BLUE_ONE,BARBARIAN,5,200,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(BLUE_ONE,THIEF,5,100,STEAL_GOLD,0)
    ADD_TO_PARTY(BLUE_ONE,DWARFA,5,200,ATTACK_ROOMS,0)
    ADD_TO_PARTY(BLUE_ONE,ARCHER,4,300,DEFEND_PARTY,0)
    ADD_TO_PARTY(BLUE_ONE,ARCHER,4,300,DEFEND_PARTY,0)

CREATE_PARTY(BLUE_TWO)
    ADD_TO_PARTY(BLUE_TWO,BARBARIAN,7,300,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(BLUE_TWO,BARBARIAN,6,400,ATTACK_ROOMS,0)
    ADD_TO_PARTY(BLUE_TWO,GIANT,6,200,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(BLUE_TWO,MONK,6,300,STEAL_SPELLS,0)
    ADD_TO_PARTY(BLUE_TWO,ARCHER,7,300,DEFEND_PARTY,0)

CREATE_PARTY(BLUE_THREE)
    ADD_TO_PARTY(BLUE_THREE,SAMURAI,9,600,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(BLUE_THREE,WIZARD,9,700,STEAL_SPELLS,0)
    ADD_TO_PARTY(BLUE_THREE,GIANT,10,500,STEAL_SPELLS,0)
    ADD_TO_PARTY(BLUE_THREE,MONK,8,500,STEAL_SPELLS,0)
    ADD_TO_PARTY(BLUE_THREE,BARBARIAN,8,500,ATTACK_ROOMS,0)

CREATE_PARTY(WITCH_QUEEN)
    ADD_TO_PARTY(WITCH_QUEEN,WITCH,10,10000,DEFEND_HEART,150000)

CREATE_PARTY(RAIDERS)
    ADD_TO_PARTY(RAIDERS,BARBARIAN,5,400,ATTACK_ROOMS,0)
    ADD_TO_PARTY(RAIDERS,BARBARIAN,6,400,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(RAIDERS,ARCHER,6,300,STEAL_SPELLS,0)
    ADD_TO_PARTY(RAIDERS,DWARFA,5,200,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(RAIDERS,THIEF,5,100,STEAL_GOLD,0)

CREATE_PARTY(HOLY)
    ADD_TO_PARTY(HOLY,MONK,8,500,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(HOLY,WIZARD,8,400,STEAL_SPELLS,0)
    ADD_TO_PARTY(HOLY,SAMURAI,7,500,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(HOLY,SAMURAI,7,500,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(HOLY,MONK,7,200,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(HOLY,MONK,7,200,ATTACK_ENEMIES,0)

CREATE_PARTY(ELITE)
    ADD_TO_PARTY(ELITE,SAMURAI,9,1000,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(ELITE,WIZARD,10,1500,STEAL_SPELLS,0)
    ADD_TO_PARTY(ELITE,BARBARIAN,8,1000,STEAL_GOLD,0)
    ADD_TO_PARTY(ELITE,GIANT,8,1000,ATTACK_ROOMS,0)
    ADD_TO_PARTY(ELITE,ARCHER,9,800,DEFEND_PARTY,0)
    ADD_TO_PARTY(ELITE,SAMURAI,8,1000,DEFEND_PARTY,0)

CREATE_PARTY(SCOUTS)
    ADD_TO_PARTY(SCOUTS,DWARFA,10,500,ATTACK_ROOMS,0)
    ADD_TO_PARTY(SCOUTS,THIEF,9,100,STEAL_GOLD,0)
    ADD_TO_PARTY(SCOUTS,THIEF,9,100,STEAL_GOLD,0)
    ADD_TO_PARTY(SCOUTS,THIEF,8,100,STEAL_GOLD,0)
    ADD_TO_PARTY(SCOUTS,ARCHER,9,300,DEFEND_PARTY,0)
    ADD_TO_PARTY(SCOUTS,ARCHER,8,200,DEFEND_PARTY,0)
    ADD_TO_PARTY(SCOUTS,ARCHER,8,200,DEFEND_PARTY,0)

CREATE_PARTY(RPG)
    ADD_TO_PARTY(RPG,WIZARD,10,1000,STEAL_SPELLS,1600)
    ADD_TO_PARTY(RPG,KNIGHT,10,1500,ATTACK_DUNGEON_HEART,1600)
	ADD_TO_PARTY(RPG,AVATAR,9,1500,DEFEND_PARTY,1600)
    ADD_TO_PARTY(RPG,MONK,9,1000,STEAL_SPELLS,1600)
    ADD_TO_PARTY(RPG,BARBARIAN,9,1000,ATTACK_ENEMIES,1600)
    ADD_TO_PARTY(RPG,SAMURAI,9,1000,DEFEND_PARTY,1600)
    ADD_TO_PARTY(RPG,ARCHER,9,800,DEFEND_PARTY,1600)

CREATE_PARTY(BRUTES)
    ADD_TO_PARTY(BRUTES,BARBARIAN,9,400,DEFEND_PARTY,0)
    ADD_TO_PARTY(BRUTES,GIANT,9,600,STEAL_SPELLS,0)
    ADD_TO_PARTY(BRUTES,GIANT,8,500,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(BRUTES,BARBARIAN,8,400,DEFEND_PARTY,0)

CREATE_PARTY(GIANT_LIGHT)
	ADD_TO_PARTY(GIANT_LIGHT,GIANT,5,0,ATTACK_ROOMS,0)
	
CREATE_PARTY(FAIRY_LIGHT)
	ADD_TO_PARTY(FAIRY_LIGHT,FAIRY,5,0,STEAL_SPELLS,0)

CREATE_PARTY(GIANT_ATTACKER)
	ADD_TO_PARTY(GIANT_ATTACKER,GIANT,9,0,ATTACK_ROOMS,0)

CREATE_PARTY(GIANT_DESTROYER)
	ADD_TO_PARTY(GIANT_DESTROYER,GIANT,10,0,SABOTAGE_ROOMS,0)

CREATE_PARTY(FAIRY_GOLD_THIEF)
	ADD_TO_PARTY(FAIRY_GOLD_THIEF,FAIRY,9,0,STEAL_GOLD,0)

CREATE_PARTY(FAIRY_SPELL_THIEF)
	ADD_TO_PARTY(FAIRY_SPELL_THIEF,FAIRY,10,0,STEAL_SPELLS,0)

CREATE_PARTY(THIEF_SPELL)
	ADD_TO_PARTY(THIEF_SPELL,THIEF,10,0,STEAL_SPELLS,0)

CREATE_PARTY(THIEF_GOLD)
	ADD_TO_PARTY(THIEF_GOLD,THIEF,10,0,STEAL_GOLD,0)
	
CREATE_PARTY(ASSASSIN)
	ADD_TO_PARTY(ASSASSIN,SAMURAI,10,0,SNIPE_DUNGEON_HEART,0)

REM -------------------------------------------------------------------------------------------------------------------------------------------

QUICK_OBJECTIVE(0,"您的任务即将完成。最后的守护者，女巫之王，控制着最后的英雄王国夏日之地。杀死并埋葬您遇到的所有敌人，直到没有一个人留下。",PLAYER1)

SET_TIMER(PLAYER3,TIMER3)

IF(PLAYER1,GAME_TURN >= 1000)
	QUICK_INFORMATION(1,"夏日之地战争状态：寂静的战场上只能听到微风的吹拂。胜利对于最后的英雄来说似乎是一个遥不可及的梦想。",PLAYER1)
ENDIF


IF(PLAYER3,TIMER3 >= 4000)
	IF(PLAYER3,DUNGEON_DESTROYED == 0)
		IF(PLAYER3,TUNNELLER < 4)
			IF(PLAYER3,TOTAL_CREATURES < 20)
				NEXT_COMMAND_REUSABLE
				ADD_CREATURE_TO_LEVEL(PLAYER3,TUNNELLER,-1,1,5,0)
				NEXT_COMMAND_REUSABLE
				SET_TIMER(PLAYER3,TIMER3)
			ENDIF
		ENDIF
	ENDIF
ENDIF

IF(PLAYER1,GAME_TURN >= 1500)
	QUICK_INFORMATION(2,"两座城堡保卫着圣徒堡；一座在西边，另一座大得多的城堡在南边。女巫之王很可能住在后者。",PLAYER1)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,WITCH_QUEEN,2,1)
	CREATURE_AVAILABLE(PLAYER3,DWARFA,1,2)
	CREATURE_AVAILABLE(PLAYER3,ARCHER,1,2)
	CREATURE_AVAILABLE(PLAYER3,THIEF,1,2)
	CREATURE_AVAILABLE(PLAYER3,BARBARIAN,1,2)
	CREATURE_AVAILABLE(PLAYER3,GIANT,1,10)
	CREATURE_AVAILABLE(PLAYER3,FAIRY,1,2)
	CREATURE_AVAILABLE(PLAYER3,MONK,1,2)
	CREATURE_AVAILABLE(PLAYER3,WIZARD,1,2)
	CREATURE_AVAILABLE(PLAYER3,SAMURAI,1,2)
	CREATURE_AVAILABLE(PLAYER3,KNIGHT,1,30)
	CREATURE_AVAILABLE(PLAYER3,AVATAR,1,6)
ENDIF

IF(PLAYER1,TOTAL_CREATURES >= 10)
	QUICK_INFORMATION(3,"有人发现这个王国有亡灵战士，尤其是在西部。他们似乎并没有结盟。小心，往西走会让你危险地靠近西部城堡。",PLAYER1)
ENDIF

IF(PLAYER1,VAMPIRE >= 1)
	QUICK_MESSAGE(4, "非常感谢您的帮助......守护者。", Vampire)
ENDIF

IF(PLAYER1,ENTRANCE == 18)
	MAX_CREATURES(PLAYER1,25)
	CREATURE_ENTRANCE_LEVEL(PLAYER1,3)
ENDIF

IF(PLAYER1,ENTRANCE == 27)
	MAX_CREATURES(PLAYER1,30)
	CREATURE_ENTRANCE_LEVEL(PLAYER1,5)
ENDIF


IF_ACTION_POINT(8,PLAYER1)
    QUICK_OBJECTIVE(5,"你正在接近圣堡的两座城堡之一，女巫女王就住在这座城堡里。准备好后，就可以进攻该地。",PLAYER1)
ENDIF

IF_ACTION_POINT(8,PLAYER1)
	IF(PLAYER3,DUNGEON_DESTROYED == 0)
		QUICK_INFORMATION(6,"你还没有摧毁西部城堡。也许先确保侧翼安全再攻占主战场才是明智之举。",PLAYER1)
	ENDIF
ENDIF

IF_ACTION_POINT(1,PLAYER1)
    SET_TIMER(PLAYER3,TIMER0)
    SET_FLAG(PLAYER3,FLAG0,1)
ENDIF

IF(PLAYER3,FLAG0 == 1)
    IF(PLAYER3,DUNGEON_DESTROYED == 0)
		IF(PLAYER_GOOD,DUNGEON_DESTROYED == 0)
			IF(PLAYER3,TIMER0 >= 7500)
				NEXT_COMMAND_REUSABLE
				ADD_PARTY_TO_LEVEL(PLAYER_GOOD,BLUE_ONE,-1,1)
				NEXT_COMMAND_REUSABLE
				SET_TIMER(PLAYER3,TIMER0)
			ENDIF
		ENDIF
	ENDIF
ENDIF

IF(PLAYER3,FLAG0 == 2)
    IF(PLAYER3,DUNGEON_DESTROYED == 0)
			IF(PLAYER_GOOD,DUNGEON_DESTROYED == 0)
				IF(PLAYER3,TIMER1 >= 7000)
				NEXT_COMMAND_REUSABLE
				ADD_PARTY_TO_LEVEL(PLAYER_GOOD,BLUE_TWO,-1,1)
				NEXT_COMMAND_REUSABLE
				SET_TIMER(PLAYER3,TIMER1)
			ENDIF
		ENDIF
	ENDIF
ENDIF

IF(PLAYER3,FLAG0 == 3)
    IF(PLAYER3,DUNGEON_DESTROYED == 0)
			IF(PLAYER_GOOD,DUNGEON_DESTROYED == 0)
				IF(PLAYER3,TIMER2 >= 6500)
				NEXT_COMMAND_REUSABLE
				ADD_PARTY_TO_LEVEL(PLAYER_GOOD,ASSASSIN,-1,1)
				NEXT_COMMAND_REUSABLE
				ADD_PARTY_TO_LEVEL(PLAYER_GOOD,BLUE_THREE,-1,1)
				NEXT_COMMAND_REUSABLE
				SET_TIMER(PLAYER3,TIMER2)
			ENDIF
		ENDIF
	ENDIF
ENDIF

IF_ACTION_POINT(3,PLAYER1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,RAIDERS,-6,1)
    SET_TIMER(PLAYER_GOOD,TIMER0)
	MAGIC_AVAILABLE(PLAYER3,POWER_SPEED,1,1)
	MAGIC_AVAILABLE(PLAYER3,POWER_HEAL_CREATURE,1,1)
	MAGIC_AVAILABLE(PLAYER3,POWER_PROTECT,1,1)
    SET_FLAG(PLAYER_GOOD,FLAG0,1)
	QUICK_OBJECTIVE(7,"不幸的是，女巫之王已为她的英雄同伴们赐予了保护祝福。现在为时已晚。")
    SET_CREATURE_ARMOUR(AVATAR,80)
	SET_CREATURE_ARMOUR(FAIRY,30)
    SET_CREATURE_ARMOUR(WIZARD,30)
    SET_CREATURE_ARMOUR(ARCHER,40)
    SET_CREATURE_ARMOUR(MONK,45)
    SET_CREATURE_ARMOUR(THIEF,40)
    SET_CREATURE_ARMOUR(DWARFA,50)
    SET_CREATURE_ARMOUR(BARBARIAN,60)
    SET_CREATURE_ARMOUR(SAMURAI,70)
    SET_CREATURE_ARMOUR(GIANT,70)
    SET_CREATURE_ARMOUR(KNIGHT,120)
	MAX_CREATURES(PLAYER3,22)
	CREATURE_ENTRANCE_LEVEL(PLAYER3,3)
	ADD_GOLD_TO_PLAYER(PLAYER3,25000)
ENDIF

IF(PLAYER_GOOD,FLAG0 == 1)
    IF(PLAYER_GOOD,TIMER0 >= 6000)
        NEXT_COMMAND_REUSABLE
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,RAIDERS,-6,1)
        NEXT_COMMAND_REUSABLE
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,FAIRY_LIGHT,9,1)
        NEXT_COMMAND_REUSABLE
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,GIANT_LIGHT,10,1)
        NEXT_COMMAND_REUSABLE
        SET_TIMER(PLAYER_GOOD,TIMER0)
    ENDIF
ENDIF

IF_ACTION_POINT(4,PLAYER1)
	MAGIC_AVAILABLE(PLAYER3,POWER_CHICKEN,1,1)
    QUICK_OBJECTIVE(8,"女巫之王正在对你的生物施咒，并对她的同伴英雄施予额外的祝福，以减缓你的前进。")
    SET_TIMER(PLAYER_GOOD,TIMER1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,RAIDERS,-4,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,RAIDERS,-5,1)
	MAX_CREATURES(PLAYER3,25)
	CREATURE_ENTRANCE_LEVEL(PLAYER3,5)
	USE_POWER_ON_CREATURE(PLAYER1,ANY_CREATURE,ON_ENEMY_GROUND,PLAYER_GOOD,POWER_CHICKEN,9,0)
	USE_SPELL_ON_CREATURE(PLAYER1,ANY_CREATURE,ON_ENEMY_GROUND,SPELL_FREEZE,0)
	SET_FLAG(PLAYER1,FLAG0,1)
	SET_TIMER(PLAYER1,TIMER1)
	SET_TIMER(PLAYER1,TIMER2)
	SET_TIMER(PLAYER1,TIMER3)
	SET_TIMER(PLAYER1,TIMER4)
	ADD_GOLD_TO_PLAYER(PLAYER3,100000)
	SET_PLAYER_MODIFIER(PLAYER_GOOD,Speed,115)
	SET_PLAYER_MODIFIER(PLAYER3,Speed,115)
	SET_PLAYER_MODIFIER(PLAYER_GOOD,Health,125)
	SET_PLAYER_MODIFIER(PLAYER3,Health,125)
ENDIF

IF(PLAYER1,FLAG0 == 1)
    IF(PLAYER1,TIMER1 >= 475)
		NEXT_COMMAND_REUSABLE
		USE_POWER_ON_CREATURE(PLAYER_GOOD,ANY_CREATURE,LEAST_EXP_FIGHTING,PLAYER_GOOD,POWER_SPEED,9,0)
		NEXT_COMMAND_REUSABLE
		SET_TIMER(PLAYER1,TIMER1)
	 ENDIF
ENDIF

IF(PLAYER1,FLAG0 == 1)
    IF(PLAYER1,TIMER2 >= 425)
		NEXT_COMMAND_REUSABLE
		USE_POWER_ON_CREATURE(PLAYER_GOOD,ANY_CREATURE,LEAST_EXP_FIGHTING,PLAYER_GOOD,POWER_PROTECT,9,0)
		NEXT_COMMAND_REUSABLE
		SET_TIMER(PLAYER1,TIMER2)
	 ENDIF
ENDIF

IF(PLAYER1,FLAG0 == 1)
    IF(PLAYER1,TIMER3 >= 400)
		NEXT_COMMAND_REUSABLE
		USE_SPELL_ON_CREATURE(PLAYER1,ANY_CREATURE,ON_ENEMY_GROUND,SPELL_FREEZE,0)
		NEXT_COMMAND_REUSABLE
		SET_TIMER(PLAYER1,TIMER3)
	 ENDIF
ENDIF

IF(PLAYER1,FLAG0 == 1)
    IF(PLAYER1,TIMER4 >= 350)
		NEXT_COMMAND_REUSABLE
		USE_POWER_ON_CREATURE(PLAYER1,ANY_CREATURE,ON_ENEMY_GROUND,PLAYER_GOOD,POWER_CHICKEN,9,0)
		NEXT_COMMAND_REUSABLE
		SET_TIMER(PLAYER1,TIMER4)
	 ENDIF
ENDIF

IF_ACTION_POINT(4,PLAYER1)
    IF_ACTION_POINT(1,PLAYER1)
		ADD_TO_FLAG(PLAYER3,FLAG0,1)
        SET_TIMER(PLAYER3,TIMER1)
    ENDIF
ENDIF

IF_ACTION_POINT(5,PLAYER1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,BRUTES,-7,1)
ENDIF

IF_ACTION_POINT(6,PLAYER1)
    SET_TIMER(PLAYER_GOOD,TIMER1)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,HOLY,-3,1)
	SET_FLAG(PLAYER_GOOD,FLAG0,2)
ENDIF

IF(PLAYER_GOOD,FLAG0 == 2)
    IF(PLAYER_GOOD,TIMER1 >= 5000)
        NEXT_COMMAND_REUSABLE
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,HOLY,-3,1)
        NEXT_COMMAND_REUSABLE
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,FAIRY_GOLD_THIEF,9,1)
        NEXT_COMMAND_REUSABLE
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,FAIRY_SPELL_THIEF,10,1)
        NEXT_COMMAND_REUSABLE
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,GIANT_ATTACKER,9,1)
        NEXT_COMMAND_REUSABLE
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,GIANT_DESTROYER,10,1)
        NEXT_COMMAND_REUSABLE
        SET_TIMER(PLAYER_GOOD,TIMER1)
    ENDIF
ENDIF

IF_ACTION_POINT(7,PLAYER1)
    SET_FLAG(PLAYER_GOOD,FLAG0,3)
	SET_FLAG(PLAYER3,FLAG0,3)
    SET_TIMER(PLAYER_GOOD,TIMER2)
	SET_TIMER(PLAYER3,TIMER2)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,RPG,-2,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SCOUTS,-3,1)
    QUICK_OBJECTIVE(9,"英雄们正在竭尽全力击退你们。真可悲。")
	MAX_CREATURES(PLAYER3,25)
	CREATURE_ENTRANCE_LEVEL(PLAYER3,4)
	ADD_GOLD_TO_PLAYER(PLAYER3,100000)
	MAGIC_AVAILABLE(PLAYER_GOOD,POWER_DISEASE,1,1)
	ROOM_AVAILABLE(ALL_PLAYERS,BRIDGE,1,0)
ENDIF

IF(PLAYER_GOOD,TIMER2 >= 900)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SCOUTS,-2,1)
	QUICK_MESSAGE(10, "让瘟疫降临到你身上！", WITCH)
	USE_POWER_ON_CREATURE(PLAYER1,ANY_CREATURE,ON_ENEMY_GROUND,PLAYER_GOOD,POWER_DISEASE,9,0)
ENDIF

IF(PLAYER_GOOD,TIMER2 >= 1800)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,BRUTES,-2,1)
	USE_POWER_ON_CREATURE(PLAYER1,ANY_CREATURE,ON_ENEMY_GROUND,PLAYER_GOOD,POWER_DISEASE,9,0)
ENDIF

IF(PLAYER_GOOD,TIMER2 >= 3000)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,ELITE,-2,1)
	USE_POWER_ON_CREATURE(PLAYER1,ANY_CREATURE,ON_ENEMY_GROUND,PLAYER_GOOD,POWER_DISEASE,9,0)
ENDIF

IF(PLAYER_GOOD,TIMER2 >= 4000)
	MAGIC_AVAILABLE(PLAYER3,POWER_DISEASE,1,1)
    SET_TIMER(PLAYER_GOOD,TIMER3)
    SET_TIMER(PLAYER_GOOD,TIMER4)
    MAX_CREATURES(PLAYER3,25)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,BRUTES,-7,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SCOUTS,-2,1)
	CREATURE_ENTRANCE_LEVEL(PLAYER3,5)
	USE_POWER_ON_CREATURE(PLAYER1,ANY_CREATURE,ON_ENEMY_GROUND,PLAYER_GOOD,POWER_DISEASE,9,0)
ENDIF

IF(PLAYER_GOOD,TIMER3 >= 8000)
    IF(PLAYER_GOOD,TOTAL_CREATURES <= 8)
        IF(PLAYER_GOOD,WITCH == 1)
			NEXT_COMMAND_REUSABLE
			ADD_PARTY_TO_LEVEL(PLAYER_GOOD,THIEF_GOLD,-2,1)
			NEXT_COMMAND_REUSABLE
			ADD_PARTY_TO_LEVEL(PLAYER_GOOD,THIEF_SPELL,-2,1)
		    NEXT_COMMAND_REUSABLE
			ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SCOUTS,-2,1)
            NEXT_COMMAND_REUSABLE
            ADD_PARTY_TO_LEVEL(PLAYER_GOOD,RPG,-2,1)
            SET_TIMER(PLAYER_GOOD,TIMER3)
        ENDIF
    ENDIF
ENDIF

IF(PLAYER_GOOD,TIMER4 >= 4800)
    IF(PLAYER_GOOD,TOTAL_CREATURES <= 36)
        IF(PLAYER_GOOD,WITCH == 1)
            IF(PLAYER_GOOD,DUNGEON_DESTROYED == 0)
				NEXT_COMMAND_REUSABLE
                ADD_PARTY_TO_LEVEL(PLAYER_GOOD,ASSASSIN,-2,1)
                NEXT_COMMAND_REUSABLE
                ADD_PARTY_TO_LEVEL(PLAYER_GOOD,ELITE,-2,1)
				NEXT_COMMAND_REUSABLE
				ADD_PARTY_TO_LEVEL(PLAYER_GOOD,FAIRY_GOLD_THIEF,9,1)
				NEXT_COMMAND_REUSABLE
				ADD_PARTY_TO_LEVEL(PLAYER_GOOD,FAIRY_SPELL_THIEF,10,1)
				NEXT_COMMAND_REUSABLE
				ADD_PARTY_TO_LEVEL(PLAYER_GOOD,GIANT_ATTACKER,9,1)
				NEXT_COMMAND_REUSABLE
				ADD_PARTY_TO_LEVEL(PLAYER_GOOD,GIANT_DESTROYER,10,1)
                NEXT_COMMAND_REUSABLE
                SET_TIMER(PLAYER_GOOD,TIMER4)
            ENDIF
        ENDIF
    ENDIF
ENDIF

IF(PLAYER_GOOD,FLAG0 == 3)
    IF(PLAYER_GOOD,WITCH == 0)
		QUICK_OBJECTIVE(11,"你成功了！最后的守护者死掉了！现在没有什么能阻止你了！")
        MAGIC_AVAILABLE(PLAYER_GOOD,POWER_SPEED,0,0)
        MAGIC_AVAILABLE(PLAYER_GOOD,POWER_CHICKEN,0,0)
		MAGIC_AVAILABLE(PLAYER_GOOD,POWER_PROTECT,0,0)
        MAGIC_AVAILABLE(PLAYER_GOOD,POWER_DISEASE,0,0)
		MAGIC_AVAILABLE(PLAYER3,POWER_DISEASE,0,0)
		MAGIC_AVAILABLE(PLAYER3,POWER_CHICKEN,0,0)
		MAGIC_AVAILABLE(PLAYER3,POWER_SPEED,0,0)
		MAGIC_AVAILABLE(PLAYER3,POWER_HEAL_CREATURE,0,0)
		MAGIC_AVAILABLE(PLAYER3,POWER_PROTECT,0,0)
        SET_FLAG(PLAYER_GOOD,FLAG0,4)
		ADD_TO_FLAG(PLAYER1,FLAG0,-1)
    ENDIF
ENDIF

IF(PLAYER1,ALL_DUNGEONS_DESTROYED == 1)
    IF(PLAYER_GOOD,FLAG0 == 4)
		IF(PLAYER_GOOD,TOTAL_CREATURES == 0)
			WIN_GAME
			QUICK_MESSAGE(12, "真是无与伦比的力量。我的名字，将成为他们心中的宿敌。", PLAYER1)
		ENDIF
	ENDIF
ENDIF