LEVEL_VERSION(1)

SET_GENERATE_SPEED(600)

COMPUTER_PLAYER(PLAYER3,2)

START_MONEY(ALL_PLAYERS,1000)

MAX_CREATURES(PLAYER1,8)
MAX_CREATURES(PLAYER3,12)

ALLY_PLAYERS(PLAYER1,PLAYER3,1)

QUICK_MESSAGE(0, "蓝色风暴，我服从你的命令。", PLAYER3)

ADD_HEART_HEALTH(PLAYER1,-15000,0)

SET_CREATURE_HEALTH(GIANT,2000)

SET_PLAYER_MODIFIER(PLAYER3,Health,50)

ADD_CREATURE_TO_POOL(TROLL,20)
ADD_CREATURE_TO_POOL(DEMONSPAWN,20)
ADD_CREATURE_TO_POOL(FLY,20)
ADD_CREATURE_TO_POOL(DARK_MISTRESS,1)
ADD_CREATURE_TO_POOL(SORCEROR,20)
ADD_CREATURE_TO_POOL(BUG,10)
ADD_CREATURE_TO_POOL(SPIDER,10)
ADD_CREATURE_TO_POOL(HELL_HOUND,4)

CREATURE_AVAILABLE(ALL_PLAYERS,TROLL,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,DEMONSPAWN,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,FLY,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,BUG,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,DARK_MISTRESS,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,SORCEROR,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,SPIDER,1,0)
CREATURE_AVAILABLE(PLAYER3,HELL_HOUND,1,0)

SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,IMP,5)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,TROLL,5)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,DEMONSPAWN,5)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,FLY,5)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,BUG,5)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,DARK_MISTRESS,5)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,SORCEROR,5)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,SPIDER,5)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,HELL_HOUND,5)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,THIEF,5)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,ARCHER,5)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,DWARFA,5)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,FAIRY,5)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,WIZARD,5)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,MONK,5)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,GIANT,5)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,BARBARIAN,5)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,WITCH,5)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,GHOST,5)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,TENTACLE,5)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,DRAGON,7)
SET_CREATURE_MAX_LEVEL(PLAYER_GOOD,KNIGHT,6)

REM CREATURES FEAR
SET_CREATURE_FEAR_WOUNDED(FLY,35)
SET_CREATURE_FEAR_WOUNDED(BUG,35)
SET_CREATURE_FEAR_WOUNDED(TROLL,35)
SET_CREATURE_FEAR_WOUNDED(DEMONSPAWN,35)
SET_CREATURE_FEAR_WOUNDED(SORCEROR,35)
SET_CREATURE_FEAR_WOUNDED(DARK_MISTRESS,35)
SET_CREATURE_FEAR_WOUNDED(SPIDER,35)
SET_CREATURE_FEAR_WOUNDED(HELL_HOUND,35)
SET_CREATURE_FEAR_WOUNDED(TENTACLE,35)
SET_CREATURE_FEAR_WOUNDED(DRAGON,35)

ROOM_AVAILABLE(ALL_PLAYERS,TREASURE,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,RESEARCH,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,TORTURE,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,TRAINING,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,WORKSHOP,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,GARDEN,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,LAIR,1,1)

DOOR_AVAILABLE(ALL_PLAYERS,WOOD,1,0)
DOOR_AVAILABLE(ALL_PLAYERS,BRACED,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,ALARM,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,POISON_GAS,1,0)

MAGIC_AVAILABLE(PLAYER3,POWER_SIGHT,1,1)
MAGIC_AVAILABLE(PLAYER3,POWER_CALL_TO_ARMS,1,1)
MAGIC_AVAILABLE(PLAYER1,POWER_CALL_TO_ARMS,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HEAL_CREATURE,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_POSSESS,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SLAP,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HAND,1,1)
MAGIC_AVAILABLE(PLAYER1,POWER_IMP,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SPEED,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CONCEAL,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HOLD_AUDIENCE,1,0)

CREATE_PARTY(FIRST_GIANT1)
    ADD_TO_PARTY(FIRST_GIANT1,GIANT,2,250,ATTACK_DUNGEON_HEART,1000)

CREATE_PARTY(FIRST_GIANT2)
    ADD_TO_PARTY(FIRST_GIANT2,GIANT,3,250,ATTACK_ENEMIES,20000)

CREATE_PARTY(FIRST_GIANT3)
    ADD_TO_PARTY(FIRST_GIANT3,GIANT,2,250,ATTACK_ROOMS,10000)

CREATE_PARTY(FAIRY_GUARD4)
    ADD_TO_PARTY(FAIRY_GUARD4,FAIRY,4,150,DEFEND_LOCATION,10000)

CREATE_PARTY(GIANT_GUARD4)
	ADD_TO_PARTY(GIANT_GUARD4,GIANT,4,250,DEFEND_LOCATION,10000)

CREATE_PARTY(GIANT_SABOTEUR)
	    ADD_TO_PARTY(GIANT_SABOTEUR,GIANT,4,250,ATTACK_ROOMS,0)

CREATE_PARTY(GIANT_SNIPER)
	    ADD_TO_PARTY(GIANT_SNIPER,GIANT,4,250,SNIPE_DUNGEON_HEART,0)

CREATE_PARTY(DEFENDERS)
    ADD_TO_PARTY(DEFENDERS,GIANT,4,200,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(DEFENDERS,GIANT,4,200,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(DEFENDERS,ARCHER,5,100,DEFEND_PARTY,0)

CREATE_PARTY(LORD)
    ADD_TO_PARTY(LORD,WIZARD,5,500,STEAL_SPELLS,1500)
    ADD_TO_PARTY(LORD,KNIGHT,6,5000,ATTACK_DUNGEON_HEART,1500)
    ADD_TO_PARTY(LORD,MONK,5,400,STEAL_GOLD,1500)
    ADD_TO_PARTY(LORD,ARCHER,5,300,DEFEND_PARTY,1500)
    ADD_TO_PARTY(LORD,GIANT,5,200,ATTACK_ROOMS,1500)
    ADD_TO_PARTY(LORD,GIANT,5,200,ATTACK_ROOMS,1500)

CREATE_PARTY(TITAN)
    ADD_TO_PARTY(TITAN,GIANT,10,2500,DEFEND_HEART,1000)

IF(PLAYER1,GAME_TURN > 100)
	QUICK_OBJECTIVE(1,"欢迎来到火焰堡。你运气不错，因为当地的守护者玛拉想要巴结你。要知道即使是弱小的守护者，也很少轻易屈服。所以我只能认为你的名声比你更响亮。",PLAYER1)
ENDIF

IF(PLAYER1,GAME_TURN > 300)
	QUICK_INFORMATION(2,"你来自布里特尔登的部分部队已经抵达西南部。",PLAYER3)
ENDIF

IF(PLAYER1,GAME_TURN > 600)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,FIRST_GIANT1,1,1)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,FIRST_GIANT2,1,1)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,FIRST_GIANT3,1,1)
	QUICK_MESSAGE(3, "蓝色风暴，我需要你的帮助！", PLAYER3)
	ADD_HEART_HEALTH(PLAYER3,-22500,0)
ENDIF

IF(PLAYER1,GAME_TURN > 750)
	ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,GIANT,8,1,1,0)
	ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,GIANT,9,1,2,0)
	QUICK_OBJECTIVE(4,"这个王国受到强大巨人的保护，所以要做好迎接一场艰苦冲突的准备。例如，仔细观察守护者玛拉。",PLAYER3)
	REVEAL_MAP_LOCATION(PLAYER1,PLAYER3,21)
ENDIF

IF(PLAYER1,GAME_TURN > 1000)
	QUICK_INFORMATION(5,"夏日之地战争状态：英雄部队在关键地点稳住阵脚并取得胜利。",PLAYER1)
ENDIF

IF(PLAYER1,TIMES_LEVELUP_CREATURE >= 5)
	IF(PLAYER1,ENTRANCE == 18)
		SET_TIMER(PLAYER_GOOD,TIMER0)
		SET_FLAG(PLAYER_GOOD,FLAG0,1)
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,FAIRY_GUARD4,-3,2)
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,FAIRY_GUARD4,3,2)
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,FAIRY_GUARD4,19,2)
	ENDIF
ENDIF

IF(PLAYER1, HELL_HOUND >= 1)
	CREATURE_AVAILABLE(PLAYER1,HELL_HOUND,1,0)
ENDIF

IF(PLAYER3,DUNGEON_DESTROYED == 1)
	QUICK_OBJECTIVE(6,"看来守护者玛拉确实需要保护。好吧，最好让其他人承担责任，而不是你。接管玛拉的地下城吧。通往地下城的路位于您当前位置的北边。",PLAYER1)
	QUICK_INFORMATION(7,"你的怪物们害怕与巨人战斗。当感觉打不过时，他们更有可能逃跑。",PLAYER1)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,GIANT_GUARD4,10,1)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,GIANT,10,1,4,0)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,GIANT_GUARD4,4,1)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,GIANT,5,1,4,0)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,GIANT,6,1,4,0)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,GIANT,7,1,3,0)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,GIANT,12,1,5,0)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,GIANT,11,1,3,0)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,GIANT,13,1,4,0)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,GIANT,14,1,5,0)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,GIANT,14,1,4,0)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,GIANT,17,1,4,0)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,GIANT,18,1,5,0)
ENDIF

IF(PLAYER1,ENTRANCE == 18)
	QUICK_OBJECTIVE(8,"你已接管玛拉的地下城。从这里开始寻找守护火焰堡的英雄和巨人的隐藏堡垒。",PLAYER1)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,GIANT_GUARD4,20,1)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,GIANT_GUARD4,-2,1)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,GIANT_GUARD4,2,1)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,GIANT_GUARD4,13,1)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,GIANT_GUARD4,19,1)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,GIANT_GUARD4,14,1)
	MAX_CREATURES(PLAYER1,10)	
	SET_CREATURE_HEALTH(GIANT,850)
ENDIF

IF(PLAYER1,ENTRANCE == 27)
    MAX_CREATURES(PLAYER1,12)
ENDIF

IF(PLAYER1,GAME_TURN > 15000)
	QUICK_INFORMATION(9,"战士之王和他的军队现在距离你很远。你不会受到他的威胁。",PLAYER1)
ENDIF

IF(PLAYER1,GAME_TURN > 30000)
	QUICK_INFORMATION(10,"来自格罗利代尔的传言是，魔法领主和巫后正在与至高王争论沉睡者苏醒的原因以及如何部署军队来应对这一威胁。这意味着当地领主必须自己做出决定，非常幸运。 ",PLAYER1)
ENDIF

IF(PLAYER1,GAME_TURN > 50000)
	QUICK_INFORMATION(11,"来自外界的更多消息。守护者克罗钮斯与战士之王发生冲突，被击败但成功逃脱。离你的位置更近了：白色部落彻底摧毁了派来对付他们的英雄部队。也许这个派系值得你关注。",PLAYER1)
ENDIF

IF(PLAYER_GOOD,TIMER0 >= 8000)
    SET_TIMER(PLAYER_GOOD,TIMER1)
    SET_TIMER(PLAYER_GOOD,TIMER2)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,GIANT_SABOTEUR,-3,1)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,GIANT_SABOTEUR,3,1)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,GIANT_SNIPER,DRAWFROM(-3,3),1)
ENDIF

IF(PLAYER_GOOD,TIMER1 >= 8000)
    IF(PLAYER_GOOD,FLAG0 == 1)
        NEXT_COMMAND_REUSABLE
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,GIANT_SABOTEUR,-3,1)
		NEXT_COMMAND_REUSABLE
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,GIANT_SABOTEUR,3,1)
		NEXT_COMMAND_REUSABLE
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,GIANT_SNIPER,DRAWFROM(-3,3),1)
		NEXT_COMMAND_REUSABLE
		SET_TIMER(PLAYER_GOOD,TIMER1)
    ENDIF
ENDIF

IF_ACTION_POINT(5,PLAYER1)
	ADD_TO_PARTY(GIANT_SABOTEUR,GIANT,3,400,ATTACK_ROOMS,0)
ENDIF

IF(PLAYER_GOOD,TIMER2 >= 12500)
    IF(PLAYER_GOOD,FLAG0 == 1)
        NEXT_COMMAND_REUSABLE
        ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,FAIRY,3,1,3,100)
        NEXT_COMMAND_REUSABLE
        SET_TIMER(PLAYER_GOOD,TIMER2)
    ENDIF
ENDIF

IF_ACTION_POINT(15,PLAYER1)
    SET_FLAG(PLAYER_GOOD,FLAG0,2)
    SET_TIMER(PLAYER_GOOD,TIMER3)
	QUICK_INFORMATION(12,"你正在接近英雄的堡垒。把它夷为平地吧。",PLAYER3)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,DEFENDERS,-2,1)
ENDIF

IF(PLAYER_GOOD,TIMER3 >= 4000)
    IF(PLAYER_GOOD,FLAG0 == 2)
        IF(PLAYER_GOOD,TOTAL_CREATURES <= 7)
			IF_SLAB_OWNER(72,66,PLAYER_GOOD)
				NEXT_COMMAND_REUSABLE
				ADD_PARTY_TO_LEVEL(PLAYER_GOOD,DEFENDERS,-2,1)
				NEXT_COMMAND_REUSABLE
				SET_TIMER(PLAYER_GOOD,TIMER3)
			ENDIF
		ENDIF
	ENDIF
ENDIF

IF_ACTION_POINT(16,PLAYER1)
    SET_FLAG(PLAYER_GOOD,FLAG0,3)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,LORD,-1,1)
ENDIF

IF_ACTION_POINT(16,PLAYER1)
    IF(PLAYER1,DRAGON >= 1)
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,TITAN,-1,1)
    ENDIF
ENDIF

IF(PLAYER1,HEART_HEALTH <= 2500)
	ALLY_PLAYERS(PLAYER1,PLAYER_GOOD,3)
	ALLY_PLAYERS(PLAYER1,PLAYER3,3)
	LOSE_GAME
ENDIF

IF(PLAYER_GOOD,FLAG0 == 3)
	IF(PLAYER_GOOD,DUNGEON_DESTROYED == 1)
		IF(PLAYER_GOOD,KNIGHT == 0)
			QUICK_OBJECTIVE(13,"我听说惊恐的当地居民已经将你称为“巨人杀手”，这再恰当不过了。",PLAYER3)
			WIN_GAME
		ENDIF
	ENDIF
ENDIF
