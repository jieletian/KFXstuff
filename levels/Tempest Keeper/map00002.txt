LEVEL_VERSION(1)

REM -------------------------------------------------------------------------------------------------------------------------------------------

QUICK_OBJECTIVE(0,"安贝尔加德的幸存者已经逃到了这个王国，他们马上就会把你的越狱情况和当前位置通报给南方的当地英雄。",PLAYER1)

COMPUTER_PLAYER(PLAYER3,2)
SET_PLAYER_COLOR(PLAYER3,WHITE)

ADD_HEART_HEALTH(PLAYER1,-20000,0)

SET_DOOR_CONFIGURATION(WOOD,ManufactureRequired,10000)
SET_TRAP_CONFIGURATION(POISON_GAS,ManufactureRequired,12000)

SET_CREATURE_CONFIGURATION(TUNNELLER,BaseSpeed,80)
SET_CREATURE_CONFIGURATION(TUNNELLER,PAY,0)

SET_CREATURE_STRENGTH(AVATAR,150)
SET_CREATURE_HEALTH(AVATAR,7000)
SET_CREATURE_ARMOUR(AVATAR,150)
SET_CREATURE_PROPERTY(AVATAR,ILLUMINATED,1)
SET_CREATURE_PROPERTY(AVATAR,NO_IMPRISONMENT,1)
SET_CREATURE_CONFIGURATION(AVATAR,BaseSpeed,30)
SET_CREATURE_CONFIGURATION(AVATAR,FearsomeFactor,10)

SET_CREATURE_INSTANCE(AVATAR,2, ARMOUR, 2)
SET_CREATURE_INSTANCE(AVATAR,3, SIGHT, 3)
SET_CREATURE_INSTANCE(AVATAR,4, FREEZE, 4)
SET_CREATURE_INSTANCE(AVATAR,6, SLOW, 7)
SET_CREATURE_INSTANCE(AVATAR,7, HAILSTORM, 8)
SET_CREATURE_INSTANCE(AVATAR,9, WORD_OF_POWER, 10)


START_MONEY(PLAYER1,1000)
START_MONEY(PLAYER3,10000)

MAX_CREATURES(PLAYER1,12)
MAX_CREATURES(PLAYER3,16)

SET_GENERATE_SPEED(650)
ADD_CREATURE_TO_POOL(FLY,10)
ADD_CREATURE_TO_POOL(BUG,10)
ADD_CREATURE_TO_POOL(SPIDER,4)
ADD_CREATURE_TO_POOL(TROLL,10)
ADD_CREATURE_TO_POOL(SORCEROR,10)
ADD_CREATURE_TO_POOL(DEMONSPAWN,10)

ADD_CREATURE_TO_POOL(BARBARIAN,1)
ADD_CREATURE_TO_POOL(DWARFA,2)
ADD_CREATURE_TO_POOL(ARCHER,5)
ADD_CREATURE_TO_POOL(THIEF,5)
ADD_CREATURE_TO_POOL(WIZARD,1)
ADD_CREATURE_TO_POOL(MONK,1)
ADD_CREATURE_TO_POOL(FAIRY,1)
ADD_CREATURE_TO_POOL(WITCH,1)

SET_CREATURE_STRENGTH(AVATAR,250)
SET_CREATURE_HEALTH(AVATAR,8000)
SET_CREATURE_ARMOUR(AVATAR,250)

SET_CREATURE_HEALTH(KNIGHT,1400)
SET_CREATURE_ARMOUR(KNIGHT,50)
SET_CREATURE_STRENGTH(KNIGHT,25)
SET_CREATURE_CONFIGURATION(KNIGHT,BaseSpeed,32)

CREATURE_AVAILABLE(PLAYER1,FLY,1,0)
CREATURE_AVAILABLE(PLAYER1,BUG,1,0)
CREATURE_AVAILABLE(PLAYER1,SPIDER,1,0)
CREATURE_AVAILABLE(PLAYER1,TROLL,0,0)
CREATURE_AVAILABLE(PLAYER1,SORCEROR,0,0)
CREATURE_AVAILABLE(PLAYER1,DEMONSPAWN,0,0)


ROOM_AVAILABLE(ALL_PLAYERS,LAIR,1,1)
ROOM_AVAILABLE(PLAYER1,TREASURE,1,1)
ROOM_AVAILABLE(PLAYER1,TRAINING,1,1)
ROOM_AVAILABLE(PLAYER3,RESEARCH,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,GARDEN,1,1)
ROOM_AVAILABLE(PLAYER3,TORTURE,1,0)
ROOM_AVAILABLE(PLAYER3,BARRACKS,1,0)
ROOM_AVAILABLE(PLAYER3,SCAVENGER,1,0)

MAGIC_AVAILABLE(PLAYER1,POWER_IMP,1,1)
MAGIC_AVAILABLE(PLAYER1,POWER_SPEED,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HAND,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CALL_TO_ARMS,1,0)
MAGIC_AVAILABLE(PLAYER3,POWER_HOLD_AUDIENCE,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CONCEAL,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HEAL_CREATURE,1,0)

DOOR_AVAILABLE(ALL_PLAYERS,WOOD,1,0)
DOOR_AVAILABLE(ALL_PLAYERS,BRACED,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,POISON_GAS,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,ALARM,1,0)

ALLY_PLAYERS(PLAYER3,PLAYER_GOOD,1)
ALLY_PLAYERS(PLAYER1,PLAYER3,2)
SET_COMPUTER_GLOBALS(PLAYER3,90,1,30,1,200,1,5)

SET_CREATURE_TENDENCIES(PLAYER3,FLEE,1)

SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,TUNNELLER,4)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,FLY,4)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,BUG,4)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,SPIDER,4)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,DEMONSPAWN,4)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,IMP,4)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,SORCEROR,4)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,TROLL,4)

SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,THIEF,4)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,ARCHER,4)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,DWARFA,4)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,FAIRY,4)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,WIZARD,4)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,KNIGHT,4)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,MONK,4)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,GIANT,4)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,BARBARIAN,4)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,WITCH,4)



REM -------------------------------------------------------------------------------------------------------------------------------------------

CREATE_PARTY(WARRIOR_KING)
    ADD_TO_PARTY(WARRIOR_KING,FAIRY,9,1500,DEFEND_PARTY,10000)
    ADD_TO_PARTY(WARRIOR_KING,AVATAR,10,50000,ATTACK_DUNGEON_HEART,10000)
    ADD_TO_PARTY(WARRIOR_KING,WIZARD,10,1500,STEAL_SPELLS,10000)
    ADD_TO_PARTY(WARRIOR_KING,WITCH,10,1000,STEAL_SPELLS,10000)
    ADD_TO_PARTY(WARRIOR_KING,SAMURAI,9,1500,ATTACK_ENEMIES,10000)
    ADD_TO_PARTY(WARRIOR_KING,MONK,9,1000,DEFEND_PARTY,10000)
	
CREATE_PARTY(ELITE)
    ADD_TO_PARTY(ELITE,SAMURAI,9,1000,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(ELITE,WIZARD,9,1500,STEAL_SPELLS,0)
    ADD_TO_PARTY(ELITE,BARBARIAN,8,1000,STEAL_GOLD,0)
    ADD_TO_PARTY(ELITE,GIANT,8,1000,ATTACK_ROOMS,0)
    ADD_TO_PARTY(ELITE,ARCHER,9,800,DEFEND_PARTY,0)
    ADD_TO_PARTY(ELITE,SAMURAI,8,1000,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(ELITE,FAIRY,10,1200,DEFEND_PARTY,0)

CREATE_PARTY(HOLY)
	ADD_TO_PARTY(HOLY,MONK,2,100,STEAL_GOLD,500)
	ADD_TO_PARTY(HOLY,MONK,2,100,ATTACK_ENEMIES,500)
	ADD_TO_PARTY(HOLY,MONK,2,100,STEAL_GOLD,500)
	ADD_TO_PARTY(HOLY,FAIRY,3,200,ATTACK_ROOMS,500)
	ADD_TO_PARTY(HOLY,FAIRY,3,200,STEAL_SPELLS,500)
	
CREATE_PARTY(LORD)
    ADD_TO_PARTY(LORD,WIZARD,3,500,STEAL_SPELLS,1500)
    ADD_TO_PARTY(LORD,KNIGHT,4,5000,ATTACK_DUNGEON_HEART,1500)
    ADD_TO_PARTY(LORD,MONK,2,400,STEAL_GOLD,1500)
    ADD_TO_PARTY(LORD,ARCHER,2,300,DEFEND_PARTY,1500)
    ADD_TO_PARTY(LORD,FAIRY,3,200,ATTACK_ROOMS,1500)
    ADD_TO_PARTY(LORD,WITCH,3,300,STEAL_SPELLS,1500)

REM -------------------------------------------------------------------------------------------------------------------------------------------

IF(PLAYER1,GAME_TURN > 1000)
	QUICK_INFORMATION(1,"夏日之地正处于战争状态：英雄部队在关键地点已经稳住阵脚并取得胜利。",PLAYER1)
ENDIF

IF(PLAYER1,TOTAL_CREATURES >= 3)
	QUICK_INFORMATION(2,"到目前为止，你的昆虫仆从为你提供了很好的服务，但这里的英雄比安贝尔加德的更强大，数量也更多。所以你可能想四处寻找吸引更多怪物的方法。有几个地方可能会提起你的兴趣：西北部的法师竞技场、东北部的洞穴和东南的修道院。",PLAYER1)
ENDIF

IF(PLAYER1,DEMONSPAWN >= 1)
	QUICK_INFORMATION(3,"你找到了一些恶鬼和一个额外的入口。找到一种方法将其连接到你的地牢。",PLAYER1)
ENDIF

IF(PLAYER1,SORCEROR >= 1)
	ROOM_AVAILABLE(PLAYER1,RESEARCH,1,1)
	CREATURE_AVAILABLE(PLAYER1,SORCEROR,1,0)
	QUICK_INFORMATION(4,"你发现的这些邪恶的人类渴望为你效力，并为你提供建造图书馆的方法。",PLAYER1)
ENDIF

IF(PLAYER1,WORKSHOP >= 1)
	CREATURE_AVAILABLE(PLAYER1,TROLL,1,0)
	ROOM_AVAILABLE(PLAYER1,BRIDGE,1,0)
	ROOM_AVAILABLE(PLAYER1,WORKSHOP,1,0)
	QUICK_INFORMATION(5,"你占领了一个工厂。精灵很可能会向你走来。",PLAYER1)
ENDIF

IF_SLAB_OWNER(72,4,PLAYER1)
	MAX_CREATURES(PLAYER1,20)
	CREATURE_AVAILABLE(PLAYER1,DEMONSPAWN,1,0)
	QUICK_INFORMATION(6,"太好了，恶鬼将会从深坑中爬起并加入你的战斗。",PLAYER1)
ENDIF


IF(PLAYER1,GAME_TURN > 4500)
	QUICK_INFORMATION(7,"越来越多的沉睡者正在苏醒。最值得注意的守护者包括克罗纽斯、阿斯塔罗斯和詹达佐恩。他们距离格洛里代尔相对较近。这很好；他们的活动应该会提供有用的干扰。",PLAYER1)
ENDIF

IF(PLAYER1,GAME_TURN > 4750)
	QUICK_INFORMATION(8,"守护者克罗纽斯是一位善于应对冲突的守护者。他的好战和破坏性可能很快就会引起人们的注意。",PLAYER1)
ENDIF

IF(PLAYER1,GAME_TURN > 4950)
	QUICK_INFORMATION(9,"守护者阿斯塔罗斯是个狡猾的对手。尽管他距离格洛里代尔和黎明霍尔德很近，但他可能会等待时机再采取行动。",PLAYER1)
ENDIF

IF(PLAYER1,GAME_TURN > 5150)
	QUICK_INFORMATION(10,"守护者詹达佐恩是一位古老而危险的守护者。在所有苏醒的沉睡者中，他是迄今为止最强大的存在。远离他的路径。",PLAYER1)
ENDIF

IF(PLAYER1,GAME_TURN > 8000)
	QUICK_INFORMATION(11,"战士之王正率领一支庞大的军队靠近布里特尔登。他似乎没有注意到你，正准备离开去对付更靠近格洛里代尔的更危险的守护者。",PLAYER1)
ENDIF

IF(PLAYER3,GAME_TURN > 10000)
	IF(PLAYER1,TOTAL_CREATURES >= 8)
		CREATURE_AVAILABLE(PLAYER3,DWARFA,1,0)
		CREATURE_AVAILABLE(PLAYER3,ARCHER,1,0)
		CREATURE_AVAILABLE(PLAYER3,THIEF,1,0)
		CREATURE_AVAILABLE(PLAYER3,BARBARIAN,1,0)
		CREATURE_AVAILABLE(PLAYER3,WIZARD,1,0)
		CREATURE_AVAILABLE(PLAYER3,MONK,1,1)
		CREATURE_AVAILABLE(PLAYER3,FAIRY,1,0)
		CREATURE_AVAILABLE(PLAYER3,WITCH,1,1)
		SET_TIMER(PLAYER3,TIMER0)
		BONUS_LEVEL_TIME(60000,0)
		SET_TIMER(PLAYER_GOOD,TIMER1)
		QUICK_OBJECTIVE(12,"当地的英雄们现在已经知道了你的存在，并准备战斗。虽然你侵犯他们的领土可能会激起他们的反应，但他们更有可能在增援部队到达之前保持防御。在消息传出之前迅速消灭他们，否则战士之王会调动他的军队来对付你。",PLAYER1)
	ENDIF
ENDIF

IF(PLAYER3,TIMER0 >= 2500)
	IF(PLAYER3,DUNGEON_DESTROYED == 0)
		IF(PLAYER3,TUNNELLER < 6)
			IF(PLAYER3,TOTAL_CREATURES < 16)
				NEXT_COMMAND_REUSABLE
				ADD_CREATURE_TO_LEVEL(PLAYER3,TUNNELLER,-3,1,1,0)
				NEXT_COMMAND_REUSABLE
				SET_TIMER(PLAYER3,TIMER0)
			ENDIF
		ENDIF
	ENDIF
ENDIF

IF(PLAYER_GOOD,BATTLES_LOST >= 30)
	IF_ACTION_POINT(1,PLAYER1)
		COMPUTER_DIG_TO_LOCATION(PLAYER3,3,6)
		QUICK_MESSAGE(13, "防守西北方向！", KNIGHT)
	ENDIF
ENDIF

IF(PLAYER_GOOD,BATTLES_LOST >= 30)
	IF_ACTION_POINT(4,PLAYER1)
		COMPUTER_DIG_TO_LOCATION(PLAYER3,5,4)
		QUICK_MESSAGE(14, "英雄入侵！保卫我们的北翼！", KNIGHT)
	ENDIF
ENDIF


IF_ACTION_POINT(2,PLAYER1)
	IF(PLAYER_GOOD,MONK <= 2)
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,HOLY,-2,1)
	ENDIF
ENDIF

IF(PLAYER_GOOD,TOTAL_CREATURES <= 3)
	SET_FLAG(PLAYER_GOOD,FLAG0,1)
ENDIF

IF(PLAYER3,DUNGEON_DESTROYED == 1)
	SET_FLAG(PLAYER_GOOD,FLAG0,2)
ENDIF

IF(PLAYER_GOOD,FLAG0 != 0)
	SET_TIMER(PLAYER_GOOD,TIMER0)
	QUICK_MESSAGE(15, "我将保护我的人民！", KNIGHT)
ENDIF

IF(PLAYER_GOOD,TIMER0 >= 1200)
	ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,LORD,-1,DUNGEON,1,4,400)
	SET_FLAG(PLAYER_GOOD,FLAG1,1)
ENDIF

IF(PLAYER_GOOD,TIMER1 >= 60000)
	ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,ELITE,-1,DUNGEON,1,10,800)
	ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,ELITE,-2,DUNGEON,1,10,800)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,WARRIOR_KING,-3,1)
	SET_TIMER(PLAYER_GOOD,TIMER2)
	QUICK_OBJECTIVE(16,"你没能及时消灭当地的英雄，你的存在已经暴露了。现在，战士之王亲自率领大军到来。你完蛋了。",PLAYER1)
ENDIF

IF(PLAYER_GOOD,TIMER2 >= 1000)
	IF(PLAYER_GOOD,AVATAR >= 1)
		IF(PLAYER_GOOD,TOTAL_CREATURES <= 100)
			NEXT_COMMAND_REUSABLE
			ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,ELITE,-1,DUNGEON,1,10,800)
			NEXT_COMMAND_REUSABLE
			ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,ELITE,-2,DUNGEON,1,10,800)
			NEXT_COMMAND_REUSABLE
			SET_TIMER(PLAYER_GOOD,TIMER2)
			QUICK_MESSAGE(17,"过去我们曾经击败过你。现在我们还能再一次击败你！",AVATAR)
		ENDIF
	ENDIF
ENDIF

IF(PLAYER1,HEART_HEALTH <= 2500)
	ALLY_PLAYERS(PLAYER1,PLAYER_GOOD,3)
	ALLY_PLAYERS(PLAYER1,PLAYER3,3)
	LOSE_GAME
ENDIF

IF(PLAYER_GOOD,FLAG1 == 1)
	IF(PLAYER1,ALL_DUNGEONS_DESTROYED == 1)
		IF(PLAYER_GOOD,TOTAL_CREATURES == 0)
			WIN_GAME
			QUICK_OBJECTIVE(18,"你已成功地消灭了当地英雄。更棒的是，全世界还不知道你已经王者归来。",PLAYER1)
		ENDIF
	ENDIF
ENDIF
