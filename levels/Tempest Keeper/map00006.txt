LEVEL_VERSION(1)

SET_GENERATE_SPEED(750)

QUICK_OBJECTIVE(0,"你正在接近守护者克罗纽斯居住的拾心之地。然而，战士之王和他的军队就在附近。正如之前在布赖特温提到的，克罗纽斯将英雄们引出这个地牢并在空旷的地方杀死他们，因此这里一片荒芜。所以抓住这个机会，占领你的住所，观察目前的情况将如何发展。",PLAYER1)

MAX_CREATURES(PLAYER1,24)

START_MONEY(PLAYER1,5000)

ADD_HEART_HEALTH(PLAYER1,-3000,0)

ALLY_PLAYERS(PLAYER1,PLAYER0,2)

REMOVE_SACRIFICE_RECIPE(TROLL,BILE_DEMON,DARK_MISTRESS)

SET_CREATURE_PROPERTY(KNIGHT,LORD,0)
SET_CREATURE_PROPERTY(AVATAR,NO_IMPRISONMENT,1)
SET_CREATURE_PROPERTY(AVATAR,LORD,0)

SET_CREATURE_STRENGTH(AVATAR,160)
SET_CREATURE_HEALTH(AVATAR,7000)
SET_CREATURE_ARMOUR(AVATAR,150)
SET_CREATURE_PROPERTY(AVATAR,ILLUMINATED,1)
SET_CREATURE_PROPERTY(AVATAR,NO_IMPRISONMENT,1)
SET_CREATURE_CONFIGURATION(AVATAR,BaseSpeed,30)
SET_CREATURE_CONFIGURATION(AVATAR,FearsomeFactor,10)

SET_CREATURE_INSTANCE(AVATAR,2, ARMOUR, 2)
SET_CREATURE_INSTANCE(AVATAR,3, SIGHT, 3)
SET_CREATURE_INSTANCE(AVATAR,4, FREEZE, 4)
SET_CREATURE_INSTANCE(AVATAR,6, SLOW, 7)
SET_CREATURE_INSTANCE(AVATAR,7, HAILSTORM, 8)
SET_CREATURE_INSTANCE(AVATAR,9, WORD_OF_POWER, 10)

SET_GAME_RULE(PayDayGap,20000)

ROOM_AVAILABLE(ALL_PLAYERS,TREASURE,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,RESEARCH,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,TORTURE,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,TRAINING,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,WORKSHOP,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,GARDEN,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,LAIR,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,BARRACKS,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,TEMPLE,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,PRISON,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,GRAVEYARD,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,SCAVENGER,1,0)

ADD_CREATURE_TO_POOL(TROLL,30)
ADD_CREATURE_TO_POOL(DEMONSPAWN,30)
ADD_CREATURE_TO_POOL(FLY,10)
ADD_CREATURE_TO_POOL(DARK_MISTRESS,20)
ADD_CREATURE_TO_POOL(SORCEROR,30)
ADD_CREATURE_TO_POOL(BUG,10)
ADD_CREATURE_TO_POOL(SPIDER,10)
ADD_CREATURE_TO_POOL(TENTACLE,20)
ADD_CREATURE_TO_POOL(DRAGON,20)
ADD_CREATURE_TO_POOL(HELL_HOUND,10)
ADD_CREATURE_TO_POOL(BILE_DEMON,20)
ADD_CREATURE_TO_POOL(ORC,30)
ADD_CREATURE_TO_POOL(VAMPIRE,1)

CREATURE_AVAILABLE(ALL_PLAYERS,TROLL,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,DEMONSPAWN,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,FLY,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,BUG,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,DARK_MISTRESS,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,SORCEROR,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,SPIDER,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,TENTACLE,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,ORC,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,DRAGON,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,BILE_DEMON,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,HELL_HOUND,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,VAMPIRE,1,0)

SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,TROLL,9)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,FLY,9)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,BUG,9)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,DARK_MISTRESS,9)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,SORCEROR,9)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,SPIDER,9)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,TENTACLE,9)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,ORC,9)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,HELL_HOUND,9)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,DRAGON,9)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,BILE_DEMON,9)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,SKELETON,9)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,TENTACLE,9)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,DRAGON,9)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,GHOST,9)
SET_CREATURE_MAX_LEVEL(ALL_PLAYERS,VAMPIRE,9)

DOOR_AVAILABLE(ALL_PLAYERS,WOOD,1,0)
DOOR_AVAILABLE(ALL_PLAYERS,BRACED,1,0)
DOOR_AVAILABLE(ALL_PLAYERS,STEEL,1,0)
DOOR_AVAILABLE(ALL_PLAYERS,MAGIC,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,ALARM,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,POISON_GAS,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,LIGHTNING,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,WORD_OF_POWER,1,0)

MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SIGHT,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CALL_TO_ARMS,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HEAL_CREATURE,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_POSSESS,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SLAP,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HAND,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CAVE_IN,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_IMP,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SPEED,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CONCEAL,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CHICKEN,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_LIGHTNING,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_PROTECT,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HOLD_AUDIENCE,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_OBEY,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_DISEASE,1,0)

CREATE_PARTY(EASY_FIRST)
    ADD_TO_PARTY(EASY_FIRST,THIEF,2,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(EASY_FIRST,THIEF,2,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(EASY_FIRST,ARCHER,2,0,DEFEND_PARTY,0)

CREATE_PARTY(EASY_SCOUTS)
    ADD_TO_PARTY(EASY_SCOUTS,THIEF,3,0,ATTACK_ROOMS,0)
    ADD_TO_PARTY(EASY_SCOUTS,THIEF,3,0,STEAL_GOLD,0)
    ADD_TO_PARTY(EASY_SCOUTS,ARCHER,3,0,DEFEND_PARTY,0)
    ADD_TO_PARTY(EASY_SCOUTS,ARCHER,3,0,DEFEND_PARTY,0)

CREATE_PARTY(EASY_BRUTES)
    ADD_TO_PARTY(EASY_BRUTES,BARBARIAN,3,0,ATTACK_ROOMS,0)
	ADD_TO_PARTY(EASY_BRUTES,DWARFA,3,0,ATTACK_ENEMIES,0)
	
CREATE_PARTY(EASY_GIANT)
	ADD_TO_PARTY(EASY_GIANT,GIANT,2,0,ATTACK_ROOMS,0)
	
CREATE_PARTY(EASY_FAIRY)
	ADD_TO_PARTY(EASY_FAIRY,FAIRY,3,0,STEAL_SPELLS,0)

CREATE_PARTY(EASY_MAGIC)
    ADD_TO_PARTY(EASY_MAGIC,MONK,3,0,DEFEND_PARTY,0)
    ADD_TO_PARTY(EASY_MAGIC,WIZARD,4,0,STEAL_SPELLS,0)
    ADD_TO_PARTY(EASY_MAGIC,WITCH,4,0,STEAL_SPELLS,0)

CREATE_PARTY(EASY_LORD)
    ADD_TO_PARTY(EASY_LORD,WITCH,3,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(EASY_LORD,KNIGHT,4,20000,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(EASY_LORD,ARCHER,3,0,DEFEND_PARTY,0)

CREATE_PARTY(MEDIUM_BRUTES)
    ADD_TO_PARTY(MEDIUM_BRUTES,BARBARIAN,6,0,ATTACK_ROOMS,0)
    ADD_TO_PARTY(MEDIUM_BRUTES,BARBARIAN,6,0,ATTACK_ROOMS,0)
	
		
CREATE_PARTY(MEDIUM_GIANT)
	ADD_TO_PARTY(MEDIUM_GIANT,GIANT,5,0,ATTACK_ROOMS,0)
	
CREATE_PARTY(MEDIUM_FAIRY)
	ADD_TO_PARTY(MEDIUM_FAIRY,FAIRY,5,0,STEAL_SPELLS,0)

CREATE_PARTY(MEDIUM_MAGIC)
    ADD_TO_PARTY(MEDIUM_MAGIC,MONK,5,0,DEFEND_PARTY,0)
    ADD_TO_PARTY(MEDIUM_MAGIC,WIZARD,6,0,STEAL_SPELLS,0)
    ADD_TO_PARTY(MEDIUM_MAGIC,MONK,5,0,DEFEND_PARTY,0)
    ADD_TO_PARTY(MEDIUM_MAGIC,WITCH,5,0,STEAL_SPELLS,0)

CREATE_PARTY(MEDIUM_SAMURAI)
    ADD_TO_PARTY(MEDIUM_SAMURAI,SAMURAI,4,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(MEDIUM_SAMURAI,SAMURAI,6,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(MEDIUM_SAMURAI,SAMURAI,5,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(MEDIUM_SAMURAI,SAMURAI,4,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(MEDIUM_SAMURAI,SAMURAI,4,0,ATTACK_ENEMIES,0)

CREATE_PARTY(MEDIUM_RPG)
    ADD_TO_PARTY(MEDIUM_RPG,MONK,5,0,DEFEND_PARTY,0)
    ADD_TO_PARTY(MEDIUM_RPG,WIZARD,6,0,STEAL_SPELLS,0)
    ADD_TO_PARTY(MEDIUM_RPG,BARBARIAN,5,0,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(MEDIUM_RPG,ARCHER,6,0,DEFEND_PARTY,0)
    ADD_TO_PARTY(MEDIUM_RPG,SAMURAI,5,0,ATTACK_ROOMS,0)

CREATE_PARTY(MEDIUM_LORD)
    ADD_TO_PARTY(MEDIUM_LORD,ARCHER,5,0,DEFEND_PARTY,0)
    ADD_TO_PARTY(MEDIUM_LORD,KNIGHT,6,30000,ATTACK_DUNGEON_HEART,0)
    ADD_TO_PARTY(MEDIUM_LORD,BARBARIAN,5,0,ATTACK_ROOMS,0)
    ADD_TO_PARTY(MEDIUM_LORD,WITCH,6,0,STEAL_SPELLS,0)

CREATE_PARTY(ELITE_SCOUTS)
    ADD_TO_PARTY(ELITE_SCOUTS,DWARFA,8,1400,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(ELITE_SCOUTS,ARCHER,8,1300,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(ELITE_SCOUTS,DWARFA,9,1450,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(ELITE_SCOUTS,THIEF,8,1300,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(ELITE_SCOUTS,THIEF,8,1300,DEFEND_PARTY,0)
    ADD_TO_PARTY(ELITE_SCOUTS,ARCHER,8,1300,DEFEND_PARTY,0)

CREATE_PARTY(HARD_BRUTES)
    ADD_TO_PARTY(HARD_BRUTES,BARBARIAN,8,1500,ATTACK_ROOMS,0)
    ADD_TO_PARTY(HARD_BRUTES,BARBARIAN,8,1500,ATTACK_ROOMS,0)

CREATE_PARTY(HARD_MAGIC)
    ADD_TO_PARTY(HARD_MAGIC,MONK,8,1500,DEFEND_PARTY,0)
    ADD_TO_PARTY(HARD_MAGIC,WIZARD,9,1800,STEAL_SPELLS,0)
    ADD_TO_PARTY(HARD_MAGIC,MONK,7,1400,DEFEND_PARTY,0)
    ADD_TO_PARTY(HARD_MAGIC,WITCH,8,1400,STEAL_SPELLS,0)
    ADD_TO_PARTY(HARD_MAGIC,MONK,7,1300,DEFEND_PARTY,0)

CREATE_PARTY(FAKE_END_BOSS)
    ADD_TO_PARTY(FAKE_END_BOSS,BARBARIAN,8,1800,ATTACK_ENEMIES,1500)
    ADD_TO_PARTY(FAKE_END_BOSS,KNIGHT,10,35000,ATTACK_DUNGEON_HEART,1500)
    ADD_TO_PARTY(FAKE_END_BOSS,WIZARD,8,1500,STEAL_SPELLS,1500)
    ADD_TO_PARTY(FAKE_END_BOSS,SAMURAI,9,2000,ATTACK_ENEMIES,1500)
    ADD_TO_PARTY(FAKE_END_BOSS,GIANT,7,1500,ATTACK_ROOMS,1500)

CREATE_PARTY(SUPPORT)
    ADD_TO_PARTY(SUPPORT,ARCHER,9,1000,DEFEND_PARTY,0)
    ADD_TO_PARTY(SUPPORT,BARBARIAN,9,1500,ATTACK_ENEMIES,0)
    ADD_TO_PARTY(SUPPORT,ARCHER,8,800,DEFEND_PARTY,0)
    ADD_TO_PARTY(SUPPORT,BARBARIAN,8,1000,ATTACK_ENEMIES,0)

CREATE_PARTY(WARRIOR_KING)
    ADD_TO_PARTY(WARRIOR_KING,KNIGHT,9,0,DEFEND_PARTY,1500)
    ADD_TO_PARTY(WARRIOR_KING,AVATAR,10,50000,ATTACK_DUNGEON_HEART,1500)
    ADD_TO_PARTY(WARRIOR_KING,WIZARD,10,0,STEAL_SPELLS,1500)
    ADD_TO_PARTY(WARRIOR_KING,WITCH,10,0,STEAL_SPELLS,1500)
    ADD_TO_PARTY(WARRIOR_KING,SAMURAI,9,0,ATTACK_ENEMIES,1500)
    ADD_TO_PARTY(WARRIOR_KING,MONK,9,0,DEFEND_PARTY,1500)

REM 1 North East 2 South East 3 South West 4 West 5 North West

IF(PLAYER1,GAME_TURN >= 1000)
	QUICK_INFORMATION(1,"夏日之地的战争状态：随着每一天的过去，英雄的队伍变得越来越薄弱，他们的决心受到了考验。",PLAYER1)
ENDIF


IF(PLAYER1,GAME_TURN >= 3500)
	QUICK_INFORMATION(2,"注意！战士之王和他的大军正在向这个王国进军！赶紧做好防御准备！",PLAYER1)
ENDIF

IF(PLAYER1,GAME_TURN >= 8000)
	QUICK_INFORMATION(3,"鉴于夏日之地的混乱局势，英雄们将不再试图按照至高王的命令囚禁任何沉睡者。你出乎意料地突然摧毁了白色部落，这似乎是导致这一事件的直接原因，也是战士之王决定向你进军的原因。愚蠢的人，他们无法阻止即将到来的暴风雨。",PLAYER1)
ENDIF

IF(PLAYER1,GAME_TURN >= 12500)
	IF(PLAYER1,TOTAL_AREA >= 550)
		SET_TIMER(PLAYER1,TIMER0)
		QUICK_INFORMATION(4,"第一波攻势很快就会袭来。",PLAYER1)
	ENDIF
ENDIF

IF(PLAYER1,GAME_TURN >= 15000)
	IF(PLAYER1,PRISON > 0)
		QUICK_INFORMATION(5,"如果你想俘获英雄并让他们加入您的阵营，请仔细考虑。众所周知，贵族随身携带大量黄金。",PLAYER1)
	ENDIF
ENDIF

REM TUNNELLERS

IF(PLAYER1,TIMER0 >= 500)		
	ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,EASY_FIRST,-5,DUNGEON,0,2,0)
ENDIF

IF(PLAYER1,TIMER0 >= 900)
    ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,EASY_FIRST,-2,DUNGEON,0,2,0)
ENDIF

IF(PLAYER1,TIMER0 >= 1400)
    ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,EASY_FIRST,-3,DUNGEON,0,2,0)
ENDIF

IF(PLAYER1,TIMER0 >= 2000)
    ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,EASY_FIRST,-1,DUNGEON,0,3,0)
ENDIF

IF(PLAYER1,TIMER0 >= 2500)
    ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,EASY_FIRST,-4,DUNGEON,0,3,0)
    ADD_TO_FLAG(PLAYER1,FLAG0,1)
	SET_FLAG(PLAYER_GOOD,FLAG0,1)
ENDIF

REM EASY WAVE

IF(PLAYER_GOOD,FLAG0 == 1)
    IF(PLAYER_GOOD,TOTAL_CREATURES < 5)
        SET_TIMER(PLAYER1,TIMER0)
        SET_TIMER(PLAYER1,TIMER1)
        SET_TIMER(PLAYER1,TIMER2)
        SET_TIMER(PLAYER1,TIMER3)
        SET_TIMER(PLAYER1,TIMER4)
        SET_TIMER(PLAYER1,TIMER5)
    ENDIF
ENDIF

IF(PLAYER_GOOD,FLAG0 == 1)
    IF(PLAYER1,TIMER1 > 2150)
        NEXT_COMMAND_REUSABLE
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,EASY_SCOUTS,-5,1)
        NEXT_COMMAND_REUSABLE
        SET_TIMER(PLAYER1,TIMER1)
    ENDIF
ENDIF

IF(PLAYER_GOOD,FLAG0 == 1)
    IF(PLAYER1,TIMER2 > 2850)
        NEXT_COMMAND_REUSABLE
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,EASY_BRUTES,-4,1)
        NEXT_COMMAND_REUSABLE
        SET_TIMER(PLAYER1,TIMER2)
    ENDIF
ENDIF

IF(PLAYER_GOOD,FLAG0 == 1)
    IF(PLAYER1,TIMER3 > 3750)
        NEXT_COMMAND_REUSABLE
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,EASY_MAGIC,-1,1)
        NEXT_COMMAND_REUSABLE
        SET_TIMER(PLAYER1,TIMER3)
    ENDIF
ENDIF

IF(PLAYER_GOOD,FLAG0 == 1)
    IF(PLAYER1,TIMER4 > 2350)
        NEXT_COMMAND_REUSABLE
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,EASY_SCOUTS,-2,1)
        NEXT_COMMAND_REUSABLE
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,EASY_FAIRY,1,1)
        NEXT_COMMAND_REUSABLE
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD,EASY_GIANT,1,1)
        NEXT_COMMAND_REUSABLE
        SET_TIMER(PLAYER1,TIMER4)
    ENDIF
ENDIF

IF(PLAYER_GOOD,FLAG0 == 1)
    IF(PLAYER1,TIMER5 > 6000)
        NEXT_COMMAND_REUSABLE
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,EASY_LORD,-3,1)
        NEXT_COMMAND_REUSABLE
        SET_TIMER(PLAYER1,TIMER5)
    ENDIF
ENDIF

IF(PLAYER1,TIMER0 >= 18500)
	ADD_TO_FLAG(PLAYER_GOOD,FLAG0,1)
ENDIF

IF(PLAYER1,TIMER0 >= 18500)
	IF(PLAYER_GOOD,FLAG0 == 2)
		IF(PLAYER_GOOD,TOTAL_CREATURES <= 2)
			QUICK_OBJECTIVE(6,"你已经击退了第一波攻击。然而，英雄们正在重整旗鼓。",PLAYER1)
			CREATURE_ENTRANCE_LEVEL (PLAYER1,2)
			SET_GENERATE_SPEED(650)
			ADD_TO_FLAG(PLAYER_GOOD,FLAG0,1)
			ADD_GOLD_TO_PLAYER(PLAYER1,5000)
		ENDIF
	ENDIF
ENDIF

REM MEDIUM WAVE

IF(PLAYER_GOOD,FLAG0 == 3)
    SET_TIMER(PLAYER2,TIMER0)
    SET_TIMER(PLAYER2,TIMER1)
    SET_TIMER(PLAYER2,TIMER2)
    SET_TIMER(PLAYER2,TIMER3)
    SET_TIMER(PLAYER2,TIMER4)
    SET_TIMER(PLAYER2,TIMER5)
ENDIF


IF(PLAYER_GOOD,FLAG0 == 3)
    IF(PLAYER2,TIMER1 > 2400)
        NEXT_COMMAND_REUSABLE
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,MEDIUM_BRUTES,-1,1)
        NEXT_COMMAND_REUSABLE
        SET_TIMER(PLAYER2,TIMER1)
    ENDIF
ENDIF

IF(PLAYER_GOOD,FLAG0 == 3)
    IF(PLAYER2,TIMER0 >= 4500)
        IF(PLAYER2,TIMER2 > 3000)
            NEXT_COMMAND_REUSABLE
            ADD_PARTY_TO_LEVEL(PLAYER_GOOD,MEDIUM_RPG,-3,1)
            NEXT_COMMAND_REUSABLE
            SET_TIMER(PLAYER2,TIMER2)
        ENDIF
    ENDIF
ENDIF

IF(PLAYER_GOOD,FLAG0 == 3)
    IF(PLAYER2,TIMER0 >= 6500)
        IF(PLAYER2,TIMER3 > 3000)
            NEXT_COMMAND_REUSABLE
            ADD_PARTY_TO_LEVEL(PLAYER_GOOD,MEDIUM_MAGIC,-5,1)
            NEXT_COMMAND_REUSABLE
            ADD_PARTY_TO_LEVEL(PLAYER_GOOD,MEDIUM_FAIRY,1,1)
            NEXT_COMMAND_REUSABLE
			ADD_PARTY_TO_LEVEL(PLAYER_GOOD,MEDIUM_GIANT,1,1)
            NEXT_COMMAND_REUSABLE
            SET_TIMER(PLAYER2,TIMER3)
        ENDIF
    ENDIF
ENDIF

IF(PLAYER_GOOD,FLAG0 == 3)
    IF(PLAYER2,TIMER0 >= 7500)
        IF(PLAYER2,TIMER4 > 4000)
            NEXT_COMMAND_REUSABLE
            ADD_PARTY_TO_LEVEL(PLAYER_GOOD,MEDIUM_SAMURAI,-2,1)
            NEXT_COMMAND_REUSABLE
            SET_TIMER(PLAYER2,TIMER4)
        ENDIF
    ENDIF
ENDIF

IF(PLAYER_GOOD,FLAG0 == 3)
    IF(PLAYER2,TIMER0 >= 9000)
        IF(PLAYER2,TIMER5 > 6500)
            NEXT_COMMAND_REUSABLE
            ADD_PARTY_TO_LEVEL(PLAYER_GOOD,MEDIUM_LORD,-4,1)
            NEXT_COMMAND_REUSABLE
            SET_TIMER(PLAYER2,TIMER5)
        ENDIF
    ENDIF
ENDIF

IF(PLAYER2,TIMER0 >= 23500)
	ADD_TO_FLAG(PLAYER_GOOD,FLAG0,1)
	CREATURE_ENTRANCE_LEVEL (PLAYER1,3)
	SET_GENERATE_SPEED(550)
	ROOM_AVAILABLE(ALL_PLAYERS,BRIDGE,1,0)
ENDIF

IF(PLAYER1,TIMER0 >= 18500)
	IF(PLAYER_GOOD,FLAG0 == 4)
		IF(PLAYER_GOOD,TOTAL_CREATURES <= 2)
			QUICK_OBJECTIVE(7,"第二波进攻已被击退。剩下的只有他们的精锐战士。",PLAYER1)
			ADD_TO_FLAG(PLAYER_GOOD,FLAG0,1)
			ADD_GOLD_TO_PLAYER(PLAYER0,10000)
		ENDIF
	ENDIF
ENDIF

IF(PLAYER_GOOD,FLAG0 == 5)
    SET_TIMER(PLAYER3,TIMER0)
ENDIF

REM ELITE WAVE

IF(PLAYER3,TIMER0 >= 3400)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,ELITE_SCOUTS,-5,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,ELITE_SCOUTS,-2,1)
ENDIF

IF(PLAYER3,TIMER0 >= 4200)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,HARD_BRUTES,-4,1)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,HARD_MAGIC,-1,1)
	SET_CREATURE_PROPERTY(KNIGHT,LORD,1)
ENDIF

IF(PLAYER3,TIMER0 >= 5450)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,FAKE_END_BOSS,-3,1)
	QUICK_OBJECTIVE(8,"英雄军几乎全军覆没，只剩下战士之王和他的扈从。",PLAYER1)
ENDIF

IF(PLAYER3,TIMER0 >= 6600)
    IF(PLAYER_GOOD,TOTAL_CREATURES == 0)
        SET_TIMER(PLAYER_GOOD,TIMER0)
		SET_CREATURE_PROPERTY(KNIGHT,LORD,0)
		QUICK_OBJECTIVE(9,"战士之王不在这些英雄之中。他在哪里？",PLAYER1)
    ENDIF
ENDIF

IF(PLAYER_GOOD,TIMER0 >= 1500)
    ADD_PARTY_TO_LEVEL(PLAYER_GOOD,WARRIOR_KING,2,1)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,ELITE_SCOUTS,2,1)
    ADD_TO_FLAG(PLAYER_GOOD,FLAG0,1)
    SET_TIMER(PLAYER_GOOD,TIMER1)
	QUICK_OBJECTIVE(10,"他就在那儿！以前所未有的方式战斗吧！",2)
	QUICK_MESSAGE(11,"守护者，你的胜利就到此为止了！",AVATAR)
ENDIF

IF(PLAYER_GOOD,AVATAR == 1)
    IF(PLAYER_GOOD,TIMER1 >= 4250)
        NEXT_COMMAND_REUSABLE
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,ELITE_SCOUTS,2,1)
        NEXT_COMMAND_REUSABLE
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,SUPPORT,2,1)
        NEXT_COMMAND_REUSABLE
        SET_TIMER(PLAYER_GOOD,TIMER1)
    ENDIF
ENDIF


IF(PLAYER_GOOD,FLAG0 == 6)
    IF(PLAYER_GOOD,TOTAL_CREATURES == 0)
		QUICK_OBJECTIVE(12,"干得好！通过消灭这支英雄军队和他们的一名守卫，你大大削弱了英雄们的实力。",2)
        WIN_GAME
    ENDIF
ENDIF
