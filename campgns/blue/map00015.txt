
LEVEL_VERSION(1)

SET_GENERATE_SPEED(500)

REM Computer Player stuff
COMPUTER_PLAYER(PLAYER0,7)
COMPUTER_PLAYER(PLAYER2,6)

START_MONEY(PLAYER0,15000)
START_MONEY(PLAYER1,1500)
START_MONEY(PLAYER2,15000)

MAX_CREATURES(PLAYER0,40)
MAX_CREATURES(PLAYER1,25)
MAX_CREATURES(PLAYER2,35)

ALLY_PLAYERS(PLAYER0,PLAYER2,1)

ADD_CREATURE_TO_POOL(TROLL,20)
ADD_CREATURE_TO_POOL(SORCEROR,25)
ADD_CREATURE_TO_POOL(DARK_MISTRESS,15)
ADD_CREATURE_TO_POOL(DRAGON,17)
ADD_CREATURE_TO_POOL(HELL_HOUND,15)
ADD_CREATURE_TO_POOL(VAMPIRE,3)
ADD_CREATURE_TO_POOL(TENTACLE,4)
ADD_CREATURE_TO_POOL(ORC,20)

REM Creature availability
CREATURE_AVAILABLE(ALL_PLAYERS,SORCEROR,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,DARK_MISTRESS,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,DRAGON,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,HELL_HOUND,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,TROLL,1,0)
CREATURE_AVAILABLE(PLAYER0,VAMPIRE,1,0)
CREATURE_AVAILABLE(PLAYER0,ORC,1,0)
CREATURE_AVAILABLE(PLAYER1,TENTACLE,1,2)
CREATURE_AVAILABLE(PLAYER2,TENTACLE,1,2)

REM Room availability
ROOM_AVAILABLE(ALL_PLAYERS,TREASURE,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,LAIR,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,GARDEN,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,TRAINING,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,RESEARCH,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,WORKSHOP,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,BARRACKS,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,BRIDGE,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,PRISON,2,0)
ROOM_AVAILABLE(ALL_PLAYERS,TORTURE,1,0)
ROOM_AVAILABLE(PLAYER0,SCAVENGER,1,0)
ROOM_AVAILABLE(PLAYER0,TEMPLE,1,0)
ROOM_AVAILABLE(PLAYER2,TEMPLE,1,0)
ROOM_AVAILABLE(ALL_PLAYERS,GRAVEYARD,1,0)

REM Spells availability
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HAND,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_IMP,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SLAP,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SIGHT,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CALL_TO_ARMS,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HEAL_CREATURE,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SPEED,1,0)
MAGIC_AVAILABLE(PLAYER0,POWER_LIGHTNING,1,0)
MAGIC_AVAILABLE(PLAYER2,POWER_LIGHTNING,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_PROTECT,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CONCEAL,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_DISEASE,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CHICKEN,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_DESTROY_WALLS,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HOLD_AUDIENCE,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_ARMAGEDDON,1,0)

REM Doors and traps availability
DOOR_AVAILABLE(ALL_PLAYERS,STEEL,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,WORD_OF_POWER,1,0)

CREATE_PARTY(ARCHERS)
	ADD_TO_PARTY(ARCHERS,ARCHER,6,500,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(ARCHERS,ARCHER,6,500,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(ARCHERS,ARCHER,6,500,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(ARCHERS,ARCHER,6,500,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(ARCHERS,ARCHER,6,500,ATTACK_ENEMIES,0)

CREATE_PARTY(DWARVES)
	ADD_TO_PARTY(DWARVES,DWARFA,7,800,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(DWARVES,DWARFA,7,800,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(DWARVES,DWARFA,7,800,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(DWARVES,DWARFA,7,800,ATTACK_ENEMIES,0)

CREATE_PARTY(WITCHES)
	ADD_TO_PARTY(WITCHES,WITCH,8,1800,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(WITCHES,WITCH,8,1800,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(WITCHES,WITCH,8,1800,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(WITCHES,WITCH,8,1800,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(WITCHES,WITCH,8,1800,ATTACK_ENEMIES,0)

CREATE_PARTY(BARBS)
	ADD_TO_PARTY(BARBS,BARBARIAN,5,900,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(BARBS,BARBARIAN,6,900,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(BARBS,BARBARIAN,7,900,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(BARBS,BARBARIAN,8,900,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(BARBS,BARBARIAN,9,900,ATTACK_ENEMIES,0)
	
REM ****************************************************************************

SET_COMPUTER_PROCESS(PLAYER2,"DIG TO GOLD",0,50000,800,7,15)
SET_COMPUTER_PROCESS(PLAYER2,"DIG TO GREEDY GOLD",0,50000,800,7,15)

QUICK_OBJECTIVE(1,"看来这两个对立的守护者在这一领域都发展得很好。更糟糕的是，绿守护者已经与红守护者结盟。英雄和他们的领主也藏在某个地方。这将是对你的邪恶的考验，主人。",PLAYER1)

IF(PLAYER1,GAME_TURN>=21500)
SET_TIMER(PLAYER_GOOD,TIMER5)
  QUICK_INFORMATION(3, "传说这个湖里有一些水怪。", PLAYER1)
ENDIF

IF(PLAYER_GOOD,TIMER5>=5000)
		NEXT_COMMAND_REUSABLE
		ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,TENTACLE,6,1,5,0)
		NEXT_COMMAND_REUSABLE
		ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,TENTACLE,7,1,5,0)
		NEXT_COMMAND_REUSABLE
		ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,TENTACLE,8,1,5,0)
		NEXT_COMMAND_REUSABLE
		ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,TENTACLE,9,1,5,0)
		NEXT_COMMAND_REUSABLE
		ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,TENTACLE,10,1,5,0)
		NEXT_COMMAND_REUSABLE
		ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,TENTACLE,11,1,5,0)
		NEXT_COMMAND_REUSABLE
		ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,TENTACLE,12,1,5,0)
		NEXT_COMMAND_REUSABLE
		ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,TENTACLE,13,1,5,0)
		NEXT_COMMAND_REUSABLE
		ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,TENTACLE,14,1,5,0)
		NEXT_COMMAND_REUSABLE
		ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,TENTACLE,15,1,5,0)
		NEXT_COMMAND_REUSABLE
		SET_TIMER(PLAYER_GOOD,TIMER5)
ENDIF

IF(PLAYER1,REWARDED[HORNY] >= 1)
  QUICK_INFORMATION(2, "你只能从你的神殿召唤一个镰刀怪。", PLAYER1)
  REMOVE_SACRIFICE_RECIPE(TROLL,BILE_DEMON,DARK_MISTRESS)
ENDIF

IF(PLAYER1,SKELETONS_RAISED >= 10)
	SET_GAME_RULE(PrisonSkeletonChance,0)
	QUICK_INFORMATION(11, "你不能从监狱里得到更多的骷髅了。", PLAYER1)
ENDIF

IF(PLAYER1,GHOSTS_RAISED >= 10)
	SET_GAME_RULE(GhostConvertChance,0)
	QUICK_INFORMATION(12, "你不能从刑讯室里得到更多的鬼魂。", PLAYER1)
ENDIF

IF(PLAYER1,CREATURES_CONVERTED >= 10)
    SET_GAME_RULE(TortureConvertChance,0)
	SET_GAME_RULE(TortureDeathChance,100)
	QUICK_INFORMATION(13, "你不能从拷问室转换更多生物。", PLAYER1)
ENDIF

IF(PLAYER1,VAMPIRES_RAISED >= 3)
    SET_GAME_RULE(BodiesForVampire,255)
	QUICK_INFORMATION(14, "你无法从墓地获得更多的吸血鬼。", PLAYER1)
ENDIF

IF_ACTION_POINT(16,PLAYER1)
	ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,BILE_DEMON,16,5,6,0)
ENDIF

IF(PLAYER0,FLAG0 == 0)
	IF_AVAILABLE(PLAYER0,POWER_DESTROY_WALLS >= 1)
		REM "One of your opponents has developed a spell that can break through fortified walls. Typical. Be on your guard."
		DISPLAY_INFORMATION(118,ALL_PLAYERS)
		SET_FLAG(PLAYER0,FLAG0,1)
	ENDIF
	IF_AVAILABLE(PLAYER2,POWER_DESTROY_WALLS >= 1)
		DISPLAY_INFORMATION(118,ALL_PLAYERS)
		SET_FLAG(PLAYER0,FLAG0,1)
	ENDIF
ENDIF

IF(PLAYER0,PRISON >= 1)
	SET_CREATURE_TENDENCIES(PLAYER0,IMPRISON,1)
ENDIF

IF(PLAYER2,PRISON >= 1)
	SET_CREATURE_TENDENCIES(PLAYER2,IMPRISON,1)
ENDIF

REM Action points AP2 and AP3 are exclusive - add ARCHERS and BARBS only when first of those is reached

IF_ACTION_POINT(2,PLAYER1)
	SET_FLAG(PLAYER_GOOD,FLAG5,1)
ENDIF

IF_ACTION_POINT(3,PLAYER1)
	SET_FLAG(PLAYER_GOOD,FLAG5,1)
ENDIF

IF(PLAYER_GOOD,FLAG5 == 1)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,ARCHERS,2,1)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,BARBS,2,1)
ENDIF

IF_ACTION_POINT(1,PLAYER1)
	SET_TIMER(PLAYER_GOOD,TIMER0)
ENDIF

IF(PLAYER_GOOD,TIMER0 >= 500)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,DWARVES,4,1)
ENDIF

IF(PLAYER_GOOD,TIMER0 >= 1000)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,WITCHES,5,1)
ENDIF


IF(PLAYER0,DUNGEON_DESTROYED == 1)
	IF(PLAYER2,DUNGEON_DESTROYED == 1)
		IF_CONTROLS(PLAYER_GOOD,TOTAL_CREATURES <= 0)
			WIN_GAME
		ENDIF
	ENDIF
ENDIF
