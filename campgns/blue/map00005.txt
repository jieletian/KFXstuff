LEVEL_VERSION(1)

SET_GENERATE_SPEED(500)

COMPUTER_PLAYER(PLAYER0,7)

START_MONEY(PLAYER1,3000)
START_MONEY(PLAYER0,5000)

MAX_CREATURES(PLAYER1,18)
MAX_CREATURES(PLAYER0,20)

ADD_CREATURE_TO_POOL(BUG,3)
ADD_CREATURE_TO_POOL(FLY,2)
ADD_CREATURE_TO_POOL(SPIDER,12)
ADD_CREATURE_TO_POOL(TROLL,8)
ADD_CREATURE_TO_POOL(DEMONSPAWN,12)
ADD_CREATURE_TO_POOL(SORCEROR,6)

SET_CREATURE_MAX_LEVEL(PLAYER1,DEMONSPAWN,10)

CREATURE_AVAILABLE(ALL_PLAYERS,TROLL,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,DEMONSPAWN,1,0)
CREATURE_AVAILABLE(PLAYER1,FLY,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,SORCEROR,1,0)
CREATURE_AVAILABLE(PLAYER1,BUG,1,0)
CREATURE_AVAILABLE(ALL_PLAYERS,SPIDER,1,0)
CREATURE_AVAILABLE(PLAYER0,BILE_DEMON,1,0)
	
REM	Room availability
ROOM_AVAILABLE(ALL_PLAYERS,TREASURE,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,LAIR,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,GARDEN,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,TRAINING,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,RESEARCH,1,1)
ROOM_AVAILABLE(ALL_PLAYERS,WORKSHOP,1,0)
ROOM_AVAILABLE(PLAYER0,GUARD_POST,1,0)
ROOM_AVAILABLE(PLAYER1,BRIDGE,1,0)

REM	Spells availability
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HAND,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_IMP,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SLAP,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SIGHT,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_CALL_TO_ARMS,1,0)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HEAL_CREATURE,1,0)
MAGIC_AVAILABLE(PLAYER0,POWER_SPEED,1,1)

REM	Doors and traps availability
DOOR_AVAILABLE(ALL_PLAYERS,WOOD,1,0)
TRAP_AVAILABLE(ALL_PLAYERS,POISON_GAS,1,0)

CREATE_PARTY(ONE)
	ADD_TO_PARTY(ONE,DWARFA,3,100,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(ONE,ARCHER,3,100,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(ONE,DWARFA,3,100,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(ONE,ARCHER,3,100,ATTACK_ENEMIES,0)

CREATE_PARTY(TWO)
	ADD_TO_PARTY(TWO,FAIRY,4,200,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(TWO,FAIRY,4,200,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(TWO,BARBARIAN,3,200,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(TWO,BARBARIAN,3,200,ATTACK_ENEMIES,0)

CREATE_PARTY(THREE)
	ADD_TO_PARTY(THREE,GIANT,3,300,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(THREE,GIANT,3,300,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(THREE,BARBARIAN,4,300,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(THREE,BARBARIAN,4,300,ATTACK_ENEMIES,0)
	ADD_TO_PARTY(THREE,ARCHER,4,300,ATTACK_ENEMIES,0)

REM 文本进行了粗略翻译，原版为英文

QUICK_OBJECTIVE(1,"这一次，你将扮演蓝色守护者。你在魔法的力量下复活，并要向过去打败你的守护者复仇。在这个王国中找到你的第一个对手并打败他。",PLAYER1)

IF(PLAYER1,PRISON > 0)
	QUICK_INFORMATION(2,"干得好 你们夺回了监狱。但是敌人的守护者已经救出了里面的蜘蛛和胖魔。",PLAYER1)
	ROOM_AVAILABLE(PLAYER1,PRISON,1,1)
ENDIF

IF(PLAYER1,SKELETONS_RAISED >= 8)
	SET_GAME_RULE(PrisonSkeletonChance,0)
	QUICK_INFORMATION(11, "你不能从监狱里得到更多的骷髅了。", PLAYER1)
ENDIF

IF(PLAYER0,PRISON > 0)
	SET_CREATURE_TENDENCIES(PLAYER0,IMPRISON,1)
	ROOM_AVAILABLE(PLAYER0,PRISON,1,1)
ENDIF

IF(PLAYER1,GAME_TURN>=10000)
	ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,ONE,-1,DUNGEON,1,1,100)
	QUICK_INFORMATION(3,"我注意到英雄们正派人入侵你的地下城。等等，这里原本有英雄吗？他们是什么时候来到这里的？",PLAYER1)
ENDIF

IF(PLAYER1,GAME_TURN>=13000)
	ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,ONE,-2,DUNGEON,1,1,100)
ENDIF

IF(PLAYER1,GAME_TURN>=18000)
	ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,TWO,-1,DUNGEON,1,2,100)
ENDIF

IF(PLAYER1,GAME_TURN>=21000)
	ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,TWO,-2,DUNGEON,1,2,100)
ENDIF

IF(PLAYER1,GAME_TURN>=31000)
	ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,THREE,-1,DUNGEON,1,4,100)
ENDIF

IF(PLAYER1,GAME_TURN>=35000)
	ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD,THREE,-2,DUNGEON,1,4,100)
ENDIF

IF(PLAYER1,GAME_TURN>=8000)	
	IF_SLAB_TYPE(42,49,LAVA)
			CHANGE_SLAB_TYPE(42,49,BRIDGE_FRAME)
			CHANGE_SLAB_OWNER(42,49,PLAYER0)
	ENDIF
	IF_SLAB_TYPE(42,50,LAVA)
			CHANGE_SLAB_TYPE(42,50,BRIDGE_FRAME)
			CHANGE_SLAB_OWNER(42,50,PLAYER0)
	ENDIF
	IF_SLAB_TYPE(42,51,LAVA)
			CHANGE_SLAB_TYPE(42,51,BRIDGE_FRAME)
			CHANGE_SLAB_OWNER(42,51,PLAYER0)
	ENDIF
	IF_SLAB_TYPE(42,52,LAVA)
			CHANGE_SLAB_TYPE(42,52,BRIDGE_FRAME)
			CHANGE_SLAB_OWNER(42,52,PLAYER0)
	ENDIF
	ROOM_AVAILABLE(PLAYER0,BRIDGE,1,0)
ENDIF

IF(PLAYER0,DUNGEON_DESTROYED >= 1)
	QUICK_OBJECTIVE(4,"你击溃了第一个对手，夺回了第一个王国，但这仅仅是个开始。",PLAYER1)
	WIN_GAME
ENDIF

REM ****************************************************************************
