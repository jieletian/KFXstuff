REM ANOTHER DUNGEON LEVEL11
REM ƒn[ƒX`–À‘–‚Ì’n`

REM *** SETUP SECTION ***

SET_GENERATE_SPEED(750)

START_MONEY(PLAYER0, 3000)

MAX_CREATURES(PLAYER0, 20)

ADD_CREATURE_TO_POOL(BUG, 20)
ADD_CREATURE_TO_POOL(DRAGON, 20)
ADD_CREATURE_TO_POOL(BILE_DEMON, 20)
ADD_CREATURE_TO_POOL(TROLL, 20)
ADD_CREATURE_TO_POOL(SKELETON, 20)
ADD_CREATURE_TO_POOL(GHOST, 20)
ADD_CREATURE_TO_POOL(TENTACLE, 20)
ADD_CREATURE_TO_POOL(HELL_HOUND, 20)
ADD_CREATURE_TO_POOL(VAMPIRE, 15)

ROOM_AVAILABLE(ALL_PLAYERS, TREASURE, 1, 1)
ROOM_AVAILABLE(ALL_PLAYERS, LAIR, 1, 1)
ROOM_AVAILABLE(ALL_PLAYERS, GARDEN, 1, 1)
ROOM_AVAILABLE(ALL_PLAYERS, RESEARCH, 1, 1)
ROOM_AVAILABLE(ALL_PLAYERS, BARRACKS, 1, 0)
ROOM_AVAILABLE(ALL_PLAYERS, TRAINING, 1, 1)
ROOM_AVAILABLE(ALL_PLAYERS, WORKSHOP, 1, 0)
ROOM_AVAILABLE(ALL_PLAYERS, GRAVEYARD, 1, 0)

MAGIC_AVAILABLE(ALL_PLAYERS, POWER_POSSESS, 1, 1)
MAGIC_AVAILABLE(ALL_PLAYERS, POWER_IMP, 1, 1)
MAGIC_AVAILABLE(ALL_PLAYERS, POWER_SIGHT, 1, 0)
MAGIC_AVAILABLE(ALL_PLAYERS, POWER_OBEY, 1, 0)
MAGIC_AVAILABLE(ALL_PLAYERS, POWER_HOLD_AUDIENCE, 1, 0)
MAGIC_AVAILABLE(ALL_PLAYERS, POWER_PROTECT, 1, 0)
MAGIC_AVAILABLE(ALL_PLAYERS, POWER_CHICKEN, 1, 0)
MAGIC_AVAILABLE(ALL_PLAYERS, POWER_LIGHTNING, 1, 0)
MAGIC_AVAILABLE(ALL_PLAYERS, POWER_DISEASE, 1, 0)
MAGIC_AVAILABLE(ALL_PLAYERS, POWER_DESTROY_WALLS, 1, 0)

TRAP_AVAILABLE(ALL_PLAYERS, POISON_GAS, 1, 0)
rem TRAP_AVAILABLE(ALL_PLAYERS, LIGHTNING, 1, 0)

DOOR_AVAILABLE(ALL_PLAYERS, WOOD, 1, 0)
rem DOOR_AVAILABLE(PLAYER0, MAGIC, 1, 0)
rem DOOR_AVAILABLE(PLAYER1, STEEL, 1, 0)

REM *** HERO PARTY ***

CREATE_PARTY(AT_ROOM1)
ADD_TO_PARTY(AT_ROOM1, DWARFA, 4, 0, ATTACK_ROOMS, 0)

CREATE_PARTY(AT_ROOM2)
ADD_TO_PARTY(AT_ROOM2, BARBARIAN, 7, 0, ATTACK_ROOMS, 0)
ADD_TO_PARTY(AT_ROOM2, WICTH, 1, 0, ATTACK_ENEMIES, 0)
ADD_TO_PARTY(AT_ROOM2, BARBARIAN, 7, 0, ATTACK_ROOMS, 0)

CREATE_PARTY(SCOUT1)
ADD_TO_PARTY(SCOUT1, DWARFA, 2, 400, ATTACK_ROOMS, 0)
ADD_TO_PARTY(SCOUT1, DWARFA, 4, 800, ATTACK_ENEMIES, 0)
ADD_TO_PARTY(SCOUT1, DWARFA, 2, 400, ATTACK_ROOMS, 0)
ADD_TO_PARTY(SCOUT1, DWARFA, 2, 400, ATTACK_ROOMS, 0)

CREATE_PARTY(SCOUT2)
ADD_TO_PARTY(SCOUT2, ARCHER, 5, 400, ATTACK_ENEMIES, 0)
ADD_TO_PARTY(SCOUT2, ARCHER, 5, 400, ATTACK_ENEMIES, 0)
ADD_TO_PARTY(SCOUT2, DWARFA, 3, 400, ATTACK_ROOMS, 0)
ADD_TO_PARTY(SCOUT2, DWARFA, 3, 400, ATTACK_ROOMS, 0)

CREATE_PARTY(SCOUT3)
ADD_TO_PARTY(SCOUT3, MONK, 3, 800, ATTACK_ENEMIES, 0)
ADD_TO_PARTY(SCOUT3, BARBARIAN, 5, 400, ATTACK_DUNGEON_HEART, 0)
ADD_TO_PARTY(SCOUT3, MONK, 3, 400, ATTACK_ENEMIES, 0)

CREATE_PARTY(SCOUT4)
ADD_TO_PARTY(SCOUT4, GIANT, 4, 400, ATTACK_ENEMIES, 0)
ADD_TO_PARTY(SCOUT4, GIANT, 4, 400, ATTACK_ENEMIES, 0)
ADD_TO_PARTY(SCOUT4, GIANT, 4, 400, ATTACK_ENEMIES, 0)
ADD_TO_PARTY(SCOUT4, GIANT, 4, 400, ATTACK_ENEMIES, 0)
ADD_TO_PARTY(SCOUT4, GIANT, 4, 400, ATTACK_ENEMIES, 0)

CREATE_PARTY(SCOUT5)
ADD_TO_PARTY(SCOUT5, BARBARIAN, 4, 400, ATTACK_ENEMIES, 0)
ADD_TO_PARTY(SCOUT5, WITCH, 8, 800, ATTACK_ENEMIES, 0)
ADD_TO_PARTY(SCOUT5, BARBARIAN, 4, 400, ATTACK_ENEMIES, 0)
ADD_TO_PARTY(SCOUT5, BARBARIAN, 4, 400, ATTACK_ENEMIES, 0)

CREATE_PARTY(SCOUT6)
ADD_TO_PARTY(SCOUT6, DWARFA, 4, 400, ATTACK_ENEMIES, 0)
ADD_TO_PARTY(SCOUT6, DWARFA, 6, 800, ATTACK_DUNGEON_HEART, 0)
ADD_TO_PARTY(SCOUT6, DWARFA, 4, 400, ATTACK_ENEMIES, 0)
ADD_TO_PARTY(SCOUT6, WIZARD, 5, 400, ATTACK_ENEMIES, 0)
ADD_TO_PARTY(SCOUT6, WIZARD, 5, 400, ATTACK_ENEMIES, 0)

CREATE_PARTY(SCOUT7)
ADD_TO_PARTY(SCOUT7, MONK, 6, 400, ATTACK_DUNGEON_HEART, 200)
ADD_TO_PARTY(SCOUT7, SAMURAI, 7, 800, ATTACK_ENEMIES, 200)
ADD_TO_PARTY(SCOUT7, MONK, 6, 400, ATTACK_DUNGEON_HEART, 200)

CREATE_PARTY(ATTACK1)
ADD_TO_PARTY(ATTACK1, DWARFA, 7, 400, STEAL_GOLD, 0)
ADD_TO_PARTY(ATTACK1, BARBARIAN, 10, 1000, ATTACK_ENEMIES, 0)
ADD_TO_PARTY(ATTACK1, DWARFA, 7, 400, STEAL_GOLD, 0)
ADD_TO_PARTY(ATTACK1, DWARFA, 7, 400, STEAL_GOLD, 0)
ADD_TO_PARTY(ATTACK1, DWARFA, 7, 400, STEAL_GOLD, 0)
ADD_TO_PARTY(ATTACK1, DWARFA, 7, 400, STEAL_GOLD, 0)

CREATE_PARTY(ATTACK2)
ADD_TO_PARTY(ATTACK2, ARCHER, 7, 400, ATTACK_ENEMIES, 0)
ADD_TO_PARTY(ATTACK2, GIANT, 10, 1000, ATTACK_ENEMIES, 0)
ADD_TO_PARTY(ATTACK2, ARCHER, 7, 400, ATTACK_ENEMIES, 0)
ADD_TO_PARTY(ATTACK2, SAMURAI, 7, 400, ATTACK_DUNGEON_HEART, 0)
ADD_TO_PARTY(ATTACK2, SAMURAI, 7, 400, ATTACK_DUNGEON_HEART, 0)

CREATE_PARTY(ATTACK3)
ADD_TO_PARTY(ATTACK3, MONK, 6, 400, ATTACK_ENEMIES, 0)
ADD_TO_PARTY(ATTACK3, MONK, 10, 1000, ATTACK_ENEMIES, 0)
ADD_TO_PARTY(ATTACK3, MONK, 6, 400, ATTACK_ENEMIES, 0)
ADD_TO_PARTY(ATTACK3, BARBARIAN, 6, 400, ATTACK_DUNGEON_HEART, 0)
ADD_TO_PARTY(ATTACK3, BARBARIAN, 6, 400, ATTACK_DUNGEON_HEART, 0)
ADD_TO_PARTY(ATTACK3, BARBARIAN, 6, 400, ATTACK_DUNGEON_HEART, 0)

CREATE_PARTY(ATTACK4)
ADD_TO_PARTY(ATTACK4, FAIRY, 8, 400, ATTACK_ENEMIES, 0)
ADD_TO_PARTY(ATTACK4, WIZARD, 9, 1000, ATTACK_ENEMIES, 0)
ADD_TO_PARTY(ATTACK4, WITCH, 8, 400, ATTACK_ENEMIES, 0)
ADD_TO_PARTY(ATTACK4, WITCH, 8, 400, ATTACK_ENEMIES, 0)

CREATE_PARTY(ATTACK5)
ADD_TO_PARTY(ATTACK5, FAIRY, 7, 0, ATTACK_ENEMIES, 0)
ADD_TO_PARTY(ATTACK5, SAMURAI, 9, 0, ATTACK_ENEMIES, 0)
ADD_TO_PARTY(ATTACK5, FAIRY, 7, 0, ATTACK_ENEMIES, 0)
ADD_TO_PARTY(ATTACK5, DWARFA, 10, 0, ATTACK_ROOMS, 0)
ADD_TO_PARTY(ATTACK5, DWARFA, 10, 0, ATTACK_ROOMS, 0)

CREATE_PARTY(BOSS)
ADD_TO_PARTY(BOSS, GIANT, 10, 1000, ATTACK_DUNGEON_HEART, 0)
ADD_TO_PARTY(BOSS, KNIGHT, 10, 8000, ATTACK_DUNGEON_HEART, 0)
ADD_TO_PARTY(BOSS, GIANT, 10, 1000, ATTACK_DUNGEON_HEART, 0)

REM *** MAIN ***

IF_ACTION_POINT(2, PLAYER0)
	ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, GIANT, 2, 6, 4, 0)
ENDIF

IF_ACTION_POINT(7, PLAYER0)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD, SCOUT7, 8, 3)
ENDIF

IF_ACTION_POINT(9, PLAYER0)
	ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, ARCHER, -2, 8, 5, 0)
ENDIF

IF_ACTION_POINT(10, PLAYER0)
	ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, BUG, 10, 6, 7, 0)
ENDIF

IF_ACTION_POINT(11, PLAYER0)
	ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, BUG, 11, 6, 10, 0)
ENDIF

IF(PLAYER_GOOD, GAME_TURN >= 7500)
	DISPLAY_OBJECTIVE(81, PLAYER0)
	ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD, SCOUT1, 4, DUNGEON, PLAYER0, 1, 500)
	ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, MONK, 4, 4, 2, 0)
ENDIF

IF(PLAYER_GOOD, GAME_TURN >= 12500)
	SET_FLAG(PLAYER0, FLAG0, 2)
	ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD, SCOUT2, 1, DUNGEON, PLAYER0, 3, 500)
	ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, GIANT, 1, 6, 2, 0)
ENDIF

IF(PLAYER_GOOD, GAME_TURN >= 17000)
	ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD, SCOUT4, 2, DUNGEON_HEART, PLAYER0, 4, 500)
	ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, ARCHER, 2, 4, 4, 0)
ENDIF

IF(PLAYER_GOOD, GAME_TURN >= 22000)
	ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD, SCOUT5, -2, ACTION_POINT, 4, 5, 500)
	ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, FAIRY, 9, 3, 4, 0)
ENDIF

IF(PLAYER_GOOD, GAME_TURN >= 25500)
	ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD, SCOUT3, 3, DUNGEON_HEART, PLAYER0, 3, 500)
	ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, WIZARD, 3, 4, 4, 0)
ENDIF

IF(PLAYER_GOOD, GAME_TURN >= 32500)
	ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD, SCOUT4, 5, DUNGEON_HEART, PLAYER0, 4, 500)
	ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, WITCH, 5, 5, 5, 0)
ENDIF

IF(PLAYER_GOOD, GAME_TURN >= 36500)
	ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD, SCOUT6, 6, ACTION_POINT, 1, 6, 500)
	ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, DWARFA, 6, 6, 3, 0)
ENDIF

IF(PLAYER_GOOD, GAME_TURN >= 44000)
	DISPLAY_OBJECTIVE(85, PLAYER0)
	ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD, SCOUT7, -2, ACTION_POINT, 1, 7, 500)
	ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD, SCOUT5, 2, ACTION_POINT, 4, 5, 500)
	ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, SAMURAI, -2, 2, 6, 0)
	ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, ARCHER, 2, 3, 5, 0)
ENDIF

IF(PLAYER_GOOD, TUNNELLER >= 1)
	IF(PLAYER_GOOD, GAME_TURN >= 12000)
		IF(PLAYER_GOOD, GAME_TURN <= 12400)
			ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, DWARFA, 12, 6, 7, 0)
			SET_FLAG(PLAYER_GOOD, FLAG6, 1)
		ENDIF
	ENDIF
	IF(PLAYER_GOOD, GAME_TURN >= 16500)
		IF(PLAYER_GOOD, GAME_TURN <= 16900)
			ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, GIANT, 13, 3, 10, 0)
			ADD_PARTY_TO_LEVEL(PLAYER_GOOD, AT_ROOM2, 12, 2)
			SET_FLAG(PLAYER_GOOD, FLAG6, 1)
		ENDIF
	ENDIF
	IF(PLAYER_GOOD, GAME_TURN >= 21500)
		IF(PLAYER_GOOD, GAME_TURN <= 21900)
			ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, WITCH, 6, 4, 10, 0)
			ADD_PARTY_TO_LEVEL(PLAYER_GOOD, SCOUT6, 14, 3)
			SET_FLAG(PLAYER_GOOD, FLAG6, 1)
		ENDIF
	ENDIF
	IF(PLAYER_GOOD, GAME_TURN >= 29000)
		IF(PLAYER_GOOD, GAME_TURN <= 29400)
			ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, BARBARIAN, 3, 4, 9, 0)
			ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, SAMURAI, 12, 6, 8, 0)
			SET_FLAG(PLAYER_GOOD, FLAG6, 1)
		ENDIF
	ENDIF
	IF(PLAYER_GOOD, GAME_TURN >= 39500)
		IF(PLAYER_GOOD, GAME_TURN <= 39900)
			ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, SAMURAI, 4, 4, 8, 0)
			ADD_PARTY_TO_LEVEL(PLAYER_GOOD, ATTACK2, 13, 2)
			SET_FLAG(PLAYER_GOOD, FLAG6, 1)
		ENDIF
	ENDIF
	IF(PLAYER_GOOD, GAME_TURN >= 49000)
		IF(PLAYER_GOOD, GAME_TURN <= 49400)
			ADD_PARTY_TO_LEVEL(PLAYER_GOOD, ATTACK1, 2, 2)
			ADD_PARTY_TO_LEVEL(PLAYER_GOOD, AT_ROOM2, 14, 6)
			SET_FLAG(PLAYER_GOOD, FLAG6, 1)
		ENDIF
	ENDIF
ENDIF

IF(PLAYER_GOOD, GAME_TURN >= 50000)
	IF(PLAYER0, PRISON == 0)
		DISPLAY_INFORMATION(86, PLAYER0)
	ENDIF
ENDIF

IF(PLAYER_GOOD, GAME_TURN >= 63000)
	ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD, SCOUT5, -1, DUNGEON, PLAYER0, 8, 500)
	ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD, SCOUT5, -2, DUNGEON, PLAYER0, 8, 500)
	ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD, SCOUT6, 6, DUNGEON, PLAYER0, 8, 500)
	ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, GIANT, 4, 3, 10, 500)
	ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, ARCHER, 4, 6, 7, 300)
	ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, BARBARIAN, 3, 3, 10, 500)
	ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, GIANT, 3, 6, 5, 300)
ENDIF

IF(PLAYER_GOOD, GAME_TURN >= 65000)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD, SCOUT2, 5, 3)
	ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, WITCH, 2, 1, 10, 1000)
	ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, SAMURAI, 2, 3, 7, 700)
	IF(PLAYER_GOOD, TOTAL_CREATURES <= 16)
		SET_FLAG(PLAYER_GOOD, FLAG5, 1)
	ENDIF
ENDIF

IF(PLAYER_GOOD, FLAG5 == 1)
	DISPLAY_OBJECTIVE(87, PLAYER0)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD, BOSS, -2, 1)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD, ATTACK1, 1, 1)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD, ATTACK2, 2, 1)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD, ATTACK3, 3, 1)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD, ATTACK4, 4, 1)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD, ATTACK5, 5, 1)
	SET_TIMER(PLAYER_GOOD, TIMER0)
	IF(PLAYER_GOOD, TIMER0 > 2500)
		SET_TIMER(PLAYER_GOOD, TIMER1)
		SET_TIMER(PLAYER_GOOD, TIMER2)
		SET_TIMER(PLAYER_GOOD, TIMER3)
		SET_TIMER(PLAYER_GOOD, TIMER4)
		SET_TIMER(PLAYER_GOOD, TIMER5)
		IF(PLAYER_GOOD, TOTAL_CREATURES > 100)
			NEXT_COMMAND_REUSABLE
			SET_TIMER(PLAYER_GOOD, TIMER1)
			NEXT_COMMAND_REUSABLE
			SET_TIMER(PLAYER_GOOD, TIMER2)
			NEXT_COMMAND_REUSABLE
			SET_TIMER(PLAYER_GOOD, TIMER3)
			NEXT_COMMAND_REUSABLE
			SET_TIMER(PLAYER_GOOD, TIMER4)
			NEXT_COMMAND_REUSABLE
			SET_TIMER(PLAYER_GOOD, TIMER5)
		ENDIF
		IF(PLAYER_GOOD, TIMER1 >= 2600)
			DISPLAY_OBJECTIVE(83, PLAYER0)
			NEXT_COMMAND_REUSABLE
			ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, GIANT, 9, 5, 7, 0)
			NEXT_COMMAND_REUSABLE
			ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, MONK, 1, 5, 6, 0)
			NEXT_COMMAND_REUSABLE
			SET_TIMER(PLAYER_GOOD, TIMER1)
		ENDIF
		IF(PLAYER_GOOD, TIMER2 >= 3800)
			NEXT_COMMAND_REUSABLE
			ADD_PARTY_TO_LEVEL(PLAYER_GOOD, AT_ROOM1, 2, 4)
			NEXT_COMMAND_REUSABLE
			ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, WIZARD, 4, 5, 8, 0)
			NEXT_COMMAND_REUSABLE
			SET_TIMER(PLAYER_GOOD, TIMER2)
		ENDIF
		IF(PLAYER_GOOD, TIMER3 >= 5100)
			NEXT_COMMAND_REUSABLE
			ADD_PARTY_TO_LEVEL(PLAYER_GOOD, AT_ROOM2, 3, 4)
			NEXT_COMMAND_REUSABLE
			ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, ARCHER, 6, 5, 8, 0)
			NEXT_COMMAND_REUSABLE
			SET_TIMER(PLAYER_GOOD, TIMER3)
		ENDIF
		IF(PLAYER_GOOD, TIMER4 >= 6700)
			NEXT_COMMAND_REUSABLE
			ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, SAMURAI, -1, 4, 7, 0)
			NEXT_COMMAND_REUSABLE
			ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, WITCH, -2, 2, 9, 0)
			NEXT_COMMAND_REUSABLE
			SET_TIMER(PLAYER_GOOD, TIMER4)
		ENDIF
		IF(PLAYER_GOOD, TIMER5 >= 8500)
			NEXT_COMMAND_REUSABLE
			ADD_PARTY_TO_LEVEL(PLAYER_GOOD, ATTACK5, 1, 2)
			NEXT_COMMAND_REUSABLE
			ADD_PARTY_TO_LEVEL(PLAYER_GOOD, AT_ROOM1, 4, 4)
			NEXT_COMMAND_REUSABLE
			SET_TIMER(PLAYER_GOOD, TIMER5)
		ENDIF
		IF(PLAYER_GOOD, KNIGHT == 0)
			SET_FLAG(PLAYER_GOOD, FLAG7, 1)
		ENDIF
	ENDIF
ENDIF

IF(PLAYER_GOOD, FLAG7 == 1)
	IF(PLAYER0, KNIGHT >= 1)
		DISPLAY_OBJECTIVE(89, PLAYER0)
		WIN_GAME
	ENDIF
	IF(PLAYER0, KNIGHT == 0)
		DISPLAY_OBJECTIVE(88, PLAYER0)
		LOSE_GAME
	ENDIF
ENDIF

IF(PLAYER_GOOD, FLAG6 == 1)
	DISPLAY_INFORMATION(82, PLAYER0)
ENDIF

IF(PLAYER_GOOD, LAIR == 0)
	DISPLAY_INFORMATION(84, PLAYER0)
	CREATURE_AVAILABLE(PLAYER0, BUG, 1, 1)
ENDIF

IF(PLAYER0, ENTRANCE >= 10)
	SET_GENERATE_SPEED(400)
ENDIF

IF_AVAILABLE(PLAYER0, WORD_OF_POWER == 1)
	TRAP_AVAILABLE(ALL_PLAYERS, WORD_OF_POWER, 1, 1)
ENDIF

IF_AVAILABLE(PLAYER0, BOULDER == 1)
	TRAP_AVAILABLE(ALL_PLAYERS, BOULDER, 1, 1)
ENDIF

DISPLAY_OBJECTIVE(80, PLAYER0)
