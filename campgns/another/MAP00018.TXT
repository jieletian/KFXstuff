REM ANOTHER DUNGEON LEVEL18
REM ブレイズ・エンド〜神の雷〜

REM *** SETUP SECTION ***

SET_GENERATE_SPEED(450)

START_MONEY(ALL_PLAYERS, 16000)

MAX_CREATURES(ALL_PLAYERS, 50)

COMPUTER_PLAYER(PLAYER2, 0)
rem SET_COMPUTER_PROCESS(PLAYER2, "DIG TO CLOSE GOLD", 0, 9500, 200, 7, 45)
SET_COMPUTER_PROCESS(PLAYER2, "BUILD A SCAVENGER ROOM", -10, 4, 4, 9, 8)

ADD_CREATURE_TO_POOL(SPIDER, 12)
ADD_CREATURE_TO_POOL(BILE_DEMON, 10)
ADD_CREATURE_TO_POOL(TENTACLE, 20)
ADD_CREATURE_TO_POOL(TROLL, 15)
ADD_CREATURE_TO_POOL(HELL_HOUND, 10)
ADD_CREATURE_TO_POOL(DRAGON, 20)
ADD_CREATURE_TO_POOL(ORC, 20)
ADD_CREATURE_TO_POOL(SAMURAI, 15)
ADD_CREATURE_TO_POOL(WIZARD, 20)

CREATURE_AVAILABLE(ALL_PLAYERS, TROLL, 1, 1)
CREATURE_AVAILABLE(PLAYER0, SPIDER, 1, 1)
CREATURE_AVAILABLE(PLAYER0, BILE_DEMON, 1, 1)
CREATURE_AVAILABLE(PLAYER0, TENTACLE, 1, 1)
CREATURE_AVAILABLE(PLAYER0, HELL_HOUND, 1, 1)
CREATURE_AVAILABLE(PLAYER2, DRAGON, 1, 1)
CREATURE_AVAILABLE(PLAYER2, ORC, 1, 1)

ROOM_AVAILABLE(ALL_PLAYERS, TREASURE, 1, 1)
ROOM_AVAILABLE(ALL_PLAYERS, GARDEN, 1, 1)
ROOM_AVAILABLE(ALL_PLAYERS, RESEARCH, 1, 1)
ROOM_AVAILABLE(ALL_PLAYERS, BARRACKS, 1, 0)
ROOM_AVAILABLE(ALL_PLAYERS, GRAVEYARD, 1, 0)
ROOM_AVAILABLE(ALL_PLAYERS, SCAVENGER, 1, 0)
ROOM_AVAILABLE(PLAYER0, PRISON, 1, 0)
ROOM_AVAILABLE(PLAYER2, LAIR, 1, 1)
ROOM_AVAILABLE(PLAYER2, TRAINING, 1, 1)
ROOM_AVAILABLE(PLAYER2, BRIDGE, 1, 0)
ROOM_AVAILABLE(PLAYER2, WORKSHOP, 1, 0)
rem ROOM_AVAILABLE(PLAYER2, SCAVENGER, 1, 0)

MAGIC_AVAILABLE(ALL_PLAYERS, POWER_IMP, 1, 1)
MAGIC_AVAILABLE(ALL_PLAYERS, POWER_SIGHT, 1, 0)
MAGIC_AVAILABLE(ALL_PLAYERS, POWER_SPEED, 1, 0)
MAGIC_AVAILABLE(ALL_PLAYERS, POWER_CALL_TO_ARMS, 1, 0)
MAGIC_AVAILABLE(ALL_PLAYERS, POWER_CONCEAL, 1, 0)
MAGIC_AVAILABLE(PLAYER0, POWER_OBEY, 1, 0)
MAGIC_AVAILABLE(PLAYER0, POWER_CALL_TO_ARMS, 1, 0)
MAGIC_AVAILABLE(PLAYER0, POWER_CAVE_IN, 1, 0)
MAGIC_AVAILABLE(PLAYER0, POWER_CHICKEN, 1, 0)
MAGIC_AVAILABLE(PLAYER0, POWER_DISEASE, 1, 0)
MAGIC_AVAILABLE(PLAYER2, POWER_LIGHTNING, 1, 1)
MAGIC_AVAILABLE(PLAYER2, POWER_DISEASE, 1, 1)
MAGIC_AVAILABLE(PLAYER2, POWER_DESTROY_WALLS, 1, 0)

TRAP_AVAILABLE(ALL_PLAYERS, POISON_GAS, 1, 0)
TRAP_AVAILABLE(PLAYER0, ALARM, 1, 0)
TRAP_AVAILABLE(PLAYER0, LAVA, 1, 0)
TRAP_AVAILABLE(PLAYER2, LIGHTNING, 1, 0)
TRAP_AVAILABLE(PLAYER2, WORD_OF_POWER, 1, 0)

DOOR_AVAILABLE(ALL_PLAYERS, WOOD, 1, 0)
DOOR_AVAILABLE(ALL_PLAYERS, BRACED, 1, 0)
DOOR_AVAILABLE(PLAYER2, MAGIC, 1, 0)

REM *** HERO PARTY ***

CREATE_PARTY(ST_GOLD)
ADD_TO_PARTY(ST_GOLD, THIEF, 8, 0, STEAL_GOLD, 0)

CREATE_PARTY(AT_ROOM)
ADD_TO_PARTY(AT_ROOM, HELL_HOUND, 5, 400, ATTACK_ROOMS, 0)
ADD_TO_PARTY(AT_ROOM, HELL_HOUND, 7, 400, ATTACK_ROOMS, 0)
ADD_TO_PARTY(AT_ROOM, HELL_HOUND, 5, 400, ATTACK_ROOMS, 0)

CREATE_PARTY(AT_ROOM2)
ADD_TO_PARTY(AT_ROOM2, TROLL, 7, 800, ATTACK_ROOMS, 0)

CREATE_PARTY(AT_ROOM3)
ADD_TO_PARTY(AT_ROOM3, HELL_HOUND, 6, 400, ATTACK_ROOMS, 0)

CREATE_PARTY(HERO1)
ADD_TO_PARTY(HERO1, DWARFA, 8, 0, ATTACK_ROOMS, 0)
ADD_TO_PARTY(HERO1, DWARFA, 8, 0, ATTACK_ROOMS, 0)
ADD_TO_PARTY(HERO1, ARCHER, 7, 0, ATTACK_ENEMIES, 0)
ADD_TO_PARTY(HERO1, ARCHER, 7, 0, ATTACK_ENEMIES, 0)

CREATE_PARTY(HERO2)
ADD_TO_PARTY(HERO2, GIANT, 9, 0, ATTACK_DUNGEON_HEART, 0)
ADD_TO_PARTY(HERO2, GIANT, 9, 0, ATTACK_DUNGEON_HEART, 0)
ADD_TO_PARTY(HERO2, MONK, 10, 0, ATTACK_DUNGEON_HEART, 0)
ADD_TO_PARTY(HERO2, MONK, 10, 0, ATTACK_DUNGEON_HEART, 0)
ADD_TO_PARTY(HERO2, MONK, 10, 0, ATTACK_DUNGEON_HEART, 0)

CREATE_PARTY(HERO3)
ADD_TO_PARTY(HERO3, DWRAFA, 10, 0, ATTACK_ROOMS, 0)
ADD_TO_PARTY(HERO3, ARCHER, 8, 0, ATTACK_ENEMIES, 0)
ADD_TO_PARTY(HERO3, ARCHER, 8, 0, ATTACK_ENEMIES, 0)
ADD_TO_PARTY(HERO3, DWARFA, 10, 0, ATTACK_ROOMS, 0)
ADD_TO_PARTY(HERO3, SAMURAI, 8, 0, ATTACK_ENEMIES, 0)
ADD_TO_PARTY(HERO3, SAMURAI, 8, 0, ATTACK_ENEMIES, 0)

CREATE_PARTY(HERO4)
ADD_TO_PARTY(HERO4, FAIRY, 7, 0, ATTACK_ENEMIES, 0)
ADD_TO_PARTY(HERO4, BARBARIAN, 9, 0, ATTACK_ENEMIES, 0)
ADD_TO_PARTY(HERO4, BARBARIAN, 9, 0, ATTACK_ENEMIES, 0)
ADD_TO_PARTY(HERO4, BARBARIAN, 9, 0, ATTACK_ENEMIES, 0)
ADD_TO_PARTY(HERO4, FAIRY, 7, 0, ATTACK_ENEMIES, 0)
ADD_TO_PARTY(HERO4, MONK, 8, 0, ATTACK_ENEMIES, 0)
ADD_TO_PARTY(HERO4, MONK, 8, 0, ATTACK_ENEMIES, 0)

CREATE_PARTY(GUARD1)
ADD_TO_PARTY(GUARD1, SAMURAI, 8, 800, ATTACK_DUNGEON_HEART, 0)
ADD_TO_PARTY(GUARD1, GIANT, 10, 2500, ATTACK_DUNGEON_HEART, 0)
ADD_TO_PARTY(GUARD1, SAMURAI, 8, 800, ATTACK_DUNGEON_HEART, 0)
ADD_TO_PARTY(GUARD1, MONK, 7, 800, ATTACK_DUNGEON_HEART, 0)
ADD_TO_PARTY(GUARD1, MONK, 7, 800, ATTACK_DUNGEON_HEART, 0)

CREATE_PARTY(GUARD2)
ADD_TO_PARTY(GUARD2, DRAGON, 8, 1000, ATTACK_DUNGEON_HEART, 0)
ADD_TO_PARTY(GUARD2, WITCH, 10, 2500, ATTACK_DUNGEON_HEART, 0)
ADD_TO_PARTY(GUARD2, ORC, 6, 400, ATTACK_ENEMIES, 0)
ADD_TO_PARTY(GUARD2, ORC, 6, 400, ATTACK_ENEMIES, 0)
ADD_TO_PARTY(GUARD2, HELL_HOUND, 5, 0, ATTACK_ROOMS, 0)
ADD_TO_PARTY(GUARD2, HELL_HOUND, 5, 0, ATTACK_ROOMS, 0)
ADD_TO_PARTY(GUARD2, HELL_HOUND, 5, 0, ATTACK_ROOMS, 0)

CREATE_PARTY(GUARD3)
ADD_TO_PARTY(GUARD3, GIANT, 6, 400, ATTACK_DUNGEON_HEART, 0)
ADD_TO_PARTY(GUARD3, GIANT, 10, 2500, ATTACK_DUNGEON_HEART, 0)
ADD_TO_PARTY(GUARD3, GIANT, 6, 400, ATTACK_DUNGEON_HEART, 0)
ADD_TO_PARTY(GUARD3, TROLL, 10, 0, ATTACK_ROOMS, 0)
ADD_TO_PARTY(GUARD3, TROLL, 10, 0, ATTACK_ROOMS, 0)
ADD_TO_PARTY(GUARD3, TROLL, 10, 0, ATTACK_ROOMS, 0)
ADD_TO_PARTY(GUARD3, TROLL, 10, 0, ATTACK_ROOMS, 0)

CREATE_PARTY(BOSS1)
ADD_TO_PARTY(BOSS1, SAMURAI, 9, 1000, ATTACK_DUNGEON_HEART, 0)
ADD_TO_PARTY(BOSS1, KNIGHT, 10, 5000, ATTACK_DUNGEON_HEART, 0)
ADD_TO_PARTY(BOSS1, SAMURAI, 9, 1000, ATTACK_DUNGEON_HEART, 0)
ADD_TO_PARTY(BOSS1, WIZARD, 8, 1000, ATTACK_DUNGEON_HEART, 0)
ADD_TO_PARTY(BOSS1, WIZARD, 8, 1000, ATTACK_DUNGEON_HEART, 0)
ADD_TO_PARTY(BOSS1, MONK, 8, 1000, ATTACK_DUNGEON_HEART, 0)
ADD_TO_PARTY(BOSS1, MONK, 8, 1000, ATTACK_DUNGEON_HEART, 0)

CREATE_PARTY(BOSS2)
ADD_TO_PARTY(BOSS2, GIANT, 10, 2000, ATTACK_DUNGEON_HEART, 0)
ADD_TO_PARTY(BOSS2, AVATAR, 10, 10000, ATTACK_DUNGEON_HEART, 0)
ADD_TO_PARTY(BOSS2, GIANT, 10, 2000, ATTACK_DUNGEON_HEART, 0)
ADD_TO_PARTY(BOSS2, FAIRY, 7, 2000, ATTACK_DUNGEON_HEART, 0)
ADD_TO_PARTY(BOSS2, FAIRY, 7, 2000, ATTACK_DUNGEON_HEART, 0)

REM *** MAIN ***

IF_ACTION_POINT(4, PLAYER0)
	ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, BARBARIAN, 4, 6, 9, 800)
ENDIF

IF(PLAYER0, WORKSHOP >= 50)
	ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, WITCH, -2, 5, 8, 0)
ENDIF

IF(PLAYER2, TOTAL_CREATURES > 35)
	SET_FLAG(PLAYER2, FLAG0, 1)
ENDIF

rem アバタール出現フラグ
IF(PLAYER_GOOD, FLAG0 == 0)
	IF(PLAYER2, FLAG0 == 1)
		IF(PLAYER2, TOTAL_CREATURES < 25)
			SET_FLAG(PLAYER_GOOD, FLAG0, 1)
		ENDIF
		IF(PLAYER0, CREATURES_SCAVENGED_GAINED >= 20)
			SET_FLAG(PLAYER_GOOD, FLAG0, 1)
		ENDIF
		IF_ACTION_POINT(8, PLAYER0)
			SET_FLAG(PLAYER_GOOD, FLAG0, 1)
		ENDIF
		IF(PLAYER0, GAME_TURN >= 100000)
			SET_FLAG(PLAYER_GOOD, FLAG0, 1)
		ENDIF
	ENDIF
	IF(PLAYER2, DUNGEON_DESTROYED == 1)
		SET_FLAG(PLAYER_GOOD, FLAG0, 1)
	ENDIF
ENDIF

IF(PLAYER_GOOD, FLAG0 == 1)
	SET_TIMER(PLAYER_GOOD, TIMER0)
	IF(PLAYER_GOOD, TIMER0 > 2500)
		DISPLAY_INFORMATION(143, PLAYER0)
		SET_TIMER(PLAYER_GOOD, TIMER1)
		IF(PLAYER_GOOD, TIMER0 > 6000)
			DISPLAY_OBJECTIVE(144, PLAYER0)
			ADD_PARTY_TO_LEVEL(PLAYER_GOOD, BOSS2, 1, 1)
			ADD_PARTY_TO_LEVEL(PLAYER_GOOD, GUARD1, 1, 1)
			ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, GIANT, 1, 6, 7, 800)
			SET_FLAG(PLAYER_GOOD, FLAG1, 1)
			SET_TIMER(PLAYER_GOOD, TIMER3)
			IF(PLAYER_GOOD, TIMER3 > 3000)
				ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, ARCHER, 1, 4, 6, 0)
			ENDIF
			IF(PLAYER_GOOD, TIMER3 > 6500)
				ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, DWARFA, 1, 4, 6, 0)
			ENDIF
			IF(PLAYER_GOOD, TIMER3 > 10000)
				ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, SAMURAI, 1, 4, 6, 0)
			ENDIF
			IF(PLAYER_GOOD, AVATAR == 0)
				SET_FLAG(PLAYER_GOOD, FLAG0, 2)
			ENDIF
		ENDIF
	ENDIF
ENDIF

IF(PLAYER_GOOD, FLAG0 == 2)
	rem アバタール死んだとき、まだ敵キーパーは健在
	IF(PLAYER2, DUNGEON_DESTROYED == 0)
		rem 副官寝返りルート
		DISPLAY_OBJECTIVE(145, PLAYER0)
		MAGIC_AVAILABLE(PLAYER2, POWER_CALL_TO_ARMS, 1, 1)
		MAX_CREATURES(PLAYER2, 70)
		ADD_CREATURE_TO_LEVEL(PLAYER2, GIANT, PLAYER2, 4, 10, 0)
		ADD_CREATURE_TO_LEVEL(PLAYER2, WITCH, PLAYER2, 4, 10, 0)
		CREATURE_AVAILABLE(PLAYER2, SAMURAI, 1, 1)
		CREATURE_AVAILABLE(PLAYER2, WIZARD, 1, 1)
		SET_FLAG(PLAYER_GOOD, FLAG0, 3)
	ENDIF
	rem アバタール死んだとき、すでに敵キーパーは滅亡
	IF(PLAYER2, DUNGEON_DESTROYED == 1)
		rem 副官総力戦ルート
		DISPLAY_OBJECTIVE(147, PLAYER0)
		SET_FLAG(PLAYER_GOOD, FLAG0, 4)
	ENDIF
ENDIF

rem 副官寝返りルート
IF(PLAYER_GOOD, FLAG0 == 3)
	SET_TIMER(PLAYER2, TIMER1)
	IF(PLAYER2, TOTAL_CREATURES < 60)
		IF(PLAYER2, TIMER1 > 3500)
			ADD_CREATURE_TO_LEVEL(PLAYER2, KNIGHT, PLAYER2, 1, 10, 0)
			rem 定期的にクリーチャー湧く
			IF(PLAYER2, KNIGHT >= 1)
				DISPLAY_OBJECTIVE(146, PLAYER0)
				NEXT_COMMAND_REUSABLE
				ADD_CREATURE_TO_LEVEL(PLAYER2, SAMURAI, PLAYER2, 1, 6, 0)
				NEXT_COMMAND_REUSABLE
				ADD_CREATURE_TO_LEVEL(PLAYER2, WIZARD, PLAYER2, 1, 6, 0)
				NEXT_COMMAND_REUSABLE
				SET_TIMER(PLAYER2, TIMER1)
			ENDIF
		ENDIF
	ENDIF
	IF(PLAYER2, DUNGEON_DESTROYED == 1)
		DISPLAY_OBJECTIVE(149, PLAYER0)
		WIN_GAME
	ENDIF
ENDIF

rem 副官総力戦ルート
IF(PLAYER_GOOD, FLAG0 == 4)
	ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD, HERO1, 3, DUNGEON_HEART, PLAYER0, 6, 0)
	ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, SAMURAI, 3, 3, 6, 0)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD, ST_GOLD, 1, 2)
	SET_TIMER(PLAYER_GOOD, TIMER2)
	IF(PLAYER_GOOD, TIMER2 > 3800)
		ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD, HERO2, 2, DUNGEON_HEART, PLAYER0, 6, 0)
		ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, BARBARIAN, 10, 3, 8, 0)
	ENDIF
	IF(PLAYER_GOOD, TIMER2 > 6000)
		ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD, HERO3, 5, DUNGEON_HEART, PLAYER0, 6, 0)
		ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, FAIRY, 5, 5, 6, 0)
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD, ST_GOLD, -2, 2)
	ENDIF
	IF(PLAYER_GOOD, TOTAL_CREATURES < 100)
		SET_TIMER(PLAYER_GOOD, TIMER4)
		IF(PLAYER_GOOD, TIMER4 > 5000)
			DISPLAY_INFORMATION(148, PLAYER0)
			ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD, AT_ROOM, -2, DUNGEON_HEART, PLAYER0, 8, 0)
			ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, ORC, 7, 4, 8, 400)
		ENDIF
		IF(PLAYER_GOOD, TIMER4 > 6400)
			ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, VAMPIRE, 12, 3, 5, 800)
			ADD_PARTY_TO_LEVEL(PLAYER_GOOD, AT_ROOM, -1, 2)
		ENDIF
		IF(PLAYER_GOOD, TIMER4 > 7500)
			ADD_PARTY_TO_LEVEL(PLAYER_GOOD, ST_GOLD, 3, 2)
			ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, DARK_MISTRESS, 13, 3, 9, 800)
		ENDIF
		IF(PLAYER_GOOD, TIMER4 > 8800)
			ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, HELL_HOUND, 1, 5, 8, 400)
			ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, DWARFA, 12, 6, 3, 0)
			ADD_PARTY_TO_LEVEL(PLAYER_GOOD, AT_ROOM3, 10, 3)
		ENDIF
		IF(PLAYER_GOOD, TIMER4 > 10200)
			ADD_PARTY_TO_LEVEL(PLAYER_GOOD, AT_ROOM2, 11, 3)
			ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, ORC, 5, 3, 7, 400)
			ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, MONK, 13, 3, 5, 0)
		ENDIF
		IF(PLAYER_GOOD, TIMER4 > 13000)
			ADD_PARTY_TO_LEVEL(PLAYER_GOOD, BOSS1, 5, 1)
			ADD_PARTY_TO_LEVEL(PLAYER_GOOD, GUARD2, 5, 1)
			ADD_PARTY_TO_LEVEL(PLAYER_GOOD, GUARD3, 5, 2)
			IF(PLAYER_GOOD, KNIGHT == 0)
				DISPLAY_OBJECTIVE(149, PLAYER0)
				WIN_GAME
			ENDIF
		ENDIF
	ENDIF
ENDIF

IF(PLAYER2, DUNGEON_DESTROYED == 0)
	SET_TIMER(PLAYER2, TIMER0)
	IF(PLAYER2, TIMER0 > 1000)
		IF(PLAYER2, IMP < 20)
			NEXT_COMMAND_REUSABLE
			ADD_CREATURE_TO_LEVEL(PLAYER2, IMP, PLAYER2, 1, 2, 0)
			NEXT_COMMAND_REUSABLE
			SET_TIMER(PLAYER2, TIMER0)
		ENDIF
	ENDIF
	IF(PLAYER0, TORTURE >= 1)
		ADD_CREATURE_TO_LEVEL(PLAYER2, DARK_MISTRESS, PLAYER2, 5, 6, 0)
	ENDIF
ENDIF

IF(PLAYER_GOOD, FLAG1 == 1)
	IF(PLAYER_GOOD, TIMER1 >= 80)
		NEXT_COMMAND_REUSABLE
		SET_CREATURE_FEAR(SPIDER, 255)
		NEXT_COMMAND_REUSABLE
		SET_CREATURE_FEAR(BILE_DEMON, 255)
		NEXT_COMMAND_REUSABLE
		SET_CREATURE_FEAR(DRAGON, 255)
		NEXT_COMMAND_REUSABLE
		SET_CREATURE_FEAR(ORC, 255)
		NEXT_COMMAND_REUSABLE
		SET_CREATURE_FEAR(TROLL, 255)
		NEXT_COMMAND_REUSABLE
		SET_CREATURE_FEAR(TENTACLE, 255)
		NEXT_COMMAND_REUSABLE
		SET_CREATURE_FEAR(VAMPIRE, 255)
		NEXT_COMMAND_REUSABLE
		SET_CREATURE_FEAR(SKELETON, 255)
		NEXT_COMMAND_REUSABLE
		SET_CREATURE_FEAR(GHOST, 255)
		NEXT_COMMAND_REUSABLE
		SET_CREATURE_FEAR(HELL_HOUND, 255)
		NEXT_COMMAND_REUSABLE
		SET_FLAG(PLAYER_GOOD, FLAG1, 2)
	ENDIF
ENDIF

IF(PLAYER_GOOD, FLAG1 == 2)
	IF(PLAYER_GOOD, TIMER1 >= 130)
		NEXT_COMMAND_REUSABLE
		SET_CREATURE_FEAR(SPIDER, 30)
		NEXT_COMMAND_REUSABLE
		SET_CREATURE_FEAR(BILE_DEMON, 30)
		NEXT_COMMAND_REUSABLE
		SET_CREATURE_FEAR(DRAGON, 30)
		NEXT_COMMAND_REUSABLE
		SET_CREATURE_FEAR(ORC, 30)
		NEXT_COMMAND_REUSABLE
		SET_CREATURE_FEAR(TROLL, 30)
		NEXT_COMMAND_REUSABLE
		SET_CREATURE_FEAR(TENTACLE, 30)
		NEXT_COMMAND_REUSABLE
		SET_CREATURE_FEAR(VAMPIRE, 30)
		NEXT_COMMAND_REUSABLE
		SET_CREATURE_FEAR(SKELETON, 30)
		NEXT_COMMAND_REUSABLE
		SET_CREATURE_FEAR(GHOST, 30)
		NEXT_COMMAND_REUSABLE
		SET_CREATURE_FEAR(HELL_HOUND, 30)
		IF(PLAYER_GOOD, AVATAR >= 1)
			NEXT_COMMAND_REUSABLE
			SET_TIMER(PLAYER_GOOD, TIMER1)
			NEXT_COMMAND_REUSABLE
			SET_FLAG(PLAYER_GOOD, FLAG1, 1)
		ENDIF
	ENDIF
ENDIF

IF(PLAYER0, ENTRANCE >= 10)
	SET_GENERATE_SPEED(300)
ENDIF

DISPLAY_OBJECTIVE(142, PLAYER0)
