REM ANOTHER DUNGEON SECRET LEVEL6
REM 〜フルムーンゲート〜

REM *** SETUP SECTION ***

BONUS_LEVEL_TIME(50000)

SET_GENERATE_SPEED(450)

START_MONEY(PLAYER0, 15000)

MAX_CREATURES(PLAYER0, 70)

ADD_CREATURE_TO_POOL(FLY, 10)
ADD_CREATURE_TO_POOL(BUG, 10)
ADD_CREATURE_TO_POOL(SPIDER, 10)
ADD_CREATURE_TO_POOL(DEMONSPAWN, 10)
ADD_CREATURE_TO_POOL(DRAGON, 10)
ADD_CREATURE_TO_POOL(SORCEROR, 10)
ADD_CREATURE_TO_POOL(TROLL, 10)
ADD_CREATURE_TO_POOL(BILE_DEMON, 10)
ADD_CREATURE_TO_POOL(ORC, 10)
ADD_CREATURE_TO_POOL(DARK_MISTRESS, 10)
ADD_CREATURE_TO_POOL(TENTACLE, 10)
ADD_CREATURE_TO_POOL(HELL_HOUND, 10)

CREATURE_AVAILABLE(ALL_PLAYERS, FLY, 1, 1)
CREATURE_AVAILABLE(ALL_PLAYERS, BUG, 1, 1)
CREATURE_AVAILABLE(ALL_PLAYERS, SPIDER, 1, 1)
CREATURE_AVAILABLE(ALL_PLAYERS, DEMONSPAWN, 1, 1)
CREATURE_AVAILABLE(ALL_PLAYERS, HELL_HOUND, 1, 1)
CREATURE_AVAILABLE(ALL_PLAYERS, TENTACLE, 1, 1)

ROOM_AVAILABLE(ALL_PLAYERS, TREASURE, 1, 1)
ROOM_AVAILABLE(ALL_PLAYERS, LAIR, 1, 1)
ROOM_AVAILABLE(ALL_PLAYERS, GARDEN, 1, 1)
ROOM_AVAILABLE(ALL_PLAYERS, RESEARCH, 1, 1)
ROOM_AVAILABLE(ALL_PLAYERS, TRAINING, 1, 1)

MAGIC_AVAILABLE(ALL_PLAYERS, POWER_POSSESS, 1, 1)
MAGIC_AVAILABLE(ALL_PLAYERS, POWER_IMP, 1, 1)
MAGIC_AVAILABLE(ALL_PLAYERS, POWER_SIGHT, 0, 0)
MAGIC_AVAILABLE(ALL_PLAYERS, POWER_OBEY, 1, 0)
MAGIC_AVAILABLE(ALL_PLAYERS, POWER_HOLD_AUDIENCE, 1, 0)
MAGIC_AVAILABLE(ALL_PLAYERS, POWER_CONCEAL, 1, 0)
MAGIC_AVAILABLE(ALL_PLAYERS, POWER_LIGHTNING, 1, 0)
MAGIC_AVAILABLE(ALL_PLAYERS, POWER_ARMAGEDDON, 1, 0)
MAGIC_AVAILABLE(ALL_PLAYERS, POWER_DESTROY_WALLS, 1, 0)

TRAP_AVAILABLE(ALL_PLAYERS, ALARM, 1, 0)
TRAP_AVAILABLE(ALL_PLAYERS, POISON_GAS, 1, 0)
TRAP_AVAILABLE(ALL_PLAYERS, WORD_OF_POWER, 1, 0)

DOOR_AVAILABLE(ALL_PLAYERS, WOOD, 1, 0)
DOOR_AVAILABLE(ALL_PLAYERS, BRACED, 1, 0)
DOOR_AVAILABLE(ALL_PLAYERS, STEEL, 1, 0)

SET_CREATURE_STRENGTH(KNIGHT, 255)
SET_CREATURE_ARMOUR(KNIGHT, 255)

REM *** HERO PARTY ***

CREATE_PARTY(A_ROOM1)
ADD_TO_PARTY(A_ROOM1, HELL_HOUND, 8, 0, ATTACK_ROOMS, 0)

CREATE_PARTY(A_ROOM2)
ADD_TO_PARTY(A_ROOM2, DWARFA, 8, 0, ATTACK_ROOMS, 0)

CREATE_PARTY(ATTACK1)
ADD_TO_PARTY(ATTACK1, HELL_HOUND, 10, 0, ATTACK_DUNGEON_HEART, 0)
ADD_TO_PARTY(ATTACK1, BILE_DEMON, 10, 1000, ATTACK_DUNGEON_HEART, 0)
ADD_TO_PARTY(ATTACK1, HELL_HOUND, 10, 0, ATTACK_DUNGEON_HEART, 0)

CREATE_PARTY(ATTACK2)
ADD_TO_PARTY(ATTACK2, GIANT, 10, 0, ATTACK_DUNGEON_HEART, 0)
ADD_TO_PARTY(ATTACK2, AVATAR, 10, 5000, ATTACK_DUNGEON_HEART, 0)
ADD_TO_PARTY(ATTACK2, GIANT, 10, 0, ATTACK_DUNGEON_HEART, 0)
ADD_TO_PARTY(ATTACK2, SAMURAI, 10, 0, ATTACK_DUNGEON_HEART, 0)
ADD_TO_PARTY(ATTACK2, SAMURAI, 10, 0, ATTACK_DUNGEON_HEART, 0)
ADD_TO_PARTY(ATTACK2, ARCHER, 10, 0, ATTACK_ENEMIES, 0)
ADD_TO_PARTY(ATTACK2, ARCHER, 10, 0, ATTACK_ENEMIES, 0)

CREATE_PARTY(BOSS1)
ADD_TO_PARTY(BOSS1, HORNY, 9, 0, ATTACK_ENEMIES, 0)
ADD_TO_PARTY(BOSS1, KNIGHT, 10, 5000, ATTACK_ENEMIES, 0)
ADD_TO_PARTY(BOSS1, HORNY, 9, 0, ATTACK_ENEMIES, 0)
ADD_TO_PARTY(BOSS1, HORNY, 9, 0, ATTACK_ENEMIES, 0)
ADD_TO_PARTY(BOSS1, DARK_MISTRESS, 9, 0, ATTACK_ENEMIES, 0)
ADD_TO_PARTY(BOSS1, DARK_MISTRESS, 9, 0, ATTACK_ENEMIES, 0)
ADD_TO_PARTY(BOSS1, DARK_MISTRESS, 9, 0, ATTACK_ENEMIES, 0)

CREATE_PARTY(BOSS2)
ADD_TO_PARTY(BOSS2, SAMURAI, 9, 0, DEFEND_PARTY, 0)
ADD_TO_PARTY(BOSS2, AVATAR, 10, 10000, ATTACK_DUNGEON_HEART, 0)
ADD_TO_PARTY(BOSS2, SAMURAI, 9, 0, DEFEND_PARTY, 0)
ADD_TO_PARTY(BOSS2, SAMURAI, 9, 0, DEFEND_PARTY, 0)
ADD_TO_PARTY(BOSS2, SAMURAI, 9, 0, DEFEND_PARTY, 0)
ADD_TO_PARTY(BOSS2, WIZARD, 10, 0, DEFEND_PARTY, 0)
ADD_TO_PARTY(BOSS2, WIZARD, 10, 0, DEFEND_PARTY, 0)

REM *** MAIN ***

IF(PLAYER0, ENTRANCE > 9)
	DISPLAY_OBJECTIVE(923, PLAYER0)
	CREATURE_AVAILABLE(PLAYER0, TROLL, 1, 1)
	CREATURE_AVAILABLE(PLAYER0, SORCEROR, 1, 1)
ENDIF

IF(PLAYER0, ENTRANCE > 18)
	CREATURE_AVAILABLE(PLAYER0, DRAGON, 1, 1)
	CREATURE_AVAILABLE(PLAYER0, ORC, 1, 1)
ENDIF

IF(PLAYER0, ENTRANCE > 27)
	CREATURE_AVAILABLE(PLAYER0, BILE_DEMON, 1, 1)
	CREATURE_AVAILABLE(PLAYER0, DARK_MISTRESS, 1, 1)
ENDIF

rem 丸石(罠)出現条件：バイル10体以上、勇者部屋破壊1以上
IF(PLAYER0, BILE_DEMON >= 10)
	IF(PLAYER_GOOD, ROOMS_DESTROYED >= 1)
		TRAP_AVAILABLE(PLAYER0, BOULDER, 1, 0)
		TUTORIAL_FLASH_BUTTON(53, -1)
	ENDIF
ENDIF

rem 雷撃（罠）出現条件：ミストレスゼロ、トロル6以上
IF(PLAYER0, DARK_MISTRESS == 0)
	IF(PLAYER0, TROLL >= 6)
		TRAP_AVAILABLE(PLAYER0, LIGHTNING, 1, 0)
		TUTORIAL_FLASH_BUTTON(56, -1)
	ENDIF
ENDIF

rem 溶岩（罠）出現条件：ドラゴン11以上
IF(PLAYER0, DRAGON >= 11)
	TRAP_AVAILABLE(PLAYER0, LAVA, 1, 0)
	TUTORIAL_FLASH_BUTTON(67, -1)
ENDIF

rem 魔法(扉)出現条件：ドア12以上、ウォーロック12以上
IF(PLAYER0, TOTAL_DOORS >= 12)
	IF(PLAYER0, SORCEROR >= 12)
		DOOR_AVAILABLE(PLAYER0, MAGIC, 1, 0)
		TUTORIAL_FLASH_BUTTON(61, -1)
	ENDIF
ENDIF

rem 治癒出現条件：インプ25体以上
IF(PLAYER0, IMP >= 25)
	MAGIC_AVAILABLE(PLAYER0, POWER_HEAL_CREATURE, 1, 1)
	TUTORIAL_FLASH_BUTTON(26, -1)
ENDIF

rem 落盤出現条件：ゴースト15体以上
IF(PLAYER0, GHOST >= 15)
	MAGIC_AVAILABLE(PLAYER0, POWER_CAVE_IN, 1, 1)
	TUTORIAL_FLASH_BUTTON(24, -1)
ENDIF

rem 呪い出現条件：スケルトン10体以上、ニワトリ小屋ナシ
IF(PLAYER0, SKELETON >= 10)
	IF(PLAYER0, GARDEN == 0)
		MAGIC_AVAILABLE(PLAYER0, POWER_CHICKEN, 1, 1)
		TUTORIAL_FLASH_BUTTON(30, -1)
	ENDIF
ENDIF

rem 疫病出現条件：クリーチャーを7体洗脳する
IF(PLAYER0, CREATURES_SCAVENGED_GAINED >= 7)
	MAGIC_AVAILABLE(PLAYER0, POWER_DISEASE, 1, 1)
	TUTORIAL_FLASH_BUTTON(31, -1)
ENDIF

rem 兵舎出現条件：カウント40000以下、ホーニー1体以上
IF(PLAYER0, GAME_TURN <= 40000)
	IF(PLAYER0, HORNY >= 1)
		ROOM_AVAILABLE(PLAYER0, BARRACKS, 1, 1)
		TUTORIAL_FLASH_BUTTON(16, -1)
	ENDIF
ENDIF

IF_AVAILABLE(PLAYER0, POWER_DISEASE == 1)
	rem 墓地出現条件：疫病あり、戦闘勝利数12以上
	IF(PLAYER0, BATTLES_WON >= 12)
		ROOM_AVAILABLE(PLAYER0, GRAVEYARD, 1, 1)
		TUTORIAL_FLASH_BUTTON(15, -1)
	ENDIF
	rem 神殿出現条件：疫病あり、治癒あり、呪いあり
	IF_AVAILABLE(PLAYER0, POWER_HEAL_CREATURE == 1)
		IF_AVAILABLE(PLAYER0, POWER_CHICKEN == 1)
			ROOM_AVAILABLE(PLAYER0, TEMPLE, 1, 1)
			TUTORIAL_FLASH_BUTTON(12, -1)
		ENDIF
	ENDIF
ENDIF

rem 拷問部屋出現条件:カウント20000以降、クリーチャー9匹を怒らせる
IF(PLAYER0, CREATURES_ANNOYED >= 9)
	IF(PLAYER_GOOD, GAME_TURN >= 20000)
		ROOM_AVAILABLE(PLAYER0, TORTURE, 1, 1)
		TUTORIAL_FLASH_BUTTON(17, -1)
	ENDIF
ENDIF

rem 牢獄出現条件：黄金160000以上
IF(PLAYER0, MONEY >= 160000)
	ROOM_AVAILABLE(PLAYER0, PRISON, 1, 1)
	TUTORIAL_FLASH_BUTTON(11, -1)
ENDIF

rem 工房出現条件：研究ポイント750000以上、領地1600タイル以上
IF(PLAYER0, TOTAL_RESEARCH >= 750000)
	IF(PLAYER0, TOTAL_AREA >= 1600)
		ROOM_AVAILABLE(PLAYER0, WORKSHOP, 1, 1)
		TUTORIAL_FLASH_BUTTON(13, -1)
	ENDIF
ENDIF

rem 洗脳室出現条件：研究値400000以上、怒って去ったクリーチャー3体以上
IF(PLAYER0, TOTAL_RESEARCH >= 400000)
	IF(PLAYER0, TOTAL_CREATURES_LEFT >= 3)
		ROOM_AVAILABLE(PLAYER0, SCAVENGER, 1, 1)
		TUTORIAL_FLASH_BUTTON(14, -1)
	ENDIF
ENDIF

rem 監視所出現条件：カウント10000以降
IF(PLAYER_GOOD, GAME_TURN >= 10000)
	ROOM_AVAILABLE(PLAYER0, GUARD_POST, 1, 1)
	TUTORIAL_FLASH_BUTTON(19, -1)
ENDIF

rem 橋出現条件：カウント25000以降
IF(PLAYER_GOOD, GAME_TURN >= 25000)
	ROOM_AVAILABLE(PLAYER0, BRIDGE, 1, 1)
	TUTORIAL_FLASH_BUTTON(18, -1)
ENDIF

IF(PLAYER_GOOD, GAME_TURN >= 47500)
	DISPLAY_INFORMATION(924, PLAYER0)
ENDIF

SET_FLAG(PLAYER_GOOD, FLAG0, 1)
IF(PLAYER0, GAME_TURN <= 20000)
	rem ターン20000以内に、どれか一つでも満たせば真の勇者軍団登場
	rem いずれも満たさなければ魔王軍団
	IF(PLAYER0, TOTAL_AREA >= 1500)
		SET_FLAG(PLAYER_GOOD, FLAG0, 1)
	ENDIF
	IF(PLAYER0, CREATURES_ANNOYED >= 12)
		SET_FLAG(PLAYER_GOOD, FLAG0, 1)
	ENDIF
	IF(PLAYER0, MONEY >= 160000)
		SET_FLAG(PLAYER_GOOD, FLAG0, 1)
	ENDIF
	IF(PLAYER0, BATTLES_WON >= 12)
		SET_FLAG(PLAYER_GOOD, FLAG0, 1)
	ENDIF
ENDIF

rem ゲートオープン
IF(PLAYER_GOOD, GAME_TURN >= 50000)
	SET_TIMER(PLAYER_GOOD, TIMER0)
	rem 魔王
	IF(PLAYER_GOOD, FLAG0 == 0)
		ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, BILE_DEMON, 6, 3, 10, 0)
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD, A_ROOM1, 7, 2)
		ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, TROLL, 8, 3, 10, 0)
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD, A_ROOM1, 9, 2)
		ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, SKELETON, 10, 3, 10, 0)
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD, A_ROOM1, 11, 2)
		ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, DRAGON, 12, 3, 10, 0)
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD, A_ROOM1, 13, 2)
		IF(PLAYER_GOOD, TIMER0 >= 2000)
			ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, DARK_MISTRESS, 2, 3, 9, 0)
			ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, GHOST, 3, 3, 9, 0)
			ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, TENTACLE, 4, 3, 9, 0)
			ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, ORC, 5, 3, 9, 0)
		ENDIF
		IF(PLAYER_GOOD, TIMER0 >= 4000)
			ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, TENTACLE, 1, 8, 10, 0)
			ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, SPIDER, 6, 6, 10, 0)
			ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, DEMONSPAWN, 8, 6, 10, 0)
			ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, SPIDER, 10, 6, 10, 0)
			ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, DEMONSPAWN, 12, 6, 10, 0)
		ENDIF
		IF(PLAYER_GOOD, TIMER0 >= 6000)
			ADD_PARTY_TO_LEVEL(PLAYER_GOOD, BOSS1, 1, 1)
			ADD_PARTY_TO_LEVEL(PLAYER_GOOD, ATTACK1, 1, 3)
			ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, BUG, 1, 8, 10, 0)
			ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, VAMPIRE, 7, 3, 10, 0)
			ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, SKELETON, 9, 4, 10, 0)
			ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, VAMPIRE, 11, 3, 10, 0)
			ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, GHOST, 13, 4, 10, 0)
		ENDIF
	ENDIF
	rem 勇者
	IF(PLAYER_GOOD, FLAG0 == 1)
		ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, DWARFA, 6, 4, 10, 0)
		ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD, ATTACK2, 7, DUNGEON_HEART, PLAYER0, 10, 0)
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD, A_ROOM2, 7, 2)
		ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, THIEF, 8, 4, 10, 0)
		ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD, ATTACK2, 9, DUNGEON_HEART, PLAYER0, 10, 0)
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD, A_ROOM2, 9, 2)
		ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, GIANT, 10, 4, 10, 0)
		ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD, ATTACK2, 11, DUNGEON_HEART, PLAYER0, 10, 0)
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD, A_ROOM2, 11, 2)
		ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, MONK, 12, 4, 10, 0)
		ADD_TUNNELLER_PARTY_TO_LEVEL(PLAYER_GOOD, ATTACK2, 13, DUNGEON_HEART, PLAYER0, 10, 0)
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD, A_ROOM2, 13, 2)
		IF(PLAYER_GOOD, TIMER0 >= 2000)
			ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, WITCH, 2, 4, 9, 0)
			ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, FAIRY, 3, 4, 9, 0)
			ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, BARBARIAN, 4, 4, 9, 0)
			ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, WIZARD, 5, 4, 9, 0)
		ENDIF
		IF(PLAYER_GOOD, TIMER0 >= 4000)
			ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, DWARFA, 1, 8, 10, 0)
			ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, THIEF, 6, 6, 10, 0)
			ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, ARCHER, 8, 6, 10, 0)
			ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, THIEF, 10, 6, 10, 0)
			ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, ARCHER, 12, 6, 10, 0)
		ENDIF
		IF(PLAYER_GOOD, TIMER0 >= 6000)
			SET_CREATURE_STRENGTH(AVATAR, 255)
			SET_CREATURE_ARMOUR(AVATAR, 255)
			ADD_PARTY_TO_LEVEL(PLAYER_GOOD, BOSS2, 1, 1)
			ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, GIANT, 1, 8, 10, 0)
			ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, WITCH, 1, 8, 10, 0)
			ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, DWARFA, 7, 6, 10, 0)
			ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, BARBARIAN, 9, 6, 10, 0)
			ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, FAIRY, 11, 6, 10, 0)
			ADD_CREATURE_TO_LEVEL(PLAYER_GOOD, MONK, 13, 6, 10, 0)
		ENDIF
	ENDIF
	IF(PLAYER_GOOD, TIMER0 >= 6100)
		IF(PLAYER_GOOD, TOTAL_CREATURES == 0)
			DISPLAY_OBJECTIVE(927, PLAYER0)
			WIN_GAME
		ENDIF
	ENDIF
ENDIF

DISPLAY_OBJECTIVE(912, PLAYER0)
